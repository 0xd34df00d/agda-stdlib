<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Star.Pointer</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Pointers into star-lists</a>
<a id="134" class="Comment">------------------------------------------------------------------------</a>

<a id="208" class="Keyword">module</a> <a id="215" href="Data.Star.Pointer.html" class="Module">Data.Star.Pointer</a> <a id="233" class="Keyword">where</a>

<a id="240" class="Keyword">open</a> <a id="245" class="Keyword">import</a> <a id="252" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="268" class="Keyword">using</a> <a id="274" class="Symbol">(</a><a id="275" href="Data.Maybe.Base.html#Maybe" class="Datatype">Maybe</a><a id="280" class="Symbol">;</a> <a id="282" href="Data.Maybe.Base.html#Maybe.nothing" class="InductiveConstructor">nothing</a><a id="289" class="Symbol">;</a> <a id="291" href="Data.Maybe.Base.html#Maybe.just" class="InductiveConstructor">just</a><a id="295" class="Symbol">)</a>
<a id="297" class="Keyword">open</a> <a id="302" class="Keyword">import</a> <a id="309" href="Data.Star.Decoration.html" class="Module">Data.Star.Decoration</a>
<a id="330" class="Keyword">open</a> <a id="335" class="Keyword">import</a> <a id="342" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="352" class="Keyword">open</a> <a id="357" class="Keyword">import</a> <a id="364" href="Function.html" class="Module">Function</a>
<a id="373" class="Keyword">open</a> <a id="378" class="Keyword">import</a> <a id="385" href="Level.html" class="Module">Level</a>
<a id="391" class="Keyword">open</a> <a id="396" class="Keyword">import</a> <a id="403" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="419" class="Keyword">open</a> <a id="424" class="Keyword">import</a> <a id="431" href="Relation.Binary.Closure.ReflexiveTransitive.html" class="Module">Relation.Binary.Closure.ReflexiveTransitive</a>

<a id="476" class="Comment">-- Pointers into star-lists. The edge pointed to is decorated with Q,</a>
<a id="546" class="Comment">-- while other edges are decorated with P.</a>

<a id="590" class="Keyword">data</a> <a id="Pointer" href="Data.Star.Pointer.html#Pointer" class="Datatype">Pointer</a> <a id="603" class="Symbol">{</a><a id="604" href="Data.Star.Pointer.html#604" class="Bound">I</a> <a id="606" class="Symbol">:</a> <a id="608" class="PrimitiveType">Set</a><a id="611" class="Symbol">}</a> <a id="613" class="Symbol">{</a><a id="614" href="Data.Star.Pointer.html#614" class="Bound">T</a> <a id="616" class="Symbol">:</a> <a id="618" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="622" href="Data.Star.Pointer.html#604" class="Bound">I</a> <a id="624" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="628" class="Symbol">}</a> <a id="630" class="Symbol">(</a><a id="631" href="Data.Star.Pointer.html#631" class="Bound">P</a> <a id="633" href="Data.Star.Pointer.html#633" class="Bound">Q</a> <a id="635" class="Symbol">:</a> <a id="637" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="646" href="Data.Star.Pointer.html#614" class="Bound">T</a><a id="647" class="Symbol">)</a>
       <a id="656" class="Symbol">:</a> <a id="658" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="662" class="Symbol">(</a><a id="663" href="Data.Maybe.Base.html#Maybe" class="Datatype">Maybe</a> <a id="669" class="Symbol">(</a><a id="670" href="Relation.Binary.Core.html#NonEmpty" class="Record">NonEmpty</a> <a id="679" class="Symbol">(</a><a id="680" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="685" href="Data.Star.Pointer.html#614" class="Bound">T</a><a id="686" class="Symbol">)))</a> <a id="690" href="Agda.Primitive.html#zero" class="Primitive">zero</a> <a id="695" class="Keyword">where</a>
  <a id="Pointer.step" href="Data.Star.Pointer.html#Pointer.step" class="InductiveConstructor">step</a> <a id="708" class="Symbol">:</a> <a id="710" class="Symbol">∀</a> <a id="712" class="Symbol">{</a><a id="713" href="Data.Star.Pointer.html#713" class="Bound">i</a> <a id="715" href="Data.Star.Pointer.html#715" class="Bound">j</a> <a id="717" href="Data.Star.Pointer.html#717" class="Bound">k</a><a id="718" class="Symbol">}</a> <a id="720" class="Symbol">{</a><a id="721" href="Data.Star.Pointer.html#721" class="Bound">x</a> <a id="723" class="Symbol">:</a> <a id="725" href="Data.Star.Pointer.html#614" class="Bound">T</a> <a id="727" href="Data.Star.Pointer.html#713" class="Bound">i</a> <a id="729" href="Data.Star.Pointer.html#715" class="Bound">j</a><a id="730" class="Symbol">}</a> <a id="732" class="Symbol">{</a><a id="733" href="Data.Star.Pointer.html#733" class="Bound">xs</a> <a id="736" class="Symbol">:</a> <a id="738" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="743" href="Data.Star.Pointer.html#614" class="Bound">T</a> <a id="745" href="Data.Star.Pointer.html#715" class="Bound">j</a> <a id="747" href="Data.Star.Pointer.html#717" class="Bound">k</a><a id="748" class="Symbol">}</a>
         <a id="759" class="Symbol">(</a><a id="760" href="Data.Star.Pointer.html#760" class="Bound">p</a> <a id="762" class="Symbol">:</a> <a id="764" href="Data.Star.Pointer.html#631" class="Bound">P</a> <a id="766" href="Data.Star.Pointer.html#721" class="Bound">x</a><a id="767" class="Symbol">)</a> <a id="769" class="Symbol">→</a> <a id="771" href="Data.Star.Pointer.html#Pointer" class="Datatype">Pointer</a> <a id="779" href="Data.Star.Pointer.html#631" class="Bound">P</a> <a id="781" href="Data.Star.Pointer.html#633" class="Bound">Q</a> <a id="783" class="Symbol">(</a><a id="784" href="Data.Maybe.Base.html#Maybe.just" class="InductiveConstructor">just</a> <a id="789" class="Symbol">(</a><a id="790" href="Relation.Binary.Core.html#NonEmpty.nonEmpty" class="InductiveConstructor">nonEmpty</a> <a id="799" class="Symbol">(</a><a id="800" href="Data.Star.Pointer.html#721" class="Bound">x</a> <a id="802" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="804" href="Data.Star.Pointer.html#733" class="Bound">xs</a><a id="806" class="Symbol">)))</a>
                                 <a id="843" class="Symbol">(</a><a id="844" href="Data.Maybe.Base.html#Maybe.just" class="InductiveConstructor">just</a> <a id="849" class="Symbol">(</a><a id="850" href="Relation.Binary.Core.html#NonEmpty.nonEmpty" class="InductiveConstructor">nonEmpty</a> <a id="859" href="Data.Star.Pointer.html#733" class="Bound">xs</a><a id="861" class="Symbol">))</a>
  <a id="Pointer.done" href="Data.Star.Pointer.html#Pointer.done" class="InductiveConstructor">done</a> <a id="871" class="Symbol">:</a> <a id="873" class="Symbol">∀</a> <a id="875" class="Symbol">{</a><a id="876" href="Data.Star.Pointer.html#876" class="Bound">i</a> <a id="878" href="Data.Star.Pointer.html#878" class="Bound">j</a> <a id="880" href="Data.Star.Pointer.html#880" class="Bound">k</a><a id="881" class="Symbol">}</a> <a id="883" class="Symbol">{</a><a id="884" href="Data.Star.Pointer.html#884" class="Bound">x</a> <a id="886" class="Symbol">:</a> <a id="888" href="Data.Star.Pointer.html#614" class="Bound">T</a> <a id="890" href="Data.Star.Pointer.html#876" class="Bound">i</a> <a id="892" href="Data.Star.Pointer.html#878" class="Bound">j</a><a id="893" class="Symbol">}</a> <a id="895" class="Symbol">{</a><a id="896" href="Data.Star.Pointer.html#896" class="Bound">xs</a> <a id="899" class="Symbol">:</a> <a id="901" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="906" href="Data.Star.Pointer.html#614" class="Bound">T</a> <a id="908" href="Data.Star.Pointer.html#878" class="Bound">j</a> <a id="910" href="Data.Star.Pointer.html#880" class="Bound">k</a><a id="911" class="Symbol">}</a>
         <a id="922" class="Symbol">(</a><a id="923" href="Data.Star.Pointer.html#923" class="Bound">q</a> <a id="925" class="Symbol">:</a> <a id="927" href="Data.Star.Pointer.html#633" class="Bound">Q</a> <a id="929" href="Data.Star.Pointer.html#884" class="Bound">x</a><a id="930" class="Symbol">)</a> <a id="932" class="Symbol">→</a> <a id="934" href="Data.Star.Pointer.html#Pointer" class="Datatype">Pointer</a> <a id="942" href="Data.Star.Pointer.html#631" class="Bound">P</a> <a id="944" href="Data.Star.Pointer.html#633" class="Bound">Q</a> <a id="946" class="Symbol">(</a><a id="947" href="Data.Maybe.Base.html#Maybe.just" class="InductiveConstructor">just</a> <a id="952" class="Symbol">(</a><a id="953" href="Relation.Binary.Core.html#NonEmpty.nonEmpty" class="InductiveConstructor">nonEmpty</a> <a id="962" class="Symbol">(</a><a id="963" href="Data.Star.Pointer.html#884" class="Bound">x</a> <a id="965" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="967" href="Data.Star.Pointer.html#896" class="Bound">xs</a><a id="969" class="Symbol">)))</a> <a id="973" href="Data.Maybe.Base.html#Maybe.nothing" class="InductiveConstructor">nothing</a>

<a id="982" class="Comment">-- Any P Q xs means that some edge in xs satisfies Q, while all</a>
<a id="1046" class="Comment">-- preceding edges satisfy P. A star-list of type Any Always Always xs</a>
<a id="1117" class="Comment">-- is basically a prefix of xs; the existence of such a prefix</a>
<a id="1180" class="Comment">-- guarantees that xs is non-empty.</a>

<a id="Any" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="1221" class="Symbol">:</a> <a id="1223" class="Symbol">∀</a> <a id="1225" class="Symbol">{</a><a id="1226" href="Data.Star.Pointer.html#1226" class="Bound">I</a><a id="1227" class="Symbol">}</a> <a id="1229" class="Symbol">{</a><a id="1230" href="Data.Star.Pointer.html#1230" class="Bound">T</a> <a id="1232" class="Symbol">:</a> <a id="1234" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="1238" href="Data.Star.Pointer.html#1226" class="Bound">I</a> <a id="1240" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="1244" class="Symbol">}</a> <a id="1246" class="Symbol">(</a><a id="1247" href="Data.Star.Pointer.html#1247" class="Bound">P</a> <a id="1249" href="Data.Star.Pointer.html#1249" class="Bound">Q</a> <a id="1251" class="Symbol">:</a> <a id="1253" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="1262" href="Data.Star.Pointer.html#1230" class="Bound">T</a><a id="1263" class="Symbol">)</a> <a id="1265" class="Symbol">→</a> <a id="1267" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="1276" class="Symbol">(</a><a id="1277" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="1282" href="Data.Star.Pointer.html#1230" class="Bound">T</a><a id="1283" class="Symbol">)</a>
<a id="1285" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="1289" href="Data.Star.Pointer.html#1289" class="Bound">P</a> <a id="1291" href="Data.Star.Pointer.html#1291" class="Bound">Q</a> <a id="1293" href="Data.Star.Pointer.html#1293" class="Bound">xs</a> <a id="1296" class="Symbol">=</a> <a id="1298" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="1303" class="Symbol">(</a><a id="1304" href="Data.Star.Pointer.html#Pointer" class="Datatype">Pointer</a> <a id="1312" href="Data.Star.Pointer.html#1289" class="Bound">P</a> <a id="1314" href="Data.Star.Pointer.html#1291" class="Bound">Q</a><a id="1315" class="Symbol">)</a> <a id="1317" class="Symbol">(</a><a id="1318" href="Data.Maybe.Base.html#Maybe.just" class="InductiveConstructor">just</a> <a id="1323" class="Symbol">(</a><a id="1324" href="Relation.Binary.Core.html#NonEmpty.nonEmpty" class="InductiveConstructor">nonEmpty</a> <a id="1333" href="Data.Star.Pointer.html#1293" class="Bound">xs</a><a id="1335" class="Symbol">))</a> <a id="1338" href="Data.Maybe.Base.html#Maybe.nothing" class="InductiveConstructor">nothing</a>

<a id="this" href="Data.Star.Pointer.html#this" class="Function">this</a> <a id="1352" class="Symbol">:</a> <a id="1354" class="Symbol">∀</a> <a id="1356" class="Symbol">{</a><a id="1357" href="Data.Star.Pointer.html#1357" class="Bound">I</a><a id="1358" class="Symbol">}</a> <a id="1360" class="Symbol">{</a><a id="1361" href="Data.Star.Pointer.html#1361" class="Bound">T</a> <a id="1363" class="Symbol">:</a> <a id="1365" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="1369" href="Data.Star.Pointer.html#1357" class="Bound">I</a> <a id="1371" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="1375" class="Symbol">}</a> <a id="1377" class="Symbol">{</a><a id="1378" href="Data.Star.Pointer.html#1378" class="Bound">P</a> <a id="1380" href="Data.Star.Pointer.html#1380" class="Bound">Q</a> <a id="1382" class="Symbol">:</a> <a id="1384" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="1393" href="Data.Star.Pointer.html#1361" class="Bound">T</a><a id="1394" class="Symbol">}</a>
             <a id="1409" class="Symbol">{</a><a id="1410" href="Data.Star.Pointer.html#1410" class="Bound">i</a> <a id="1412" href="Data.Star.Pointer.html#1412" class="Bound">j</a> <a id="1414" href="Data.Star.Pointer.html#1414" class="Bound">k</a><a id="1415" class="Symbol">}</a> <a id="1417" class="Symbol">{</a><a id="1418" href="Data.Star.Pointer.html#1418" class="Bound">x</a> <a id="1420" class="Symbol">:</a> <a id="1422" href="Data.Star.Pointer.html#1361" class="Bound">T</a> <a id="1424" href="Data.Star.Pointer.html#1410" class="Bound">i</a> <a id="1426" href="Data.Star.Pointer.html#1412" class="Bound">j</a><a id="1427" class="Symbol">}</a> <a id="1429" class="Symbol">{</a><a id="1430" href="Data.Star.Pointer.html#1430" class="Bound">xs</a> <a id="1433" class="Symbol">:</a> <a id="1435" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="1440" href="Data.Star.Pointer.html#1361" class="Bound">T</a> <a id="1442" href="Data.Star.Pointer.html#1412" class="Bound">j</a> <a id="1444" href="Data.Star.Pointer.html#1414" class="Bound">k</a><a id="1445" class="Symbol">}</a> <a id="1447" class="Symbol">→</a>
       <a id="1456" href="Data.Star.Pointer.html#1380" class="Bound">Q</a> <a id="1458" href="Data.Star.Pointer.html#1418" class="Bound">x</a> <a id="1460" class="Symbol">→</a> <a id="1462" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="1466" href="Data.Star.Pointer.html#1378" class="Bound">P</a> <a id="1468" href="Data.Star.Pointer.html#1380" class="Bound">Q</a> <a id="1470" class="Symbol">(</a><a id="1471" href="Data.Star.Pointer.html#1418" class="Bound">x</a> <a id="1473" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="1475" href="Data.Star.Pointer.html#1430" class="Bound">xs</a><a id="1477" class="Symbol">)</a>
<a id="1479" href="Data.Star.Pointer.html#this" class="Function">this</a> <a id="1484" href="Data.Star.Pointer.html#1484" class="Bound">q</a> <a id="1486" class="Symbol">=</a> <a id="1488" href="Data.Star.Pointer.html#Pointer.done" class="InductiveConstructor">done</a> <a id="1493" href="Data.Star.Pointer.html#1484" class="Bound">q</a> <a id="1495" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="1497" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star.%CE%B5" class="InductiveConstructor">ε</a>

<a id="that" href="Data.Star.Pointer.html#that" class="Function">that</a> <a id="1505" class="Symbol">:</a> <a id="1507" class="Symbol">∀</a> <a id="1509" class="Symbol">{</a><a id="1510" href="Data.Star.Pointer.html#1510" class="Bound">I</a><a id="1511" class="Symbol">}</a> <a id="1513" class="Symbol">{</a><a id="1514" href="Data.Star.Pointer.html#1514" class="Bound">T</a> <a id="1516" class="Symbol">:</a> <a id="1518" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="1522" href="Data.Star.Pointer.html#1510" class="Bound">I</a> <a id="1524" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="1528" class="Symbol">}</a> <a id="1530" class="Symbol">{</a><a id="1531" href="Data.Star.Pointer.html#1531" class="Bound">P</a> <a id="1533" href="Data.Star.Pointer.html#1533" class="Bound">Q</a> <a id="1535" class="Symbol">:</a> <a id="1537" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="1546" href="Data.Star.Pointer.html#1514" class="Bound">T</a><a id="1547" class="Symbol">}</a>
       <a id="1556" class="Symbol">{</a><a id="1557" href="Data.Star.Pointer.html#1557" class="Bound">i</a> <a id="1559" href="Data.Star.Pointer.html#1559" class="Bound">j</a> <a id="1561" href="Data.Star.Pointer.html#1561" class="Bound">k</a><a id="1562" class="Symbol">}</a> <a id="1564" class="Symbol">{</a><a id="1565" href="Data.Star.Pointer.html#1565" class="Bound">x</a> <a id="1567" class="Symbol">:</a> <a id="1569" href="Data.Star.Pointer.html#1514" class="Bound">T</a> <a id="1571" href="Data.Star.Pointer.html#1557" class="Bound">i</a> <a id="1573" href="Data.Star.Pointer.html#1559" class="Bound">j</a><a id="1574" class="Symbol">}</a> <a id="1576" class="Symbol">{</a><a id="1577" href="Data.Star.Pointer.html#1577" class="Bound">xs</a> <a id="1580" class="Symbol">:</a> <a id="1582" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="1587" href="Data.Star.Pointer.html#1514" class="Bound">T</a> <a id="1589" href="Data.Star.Pointer.html#1559" class="Bound">j</a> <a id="1591" href="Data.Star.Pointer.html#1561" class="Bound">k</a><a id="1592" class="Symbol">}</a> <a id="1594" class="Symbol">→</a>
       <a id="1603" href="Data.Star.Pointer.html#1531" class="Bound">P</a> <a id="1605" href="Data.Star.Pointer.html#1565" class="Bound">x</a> <a id="1607" class="Symbol">→</a> <a id="1609" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="1613" href="Data.Star.Pointer.html#1531" class="Bound">P</a> <a id="1615" href="Data.Star.Pointer.html#1533" class="Bound">Q</a> <a id="1617" href="Data.Star.Pointer.html#1577" class="Bound">xs</a> <a id="1620" class="Symbol">→</a> <a id="1622" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="1626" href="Data.Star.Pointer.html#1531" class="Bound">P</a> <a id="1628" href="Data.Star.Pointer.html#1533" class="Bound">Q</a> <a id="1630" class="Symbol">(</a><a id="1631" href="Data.Star.Pointer.html#1565" class="Bound">x</a> <a id="1633" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="1635" href="Data.Star.Pointer.html#1577" class="Bound">xs</a><a id="1637" class="Symbol">)</a>
<a id="1639" href="Data.Star.Pointer.html#that" class="Function">that</a> <a id="1644" href="Data.Star.Pointer.html#1644" class="Bound">p</a> <a id="1646" class="Symbol">=</a> <a id="1648" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">_◅_</a> <a id="1652" class="Symbol">(</a><a id="1653" href="Data.Star.Pointer.html#Pointer.step" class="InductiveConstructor">step</a> <a id="1658" href="Data.Star.Pointer.html#1644" class="Bound">p</a><a id="1659" class="Symbol">)</a>

<a id="1662" class="Comment">-- Safe lookup.</a>

<a id="1679" class="Keyword">data</a> <a id="Result" href="Data.Star.Pointer.html#Result" class="Datatype">Result</a> <a id="1691" class="Symbol">{</a><a id="1692" href="Data.Star.Pointer.html#1692" class="Bound">I</a> <a id="1694" class="Symbol">:</a> <a id="1696" class="PrimitiveType">Set</a><a id="1699" class="Symbol">}</a> <a id="1701" class="Symbol">(</a><a id="1702" href="Data.Star.Pointer.html#1702" class="Bound">T</a> <a id="1704" class="Symbol">:</a> <a id="1706" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="1710" href="Data.Star.Pointer.html#1692" class="Bound">I</a> <a id="1712" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="1716" class="Symbol">)</a> <a id="1718" class="Symbol">(</a><a id="1719" href="Data.Star.Pointer.html#1719" class="Bound">P</a> <a id="1721" href="Data.Star.Pointer.html#1721" class="Bound">Q</a> <a id="1723" class="Symbol">:</a> <a id="1725" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="1734" href="Data.Star.Pointer.html#1702" class="Bound">T</a><a id="1735" class="Symbol">)</a> <a id="1737" class="Symbol">:</a> <a id="1739" class="PrimitiveType">Set</a> <a id="1743" class="Keyword">where</a>
  <a id="Result.result" href="Data.Star.Pointer.html#Result.result" class="InductiveConstructor">result</a> <a id="1758" class="Symbol">:</a> <a id="1760" class="Symbol">∀</a> <a id="1762" class="Symbol">{</a><a id="1763" href="Data.Star.Pointer.html#1763" class="Bound">i</a> <a id="1765" href="Data.Star.Pointer.html#1765" class="Bound">j</a><a id="1766" class="Symbol">}</a> <a id="1768" class="Symbol">{</a><a id="1769" href="Data.Star.Pointer.html#1769" class="Bound">x</a> <a id="1771" class="Symbol">:</a> <a id="1773" href="Data.Star.Pointer.html#1702" class="Bound">T</a> <a id="1775" href="Data.Star.Pointer.html#1763" class="Bound">i</a> <a id="1777" href="Data.Star.Pointer.html#1765" class="Bound">j</a><a id="1778" class="Symbol">}</a>
           <a id="1791" class="Symbol">(</a><a id="1792" href="Data.Star.Pointer.html#1792" class="Bound">p</a> <a id="1794" class="Symbol">:</a> <a id="1796" href="Data.Star.Pointer.html#1719" class="Bound">P</a> <a id="1798" href="Data.Star.Pointer.html#1769" class="Bound">x</a><a id="1799" class="Symbol">)</a> <a id="1801" class="Symbol">(</a><a id="1802" href="Data.Star.Pointer.html#1802" class="Bound">q</a> <a id="1804" class="Symbol">:</a> <a id="1806" href="Data.Star.Pointer.html#1721" class="Bound">Q</a> <a id="1808" href="Data.Star.Pointer.html#1769" class="Bound">x</a><a id="1809" class="Symbol">)</a> <a id="1811" class="Symbol">→</a> <a id="1813" href="Data.Star.Pointer.html#Result" class="Datatype">Result</a> <a id="1820" href="Data.Star.Pointer.html#1702" class="Bound">T</a> <a id="1822" href="Data.Star.Pointer.html#1719" class="Bound">P</a> <a id="1824" href="Data.Star.Pointer.html#1721" class="Bound">Q</a>

<a id="1827" class="Comment">-- The first argument points out which edge to extract. The edge is</a>
<a id="1895" class="Comment">-- returned, together with proofs that it satisfies Q and R.</a>

<a id="lookup" href="Data.Star.Pointer.html#lookup" class="Function">lookup</a> <a id="1964" class="Symbol">:</a> <a id="1966" class="Symbol">∀</a> <a id="1968" class="Symbol">{</a><a id="1969" href="Data.Star.Pointer.html#1969" class="Bound">I</a><a id="1970" class="Symbol">}</a> <a id="1972" class="Symbol">{</a><a id="1973" href="Data.Star.Pointer.html#1973" class="Bound">T</a> <a id="1975" class="Symbol">:</a> <a id="1977" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="1981" href="Data.Star.Pointer.html#1969" class="Bound">I</a> <a id="1983" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="1987" class="Symbol">}</a> <a id="1989" class="Symbol">{</a><a id="1990" href="Data.Star.Pointer.html#1990" class="Bound">P</a> <a id="1992" href="Data.Star.Pointer.html#1992" class="Bound">Q</a> <a id="1994" href="Data.Star.Pointer.html#1994" class="Bound">R</a> <a id="1996" class="Symbol">:</a> <a id="1998" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="2007" href="Data.Star.Pointer.html#1973" class="Bound">T</a><a id="2008" class="Symbol">}</a>
               <a id="2025" class="Symbol">{</a><a id="2026" href="Data.Star.Pointer.html#2026" class="Bound">i</a> <a id="2028" href="Data.Star.Pointer.html#2028" class="Bound">j</a><a id="2029" class="Symbol">}</a> <a id="2031" class="Symbol">{</a><a id="2032" href="Data.Star.Pointer.html#2032" class="Bound">xs</a> <a id="2035" class="Symbol">:</a> <a id="2037" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="2042" href="Data.Star.Pointer.html#1973" class="Bound">T</a> <a id="2044" href="Data.Star.Pointer.html#2026" class="Bound">i</a> <a id="2046" href="Data.Star.Pointer.html#2028" class="Bound">j</a><a id="2047" class="Symbol">}</a> <a id="2049" class="Symbol">→</a>
         <a id="2060" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="2064" href="Data.Star.Pointer.html#1990" class="Bound">P</a> <a id="2066" href="Data.Star.Pointer.html#1992" class="Bound">Q</a> <a id="2068" href="Data.Star.Pointer.html#2032" class="Bound">xs</a> <a id="2071" class="Symbol">→</a> <a id="2073" href="Data.Star.Decoration.html#All" class="Function">All</a> <a id="2077" href="Data.Star.Pointer.html#1994" class="Bound">R</a> <a id="2079" href="Data.Star.Pointer.html#2032" class="Bound">xs</a> <a id="2082" class="Symbol">→</a> <a id="2084" href="Data.Star.Pointer.html#Result" class="Datatype">Result</a> <a id="2091" href="Data.Star.Pointer.html#1973" class="Bound">T</a> <a id="2093" href="Data.Star.Pointer.html#1992" class="Bound">Q</a> <a id="2095" href="Data.Star.Pointer.html#1994" class="Bound">R</a>
<a id="2097" href="Data.Star.Pointer.html#lookup" class="Function">lookup</a> <a id="2104" class="Symbol">(</a><a id="2105" href="Data.Star.Pointer.html#Pointer.done" class="InductiveConstructor">done</a> <a id="2110" href="Data.Star.Pointer.html#2110" class="Bound">q</a> <a id="2112" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2114" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star.%CE%B5" class="InductiveConstructor">ε</a><a id="2115" class="Symbol">)</a>      <a id="2122" class="Symbol">(</a><a id="2123" href="Data.Star.Decoration.html#DecoratedWith.%E2%86%A6" class="InductiveConstructor">↦</a> <a id="2125" href="Data.Star.Pointer.html#2125" class="Bound">r</a> <a id="2127" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2129" class="Symbol">_)</a>  <a id="2133" class="Symbol">=</a> <a id="2135" href="Data.Star.Pointer.html#Result.result" class="InductiveConstructor">result</a> <a id="2142" href="Data.Star.Pointer.html#2110" class="Bound">q</a> <a id="2144" href="Data.Star.Pointer.html#2125" class="Bound">r</a>
<a id="2146" href="Data.Star.Pointer.html#lookup" class="Function">lookup</a> <a id="2153" class="Symbol">(</a><a id="2154" href="Data.Star.Pointer.html#Pointer.step" class="InductiveConstructor">step</a> <a id="2159" href="Data.Star.Pointer.html#2159" class="Bound">p</a> <a id="2161" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2163" href="Data.Star.Pointer.html#2163" class="Bound">ps</a><a id="2165" class="Symbol">)</a>     <a id="2171" class="Symbol">(</a><a id="2172" href="Data.Star.Decoration.html#DecoratedWith.%E2%86%A6" class="InductiveConstructor">↦</a> <a id="2174" href="Data.Star.Pointer.html#2174" class="Bound">r</a> <a id="2176" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2178" href="Data.Star.Pointer.html#2178" class="Bound">rs</a><a id="2180" class="Symbol">)</a> <a id="2182" class="Symbol">=</a> <a id="2184" href="Data.Star.Pointer.html#lookup" class="Function">lookup</a> <a id="2191" href="Data.Star.Pointer.html#2163" class="Bound">ps</a> <a id="2194" href="Data.Star.Pointer.html#2178" class="Bound">rs</a>
<a id="2197" href="Data.Star.Pointer.html#lookup" class="Function">lookup</a> <a id="2204" class="Symbol">(</a><a id="2205" href="Data.Star.Pointer.html#Pointer.done" class="InductiveConstructor">done</a> <a id="2210" class="Symbol">_</a> <a id="2212" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2214" class="Symbol">()</a> <a id="2217" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2219" class="Symbol">_)</a> <a id="2222" class="Symbol">_</a>

<a id="2225" class="Comment">-- We can define something resembling init.</a>

<a id="prefixIndex" href="Data.Star.Pointer.html#prefixIndex" class="Function">prefixIndex</a> <a id="2282" class="Symbol">:</a> <a id="2284" class="Symbol">∀</a> <a id="2286" class="Symbol">{</a><a id="2287" href="Data.Star.Pointer.html#2287" class="Bound">I</a><a id="2288" class="Symbol">}</a> <a id="2290" class="Symbol">{</a><a id="2291" href="Data.Star.Pointer.html#2291" class="Bound">T</a> <a id="2293" class="Symbol">:</a> <a id="2295" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="2299" href="Data.Star.Pointer.html#2287" class="Bound">I</a> <a id="2301" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="2305" class="Symbol">}</a> <a id="2307" class="Symbol">{</a><a id="2308" href="Data.Star.Pointer.html#2308" class="Bound">P</a> <a id="2310" href="Data.Star.Pointer.html#2310" class="Bound">Q</a> <a id="2312" class="Symbol">:</a> <a id="2314" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="2323" href="Data.Star.Pointer.html#2291" class="Bound">T</a><a id="2324" class="Symbol">}</a>
                    <a id="2346" class="Symbol">{</a><a id="2347" href="Data.Star.Pointer.html#2347" class="Bound">i</a> <a id="2349" href="Data.Star.Pointer.html#2349" class="Bound">j</a><a id="2350" class="Symbol">}</a> <a id="2352" class="Symbol">{</a><a id="2353" href="Data.Star.Pointer.html#2353" class="Bound">xs</a> <a id="2356" class="Symbol">:</a> <a id="2358" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="2363" href="Data.Star.Pointer.html#2291" class="Bound">T</a> <a id="2365" href="Data.Star.Pointer.html#2347" class="Bound">i</a> <a id="2367" href="Data.Star.Pointer.html#2349" class="Bound">j</a><a id="2368" class="Symbol">}</a> <a id="2370" class="Symbol">→</a>
              <a id="2386" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="2390" href="Data.Star.Pointer.html#2308" class="Bound">P</a> <a id="2392" href="Data.Star.Pointer.html#2310" class="Bound">Q</a> <a id="2394" href="Data.Star.Pointer.html#2353" class="Bound">xs</a> <a id="2397" class="Symbol">→</a> <a id="2399" href="Data.Star.Pointer.html#2287" class="Bound">I</a>
<a id="2401" href="Data.Star.Pointer.html#prefixIndex" class="Function">prefixIndex</a> <a id="2413" class="Symbol">(</a><a id="2414" href="Data.Star.Pointer.html#Pointer.done" class="InductiveConstructor">done</a> <a id="2419" class="Symbol">{</a><a id="2420" class="Argument">i</a> <a id="2422" class="Symbol">=</a> <a id="2424" href="Data.Star.Pointer.html#2424" class="Bound">i</a><a id="2425" class="Symbol">}</a> <a id="2427" href="Data.Star.Pointer.html#2427" class="Bound">q</a> <a id="2429" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2431" class="Symbol">_)</a>  <a id="2435" class="Symbol">=</a> <a id="2437" href="Data.Star.Pointer.html#2424" class="Bound">i</a>
<a id="2439" href="Data.Star.Pointer.html#prefixIndex" class="Function">prefixIndex</a> <a id="2451" class="Symbol">(</a><a id="2452" href="Data.Star.Pointer.html#Pointer.step" class="InductiveConstructor">step</a> <a id="2457" href="Data.Star.Pointer.html#2457" class="Bound">p</a>         <a id="2467" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2469" href="Data.Star.Pointer.html#2469" class="Bound">ps</a><a id="2471" class="Symbol">)</a> <a id="2473" class="Symbol">=</a> <a id="2475" href="Data.Star.Pointer.html#prefixIndex" class="Function">prefixIndex</a> <a id="2487" href="Data.Star.Pointer.html#2469" class="Bound">ps</a>

<a id="prefix" href="Data.Star.Pointer.html#prefix" class="Function">prefix</a> <a id="2498" class="Symbol">:</a> <a id="2500" class="Symbol">∀</a> <a id="2502" class="Symbol">{</a><a id="2503" href="Data.Star.Pointer.html#2503" class="Bound">I</a><a id="2504" class="Symbol">}</a> <a id="2506" class="Symbol">{</a><a id="2507" href="Data.Star.Pointer.html#2507" class="Bound">T</a> <a id="2509" class="Symbol">:</a> <a id="2511" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="2515" href="Data.Star.Pointer.html#2503" class="Bound">I</a> <a id="2517" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="2521" class="Symbol">}</a> <a id="2523" class="Symbol">{</a><a id="2524" href="Data.Star.Pointer.html#2524" class="Bound">P</a> <a id="2526" href="Data.Star.Pointer.html#2526" class="Bound">Q</a> <a id="2528" class="Symbol">:</a> <a id="2530" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="2539" href="Data.Star.Pointer.html#2507" class="Bound">T</a><a id="2540" class="Symbol">}</a> <a id="2542" class="Symbol">{</a><a id="2543" href="Data.Star.Pointer.html#2543" class="Bound">i</a> <a id="2545" href="Data.Star.Pointer.html#2545" class="Bound">j</a><a id="2546" class="Symbol">}</a> <a id="2548" class="Symbol">{</a><a id="2549" href="Data.Star.Pointer.html#2549" class="Bound">xs</a> <a id="2552" class="Symbol">:</a> <a id="2554" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="2559" href="Data.Star.Pointer.html#2507" class="Bound">T</a> <a id="2561" href="Data.Star.Pointer.html#2543" class="Bound">i</a> <a id="2563" href="Data.Star.Pointer.html#2545" class="Bound">j</a><a id="2564" class="Symbol">}</a> <a id="2566" class="Symbol">→</a>
         <a id="2577" class="Symbol">(</a><a id="2578" href="Data.Star.Pointer.html#2578" class="Bound">ps</a> <a id="2581" class="Symbol">:</a> <a id="2583" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="2587" href="Data.Star.Pointer.html#2524" class="Bound">P</a> <a id="2589" href="Data.Star.Pointer.html#2526" class="Bound">Q</a> <a id="2591" href="Data.Star.Pointer.html#2549" class="Bound">xs</a><a id="2593" class="Symbol">)</a> <a id="2595" class="Symbol">→</a> <a id="2597" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="2602" href="Data.Star.Pointer.html#2507" class="Bound">T</a> <a id="2604" href="Data.Star.Pointer.html#2543" class="Bound">i</a> <a id="2606" class="Symbol">(</a><a id="2607" href="Data.Star.Pointer.html#prefixIndex" class="Function">prefixIndex</a> <a id="2619" href="Data.Star.Pointer.html#2578" class="Bound">ps</a><a id="2621" class="Symbol">)</a>
<a id="2623" href="Data.Star.Pointer.html#prefix" class="Function">prefix</a> <a id="2630" class="Symbol">(</a><a id="2631" href="Data.Star.Pointer.html#Pointer.done" class="InductiveConstructor">done</a> <a id="2636" href="Data.Star.Pointer.html#2636" class="Bound">q</a>         <a id="2646" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2648" class="Symbol">_)</a>  <a id="2652" class="Symbol">=</a> <a id="2654" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star.%CE%B5" class="InductiveConstructor">ε</a>
<a id="2656" href="Data.Star.Pointer.html#prefix" class="Function">prefix</a> <a id="2663" class="Symbol">(</a><a id="2664" href="Data.Star.Pointer.html#Pointer.step" class="InductiveConstructor">step</a> <a id="2669" class="Symbol">{</a><a id="2670" class="Argument">x</a> <a id="2672" class="Symbol">=</a> <a id="2674" href="Data.Star.Pointer.html#2674" class="Bound">x</a><a id="2675" class="Symbol">}</a> <a id="2677" href="Data.Star.Pointer.html#2677" class="Bound">p</a> <a id="2679" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2681" href="Data.Star.Pointer.html#2681" class="Bound">ps</a><a id="2683" class="Symbol">)</a> <a id="2685" class="Symbol">=</a> <a id="2687" href="Data.Star.Pointer.html#2674" class="Bound">x</a> <a id="2689" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2691" href="Data.Star.Pointer.html#prefix" class="Function">prefix</a> <a id="2698" href="Data.Star.Pointer.html#2681" class="Bound">ps</a>

<a id="2702" class="Comment">-- Here we are taking the initial segment of ps (all elements but the</a>
<a id="2772" class="Comment">-- last, i.e. all edges satisfying P).</a>

<a id="init" href="Data.Star.Pointer.html#init" class="Function">init</a> <a id="2817" class="Symbol">:</a> <a id="2819" class="Symbol">∀</a> <a id="2821" class="Symbol">{</a><a id="2822" href="Data.Star.Pointer.html#2822" class="Bound">I</a><a id="2823" class="Symbol">}</a> <a id="2825" class="Symbol">{</a><a id="2826" href="Data.Star.Pointer.html#2826" class="Bound">T</a> <a id="2828" class="Symbol">:</a> <a id="2830" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="2834" href="Data.Star.Pointer.html#2822" class="Bound">I</a> <a id="2836" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="2840" class="Symbol">}</a> <a id="2842" class="Symbol">{</a><a id="2843" href="Data.Star.Pointer.html#2843" class="Bound">P</a> <a id="2845" href="Data.Star.Pointer.html#2845" class="Bound">Q</a> <a id="2847" class="Symbol">:</a> <a id="2849" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="2858" href="Data.Star.Pointer.html#2826" class="Bound">T</a><a id="2859" class="Symbol">}</a> <a id="2861" class="Symbol">{</a><a id="2862" href="Data.Star.Pointer.html#2862" class="Bound">i</a> <a id="2864" href="Data.Star.Pointer.html#2864" class="Bound">j</a><a id="2865" class="Symbol">}</a> <a id="2867" class="Symbol">{</a><a id="2868" href="Data.Star.Pointer.html#2868" class="Bound">xs</a> <a id="2871" class="Symbol">:</a> <a id="2873" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="2878" href="Data.Star.Pointer.html#2826" class="Bound">T</a> <a id="2880" href="Data.Star.Pointer.html#2862" class="Bound">i</a> <a id="2882" href="Data.Star.Pointer.html#2864" class="Bound">j</a><a id="2883" class="Symbol">}</a> <a id="2885" class="Symbol">→</a>
       <a id="2894" class="Symbol">(</a><a id="2895" href="Data.Star.Pointer.html#2895" class="Bound">ps</a> <a id="2898" class="Symbol">:</a> <a id="2900" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="2904" href="Data.Star.Pointer.html#2843" class="Bound">P</a> <a id="2906" href="Data.Star.Pointer.html#2845" class="Bound">Q</a> <a id="2908" href="Data.Star.Pointer.html#2868" class="Bound">xs</a><a id="2910" class="Symbol">)</a> <a id="2912" class="Symbol">→</a> <a id="2914" href="Data.Star.Decoration.html#All" class="Function">All</a> <a id="2918" href="Data.Star.Pointer.html#2843" class="Bound">P</a> <a id="2920" class="Symbol">(</a><a id="2921" href="Data.Star.Pointer.html#prefix" class="Function">prefix</a> <a id="2928" href="Data.Star.Pointer.html#2895" class="Bound">ps</a><a id="2930" class="Symbol">)</a>
<a id="2932" href="Data.Star.Pointer.html#init" class="Function">init</a> <a id="2937" class="Symbol">(</a><a id="2938" href="Data.Star.Pointer.html#Pointer.done" class="InductiveConstructor">done</a> <a id="2943" href="Data.Star.Pointer.html#2943" class="Bound">q</a> <a id="2945" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2947" class="Symbol">_)</a>  <a id="2951" class="Symbol">=</a> <a id="2953" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star.%CE%B5" class="InductiveConstructor">ε</a>
<a id="2955" href="Data.Star.Pointer.html#init" class="Function">init</a> <a id="2960" class="Symbol">(</a><a id="2961" href="Data.Star.Pointer.html#Pointer.step" class="InductiveConstructor">step</a> <a id="2966" href="Data.Star.Pointer.html#2966" class="Bound">p</a> <a id="2968" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2970" href="Data.Star.Pointer.html#2970" class="Bound">ps</a><a id="2972" class="Symbol">)</a> <a id="2974" class="Symbol">=</a> <a id="2976" href="Data.Star.Decoration.html#DecoratedWith.%E2%86%A6" class="InductiveConstructor">↦</a> <a id="2978" href="Data.Star.Pointer.html#2966" class="Bound">p</a> <a id="2980" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star._%E2%97%85_" class="InductiveConstructor Operator">◅</a> <a id="2982" href="Data.Star.Pointer.html#init" class="Function">init</a> <a id="2987" href="Data.Star.Pointer.html#2970" class="Bound">ps</a>

<a id="2991" class="Comment">-- One can simplify the implementation by not carrying around the</a>
<a id="3057" class="Comment">-- indices in the type:</a>

<a id="last" href="Data.Star.Pointer.html#last" class="Function">last</a> <a id="3087" class="Symbol">:</a> <a id="3089" class="Symbol">∀</a> <a id="3091" class="Symbol">{</a><a id="3092" href="Data.Star.Pointer.html#3092" class="Bound">I</a><a id="3093" class="Symbol">}</a> <a id="3095" class="Symbol">{</a><a id="3096" href="Data.Star.Pointer.html#3096" class="Bound">T</a> <a id="3098" class="Symbol">:</a> <a id="3100" href="Relation.Binary.Core.html#Rel" class="Function">Rel</a> <a id="3104" href="Data.Star.Pointer.html#3092" class="Bound">I</a> <a id="3106" href="Agda.Primitive.html#zero" class="Primitive">zero</a><a id="3110" class="Symbol">}</a> <a id="3112" class="Symbol">{</a><a id="3113" href="Data.Star.Pointer.html#3113" class="Bound">P</a> <a id="3115" href="Data.Star.Pointer.html#3115" class="Bound">Q</a> <a id="3117" class="Symbol">:</a> <a id="3119" href="Data.Star.Decoration.html#EdgePred" class="Function">EdgePred</a> <a id="3128" href="Data.Star.Pointer.html#3096" class="Bound">T</a><a id="3129" class="Symbol">}</a>
             <a id="3144" class="Symbol">{</a><a id="3145" href="Data.Star.Pointer.html#3145" class="Bound">i</a> <a id="3147" href="Data.Star.Pointer.html#3147" class="Bound">j</a><a id="3148" class="Symbol">}</a> <a id="3150" class="Symbol">{</a><a id="3151" href="Data.Star.Pointer.html#3151" class="Bound">xs</a> <a id="3154" class="Symbol">:</a> <a id="3156" href="Relation.Binary.Closure.ReflexiveTransitive.html#Star" class="Datatype">Star</a> <a id="3161" href="Data.Star.Pointer.html#3096" class="Bound">T</a> <a id="3163" href="Data.Star.Pointer.html#3145" class="Bound">i</a> <a id="3165" href="Data.Star.Pointer.html#3147" class="Bound">j</a><a id="3166" class="Symbol">}</a> <a id="3168" class="Symbol">→</a>
       <a id="3177" href="Data.Star.Pointer.html#Any" class="Function">Any</a> <a id="3181" href="Data.Star.Pointer.html#3113" class="Bound">P</a> <a id="3183" href="Data.Star.Pointer.html#3115" class="Bound">Q</a> <a id="3185" href="Data.Star.Pointer.html#3151" class="Bound">xs</a> <a id="3188" class="Symbol">→</a> <a id="3190" href="Data.Star.Decoration.html#NonEmptyEdgePred" class="Datatype">NonEmptyEdgePred</a> <a id="3207" href="Data.Star.Pointer.html#3096" class="Bound">T</a> <a id="3209" href="Data.Star.Pointer.html#3115" class="Bound">Q</a>
<a id="3211" href="Data.Star.Pointer.html#last" class="Function">last</a> <a id="3216" href="Data.Star.Pointer.html#3216" class="Bound">ps</a> <a id="3219" class="Keyword">with</a> <a id="3224" href="Data.Star.Pointer.html#lookup" class="Function">lookup</a> <a id="3231" href="Data.Star.Pointer.html#3216" class="Bound">ps</a> <a id="3234" class="Symbol">(</a><a id="3235" href="Data.Star.Decoration.html#decorate" class="Function">decorate</a> <a id="3244" class="Symbol">(</a><a id="3245" href="Function.html#const" class="Function">const</a> <a id="3251" href="Agda.Builtin.Unit.html#%E2%8A%A4.tt" class="InductiveConstructor">tt</a><a id="3253" class="Symbol">)</a> <a id="3255" class="Symbol">_)</a>
<a id="3258" class="Symbol">...</a> <a id="3262" class="Symbol">|</a> <a id="3264" href="Data.Star.Pointer.html#Result.result" class="InductiveConstructor">result</a> <a id="3271" href="Data.Star.Pointer.html#3271" class="Bound">q</a> <a id="3273" class="Symbol">_</a> <a id="3275" class="Symbol">=</a> <a id="3277" href="Data.Star.Decoration.html#NonEmptyEdgePred.nonEmptyEdgePred" class="InductiveConstructor">nonEmptyEdgePred</a> <a id="3294" href="Data.Star.Pointer.html#3271" class="Bound">q</a>
</pre></body></html>