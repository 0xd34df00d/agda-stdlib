<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Properties.DistributiveLattice</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Some derivable properties</a>
<a id="135" class="Comment">------------------------------------------------------------------------</a>

<a id="209" class="Symbol">{-#</a> <a id="213" class="Keyword">OPTIONS</a> <a id="221" class="Option">--without-K</a> <a id="233" class="Symbol">#-}</a>

<a id="238" class="Keyword">open</a> <a id="243" class="Keyword">import</a> <a id="250" href="Algebra.html" class="Module">Algebra</a>

<a id="259" class="Keyword">module</a> <a id="266" href="Algebra.Properties.DistributiveLattice.html" class="Module">Algebra.Properties.DistributiveLattice</a>
         <a id="314" class="Symbol">{</a><a id="315" href="Algebra.Properties.DistributiveLattice.html#315" class="Bound">dl₁</a> <a id="319" href="Algebra.Properties.DistributiveLattice.html#319" class="Bound">dl₂</a><a id="322" class="Symbol">}</a> <a id="324" class="Symbol">(</a><a id="325" href="Algebra.Properties.DistributiveLattice.html#325" class="Bound">DL</a> <a id="328" class="Symbol">:</a> <a id="330" href="Algebra.html#14973" class="Record">DistributiveLattice</a> <a id="350" href="Algebra.Properties.DistributiveLattice.html#315" class="Bound">dl₁</a> <a id="354" href="Algebra.Properties.DistributiveLattice.html#319" class="Bound">dl₂</a><a id="357" class="Symbol">)</a>
         <a id="368" class="Keyword">where</a>

<a id="375" class="Keyword">open</a> <a id="380" href="Algebra.html#14973" class="Module">DistributiveLattice</a> <a id="400" href="Algebra.Properties.DistributiveLattice.html#325" class="Bound">DL</a>
<a id="403" class="Keyword">import</a> <a id="410" href="Algebra.Properties.Lattice.html" class="Module">Algebra.Properties.Lattice</a>
<a id="437" class="Keyword">private</a>
  <a id="447" class="Keyword">open</a> <a id="452" class="Keyword">module</a> <a id="L"></a><a id="459" href="Algebra.Properties.DistributiveLattice.html#459" class="Module">L</a> <a id="461" class="Symbol">=</a> <a id="463" href="Algebra.Properties.Lattice.html" class="Module">Algebra.Properties.Lattice</a> <a id="490" href="Algebra.html#15356" class="Function">lattice</a> <a id="498" class="Keyword">public</a>
    <a id="509" class="Keyword">hiding</a> <a id="516" class="Symbol">(</a><a id="517" href="Algebra.Properties.Lattice.html#4209" class="Function">replace-equality</a><a id="533" class="Symbol">)</a>
<a id="535" class="Keyword">open</a> <a id="540" class="Keyword">import</a> <a id="547" href="Algebra.Structures.html" class="Module">Algebra.Structures</a>
<a id="566" class="Keyword">open</a> <a id="571" class="Keyword">import</a> <a id="578" href="Algebra.FunctionProperties.html" class="Module">Algebra.FunctionProperties</a> <a id="605" href="Algebra.html#15114" class="Field Operator">_≈_</a>
<a id="609" class="Keyword">open</a> <a id="614" class="Keyword">import</a> <a id="621" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="637" class="Keyword">open</a> <a id="642" class="Keyword">import</a> <a id="649" href="Relation.Binary.EqReasoning.html" class="Module">Relation.Binary.EqReasoning</a> <a id="677" href="Algebra.html#14893" class="Function">setoid</a>
<a id="684" class="Keyword">open</a> <a id="689" class="Keyword">import</a> <a id="696" href="Function.html" class="Module">Function</a>
<a id="705" class="Keyword">open</a> <a id="710" class="Keyword">import</a> <a id="717" href="Function.Equality.html" class="Module">Function.Equality</a> <a id="735" class="Keyword">using</a> <a id="741" class="Symbol">(</a><a id="742" href="Function.Equality.html#824" class="Field Operator">_⟨$⟩_</a><a id="747" class="Symbol">)</a>
<a id="749" class="Keyword">open</a> <a id="754" class="Keyword">import</a> <a id="761" href="Function.Equivalence.html" class="Module">Function.Equivalence</a> <a id="782" class="Keyword">using</a> <a id="788" class="Symbol">(</a><a id="789" href="Function.Equivalence.html#964" class="Function Operator">_⇔_</a><a id="792" class="Symbol">;</a> <a id="794" class="Keyword">module</a> <a id="801" href="Function.Equivalence.html#589" class="Module">Equivalence</a><a id="812" class="Symbol">)</a>
<a id="814" class="Keyword">open</a> <a id="819" class="Keyword">import</a> <a id="826" href="Data.Product.html" class="Module">Data.Product</a> <a id="839" class="Keyword">using</a> <a id="845" class="Symbol">(</a><a id="846" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">_,_</a><a id="849" class="Symbol">)</a>

<a id="∨-∧-distribˡ"></a><a id="852" href="Algebra.Properties.DistributiveLattice.html#852" class="Function">∨-∧-distribˡ</a> <a id="865" class="Symbol">:</a> <a id="867" href="Algebra.html#15156" class="Field Operator">_∨_</a> <a id="871" href="Algebra.FunctionProperties.html#1642" class="Function Operator">DistributesOverˡ</a> <a id="888" href="Algebra.html#15196" class="Field Operator">_∧_</a>
<a id="892" href="Algebra.Properties.DistributiveLattice.html#852" class="Function">∨-∧-distribˡ</a> <a id="905" href="Algebra.Properties.DistributiveLattice.html#905" class="Bound">x</a> <a id="907" href="Algebra.Properties.DistributiveLattice.html#907" class="Bound">y</a> <a id="909" href="Algebra.Properties.DistributiveLattice.html#909" class="Bound">z</a> <a id="911" class="Symbol">=</a> <a id="913" href="Relation.Binary.PreorderReasoning.html#1257" class="Function Operator">begin</a>
  <a id="921" href="Algebra.Properties.DistributiveLattice.html#905" class="Bound">x</a> <a id="923" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="925" href="Algebra.Properties.DistributiveLattice.html#907" class="Bound">y</a> <a id="927" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="929" href="Algebra.Properties.DistributiveLattice.html#909" class="Bound">z</a>          <a id="940" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="943" href="Algebra.Structures.html#11971" class="Function">∨-comm</a> <a id="950" class="Symbol">_</a> <a id="952" class="Symbol">_</a> <a id="954" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="958" href="Algebra.Properties.DistributiveLattice.html#907" class="Bound">y</a> <a id="960" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="962" href="Algebra.Properties.DistributiveLattice.html#909" class="Bound">z</a> <a id="964" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="966" href="Algebra.Properties.DistributiveLattice.html#905" class="Bound">x</a>          <a id="977" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="980" href="Algebra.Structures.html#12861" class="Function">∨-∧-distribʳ</a> <a id="993" class="Symbol">_</a> <a id="995" class="Symbol">_</a> <a id="997" class="Symbol">_</a> <a id="999" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1003" class="Symbol">(</a><a id="1004" href="Algebra.Properties.DistributiveLattice.html#907" class="Bound">y</a> <a id="1006" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1008" href="Algebra.Properties.DistributiveLattice.html#905" class="Bound">x</a><a id="1009" class="Symbol">)</a> <a id="1011" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1013" class="Symbol">(</a><a id="1014" href="Algebra.Properties.DistributiveLattice.html#909" class="Bound">z</a> <a id="1016" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1018" href="Algebra.Properties.DistributiveLattice.html#905" class="Bound">x</a><a id="1019" class="Symbol">)</a>  <a id="1022" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1025" href="Algebra.Structures.html#11971" class="Function">∨-comm</a> <a id="1032" class="Symbol">_</a> <a id="1034" class="Symbol">_</a> <a id="1036" href="Function.html#2541" class="Function Operator">⟨</a> <a id="1038" href="Algebra.Structures.html#12140" class="Function">∧-cong</a> <a id="1045" href="Function.html#2541" class="Function Operator">⟩</a> <a id="1047" href="Algebra.Structures.html#11971" class="Function">∨-comm</a> <a id="1054" class="Symbol">_</a> <a id="1056" class="Symbol">_</a> <a id="1058" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1062" class="Symbol">(</a><a id="1063" href="Algebra.Properties.DistributiveLattice.html#905" class="Bound">x</a> <a id="1065" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1067" href="Algebra.Properties.DistributiveLattice.html#907" class="Bound">y</a><a id="1068" class="Symbol">)</a> <a id="1070" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1072" class="Symbol">(</a><a id="1073" href="Algebra.Properties.DistributiveLattice.html#905" class="Bound">x</a> <a id="1075" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1077" href="Algebra.Properties.DistributiveLattice.html#909" class="Bound">z</a><a id="1078" class="Symbol">)</a>  <a id="1081" href="Relation.Binary.PreorderReasoning.html#1710" class="Function Operator">∎</a>

<a id="∨-∧-distrib"></a><a id="1084" href="Algebra.Properties.DistributiveLattice.html#1084" class="Function">∨-∧-distrib</a> <a id="1096" class="Symbol">:</a> <a id="1098" href="Algebra.html#15156" class="Field Operator">_∨_</a> <a id="1102" href="Algebra.FunctionProperties.html#1880" class="Function Operator">DistributesOver</a> <a id="1118" href="Algebra.html#15196" class="Field Operator">_∧_</a>
<a id="1122" href="Algebra.Properties.DistributiveLattice.html#1084" class="Function">∨-∧-distrib</a> <a id="1134" class="Symbol">=</a> <a id="1136" href="Algebra.Properties.DistributiveLattice.html#852" class="Function">∨-∧-distribˡ</a> <a id="1149" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1151" href="Algebra.Structures.html#12861" class="Function">∨-∧-distribʳ</a>

<a id="∧-∨-distribˡ"></a><a id="1165" href="Algebra.Properties.DistributiveLattice.html#1165" class="Function">∧-∨-distribˡ</a> <a id="1178" class="Symbol">:</a> <a id="1180" href="Algebra.html#15196" class="Field Operator">_∧_</a> <a id="1184" href="Algebra.FunctionProperties.html#1642" class="Function Operator">DistributesOverˡ</a> <a id="1201" href="Algebra.html#15156" class="Field Operator">_∨_</a>
<a id="1205" href="Algebra.Properties.DistributiveLattice.html#1165" class="Function">∧-∨-distribˡ</a> <a id="1218" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1220" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1222" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a> <a id="1224" class="Symbol">=</a> <a id="1226" href="Relation.Binary.PreorderReasoning.html#1257" class="Function Operator">begin</a>
  <a id="1234" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1236" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1238" class="Symbol">(</a><a id="1239" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1241" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1243" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a><a id="1244" class="Symbol">)</a>                <a id="1261" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1264" href="Relation.Binary.Core.html#5646" class="Function">sym</a> <a id="1268" class="Symbol">(</a><a id="1269" href="Algebra.Structures.html#12650" class="Function">∧-absorbs-∨</a> <a id="1281" class="Symbol">_</a> <a id="1283" class="Symbol">_)</a> <a id="1286" href="Function.html#2541" class="Function Operator">⟨</a> <a id="1288" href="Algebra.Structures.html#12140" class="Function">∧-cong</a> <a id="1295" href="Function.html#2541" class="Function Operator">⟩</a> <a id="1297" href="Relation.Binary.Core.html#5620" class="Function">refl</a> <a id="1302" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1306" class="Symbol">(</a><a id="1307" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1309" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1311" class="Symbol">(</a><a id="1312" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1314" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1316" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a><a id="1317" class="Symbol">))</a> <a id="1320" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1322" class="Symbol">(</a><a id="1323" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1325" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1327" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a><a id="1328" class="Symbol">)</a>    <a id="1333" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1336" class="Symbol">(</a><a id="1337" href="Relation.Binary.Core.html#5620" class="Function">refl</a> <a id="1342" href="Function.html#2541" class="Function Operator">⟨</a> <a id="1344" href="Algebra.Structures.html#12140" class="Function">∧-cong</a> <a id="1351" href="Function.html#2541" class="Function Operator">⟩</a> <a id="1353" href="Algebra.Structures.html#11971" class="Function">∨-comm</a> <a id="1360" class="Symbol">_</a> <a id="1362" class="Symbol">_)</a> <a id="1365" href="Function.html#2541" class="Function Operator">⟨</a> <a id="1367" href="Algebra.Structures.html#12140" class="Function">∧-cong</a> <a id="1374" href="Function.html#2541" class="Function Operator">⟩</a> <a id="1376" href="Relation.Binary.Core.html#5620" class="Function">refl</a> <a id="1381" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1385" class="Symbol">(</a><a id="1386" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1388" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1390" class="Symbol">(</a><a id="1391" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1393" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1395" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a><a id="1396" class="Symbol">))</a> <a id="1399" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1401" class="Symbol">(</a><a id="1402" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1404" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1406" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a><a id="1407" class="Symbol">)</a>    <a id="1412" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1415" href="Algebra.Structures.html#12106" class="Function">∧-assoc</a> <a id="1423" class="Symbol">_</a> <a id="1425" class="Symbol">_</a> <a id="1427" class="Symbol">_</a> <a id="1429" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1433" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1435" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1437" class="Symbol">((</a><a id="1439" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1441" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1443" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a><a id="1444" class="Symbol">)</a> <a id="1446" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1448" class="Symbol">(</a><a id="1449" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1451" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1453" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a><a id="1454" class="Symbol">))</a>    <a id="1460" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1463" href="Relation.Binary.Core.html#5620" class="Function">refl</a> <a id="1468" href="Function.html#2541" class="Function Operator">⟨</a> <a id="1470" href="Algebra.Structures.html#12140" class="Function">∧-cong</a> <a id="1477" href="Function.html#2541" class="Function Operator">⟩</a> <a id="1479" href="Relation.Binary.Core.html#5646" class="Function">sym</a> <a id="1483" class="Symbol">(</a><a id="1484" href="Algebra.Properties.DistributiveLattice.html#852" class="Function">∨-∧-distribˡ</a> <a id="1497" class="Symbol">_</a> <a id="1499" class="Symbol">_</a> <a id="1501" class="Symbol">_)</a> <a id="1504" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1508" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1510" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1512" class="Symbol">(</a><a id="1513" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1515" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1517" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1519" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1521" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a><a id="1522" class="Symbol">)</a>            <a id="1535" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1538" href="Relation.Binary.Core.html#5646" class="Function">sym</a> <a id="1542" class="Symbol">(</a><a id="1543" href="Algebra.Structures.html#12588" class="Function">∨-absorbs-∧</a> <a id="1555" class="Symbol">_</a> <a id="1557" class="Symbol">_)</a> <a id="1560" href="Function.html#2541" class="Function Operator">⟨</a> <a id="1562" href="Algebra.Structures.html#12140" class="Function">∧-cong</a> <a id="1569" href="Function.html#2541" class="Function Operator">⟩</a> <a id="1571" href="Relation.Binary.Core.html#5620" class="Function">refl</a> <a id="1576" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1580" class="Symbol">(</a><a id="1581" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1583" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1585" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1587" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1589" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a><a id="1590" class="Symbol">)</a> <a id="1592" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1594" class="Symbol">(</a><a id="1595" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1597" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1599" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1601" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1603" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a><a id="1604" class="Symbol">)</a>  <a id="1607" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1610" href="Relation.Binary.Core.html#5646" class="Function">sym</a> <a id="1614" href="Function.html#1920" class="Function Operator">$</a> <a id="1616" href="Algebra.Structures.html#12861" class="Function">∨-∧-distribʳ</a> <a id="1629" class="Symbol">_</a> <a id="1631" class="Symbol">_</a> <a id="1633" class="Symbol">_</a> <a id="1635" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1639" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1641" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1643" href="Algebra.Properties.DistributiveLattice.html#1220" class="Bound">y</a> <a id="1645" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1647" href="Algebra.Properties.DistributiveLattice.html#1218" class="Bound">x</a> <a id="1649" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1651" href="Algebra.Properties.DistributiveLattice.html#1222" class="Bound">z</a>              <a id="1666" href="Relation.Binary.PreorderReasoning.html#1710" class="Function Operator">∎</a>

<a id="∧-∨-distribʳ"></a><a id="1669" href="Algebra.Properties.DistributiveLattice.html#1669" class="Function">∧-∨-distribʳ</a> <a id="1682" class="Symbol">:</a> <a id="1684" href="Algebra.html#15196" class="Field Operator">_∧_</a> <a id="1688" href="Algebra.FunctionProperties.html#1761" class="Function Operator">DistributesOverʳ</a> <a id="1705" href="Algebra.html#15156" class="Field Operator">_∨_</a>
<a id="1709" href="Algebra.Properties.DistributiveLattice.html#1669" class="Function">∧-∨-distribʳ</a> <a id="1722" href="Algebra.Properties.DistributiveLattice.html#1722" class="Bound">x</a> <a id="1724" href="Algebra.Properties.DistributiveLattice.html#1724" class="Bound">y</a> <a id="1726" href="Algebra.Properties.DistributiveLattice.html#1726" class="Bound">z</a> <a id="1728" class="Symbol">=</a> <a id="1730" href="Relation.Binary.PreorderReasoning.html#1257" class="Function Operator">begin</a>
  <a id="1738" class="Symbol">(</a><a id="1739" href="Algebra.Properties.DistributiveLattice.html#1724" class="Bound">y</a> <a id="1741" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1743" href="Algebra.Properties.DistributiveLattice.html#1726" class="Bound">z</a><a id="1744" class="Symbol">)</a> <a id="1746" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1748" href="Algebra.Properties.DistributiveLattice.html#1722" class="Bound">x</a>    <a id="1753" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1756" href="Algebra.Structures.html#12072" class="Function">∧-comm</a> <a id="1763" class="Symbol">_</a> <a id="1765" class="Symbol">_</a> <a id="1767" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1771" href="Algebra.Properties.DistributiveLattice.html#1722" class="Bound">x</a> <a id="1773" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1775" class="Symbol">(</a><a id="1776" href="Algebra.Properties.DistributiveLattice.html#1724" class="Bound">y</a> <a id="1778" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1780" href="Algebra.Properties.DistributiveLattice.html#1726" class="Bound">z</a><a id="1781" class="Symbol">)</a>    <a id="1786" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1789" href="Algebra.Properties.DistributiveLattice.html#1165" class="Function">∧-∨-distribˡ</a> <a id="1802" class="Symbol">_</a> <a id="1804" class="Symbol">_</a> <a id="1806" class="Symbol">_</a> <a id="1808" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1812" href="Algebra.Properties.DistributiveLattice.html#1722" class="Bound">x</a> <a id="1814" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1816" href="Algebra.Properties.DistributiveLattice.html#1724" class="Bound">y</a> <a id="1818" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1820" href="Algebra.Properties.DistributiveLattice.html#1722" class="Bound">x</a> <a id="1822" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1824" href="Algebra.Properties.DistributiveLattice.html#1726" class="Bound">z</a>  <a id="1827" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1830" href="Algebra.Structures.html#12072" class="Function">∧-comm</a> <a id="1837" class="Symbol">_</a> <a id="1839" class="Symbol">_</a> <a id="1841" href="Function.html#2541" class="Function Operator">⟨</a> <a id="1843" href="Algebra.Structures.html#12039" class="Function">∨-cong</a> <a id="1850" href="Function.html#2541" class="Function Operator">⟩</a> <a id="1852" href="Algebra.Structures.html#12072" class="Function">∧-comm</a> <a id="1859" class="Symbol">_</a> <a id="1861" class="Symbol">_</a> <a id="1863" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1867" href="Algebra.Properties.DistributiveLattice.html#1724" class="Bound">y</a> <a id="1869" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1871" href="Algebra.Properties.DistributiveLattice.html#1722" class="Bound">x</a> <a id="1873" href="Algebra.html#15156" class="Field Operator">∨</a> <a id="1875" href="Algebra.Properties.DistributiveLattice.html#1726" class="Bound">z</a> <a id="1877" href="Algebra.html#15196" class="Field Operator">∧</a> <a id="1879" href="Algebra.Properties.DistributiveLattice.html#1722" class="Bound">x</a>  <a id="1882" href="Relation.Binary.PreorderReasoning.html#1710" class="Function Operator">∎</a>

<a id="∧-∨-distrib"></a><a id="1885" href="Algebra.Properties.DistributiveLattice.html#1885" class="Function">∧-∨-distrib</a> <a id="1897" class="Symbol">:</a> <a id="1899" href="Algebra.html#15196" class="Field Operator">_∧_</a> <a id="1903" href="Algebra.FunctionProperties.html#1880" class="Function Operator">DistributesOver</a> <a id="1919" href="Algebra.html#15156" class="Field Operator">_∨_</a>
<a id="1923" href="Algebra.Properties.DistributiveLattice.html#1885" class="Function">∧-∨-distrib</a> <a id="1935" class="Symbol">=</a> <a id="1937" href="Algebra.Properties.DistributiveLattice.html#1165" class="Function">∧-∨-distribˡ</a> <a id="1950" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1952" href="Algebra.Properties.DistributiveLattice.html#1669" class="Function">∧-∨-distribʳ</a>

<a id="1966" class="Comment">-- The dual construction is also a distributive lattice.</a>

<a id="∧-∨-isDistributiveLattice"></a><a id="2024" href="Algebra.Properties.DistributiveLattice.html#2024" class="Function">∧-∨-isDistributiveLattice</a> <a id="2050" class="Symbol">:</a> <a id="2052" href="Algebra.Structures.html#12760" class="Record">IsDistributiveLattice</a> <a id="2074" href="Algebra.html#15114" class="Field Operator">_≈_</a> <a id="2078" href="Algebra.html#15196" class="Field Operator">_∧_</a> <a id="2082" href="Algebra.html#15156" class="Field Operator">_∨_</a>
<a id="2086" href="Algebra.Properties.DistributiveLattice.html#2024" class="Function">∧-∨-isDistributiveLattice</a> <a id="2112" class="Symbol">=</a> <a id="2114" class="Keyword">record</a>
  <a id="2123" class="Symbol">{</a> <a id="2125" class="Field">isLattice</a>    <a id="2138" class="Symbol">=</a> <a id="2140" href="Algebra.Properties.Lattice.html#2229" class="Function">∧-∨-isLattice</a>
  <a id="2156" class="Symbol">;</a> <a id="2158" class="Field">∨-∧-distribʳ</a> <a id="2171" class="Symbol">=</a> <a id="2173" href="Algebra.Properties.DistributiveLattice.html#1669" class="Function">∧-∨-distribʳ</a>
  <a id="2188" class="Symbol">}</a>

<a id="∧-∨-distributiveLattice"></a><a id="2191" href="Algebra.Properties.DistributiveLattice.html#2191" class="Function">∧-∨-distributiveLattice</a> <a id="2215" class="Symbol">:</a> <a id="2217" href="Algebra.html#14973" class="Record">DistributiveLattice</a> <a id="2237" class="Symbol">_</a> <a id="2239" class="Symbol">_</a>
<a id="2241" href="Algebra.Properties.DistributiveLattice.html#2191" class="Function">∧-∨-distributiveLattice</a> <a id="2265" class="Symbol">=</a> <a id="2267" class="Keyword">record</a>
  <a id="2276" class="Symbol">{</a> <a id="2278" class="Field Operator">_∧_</a>                   <a id="2300" class="Symbol">=</a> <a id="2302" href="Algebra.html#15156" class="Field Operator">_∨_</a>
  <a id="2308" class="Symbol">;</a> <a id="2310" class="Field Operator">_∨_</a>                   <a id="2332" class="Symbol">=</a> <a id="2334" href="Algebra.html#15196" class="Field Operator">_∧_</a>
  <a id="2340" class="Symbol">;</a> <a id="2342" class="Field">isDistributiveLattice</a> <a id="2364" class="Symbol">=</a> <a id="2366" href="Algebra.Properties.DistributiveLattice.html#2024" class="Function">∧-∨-isDistributiveLattice</a>
  <a id="2394" class="Symbol">}</a>

<a id="2397" class="Comment">-- One can replace the underlying equality with an equivalent one.</a>

<a id="replace-equality"></a><a id="2465" href="Algebra.Properties.DistributiveLattice.html#2465" class="Function">replace-equality</a> <a id="2482" class="Symbol">:</a>
  <a id="2486" class="Symbol">{</a><a id="2487" href="Algebra.Properties.DistributiveLattice.html#2487" class="Bound Operator">_≈′_</a> <a id="2492" class="Symbol">:</a> <a id="2494" href="Relation.Binary.Core.html#969" class="Function">Rel</a> <a id="2498" href="Algebra.html#15080" class="Field">Carrier</a> <a id="2506" href="Algebra.Properties.DistributiveLattice.html#319" class="Bound">dl₂</a><a id="2509" class="Symbol">}</a> <a id="2511" class="Symbol">→</a>
  <a id="2515" class="Symbol">(∀</a> <a id="2518" class="Symbol">{</a><a id="2519" href="Algebra.Properties.DistributiveLattice.html#2519" class="Bound">x</a> <a id="2521" href="Algebra.Properties.DistributiveLattice.html#2521" class="Bound">y</a><a id="2522" class="Symbol">}</a> <a id="2524" class="Symbol">→</a> <a id="2526" href="Algebra.Properties.DistributiveLattice.html#2519" class="Bound">x</a> <a id="2528" href="Algebra.html#15114" class="Field Operator">≈</a> <a id="2530" href="Algebra.Properties.DistributiveLattice.html#2521" class="Bound">y</a> <a id="2532" href="Function.Equivalence.html#964" class="Function Operator">⇔</a> <a id="2534" class="Symbol">(</a><a id="2535" href="Algebra.Properties.DistributiveLattice.html#2519" class="Bound">x</a> <a id="2537" href="Algebra.Properties.DistributiveLattice.html#2487" class="Bound Operator">≈′</a> <a id="2540" href="Algebra.Properties.DistributiveLattice.html#2521" class="Bound">y</a><a id="2541" class="Symbol">))</a> <a id="2544" class="Symbol">→</a> <a id="2546" href="Algebra.html#14973" class="Record">DistributiveLattice</a> <a id="2566" class="Symbol">_</a> <a id="2568" class="Symbol">_</a>
<a id="2570" href="Algebra.Properties.DistributiveLattice.html#2465" class="Function">replace-equality</a> <a id="2587" class="Symbol">{</a><a id="2588" href="Algebra.Properties.DistributiveLattice.html#2588" class="Bound Operator">_≈′_</a><a id="2592" class="Symbol">}</a> <a id="2594" href="Algebra.Properties.DistributiveLattice.html#2594" class="Bound">≈⇔≈′</a> <a id="2599" class="Symbol">=</a> <a id="2601" class="Keyword">record</a>
  <a id="2610" class="Symbol">{</a> <a id="2612" class="Field Operator">_≈_</a>                   <a id="2634" class="Symbol">=</a> <a id="2636" href="Algebra.Properties.DistributiveLattice.html#2588" class="Bound Operator">_≈′_</a>
  <a id="2643" class="Symbol">;</a> <a id="2645" class="Field Operator">_∧_</a>                   <a id="2667" class="Symbol">=</a> <a id="2669" href="Algebra.html#15196" class="Field Operator">_∧_</a>
  <a id="2675" class="Symbol">;</a> <a id="2677" class="Field Operator">_∨_</a>                   <a id="2699" class="Symbol">=</a> <a id="2701" href="Algebra.html#15156" class="Field Operator">_∨_</a>
  <a id="2707" class="Symbol">;</a> <a id="2709" class="Field">isDistributiveLattice</a> <a id="2731" class="Symbol">=</a> <a id="2733" class="Keyword">record</a>
    <a id="2744" class="Symbol">{</a> <a id="2746" class="Field">isLattice</a>    <a id="2759" class="Symbol">=</a> <a id="2761" href="Algebra.html#14821" class="Field">Lattice.isLattice</a> <a id="2779" class="Symbol">(</a><a id="2780" href="Algebra.Properties.Lattice.html#4209" class="Function">L.replace-equality</a> <a id="2799" href="Algebra.Properties.DistributiveLattice.html#2594" class="Bound">≈⇔≈′</a><a id="2803" class="Symbol">)</a>
    <a id="2809" class="Symbol">;</a> <a id="2811" class="Field">∨-∧-distribʳ</a> <a id="2824" class="Symbol">=</a> <a id="2826" class="Symbol">λ</a> <a id="2828" href="Algebra.Properties.DistributiveLattice.html#2828" class="Bound">x</a> <a id="2830" href="Algebra.Properties.DistributiveLattice.html#2830" class="Bound">y</a> <a id="2832" href="Algebra.Properties.DistributiveLattice.html#2832" class="Bound">z</a> <a id="2834" class="Symbol">→</a> <a id="2836" href="Function.Equivalence.html#739" class="Function">to</a> <a id="2839" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="2843" href="Algebra.Structures.html#12861" class="Function">∨-∧-distribʳ</a> <a id="2856" href="Algebra.Properties.DistributiveLattice.html#2828" class="Bound">x</a> <a id="2858" href="Algebra.Properties.DistributiveLattice.html#2830" class="Bound">y</a> <a id="2860" href="Algebra.Properties.DistributiveLattice.html#2832" class="Bound">z</a>
    <a id="2866" class="Symbol">}</a>
  <a id="2870" class="Symbol">}</a> <a id="2872" class="Keyword">where</a> <a id="2878" class="Keyword">open</a> <a id="2883" class="Keyword">module</a> <a id="E"></a><a id="2890" href="Algebra.Properties.DistributiveLattice.html#2890" class="Module">E</a> <a id="2892" class="Symbol">{</a><a id="2893" href="Algebra.Properties.DistributiveLattice.html#2893" class="Bound">x</a> <a id="2895" href="Algebra.Properties.DistributiveLattice.html#2895" class="Bound">y</a><a id="2896" class="Symbol">}</a> <a id="2898" class="Symbol">=</a> <a id="2900" href="Function.Equivalence.html#589" class="Module">Equivalence</a> <a id="2912" class="Symbol">(</a><a id="2913" href="Algebra.Properties.DistributiveLattice.html#2594" class="Bound">≈⇔≈′</a> <a id="2918" class="Symbol">{</a><a id="2919" href="Algebra.Properties.DistributiveLattice.html#2893" class="Bound">x</a><a id="2920" class="Symbol">}</a> <a id="2922" class="Symbol">{</a><a id="2923" href="Algebra.Properties.DistributiveLattice.html#2895" class="Bound">y</a><a id="2924" class="Symbol">})</a>
</pre></body></html>