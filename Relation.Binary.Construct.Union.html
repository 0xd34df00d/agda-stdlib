<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Construct.Union</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Union of two binary relations</a>
<a id="139" class="Comment">------------------------------------------------------------------------</a>

<a id="213" class="Symbol">{-#</a> <a id="217" class="Keyword">OPTIONS</a> <a id="225" class="Pragma">--without-K</a> <a id="237" class="Pragma">--safe</a> <a id="244" class="Symbol">#-}</a>

<a id="249" class="Keyword">module</a> <a id="256" href="Relation.Binary.Construct.Union.html" class="Module">Relation.Binary.Construct.Union</a> <a id="288" class="Keyword">where</a>

<a id="295" class="Keyword">open</a> <a id="300" class="Keyword">import</a> <a id="307" href="Data.Product.html" class="Module">Data.Product</a>
<a id="320" class="Keyword">open</a> <a id="325" class="Keyword">import</a> <a id="332" href="Data.Sum.html" class="Module">Data.Sum</a> <a id="341" class="Symbol">as</a> <a id="344" class="Module">Sum</a>
<a id="348" class="Keyword">open</a> <a id="353" class="Keyword">import</a> <a id="360" href="Function.html" class="Module">Function</a> <a id="369" class="Keyword">using</a> <a id="375" class="Symbol">(</a><a id="376" href="Function.Base.html#992" class="Function Operator">_∘_</a><a id="379" class="Symbol">)</a>
<a id="381" class="Keyword">open</a> <a id="386" class="Keyword">import</a> <a id="393" href="Level.html" class="Module">Level</a> <a id="399" class="Keyword">using</a> <a id="405" class="Symbol">(</a><a id="406" href="Agda.Primitive.html#408" class="Postulate">Level</a><a id="411" class="Symbol">;</a> <a id="413" href="Agda.Primitive.html#657" class="Primitive Operator">_⊔_</a><a id="416" class="Symbol">)</a>
<a id="418" class="Keyword">open</a> <a id="423" class="Keyword">import</a> <a id="430" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="446" class="Keyword">open</a> <a id="451" class="Keyword">import</a> <a id="458" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="475" class="Keyword">using</a> <a id="481" class="Symbol">(</a><a id="482" href="Relation.Nullary.html#1645">yes</a><a id="485" class="Symbol">;</a> <a id="487" href="Relation.Nullary.html#1682">no</a><a id="489" class="Symbol">)</a>
<a id="491" class="Keyword">open</a> <a id="496" class="Keyword">import</a> <a id="503" href="Relation.Nullary.Sum.html" class="Module">Relation.Nullary.Sum</a> <a id="524" class="Keyword">using</a> <a id="530" class="Symbol">(</a><a id="531" href="Relation.Nullary.Sum.html#941" class="Function Operator">_⊎-dec_</a><a id="538" class="Symbol">)</a>

<a id="541" class="Keyword">private</a>
  <a id="551" class="Keyword">variable</a>
    <a id="564" href="Relation.Binary.Construct.Union.html#564" class="Generalizable">a</a> <a id="566" href="Relation.Binary.Construct.Union.html#566" class="Generalizable">b</a> <a id="568" href="Relation.Binary.Construct.Union.html#568" class="Generalizable">ℓ</a> <a id="570" href="Relation.Binary.Construct.Union.html#570" class="Generalizable">ℓ₁</a> <a id="573" href="Relation.Binary.Construct.Union.html#573" class="Generalizable">ℓ₂</a> <a id="576" href="Relation.Binary.Construct.Union.html#576" class="Generalizable">ℓ₃</a> <a id="579" class="Symbol">:</a> <a id="581" href="Agda.Primitive.html#408" class="Postulate">Level</a>
    <a id="591" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="593" class="Symbol">:</a> <a id="595" class="PrimitiveType">Set</a> <a id="599" href="Relation.Binary.Construct.Union.html#564" class="Generalizable">a</a>
    <a id="605" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="607" class="Symbol">:</a> <a id="609" class="PrimitiveType">Set</a> <a id="613" href="Relation.Binary.Construct.Union.html#566" class="Generalizable">b</a>

<a id="616" class="Comment">------------------------------------------------------------------------</a>
<a id="689" class="Comment">-- Definition</a>

<a id="_∪_"></a><a id="704" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">_∪_</a> <a id="708" class="Symbol">:</a> <a id="710" class="Symbol">∀</a> <a id="712" class="Symbol">{</a><a id="713" href="Relation.Binary.Construct.Union.html#713" class="Bound">A</a> <a id="715" class="Symbol">:</a> <a id="717" class="PrimitiveType">Set</a> <a id="721" href="Relation.Binary.Construct.Union.html#564" class="Generalizable">a</a><a id="722" class="Symbol">}</a> <a id="724" class="Symbol">{</a><a id="725" href="Relation.Binary.Construct.Union.html#725" class="Bound">B</a> <a id="727" class="Symbol">:</a> <a id="729" class="PrimitiveType">Set</a> <a id="733" href="Relation.Binary.Construct.Union.html#566" class="Generalizable">b</a><a id="734" class="Symbol">}</a> <a id="736" class="Symbol">→</a>
      <a id="744" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="748" href="Relation.Binary.Construct.Union.html#713" class="Bound">A</a> <a id="750" href="Relation.Binary.Construct.Union.html#725" class="Bound">B</a> <a id="752" href="Relation.Binary.Construct.Union.html#570" class="Generalizable">ℓ₁</a> <a id="755" class="Symbol">→</a> <a id="757" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="761" href="Relation.Binary.Construct.Union.html#713" class="Bound">A</a> <a id="763" href="Relation.Binary.Construct.Union.html#725" class="Bound">B</a> <a id="765" href="Relation.Binary.Construct.Union.html#573" class="Generalizable">ℓ₂</a> <a id="768" class="Symbol">→</a> <a id="770" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="774" href="Relation.Binary.Construct.Union.html#713" class="Bound">A</a> <a id="776" href="Relation.Binary.Construct.Union.html#725" class="Bound">B</a> <a id="778" class="Symbol">(</a><a id="779" href="Relation.Binary.Construct.Union.html#570" class="Generalizable">ℓ₁</a> <a id="782" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="784" href="Relation.Binary.Construct.Union.html#573" class="Generalizable">ℓ₂</a><a id="786" class="Symbol">)</a>
<a id="788" href="Relation.Binary.Construct.Union.html#788" class="Bound">L</a> <a id="790" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="792" href="Relation.Binary.Construct.Union.html#792" class="Bound">R</a> <a id="794" class="Symbol">=</a> <a id="796" class="Symbol">λ</a> <a id="798" href="Relation.Binary.Construct.Union.html#798" class="Bound">i</a> <a id="800" href="Relation.Binary.Construct.Union.html#800" class="Bound">j</a> <a id="802" class="Symbol">→</a> <a id="804" href="Relation.Binary.Construct.Union.html#788" class="Bound">L</a> <a id="806" href="Relation.Binary.Construct.Union.html#798" class="Bound">i</a> <a id="808" href="Relation.Binary.Construct.Union.html#800" class="Bound">j</a> <a id="810" href="Data.Sum.Base.html#728" class="Datatype Operator">⊎</a> <a id="812" href="Relation.Binary.Construct.Union.html#792" class="Bound">R</a> <a id="814" href="Relation.Binary.Construct.Union.html#798" class="Bound">i</a> <a id="816" href="Relation.Binary.Construct.Union.html#800" class="Bound">j</a>

<a id="819" class="Comment">------------------------------------------------------------------------</a>
<a id="892" class="Comment">-- Properties</a>

<a id="907" class="Keyword">module</a> <a id="914" href="Relation.Binary.Construct.Union.html#914" class="Module">_</a> <a id="916" class="Symbol">(</a><a id="917" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="919" class="Symbol">:</a> <a id="921" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="925" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="927" href="Relation.Binary.Construct.Union.html#568" class="Generalizable">ℓ</a><a id="928" class="Symbol">)</a> <a id="930" class="Symbol">(</a><a id="931" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a> <a id="933" class="Symbol">:</a> <a id="935" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="939" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="941" href="Relation.Binary.Construct.Union.html#568" class="Generalizable">ℓ</a><a id="942" class="Symbol">)</a> <a id="944" class="Keyword">where</a>

  <a id="953" href="Relation.Binary.Construct.Union.html#953" class="Function">reflexive</a> <a id="963" class="Symbol">:</a> <a id="965" href="Relation.Binary.Definitions.html#1339" class="Function">Reflexive</a> <a id="975" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="977" href="Data.Sum.Base.html#728" class="Datatype Operator">⊎</a> <a id="979" href="Relation.Binary.Definitions.html#1339" class="Function">Reflexive</a> <a id="989" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a> <a id="991" class="Symbol">→</a> <a id="993" href="Relation.Binary.Definitions.html#1339" class="Function">Reflexive</a> <a id="1003" class="Symbol">(</a><a id="1004" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="1006" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="1008" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a><a id="1009" class="Symbol">)</a>
  <a id="1013" href="Relation.Binary.Construct.Union.html#953" class="Function">reflexive</a> <a id="1023" class="Symbol">(</a><a id="1024" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1029" href="Relation.Binary.Construct.Union.html#1029" class="Bound">L-refl</a><a id="1035" class="Symbol">)</a> <a id="1037" class="Symbol">=</a> <a id="1039" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1044" href="Relation.Binary.Construct.Union.html#1029" class="Bound">L-refl</a>
  <a id="1053" href="Relation.Binary.Construct.Union.html#953" class="Function">reflexive</a> <a id="1063" class="Symbol">(</a><a id="1064" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1069" href="Relation.Binary.Construct.Union.html#1069" class="Bound">R-refl</a><a id="1075" class="Symbol">)</a> <a id="1077" class="Symbol">=</a> <a id="1079" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1084" href="Relation.Binary.Construct.Union.html#1069" class="Bound">R-refl</a>

  <a id="1094" href="Relation.Binary.Construct.Union.html#1094" class="Function">total</a> <a id="1100" class="Symbol">:</a> <a id="1102" href="Relation.Binary.Definitions.html#2584" class="Function">Total</a> <a id="1108" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="1110" href="Data.Sum.Base.html#728" class="Datatype Operator">⊎</a> <a id="1112" href="Relation.Binary.Definitions.html#2584" class="Function">Total</a> <a id="1118" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a> <a id="1120" class="Symbol">→</a> <a id="1122" href="Relation.Binary.Definitions.html#2584" class="Function">Total</a> <a id="1128" class="Symbol">(</a><a id="1129" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="1131" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="1133" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a><a id="1134" class="Symbol">)</a>
  <a id="1138" href="Relation.Binary.Construct.Union.html#1094" class="Function">total</a> <a id="1144" class="Symbol">(</a><a id="1145" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1150" href="Relation.Binary.Construct.Union.html#1150" class="Bound">L-total</a><a id="1157" class="Symbol">)</a> <a id="1159" href="Relation.Binary.Construct.Union.html#1159" class="Bound">x</a> <a id="1161" href="Relation.Binary.Construct.Union.html#1161" class="Bound">y</a> <a id="1163" class="Symbol">=</a> <a id="1165" href="Data.Sum.Base.html#914" class="Function Operator">[</a> <a id="1167" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1172" href="Function.Base.html#992" class="Function Operator">∘</a> <a id="1174" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1179" href="Data.Sum.Base.html#914" class="Function Operator">,</a> <a id="1181" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1186" href="Function.Base.html#992" class="Function Operator">∘</a> <a id="1188" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1193" href="Data.Sum.Base.html#914" class="Function Operator">]</a> <a id="1195" class="Symbol">(</a><a id="1196" href="Relation.Binary.Construct.Union.html#1150" class="Bound">L-total</a> <a id="1204" href="Relation.Binary.Construct.Union.html#1159" class="Bound">x</a> <a id="1206" href="Relation.Binary.Construct.Union.html#1161" class="Bound">y</a><a id="1207" class="Symbol">)</a>
  <a id="1211" href="Relation.Binary.Construct.Union.html#1094" class="Function">total</a> <a id="1217" class="Symbol">(</a><a id="1218" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1223" href="Relation.Binary.Construct.Union.html#1223" class="Bound">R-total</a><a id="1230" class="Symbol">)</a> <a id="1232" href="Relation.Binary.Construct.Union.html#1232" class="Bound">x</a> <a id="1234" href="Relation.Binary.Construct.Union.html#1234" class="Bound">y</a> <a id="1236" class="Symbol">=</a> <a id="1238" href="Data.Sum.Base.html#914" class="Function Operator">[</a> <a id="1240" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1245" href="Function.Base.html#992" class="Function Operator">∘</a> <a id="1247" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1252" href="Data.Sum.Base.html#914" class="Function Operator">,</a> <a id="1254" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1259" href="Function.Base.html#992" class="Function Operator">∘</a> <a id="1261" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1266" href="Data.Sum.Base.html#914" class="Function Operator">]</a> <a id="1268" class="Symbol">(</a><a id="1269" href="Relation.Binary.Construct.Union.html#1223" class="Bound">R-total</a> <a id="1277" href="Relation.Binary.Construct.Union.html#1232" class="Bound">x</a> <a id="1279" href="Relation.Binary.Construct.Union.html#1234" class="Bound">y</a><a id="1280" class="Symbol">)</a>

  <a id="1285" href="Relation.Binary.Construct.Union.html#1285" class="Function">min</a> <a id="1289" class="Symbol">:</a> <a id="1291" class="Symbol">∀</a> <a id="1293" class="Symbol">{</a><a id="1294" href="Relation.Binary.Construct.Union.html#1294" class="Bound">⊤</a><a id="1295" class="Symbol">}</a> <a id="1297" class="Symbol">→</a> <a id="1299" href="Relation.Binary.Definitions.html#3337" class="Function">Minimum</a> <a id="1307" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="1309" href="Relation.Binary.Construct.Union.html#1294" class="Bound">⊤</a> <a id="1311" href="Data.Sum.Base.html#728" class="Datatype Operator">⊎</a> <a id="1313" href="Relation.Binary.Definitions.html#3337" class="Function">Minimum</a> <a id="1321" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a> <a id="1323" href="Relation.Binary.Construct.Union.html#1294" class="Bound">⊤</a> <a id="1325" class="Symbol">→</a> <a id="1327" href="Relation.Binary.Definitions.html#3337" class="Function">Minimum</a> <a id="1335" class="Symbol">(</a><a id="1336" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="1338" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="1340" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a><a id="1341" class="Symbol">)</a> <a id="1343" href="Relation.Binary.Construct.Union.html#1294" class="Bound">⊤</a>
  <a id="1347" href="Relation.Binary.Construct.Union.html#1285" class="Function">min</a> <a id="1351" class="Symbol">=</a> <a id="1353" href="Data.Sum.Base.html#914" class="Function Operator">[</a> <a id="1355" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1360" href="Function.Base.html#992" class="Function Operator">∘_</a> <a id="1363" href="Data.Sum.Base.html#914" class="Function Operator">,</a> <a id="1365" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1370" href="Function.Base.html#992" class="Function Operator">∘_</a> <a id="1373" href="Data.Sum.Base.html#914" class="Function Operator">]</a>

  <a id="1378" href="Relation.Binary.Construct.Union.html#1378" class="Function">max</a> <a id="1382" class="Symbol">:</a> <a id="1384" class="Symbol">∀</a> <a id="1386" class="Symbol">{</a><a id="1387" href="Relation.Binary.Construct.Union.html#1387" class="Bound">⊥</a><a id="1388" class="Symbol">}</a> <a id="1390" class="Symbol">→</a> <a id="1392" href="Relation.Binary.Definitions.html#3188" class="Function">Maximum</a> <a id="1400" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="1402" href="Relation.Binary.Construct.Union.html#1387" class="Bound">⊥</a> <a id="1404" href="Data.Sum.Base.html#728" class="Datatype Operator">⊎</a> <a id="1406" href="Relation.Binary.Definitions.html#3188" class="Function">Maximum</a> <a id="1414" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a> <a id="1416" href="Relation.Binary.Construct.Union.html#1387" class="Bound">⊥</a> <a id="1418" class="Symbol">→</a> <a id="1420" href="Relation.Binary.Definitions.html#3188" class="Function">Maximum</a> <a id="1428" class="Symbol">(</a><a id="1429" href="Relation.Binary.Construct.Union.html#917" class="Bound">L</a> <a id="1431" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="1433" href="Relation.Binary.Construct.Union.html#931" class="Bound">R</a><a id="1434" class="Symbol">)</a> <a id="1436" href="Relation.Binary.Construct.Union.html#1387" class="Bound">⊥</a>
  <a id="1440" href="Relation.Binary.Construct.Union.html#1378" class="Function">max</a> <a id="1444" class="Symbol">=</a> <a id="1446" href="Data.Sum.Base.html#914" class="Function Operator">[</a> <a id="1448" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1453" href="Function.Base.html#992" class="Function Operator">∘_</a> <a id="1456" href="Data.Sum.Base.html#914" class="Function Operator">,</a> <a id="1458" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1463" href="Function.Base.html#992" class="Function Operator">∘_</a> <a id="1466" href="Data.Sum.Base.html#914" class="Function Operator">]</a>

<a id="1469" class="Keyword">module</a> <a id="1476" href="Relation.Binary.Construct.Union.html#1476" class="Module">_</a> <a id="1478" class="Symbol">{</a><a id="1479" href="Relation.Binary.Construct.Union.html#1479" class="Bound">L</a> <a id="1481" class="Symbol">:</a> <a id="1483" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="1487" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="1489" href="Relation.Binary.Construct.Union.html#568" class="Generalizable">ℓ</a><a id="1490" class="Symbol">}</a> <a id="1492" class="Symbol">{</a><a id="1493" href="Relation.Binary.Construct.Union.html#1493" class="Bound">R</a> <a id="1495" class="Symbol">:</a> <a id="1497" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="1501" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="1503" href="Relation.Binary.Construct.Union.html#568" class="Generalizable">ℓ</a><a id="1504" class="Symbol">}</a> <a id="1506" class="Keyword">where</a>

  <a id="1515" href="Relation.Binary.Construct.Union.html#1515" class="Function">symmetric</a> <a id="1525" class="Symbol">:</a> <a id="1527" href="Relation.Binary.Definitions.html#1498" class="Function">Symmetric</a> <a id="1537" href="Relation.Binary.Construct.Union.html#1479" class="Bound">L</a> <a id="1539" class="Symbol">→</a> <a id="1541" href="Relation.Binary.Definitions.html#1498" class="Function">Symmetric</a> <a id="1551" href="Relation.Binary.Construct.Union.html#1493" class="Bound">R</a> <a id="1553" class="Symbol">→</a> <a id="1555" href="Relation.Binary.Definitions.html#1498" class="Function">Symmetric</a> <a id="1565" class="Symbol">(</a><a id="1566" href="Relation.Binary.Construct.Union.html#1479" class="Bound">L</a> <a id="1568" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="1570" href="Relation.Binary.Construct.Union.html#1493" class="Bound">R</a><a id="1571" class="Symbol">)</a>
  <a id="1575" href="Relation.Binary.Construct.Union.html#1515" class="Function">symmetric</a> <a id="1585" href="Relation.Binary.Construct.Union.html#1585" class="Bound">L-sym</a> <a id="1591" href="Relation.Binary.Construct.Union.html#1591" class="Bound">R-sym</a> <a id="1597" class="Symbol">=</a> <a id="1599" href="Data.Sum.Base.html#914" class="Function Operator">[</a> <a id="1601" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1606" href="Function.Base.html#992" class="Function Operator">∘</a> <a id="1608" href="Relation.Binary.Construct.Union.html#1585" class="Bound">L-sym</a> <a id="1614" href="Data.Sum.Base.html#914" class="Function Operator">,</a> <a id="1616" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1621" href="Function.Base.html#992" class="Function Operator">∘</a> <a id="1623" href="Relation.Binary.Construct.Union.html#1591" class="Bound">R-sym</a> <a id="1629" href="Data.Sum.Base.html#914" class="Function Operator">]</a>

  <a id="1634" href="Relation.Binary.Construct.Union.html#1634" class="Function">respects</a> <a id="1643" class="Symbol">:</a> <a id="1645" class="Symbol">∀</a> <a id="1647" class="Symbol">{</a><a id="1648" href="Relation.Binary.Construct.Union.html#1648" class="Bound">p</a><a id="1649" class="Symbol">}</a> <a id="1651" class="Symbol">{</a><a id="1652" href="Relation.Binary.Construct.Union.html#1652" class="Bound">P</a> <a id="1654" class="Symbol">:</a> <a id="1656" href="Relation.Binary.Construct.Union.html#1487" class="Bound">A</a> <a id="1658" class="Symbol">→</a> <a id="1660" class="PrimitiveType">Set</a> <a id="1664" href="Relation.Binary.Construct.Union.html#1648" class="Bound">p</a><a id="1665" class="Symbol">}</a> <a id="1667" class="Symbol">→</a>
             <a id="1682" href="Relation.Binary.Construct.Union.html#1652" class="Bound">P</a> <a id="1684" href="Relation.Binary.Definitions.html#3593" class="Function Operator">Respects</a> <a id="1693" href="Relation.Binary.Construct.Union.html#1479" class="Bound">L</a> <a id="1695" class="Symbol">→</a> <a id="1697" href="Relation.Binary.Construct.Union.html#1652" class="Bound">P</a> <a id="1699" href="Relation.Binary.Definitions.html#3593" class="Function Operator">Respects</a> <a id="1708" href="Relation.Binary.Construct.Union.html#1493" class="Bound">R</a> <a id="1710" class="Symbol">→</a> <a id="1712" href="Relation.Binary.Construct.Union.html#1652" class="Bound">P</a> <a id="1714" href="Relation.Binary.Definitions.html#3593" class="Function Operator">Respects</a> <a id="1723" class="Symbol">(</a><a id="1724" href="Relation.Binary.Construct.Union.html#1479" class="Bound">L</a> <a id="1726" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="1728" href="Relation.Binary.Construct.Union.html#1493" class="Bound">R</a><a id="1729" class="Symbol">)</a>
  <a id="1733" href="Relation.Binary.Construct.Union.html#1634" class="Function">respects</a> <a id="1742" href="Relation.Binary.Construct.Union.html#1742" class="Bound">resp-L</a> <a id="1749" href="Relation.Binary.Construct.Union.html#1749" class="Bound">resp-R</a> <a id="1756" class="Symbol">=</a> <a id="1758" href="Data.Sum.Base.html#914" class="Function Operator">[</a> <a id="1760" href="Relation.Binary.Construct.Union.html#1742" class="Bound">resp-L</a> <a id="1767" href="Data.Sum.Base.html#914" class="Function Operator">,</a> <a id="1769" href="Relation.Binary.Construct.Union.html#1749" class="Bound">resp-R</a> <a id="1776" href="Data.Sum.Base.html#914" class="Function Operator">]</a>

<a id="1779" class="Keyword">module</a> <a id="1786" href="Relation.Binary.Construct.Union.html#1786" class="Module">_</a> <a id="1788" class="Symbol">{</a><a id="1789" href="Relation.Binary.Construct.Union.html#1789" class="Bound">≈</a> <a id="1791" class="Symbol">:</a> <a id="1793" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="1797" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="1799" href="Relation.Binary.Construct.Union.html#570" class="Generalizable">ℓ₁</a><a id="1801" class="Symbol">}</a> <a id="1803" class="Symbol">(</a><a id="1804" href="Relation.Binary.Construct.Union.html#1804" class="Bound">L</a> <a id="1806" class="Symbol">:</a> <a id="1808" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="1812" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="1814" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="1816" href="Relation.Binary.Construct.Union.html#573" class="Generalizable">ℓ₂</a><a id="1818" class="Symbol">)</a> <a id="1820" class="Symbol">(</a><a id="1821" href="Relation.Binary.Construct.Union.html#1821" class="Bound">R</a> <a id="1823" class="Symbol">:</a> <a id="1825" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="1829" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="1831" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="1833" href="Relation.Binary.Construct.Union.html#576" class="Generalizable">ℓ₃</a><a id="1835" class="Symbol">)</a> <a id="1837" class="Keyword">where</a>

  <a id="1846" href="Relation.Binary.Construct.Union.html#1846" class="Function">respˡ</a> <a id="1852" class="Symbol">:</a> <a id="1854" href="Relation.Binary.Construct.Union.html#1804" class="Bound">L</a> <a id="1856" href="Relation.Binary.Definitions.html#3914" class="Function Operator">Respectsˡ</a> <a id="1866" href="Relation.Binary.Construct.Union.html#1789" class="Bound">≈</a> <a id="1868" class="Symbol">→</a> <a id="1870" href="Relation.Binary.Construct.Union.html#1821" class="Bound">R</a> <a id="1872" href="Relation.Binary.Definitions.html#3914" class="Function Operator">Respectsˡ</a> <a id="1882" href="Relation.Binary.Construct.Union.html#1789" class="Bound">≈</a> <a id="1884" class="Symbol">→</a> <a id="1886" class="Symbol">(</a><a id="1887" href="Relation.Binary.Construct.Union.html#1804" class="Bound">L</a> <a id="1889" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="1891" href="Relation.Binary.Construct.Union.html#1821" class="Bound">R</a><a id="1892" class="Symbol">)</a> <a id="1894" href="Relation.Binary.Definitions.html#3914" class="Function Operator">Respectsˡ</a> <a id="1904" href="Relation.Binary.Construct.Union.html#1789" class="Bound">≈</a>
  <a id="1908" href="Relation.Binary.Construct.Union.html#1846" class="Function">respˡ</a> <a id="1914" href="Relation.Binary.Construct.Union.html#1914" class="Bound">Lˡ</a> <a id="1917" href="Relation.Binary.Construct.Union.html#1917" class="Bound">Rˡ</a> <a id="1920" href="Relation.Binary.Construct.Union.html#1920" class="Bound">x≈y</a> <a id="1924" class="Symbol">=</a> <a id="1926" href="Data.Sum.Base.html#1314" class="Function">Sum.map</a> <a id="1934" class="Symbol">(</a><a id="1935" href="Relation.Binary.Construct.Union.html#1914" class="Bound">Lˡ</a> <a id="1938" href="Relation.Binary.Construct.Union.html#1920" class="Bound">x≈y</a><a id="1941" class="Symbol">)</a> <a id="1943" class="Symbol">(</a><a id="1944" href="Relation.Binary.Construct.Union.html#1917" class="Bound">Rˡ</a> <a id="1947" href="Relation.Binary.Construct.Union.html#1920" class="Bound">x≈y</a><a id="1950" class="Symbol">)</a>

<a id="1953" class="Keyword">module</a> <a id="1960" href="Relation.Binary.Construct.Union.html#1960" class="Module">_</a> <a id="1962" class="Symbol">{</a><a id="1963" href="Relation.Binary.Construct.Union.html#1963" class="Bound">≈</a> <a id="1965" class="Symbol">:</a> <a id="1967" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="1971" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="1973" href="Relation.Binary.Construct.Union.html#570" class="Generalizable">ℓ₁</a><a id="1975" class="Symbol">}</a> <a id="1977" class="Symbol">(</a><a id="1978" href="Relation.Binary.Construct.Union.html#1978" class="Bound">L</a> <a id="1980" class="Symbol">:</a> <a id="1982" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="1986" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="1988" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="1990" href="Relation.Binary.Construct.Union.html#573" class="Generalizable">ℓ₂</a><a id="1992" class="Symbol">)</a> <a id="1994" class="Symbol">(</a><a id="1995" href="Relation.Binary.Construct.Union.html#1995" class="Bound">R</a> <a id="1997" class="Symbol">:</a> <a id="1999" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="2003" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2005" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="2007" href="Relation.Binary.Construct.Union.html#576" class="Generalizable">ℓ₃</a><a id="2009" class="Symbol">)</a> <a id="2011" class="Keyword">where</a>

  <a id="2020" href="Relation.Binary.Construct.Union.html#2020" class="Function">respʳ</a> <a id="2026" class="Symbol">:</a> <a id="2028" href="Relation.Binary.Construct.Union.html#1978" class="Bound">L</a> <a id="2030" href="Relation.Binary.Definitions.html#3749" class="Function Operator">Respectsʳ</a> <a id="2040" href="Relation.Binary.Construct.Union.html#1963" class="Bound">≈</a> <a id="2042" class="Symbol">→</a> <a id="2044" href="Relation.Binary.Construct.Union.html#1995" class="Bound">R</a> <a id="2046" href="Relation.Binary.Definitions.html#3749" class="Function Operator">Respectsʳ</a> <a id="2056" href="Relation.Binary.Construct.Union.html#1963" class="Bound">≈</a> <a id="2058" class="Symbol">→</a> <a id="2060" class="Symbol">(</a><a id="2061" href="Relation.Binary.Construct.Union.html#1978" class="Bound">L</a> <a id="2063" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="2065" href="Relation.Binary.Construct.Union.html#1995" class="Bound">R</a><a id="2066" class="Symbol">)</a> <a id="2068" href="Relation.Binary.Definitions.html#3749" class="Function Operator">Respectsʳ</a> <a id="2078" href="Relation.Binary.Construct.Union.html#1963" class="Bound">≈</a>
  <a id="2082" href="Relation.Binary.Construct.Union.html#2020" class="Function">respʳ</a> <a id="2088" href="Relation.Binary.Construct.Union.html#2088" class="Bound">Lʳ</a> <a id="2091" href="Relation.Binary.Construct.Union.html#2091" class="Bound">Rʳ</a> <a id="2094" href="Relation.Binary.Construct.Union.html#2094" class="Bound">x≈y</a> <a id="2098" class="Symbol">=</a> <a id="2100" href="Data.Sum.Base.html#1314" class="Function">Sum.map</a> <a id="2108" class="Symbol">(</a><a id="2109" href="Relation.Binary.Construct.Union.html#2088" class="Bound">Lʳ</a> <a id="2112" href="Relation.Binary.Construct.Union.html#2094" class="Bound">x≈y</a><a id="2115" class="Symbol">)</a> <a id="2117" class="Symbol">(</a><a id="2118" href="Relation.Binary.Construct.Union.html#2091" class="Bound">Rʳ</a> <a id="2121" href="Relation.Binary.Construct.Union.html#2094" class="Bound">x≈y</a><a id="2124" class="Symbol">)</a>

<a id="2127" class="Keyword">module</a> <a id="2134" href="Relation.Binary.Construct.Union.html#2134" class="Module">_</a> <a id="2136" class="Symbol">{</a><a id="2137" href="Relation.Binary.Construct.Union.html#2137" class="Bound">≈</a> <a id="2139" class="Symbol">:</a> <a id="2141" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="2145" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2147" href="Relation.Binary.Construct.Union.html#570" class="Generalizable">ℓ₁</a><a id="2149" class="Symbol">}</a> <a id="2151" class="Symbol">{</a><a id="2152" href="Relation.Binary.Construct.Union.html#2152" class="Bound">L</a> <a id="2154" class="Symbol">:</a> <a id="2156" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="2160" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2162" href="Relation.Binary.Construct.Union.html#573" class="Generalizable">ℓ₂</a><a id="2164" class="Symbol">}</a> <a id="2166" class="Symbol">{</a><a id="2167" href="Relation.Binary.Construct.Union.html#2167" class="Bound">R</a> <a id="2169" class="Symbol">:</a> <a id="2171" href="Relation.Binary.Core.html#845" class="Function">Rel</a> <a id="2175" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2177" href="Relation.Binary.Construct.Union.html#576" class="Generalizable">ℓ₃</a><a id="2179" class="Symbol">}</a> <a id="2181" class="Keyword">where</a>

  <a id="2190" href="Relation.Binary.Construct.Union.html#2190" class="Function">resp₂</a> <a id="2196" class="Symbol">:</a> <a id="2198" href="Relation.Binary.Construct.Union.html#2152" class="Bound">L</a> <a id="2200" href="Relation.Binary.Definitions.html#4077" class="Function Operator">Respects₂</a> <a id="2210" href="Relation.Binary.Construct.Union.html#2137" class="Bound">≈</a> <a id="2212" class="Symbol">→</a> <a id="2214" href="Relation.Binary.Construct.Union.html#2167" class="Bound">R</a> <a id="2216" href="Relation.Binary.Definitions.html#4077" class="Function Operator">Respects₂</a> <a id="2226" href="Relation.Binary.Construct.Union.html#2137" class="Bound">≈</a> <a id="2228" class="Symbol">→</a> <a id="2230" class="Symbol">(</a><a id="2231" href="Relation.Binary.Construct.Union.html#2152" class="Bound">L</a> <a id="2233" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="2235" href="Relation.Binary.Construct.Union.html#2167" class="Bound">R</a><a id="2236" class="Symbol">)</a> <a id="2238" href="Relation.Binary.Definitions.html#4077" class="Function Operator">Respects₂</a> <a id="2248" href="Relation.Binary.Construct.Union.html#2137" class="Bound">≈</a>
  <a id="2252" href="Relation.Binary.Construct.Union.html#2190" class="Function">resp₂</a> <a id="2258" class="Symbol">(</a><a id="2259" href="Relation.Binary.Construct.Union.html#2259" class="Bound">Lʳ</a> <a id="2262" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2264" href="Relation.Binary.Construct.Union.html#2264" class="Bound">Lˡ</a><a id="2266" class="Symbol">)</a> <a id="2268" class="Symbol">(</a><a id="2269" href="Relation.Binary.Construct.Union.html#2269" class="Bound">Rʳ</a> <a id="2272" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2274" href="Relation.Binary.Construct.Union.html#2274" class="Bound">Rˡ</a><a id="2276" class="Symbol">)</a> <a id="2278" class="Symbol">=</a> <a id="2280" href="Relation.Binary.Construct.Union.html#2020" class="Function">respʳ</a> <a id="2286" href="Relation.Binary.Construct.Union.html#2152" class="Bound">L</a> <a id="2288" href="Relation.Binary.Construct.Union.html#2167" class="Bound">R</a> <a id="2290" href="Relation.Binary.Construct.Union.html#2259" class="Bound">Lʳ</a> <a id="2293" href="Relation.Binary.Construct.Union.html#2269" class="Bound">Rʳ</a> <a id="2296" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2298" href="Relation.Binary.Construct.Union.html#1846" class="Function">respˡ</a> <a id="2304" href="Relation.Binary.Construct.Union.html#2152" class="Bound">L</a> <a id="2306" href="Relation.Binary.Construct.Union.html#2167" class="Bound">R</a> <a id="2308" href="Relation.Binary.Construct.Union.html#2264" class="Bound">Lˡ</a> <a id="2311" href="Relation.Binary.Construct.Union.html#2274" class="Bound">Rˡ</a>

<a id="2315" class="Keyword">module</a> <a id="2322" href="Relation.Binary.Construct.Union.html#2322" class="Module">_</a> <a id="2324" class="Symbol">(</a><a id="2325" href="Relation.Binary.Construct.Union.html#2325" class="Bound">≈</a> <a id="2327" class="Symbol">:</a> <a id="2329" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="2333" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2335" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="2337" href="Relation.Binary.Construct.Union.html#570" class="Generalizable">ℓ₁</a><a id="2339" class="Symbol">)</a> <a id="2341" class="Symbol">(</a><a id="2342" href="Relation.Binary.Construct.Union.html#2342" class="Bound">L</a> <a id="2344" class="Symbol">:</a> <a id="2346" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="2350" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2352" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="2354" href="Relation.Binary.Construct.Union.html#573" class="Generalizable">ℓ₂</a><a id="2356" class="Symbol">)</a> <a id="2358" class="Symbol">(</a><a id="2359" href="Relation.Binary.Construct.Union.html#2359" class="Bound">R</a> <a id="2361" class="Symbol">:</a> <a id="2363" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="2367" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2369" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="2371" href="Relation.Binary.Construct.Union.html#576" class="Generalizable">ℓ₃</a><a id="2373" class="Symbol">)</a> <a id="2375" class="Keyword">where</a>

  <a id="2384" href="Relation.Binary.Construct.Union.html#2384" class="Function">implies</a> <a id="2392" class="Symbol">:</a> <a id="2394" class="Symbol">(</a><a id="2395" href="Relation.Binary.Construct.Union.html#2325" class="Bound">≈</a> <a id="2397" href="Relation.Binary.Core.html#1171" class="Function Operator">⇒</a> <a id="2399" href="Relation.Binary.Construct.Union.html#2342" class="Bound">L</a><a id="2400" class="Symbol">)</a> <a id="2402" href="Data.Sum.Base.html#728" class="Datatype Operator">⊎</a> <a id="2404" class="Symbol">(</a><a id="2405" href="Relation.Binary.Construct.Union.html#2325" class="Bound">≈</a> <a id="2407" href="Relation.Binary.Core.html#1171" class="Function Operator">⇒</a> <a id="2409" href="Relation.Binary.Construct.Union.html#2359" class="Bound">R</a><a id="2410" class="Symbol">)</a> <a id="2412" class="Symbol">→</a> <a id="2414" href="Relation.Binary.Construct.Union.html#2325" class="Bound">≈</a> <a id="2416" href="Relation.Binary.Core.html#1171" class="Function Operator">⇒</a> <a id="2418" class="Symbol">(</a><a id="2419" href="Relation.Binary.Construct.Union.html#2342" class="Bound">L</a> <a id="2421" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="2423" href="Relation.Binary.Construct.Union.html#2359" class="Bound">R</a><a id="2424" class="Symbol">)</a>
  <a id="2428" href="Relation.Binary.Construct.Union.html#2384" class="Function">implies</a> <a id="2436" class="Symbol">=</a> <a id="2438" href="Data.Sum.Base.html#914" class="Function Operator">[</a> <a id="2440" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="2445" href="Function.Base.html#992" class="Function Operator">∘_</a> <a id="2448" href="Data.Sum.Base.html#914" class="Function Operator">,</a> <a id="2450" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="2455" href="Function.Base.html#992" class="Function Operator">∘_</a> <a id="2458" href="Data.Sum.Base.html#914" class="Function Operator">]</a>

  <a id="2463" href="Relation.Binary.Construct.Union.html#2463" class="Function">irreflexive</a> <a id="2475" class="Symbol">:</a> <a id="2477" href="Relation.Binary.Definitions.html#2241" class="Function">Irreflexive</a> <a id="2489" href="Relation.Binary.Construct.Union.html#2325" class="Bound">≈</a> <a id="2491" href="Relation.Binary.Construct.Union.html#2342" class="Bound">L</a> <a id="2493" class="Symbol">→</a> <a id="2495" href="Relation.Binary.Definitions.html#2241" class="Function">Irreflexive</a> <a id="2507" href="Relation.Binary.Construct.Union.html#2325" class="Bound">≈</a> <a id="2509" href="Relation.Binary.Construct.Union.html#2359" class="Bound">R</a> <a id="2511" class="Symbol">→</a> <a id="2513" href="Relation.Binary.Definitions.html#2241" class="Function">Irreflexive</a> <a id="2525" href="Relation.Binary.Construct.Union.html#2325" class="Bound">≈</a> <a id="2527" class="Symbol">(</a><a id="2528" href="Relation.Binary.Construct.Union.html#2342" class="Bound">L</a> <a id="2530" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="2532" href="Relation.Binary.Construct.Union.html#2359" class="Bound">R</a><a id="2533" class="Symbol">)</a>
  <a id="2537" href="Relation.Binary.Construct.Union.html#2463" class="Function">irreflexive</a> <a id="2549" href="Relation.Binary.Construct.Union.html#2549" class="Bound">L-irrefl</a> <a id="2558" href="Relation.Binary.Construct.Union.html#2558" class="Bound">R-irrefl</a> <a id="2567" href="Relation.Binary.Construct.Union.html#2567" class="Bound">x≈y</a> <a id="2571" class="Symbol">=</a> <a id="2573" href="Data.Sum.Base.html#914" class="Function Operator">[</a> <a id="2575" href="Relation.Binary.Construct.Union.html#2549" class="Bound">L-irrefl</a> <a id="2584" href="Relation.Binary.Construct.Union.html#2567" class="Bound">x≈y</a> <a id="2588" href="Data.Sum.Base.html#914" class="Function Operator">,</a> <a id="2590" href="Relation.Binary.Construct.Union.html#2558" class="Bound">R-irrefl</a> <a id="2599" href="Relation.Binary.Construct.Union.html#2567" class="Bound">x≈y</a> <a id="2603" href="Data.Sum.Base.html#914" class="Function Operator">]</a>

<a id="2606" class="Keyword">module</a> <a id="2613" href="Relation.Binary.Construct.Union.html#2613" class="Module">_</a> <a id="2615" class="Symbol">{</a><a id="2616" href="Relation.Binary.Construct.Union.html#2616" class="Bound">L</a> <a id="2618" class="Symbol">:</a> <a id="2620" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="2624" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2626" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="2628" href="Relation.Binary.Construct.Union.html#570" class="Generalizable">ℓ₁</a><a id="2630" class="Symbol">}</a> <a id="2632" class="Symbol">{</a><a id="2633" href="Relation.Binary.Construct.Union.html#2633" class="Bound">R</a> <a id="2635" class="Symbol">:</a> <a id="2637" href="Relation.Binary.Core.html#729" class="Function">REL</a> <a id="2641" href="Relation.Binary.Construct.Union.html#591" class="Generalizable">A</a> <a id="2643" href="Relation.Binary.Construct.Union.html#605" class="Generalizable">B</a> <a id="2645" href="Relation.Binary.Construct.Union.html#573" class="Generalizable">ℓ₂</a><a id="2647" class="Symbol">}</a> <a id="2649" class="Keyword">where</a>

  <a id="2658" href="Relation.Binary.Construct.Union.html#2658" class="Function">decidable</a> <a id="2668" class="Symbol">:</a> <a id="2670" href="Relation.Binary.Definitions.html#4575" class="Function">Decidable</a> <a id="2680" href="Relation.Binary.Construct.Union.html#2616" class="Bound">L</a> <a id="2682" class="Symbol">→</a> <a id="2684" href="Relation.Binary.Definitions.html#4575" class="Function">Decidable</a> <a id="2694" href="Relation.Binary.Construct.Union.html#2633" class="Bound">R</a> <a id="2696" class="Symbol">→</a> <a id="2698" href="Relation.Binary.Definitions.html#4575" class="Function">Decidable</a> <a id="2708" class="Symbol">(</a><a id="2709" href="Relation.Binary.Construct.Union.html#2616" class="Bound">L</a> <a id="2711" href="Relation.Binary.Construct.Union.html#704" class="Function Operator">∪</a> <a id="2713" href="Relation.Binary.Construct.Union.html#2633" class="Bound">R</a><a id="2714" class="Symbol">)</a>
  <a id="2718" href="Relation.Binary.Construct.Union.html#2658" class="Function">decidable</a> <a id="2728" href="Relation.Binary.Construct.Union.html#2728" class="Bound">L?</a> <a id="2731" href="Relation.Binary.Construct.Union.html#2731" class="Bound">R?</a> <a id="2734" href="Relation.Binary.Construct.Union.html#2734" class="Bound">x</a> <a id="2736" href="Relation.Binary.Construct.Union.html#2736" class="Bound">y</a> <a id="2738" class="Symbol">=</a> <a id="2740" href="Relation.Binary.Construct.Union.html#2728" class="Bound">L?</a> <a id="2743" href="Relation.Binary.Construct.Union.html#2734" class="Bound">x</a> <a id="2745" href="Relation.Binary.Construct.Union.html#2736" class="Bound">y</a> <a id="2747" href="Relation.Nullary.Sum.html#941" class="Function Operator">⊎-dec</a> <a id="2753" href="Relation.Binary.Construct.Union.html#2731" class="Bound">R?</a> <a id="2756" href="Relation.Binary.Construct.Union.html#2734" class="Bound">x</a> <a id="2758" href="Relation.Binary.Construct.Union.html#2736" class="Bound">y</a>
</pre></body></html>