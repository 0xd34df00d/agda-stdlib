<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Sum.Categorical</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Universe-sensitive functor and monad instances for the Sum type.</a>
<a id="174" class="Comment">------------------------------------------------------------------------</a>

<a id="248" class="Keyword">module</a> <a id="255" href="Data.Sum.Categorical.html" class="Module">Data.Sum.Categorical</a> <a id="276" class="Keyword">where</a>

<a id="283" class="Keyword">open</a> <a id="288" class="Keyword">import</a> <a id="295" href="Level.html" class="Module">Level</a>
<a id="301" class="Keyword">open</a> <a id="306" class="Keyword">import</a> <a id="313" href="Data.Sum.html" class="Module">Data.Sum</a>
<a id="322" class="Keyword">open</a> <a id="327" class="Keyword">import</a> <a id="334" href="Category.Functor.html" class="Module">Category.Functor</a>
<a id="351" class="Keyword">open</a> <a id="356" class="Keyword">import</a> <a id="363" href="Category.Applicative.html" class="Module">Category.Applicative</a>
<a id="384" class="Keyword">open</a> <a id="389" class="Keyword">import</a> <a id="396" href="Category.Monad.html" class="Module">Category.Monad</a>
<a id="411" class="Keyword">import</a> <a id="418" href="Function.Identity.Categorical.html" class="Module">Function.Identity.Categorical</a> <a id="448" class="Symbol">as</a> <a id="451" class="Module">Id</a>
<a id="454" class="Keyword">open</a> <a id="459" class="Keyword">import</a> <a id="466" href="Function.html" class="Module">Function</a>

<a id="476" class="Comment">-- To minimize the universe level of the RawFunctor, we require that elements of</a>
<a id="557" class="Comment">-- B are &quot;lifted&quot; to a copy of B at a higher universe level (a ⊔ b). See the</a>
<a id="634" class="Comment">-- examples for how this is done.</a>

<a id="669" class="Comment">------------------------------------------------------------------------</a>
<a id="742" class="Comment">-- Left-biased monad instance for _⊎_</a>

<a id="781" class="Keyword">module</a> <a id="Sumₗ"></a><a id="788" href="Data.Sum.Categorical.html#788" class="Module">Sumₗ</a> <a id="793" class="Symbol">{</a><a id="794" href="Data.Sum.Categorical.html#794" class="Bound">a</a><a id="795" class="Symbol">}</a> <a id="797" class="Symbol">(</a><a id="798" href="Data.Sum.Categorical.html#798" class="Bound">A</a> <a id="800" class="Symbol">:</a> <a id="802" class="PrimitiveType">Set</a> <a id="806" href="Data.Sum.Categorical.html#794" class="Bound">a</a><a id="807" class="Symbol">)</a> <a id="809" class="Symbol">(</a><a id="810" href="Data.Sum.Categorical.html#810" class="Bound">b</a> <a id="812" class="Symbol">:</a> <a id="814" href="Agda.Primitive.html#408" class="Postulate">Level</a><a id="819" class="Symbol">)</a> <a id="821" class="Keyword">where</a>

  <a id="Sumₗ.Sumₗ"></a><a id="830" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a> <a id="835" class="Symbol">:</a> <a id="837" class="PrimitiveType">Set</a> <a id="841" class="Symbol">(</a><a id="842" href="Data.Sum.Categorical.html#794" class="Bound">a</a> <a id="844" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="846" href="Data.Sum.Categorical.html#810" class="Bound">b</a><a id="847" class="Symbol">)</a> <a id="849" class="Symbol">→</a> <a id="851" class="PrimitiveType">Set</a> <a id="855" class="Symbol">(</a><a id="856" href="Data.Sum.Categorical.html#794" class="Bound">a</a> <a id="858" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="860" href="Data.Sum.Categorical.html#810" class="Bound">b</a><a id="861" class="Symbol">)</a>
  <a id="865" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a> <a id="870" href="Data.Sum.Categorical.html#870" class="Bound">B</a> <a id="872" class="Symbol">=</a> <a id="874" href="Data.Sum.Categorical.html#798" class="Bound">A</a> <a id="876" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="878" href="Data.Sum.Categorical.html#870" class="Bound">B</a>

  <a id="Sumₗ.functor"></a><a id="883" href="Data.Sum.Categorical.html#883" class="Function">functor</a> <a id="891" class="Symbol">:</a> <a id="893" href="Category.Functor.html#385" class="Record">RawFunctor</a> <a id="904" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a>
  <a id="911" href="Data.Sum.Categorical.html#883" class="Function">functor</a> <a id="919" class="Symbol">=</a> <a id="921" class="Keyword">record</a> <a id="928" class="Symbol">{</a> <a id="930" class="Field Operator">_&lt;$&gt;_</a> <a id="936" class="Symbol">=</a> <a id="938" href="Data.Sum.Base.html#1394" class="Function">map₂</a> <a id="943" class="Symbol">}</a>

  <a id="Sumₗ.applicative"></a><a id="948" href="Data.Sum.Categorical.html#948" class="Function">applicative</a> <a id="960" class="Symbol">:</a> <a id="962" href="Category.Applicative.html#382" class="Function">RawApplicative</a> <a id="977" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a>
  <a id="984" href="Data.Sum.Categorical.html#948" class="Function">applicative</a> <a id="996" class="Symbol">=</a> <a id="998" class="Keyword">record</a>
    <a id="1009" class="Symbol">{</a> <a id="1011" class="Field">pure</a> <a id="1016" class="Symbol">=</a> <a id="1018" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a>
    <a id="1027" class="Symbol">;</a> <a id="1029" class="Field Operator">_⊛_</a> <a id="1033" class="Symbol">=</a> <a id="1035" href="Data.Sum.Base.html#927" class="Function Operator">[</a> <a id="1037" href="Function.html#1109" class="Function">const</a> <a id="1043" href="Function.html#769" class="Function Operator">∘</a> <a id="1045" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1050" href="Data.Sum.Base.html#927" class="Function Operator">,</a> <a id="1052" href="Data.Sum.Base.html#1394" class="Function">map₂</a> <a id="1057" href="Data.Sum.Base.html#927" class="Function Operator">]′</a>
    <a id="1064" class="Symbol">}</a>

  <a id="1069" class="Comment">-- The monad instance also requires some mucking about with universe levels.</a>
  <a id="Sumₗ.monadT"></a><a id="1148" href="Data.Sum.Categorical.html#1148" class="Function">monadT</a> <a id="1155" class="Symbol">:</a> <a id="1157" href="Category.Monad.html#447" class="Function">RawMonadT</a> <a id="1167" class="Symbol">(</a><a id="1168" href="Function.html#958" class="Function Operator">_∘′</a> <a id="1172" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a><a id="1176" class="Symbol">)</a>
  <a id="1180" href="Data.Sum.Categorical.html#1148" class="Function">monadT</a> <a id="1187" href="Data.Sum.Categorical.html#1187" class="Bound">M</a> <a id="1189" class="Symbol">=</a> <a id="1191" class="Keyword">record</a>
    <a id="1202" class="Symbol">{</a> <a id="1204" class="Field">return</a> <a id="1211" class="Symbol">=</a> <a id="1213" href="Category.Applicative.Indexed.html#747" class="Function">M.pure</a> <a id="1220" href="Function.html#769" class="Function Operator">∘</a> <a id="1222" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a>
    <a id="1231" class="Symbol">;</a> <a id="1233" class="Field Operator">_&gt;&gt;=_</a>  <a id="1240" class="Symbol">=</a> <a id="1242" class="Symbol">λ</a> <a id="1244" href="Data.Sum.Categorical.html#1244" class="Bound">ma</a> <a id="1247" href="Data.Sum.Categorical.html#1247" class="Bound">f</a> <a id="1249" class="Symbol">→</a> <a id="1251" href="Data.Sum.Categorical.html#1244" class="Bound">ma</a> <a id="1254" href="Category.Monad.Indexed.html#568" class="Field Operator">M.&gt;&gt;=</a> <a id="1260" href="Data.Sum.Base.html#927" class="Function Operator">[</a> <a id="1262" href="Category.Applicative.Indexed.html#747" class="Function">M.pure</a> <a id="1269" href="Function.html#958" class="Function Operator">∘′</a> <a id="1272" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1277" href="Data.Sum.Base.html#927" class="Function Operator">,</a> <a id="1279" href="Data.Sum.Categorical.html#1247" class="Bound">f</a> <a id="1281" href="Data.Sum.Base.html#927" class="Function Operator">]′</a>
    <a id="1288" class="Symbol">}</a> <a id="1290" class="Keyword">where</a> <a id="1296" class="Keyword">module</a> <a id="M"></a><a id="1303" href="Data.Sum.Categorical.html#1303" class="Module">M</a> <a id="1305" class="Symbol">=</a> <a id="1307" href="Category.Monad.html#774" class="Module">RawMonad</a> <a id="1316" href="Data.Sum.Categorical.html#1187" class="Bound">M</a>

  <a id="Sumₗ.monad"></a><a id="1321" href="Data.Sum.Categorical.html#1321" class="Function">monad</a> <a id="1327" class="Symbol">:</a> <a id="1329" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="1338" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a>
  <a id="1345" href="Data.Sum.Categorical.html#1321" class="Function">monad</a> <a id="1351" class="Symbol">=</a> <a id="1353" href="Data.Sum.Categorical.html#1148" class="Function">monadT</a> <a id="1360" href="Function.Identity.Categorical.html#615" class="Function">Id.monad</a>

<a id="1370" class="Comment">------------------------------------------------------------------------</a>
<a id="1443" class="Comment">-- Get access to other monadic functions</a>

  <a id="1487" class="Keyword">module</a> <a id="1494" href="Data.Sum.Categorical.html#1494" class="Module">_</a> <a id="1496" class="Symbol">{</a><a id="1497" href="Data.Sum.Categorical.html#1497" class="Bound">F</a><a id="1498" class="Symbol">}</a> <a id="1500" class="Symbol">(</a><a id="1501" href="Data.Sum.Categorical.html#1501" class="Bound">App</a> <a id="1505" class="Symbol">:</a> <a id="1507" href="Category.Applicative.html#382" class="Function">RawApplicative</a> <a id="1522" class="Symbol">{</a><a id="1523" href="Data.Sum.Categorical.html#794" class="Bound">a</a> <a id="1525" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1527" href="Data.Sum.Categorical.html#810" class="Bound">b</a><a id="1528" class="Symbol">}</a> <a id="1530" href="Data.Sum.Categorical.html#1497" class="Bound">F</a><a id="1531" class="Symbol">)</a> <a id="1533" class="Keyword">where</a>

    <a id="1544" class="Keyword">open</a> <a id="1549" href="Category.Applicative.html#494" class="Module">RawApplicative</a> <a id="1564" href="Data.Sum.Categorical.html#1501" class="Bound">App</a>

    <a id="1573" href="Data.Sum.Categorical.html#1573" class="Function">sequenceA</a> <a id="1583" class="Symbol">:</a> <a id="1585" class="Symbol">∀</a> <a id="1587" class="Symbol">{</a><a id="1588" href="Data.Sum.Categorical.html#1588" class="Bound">A</a><a id="1589" class="Symbol">}</a> <a id="1591" class="Symbol">→</a> <a id="1593" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a> <a id="1598" class="Symbol">(</a><a id="1599" href="Data.Sum.Categorical.html#1497" class="Bound">F</a> <a id="1601" href="Data.Sum.Categorical.html#1588" class="Bound">A</a><a id="1602" class="Symbol">)</a> <a id="1604" class="Symbol">→</a> <a id="1606" href="Data.Sum.Categorical.html#1497" class="Bound">F</a> <a id="1608" class="Symbol">(</a><a id="1609" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a> <a id="1614" href="Data.Sum.Categorical.html#1588" class="Bound">A</a><a id="1615" class="Symbol">)</a>
    <a id="1621" href="Data.Sum.Categorical.html#1573" class="Function">sequenceA</a> <a id="1631" class="Symbol">(</a><a id="1632" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1637" href="Data.Sum.Categorical.html#1637" class="Bound">a</a><a id="1638" class="Symbol">)</a> <a id="1640" class="Symbol">=</a> <a id="1642" href="Category.Applicative.Indexed.html#747" class="Field">pure</a> <a id="1647" class="Symbol">(</a><a id="1648" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1653" href="Data.Sum.Categorical.html#1637" class="Bound">a</a><a id="1654" class="Symbol">)</a>
    <a id="1660" href="Data.Sum.Categorical.html#1573" class="Function">sequenceA</a> <a id="1670" class="Symbol">(</a><a id="1671" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="1676" href="Data.Sum.Categorical.html#1676" class="Bound">x</a><a id="1677" class="Symbol">)</a> <a id="1679" class="Symbol">=</a> <a id="1681" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="1686" href="Category.Functor.html#475" class="Function Operator">&lt;$&gt;</a> <a id="1690" href="Data.Sum.Categorical.html#1676" class="Bound">x</a>

    <a id="1697" href="Data.Sum.Categorical.html#1697" class="Function">mapA</a> <a id="1702" class="Symbol">:</a> <a id="1704" class="Symbol">∀</a> <a id="1706" class="Symbol">{</a><a id="1707" href="Data.Sum.Categorical.html#1707" class="Bound">A</a> <a id="1709" href="Data.Sum.Categorical.html#1709" class="Bound">B</a><a id="1710" class="Symbol">}</a> <a id="1712" class="Symbol">→</a> <a id="1714" class="Symbol">(</a><a id="1715" href="Data.Sum.Categorical.html#1707" class="Bound">A</a> <a id="1717" class="Symbol">→</a> <a id="1719" href="Data.Sum.Categorical.html#1497" class="Bound">F</a> <a id="1721" href="Data.Sum.Categorical.html#1709" class="Bound">B</a><a id="1722" class="Symbol">)</a> <a id="1724" class="Symbol">→</a> <a id="1726" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a> <a id="1731" href="Data.Sum.Categorical.html#1707" class="Bound">A</a> <a id="1733" class="Symbol">→</a> <a id="1735" href="Data.Sum.Categorical.html#1497" class="Bound">F</a> <a id="1737" class="Symbol">(</a><a id="1738" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a> <a id="1743" href="Data.Sum.Categorical.html#1709" class="Bound">B</a><a id="1744" class="Symbol">)</a>
    <a id="1750" href="Data.Sum.Categorical.html#1697" class="Function">mapA</a> <a id="1755" href="Data.Sum.Categorical.html#1755" class="Bound">f</a> <a id="1757" class="Symbol">=</a> <a id="1759" href="Data.Sum.Categorical.html#1573" class="Function">sequenceA</a> <a id="1769" href="Function.html#769" class="Function Operator">∘</a> <a id="1771" href="Data.Sum.Base.html#1394" class="Function">map₂</a> <a id="1776" href="Data.Sum.Categorical.html#1755" class="Bound">f</a>

    <a id="1783" href="Data.Sum.Categorical.html#1783" class="Function">forA</a> <a id="1788" class="Symbol">:</a> <a id="1790" class="Symbol">∀</a> <a id="1792" class="Symbol">{</a><a id="1793" href="Data.Sum.Categorical.html#1793" class="Bound">A</a> <a id="1795" href="Data.Sum.Categorical.html#1795" class="Bound">B</a><a id="1796" class="Symbol">}</a> <a id="1798" class="Symbol">→</a> <a id="1800" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a> <a id="1805" href="Data.Sum.Categorical.html#1793" class="Bound">A</a> <a id="1807" class="Symbol">→</a> <a id="1809" class="Symbol">(</a><a id="1810" href="Data.Sum.Categorical.html#1793" class="Bound">A</a> <a id="1812" class="Symbol">→</a> <a id="1814" href="Data.Sum.Categorical.html#1497" class="Bound">F</a> <a id="1816" href="Data.Sum.Categorical.html#1795" class="Bound">B</a><a id="1817" class="Symbol">)</a> <a id="1819" class="Symbol">→</a> <a id="1821" href="Data.Sum.Categorical.html#1497" class="Bound">F</a> <a id="1823" class="Symbol">(</a><a id="1824" href="Data.Sum.Categorical.html#830" class="Function">Sumₗ</a> <a id="1829" href="Data.Sum.Categorical.html#1795" class="Bound">B</a><a id="1830" class="Symbol">)</a>
    <a id="1836" href="Data.Sum.Categorical.html#1783" class="Function">forA</a> <a id="1841" class="Symbol">=</a> <a id="1843" href="Function.html#1536" class="Function">flip</a> <a id="1848" href="Data.Sum.Categorical.html#1697" class="Function">mapA</a>

  <a id="1856" class="Keyword">module</a> <a id="1863" href="Data.Sum.Categorical.html#1863" class="Module">_</a> <a id="1865" class="Symbol">{</a><a id="1866" href="Data.Sum.Categorical.html#1866" class="Bound">M</a><a id="1867" class="Symbol">}</a> <a id="1869" class="Symbol">(</a><a id="1870" href="Data.Sum.Categorical.html#1870" class="Bound">Mon</a> <a id="1874" class="Symbol">:</a> <a id="1876" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="1885" class="Symbol">{</a><a id="1886" href="Data.Sum.Categorical.html#794" class="Bound">a</a> <a id="1888" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1890" href="Data.Sum.Categorical.html#810" class="Bound">b</a><a id="1891" class="Symbol">}</a> <a id="1893" href="Data.Sum.Categorical.html#1866" class="Bound">M</a><a id="1894" class="Symbol">)</a> <a id="1896" class="Keyword">where</a>

    <a id="1907" class="Keyword">private</a> <a id="1915" href="Data.Sum.Categorical.html#1915" class="Function">App</a> <a id="1919" class="Symbol">=</a> <a id="1921" href="Category.Monad.Indexed.html#1104" class="Function">RawMonad.rawIApplicative</a> <a id="1946" href="Data.Sum.Categorical.html#1870" class="Bound">Mon</a>

    <a id="1955" href="Data.Sum.Categorical.html#1955" class="Function">sequenceM</a> <a id="1965" class="Symbol">=</a> <a id="1967" href="Data.Sum.Categorical.html#1573" class="Function">sequenceA</a> <a id="1977" href="Data.Sum.Categorical.html#1915" class="Function">App</a>
    <a id="1985" href="Data.Sum.Categorical.html#1985" class="Function">mapM</a> <a id="1990" class="Symbol">=</a> <a id="1992" href="Data.Sum.Categorical.html#1697" class="Function">mapA</a> <a id="1997" href="Data.Sum.Categorical.html#1915" class="Function">App</a>
    <a id="2005" href="Data.Sum.Categorical.html#2005" class="Function">forM</a> <a id="2010" class="Symbol">=</a> <a id="2012" href="Data.Sum.Categorical.html#1783" class="Function">forA</a> <a id="2017" href="Data.Sum.Categorical.html#1915" class="Function">App</a>

<a id="2022" class="Comment">------------------------------------------------------------------------</a>
<a id="2095" class="Comment">-- Right-biased monad instance for _⊎_</a>

<a id="2135" class="Keyword">module</a> <a id="Sumᵣ"></a><a id="2142" href="Data.Sum.Categorical.html#2142" class="Module">Sumᵣ</a> <a id="2147" class="Symbol">(</a><a id="2148" href="Data.Sum.Categorical.html#2148" class="Bound">a</a> <a id="2150" class="Symbol">:</a> <a id="2152" href="Agda.Primitive.html#408" class="Postulate">Level</a><a id="2157" class="Symbol">)</a> <a id="2159" class="Symbol">{</a><a id="2160" href="Data.Sum.Categorical.html#2160" class="Bound">b</a><a id="2161" class="Symbol">}</a> <a id="2163" class="Symbol">(</a><a id="2164" href="Data.Sum.Categorical.html#2164" class="Bound">B</a> <a id="2166" class="Symbol">:</a> <a id="2168" class="PrimitiveType">Set</a> <a id="2172" href="Data.Sum.Categorical.html#2160" class="Bound">b</a><a id="2173" class="Symbol">)</a> <a id="2175" class="Keyword">where</a>

  <a id="Sumᵣ.Sumᵣ"></a><a id="2184" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a> <a id="2189" class="Symbol">:</a> <a id="2191" class="PrimitiveType">Set</a> <a id="2195" class="Symbol">(</a><a id="2196" href="Data.Sum.Categorical.html#2148" class="Bound">a</a> <a id="2198" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="2200" href="Data.Sum.Categorical.html#2160" class="Bound">b</a><a id="2201" class="Symbol">)</a> <a id="2203" class="Symbol">→</a> <a id="2205" class="PrimitiveType">Set</a> <a id="2209" class="Symbol">(</a><a id="2210" href="Data.Sum.Categorical.html#2148" class="Bound">a</a> <a id="2212" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="2214" href="Data.Sum.Categorical.html#2160" class="Bound">b</a><a id="2215" class="Symbol">)</a>
  <a id="2219" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a> <a id="2224" href="Data.Sum.Categorical.html#2224" class="Bound">A</a> <a id="2226" class="Symbol">=</a> <a id="2228" href="Data.Sum.Categorical.html#2224" class="Bound">A</a> <a id="2230" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="2232" href="Data.Sum.Categorical.html#2164" class="Bound">B</a>

  <a id="Sumᵣ.functor"></a><a id="2237" href="Data.Sum.Categorical.html#2237" class="Function">functor</a> <a id="2245" class="Symbol">:</a> <a id="2247" href="Category.Functor.html#385" class="Record">RawFunctor</a> <a id="2258" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a>
  <a id="2265" href="Data.Sum.Categorical.html#2237" class="Function">functor</a> <a id="2273" class="Symbol">=</a> <a id="2275" class="Keyword">record</a> <a id="2282" class="Symbol">{</a> <a id="2284" class="Field Operator">_&lt;$&gt;_</a> <a id="2290" class="Symbol">=</a> <a id="2292" href="Data.Sum.Base.html#1288" class="Function">map₁</a> <a id="2297" class="Symbol">}</a>

  <a id="Sumᵣ.applicative"></a><a id="2302" href="Data.Sum.Categorical.html#2302" class="Function">applicative</a> <a id="2314" class="Symbol">:</a> <a id="2316" href="Category.Applicative.html#382" class="Function">RawApplicative</a> <a id="2331" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a>
  <a id="2338" href="Data.Sum.Categorical.html#2302" class="Function">applicative</a> <a id="2350" class="Symbol">=</a> <a id="2352" class="Keyword">record</a>
    <a id="2363" class="Symbol">{</a> <a id="2365" class="Field">pure</a> <a id="2370" class="Symbol">=</a> <a id="2372" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a>
    <a id="2381" class="Symbol">;</a> <a id="2383" class="Field Operator">_⊛_</a> <a id="2387" class="Symbol">=</a> <a id="2389" href="Data.Sum.Base.html#927" class="Function Operator">[</a> <a id="2391" href="Data.Sum.Base.html#1288" class="Function">map₁</a> <a id="2396" href="Data.Sum.Base.html#927" class="Function Operator">,</a> <a id="2398" href="Function.html#1109" class="Function">const</a> <a id="2404" href="Function.html#769" class="Function Operator">∘</a> <a id="2406" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="2411" href="Data.Sum.Base.html#927" class="Function Operator">]′</a>
    <a id="2418" class="Symbol">}</a>

  <a id="Sumᵣ.monadT"></a><a id="2423" href="Data.Sum.Categorical.html#2423" class="Function">monadT</a> <a id="2430" class="Symbol">:</a> <a id="2432" href="Category.Monad.html#447" class="Function">RawMonadT</a> <a id="2442" class="Symbol">(</a><a id="2443" href="Function.html#958" class="Function Operator">_∘′</a> <a id="2447" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a><a id="2451" class="Symbol">)</a>
  <a id="2455" href="Data.Sum.Categorical.html#2423" class="Function">monadT</a> <a id="2462" href="Data.Sum.Categorical.html#2462" class="Bound">M</a> <a id="2464" class="Symbol">=</a> <a id="2466" class="Keyword">record</a>
    <a id="2477" class="Symbol">{</a> <a id="2479" class="Field">return</a> <a id="2486" class="Symbol">=</a> <a id="2488" href="Category.Applicative.Indexed.html#747" class="Function">M.pure</a> <a id="2495" href="Function.html#958" class="Function Operator">∘′</a> <a id="2498" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a>
    <a id="2507" class="Symbol">;</a> <a id="2509" class="Field Operator">_&gt;&gt;=_</a>  <a id="2516" class="Symbol">=</a> <a id="2518" class="Symbol">λ</a> <a id="2520" href="Data.Sum.Categorical.html#2520" class="Bound">ma</a> <a id="2523" href="Data.Sum.Categorical.html#2523" class="Bound">f</a> <a id="2525" class="Symbol">→</a> <a id="2527" href="Data.Sum.Categorical.html#2520" class="Bound">ma</a> <a id="2530" href="Category.Monad.Indexed.html#568" class="Field Operator">M.&gt;&gt;=</a> <a id="2536" href="Data.Sum.Base.html#927" class="Function Operator">[</a> <a id="2538" href="Data.Sum.Categorical.html#2523" class="Bound">f</a> <a id="2540" href="Data.Sum.Base.html#927" class="Function Operator">,</a> <a id="2542" href="Category.Applicative.Indexed.html#747" class="Function">M.pure</a> <a id="2549" href="Function.html#958" class="Function Operator">∘′</a> <a id="2552" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="2557" href="Data.Sum.Base.html#927" class="Function Operator">]′</a>
    <a id="2564" class="Symbol">}</a> <a id="2566" class="Keyword">where</a> <a id="2572" class="Keyword">module</a> <a id="M"></a><a id="2579" href="Data.Sum.Categorical.html#2579" class="Module">M</a> <a id="2581" class="Symbol">=</a> <a id="2583" href="Category.Monad.html#774" class="Module">RawMonad</a> <a id="2592" href="Data.Sum.Categorical.html#2462" class="Bound">M</a>

  <a id="Sumᵣ.monad"></a><a id="2597" href="Data.Sum.Categorical.html#2597" class="Function">monad</a> <a id="2603" class="Symbol">:</a> <a id="2605" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="2614" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a>
  <a id="2621" href="Data.Sum.Categorical.html#2597" class="Function">monad</a> <a id="2627" class="Symbol">=</a> <a id="2629" href="Data.Sum.Categorical.html#2423" class="Function">monadT</a> <a id="2636" href="Function.Identity.Categorical.html#615" class="Function">Id.monad</a>

<a id="2646" class="Comment">------------------------------------------------------------------------</a>
<a id="2719" class="Comment">-- Get access to other monadic functions</a>

  <a id="2763" class="Keyword">module</a> <a id="2770" href="Data.Sum.Categorical.html#2770" class="Module">_</a> <a id="2772" class="Symbol">{</a><a id="2773" href="Data.Sum.Categorical.html#2773" class="Bound">F</a><a id="2774" class="Symbol">}</a> <a id="2776" class="Symbol">(</a><a id="2777" href="Data.Sum.Categorical.html#2777" class="Bound">App</a> <a id="2781" class="Symbol">:</a> <a id="2783" href="Category.Applicative.html#382" class="Function">RawApplicative</a> <a id="2798" class="Symbol">{</a><a id="2799" href="Data.Sum.Categorical.html#2148" class="Bound">a</a> <a id="2801" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="2803" href="Data.Sum.Categorical.html#2160" class="Bound">b</a><a id="2804" class="Symbol">}</a> <a id="2806" href="Data.Sum.Categorical.html#2773" class="Bound">F</a><a id="2807" class="Symbol">)</a> <a id="2809" class="Keyword">where</a>

    <a id="2820" class="Keyword">open</a> <a id="2825" href="Category.Applicative.html#494" class="Module">RawApplicative</a> <a id="2840" href="Data.Sum.Categorical.html#2777" class="Bound">App</a>

    <a id="2849" href="Data.Sum.Categorical.html#2849" class="Function">sequenceA</a> <a id="2859" class="Symbol">:</a> <a id="2861" class="Symbol">∀</a> <a id="2863" class="Symbol">{</a><a id="2864" href="Data.Sum.Categorical.html#2864" class="Bound">A</a><a id="2865" class="Symbol">}</a> <a id="2867" class="Symbol">→</a> <a id="2869" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a> <a id="2874" class="Symbol">(</a><a id="2875" href="Data.Sum.Categorical.html#2773" class="Bound">F</a> <a id="2877" href="Data.Sum.Categorical.html#2864" class="Bound">A</a><a id="2878" class="Symbol">)</a> <a id="2880" class="Symbol">→</a> <a id="2882" href="Data.Sum.Categorical.html#2773" class="Bound">F</a> <a id="2884" class="Symbol">(</a><a id="2885" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a> <a id="2890" href="Data.Sum.Categorical.html#2864" class="Bound">A</a><a id="2891" class="Symbol">)</a>
    <a id="2897" href="Data.Sum.Categorical.html#2849" class="Function">sequenceA</a> <a id="2907" class="Symbol">(</a><a id="2908" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="2913" href="Data.Sum.Categorical.html#2913" class="Bound">a</a><a id="2914" class="Symbol">)</a> <a id="2916" class="Symbol">=</a> <a id="2918" href="Category.Applicative.Indexed.html#747" class="Field">pure</a> <a id="2923" class="Symbol">(</a><a id="2924" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="2929" href="Data.Sum.Categorical.html#2913" class="Bound">a</a><a id="2930" class="Symbol">)</a>
    <a id="2936" href="Data.Sum.Categorical.html#2849" class="Function">sequenceA</a> <a id="2946" class="Symbol">(</a><a id="2947" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="2952" href="Data.Sum.Categorical.html#2952" class="Bound">x</a><a id="2953" class="Symbol">)</a> <a id="2955" class="Symbol">=</a> <a id="2957" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="2962" href="Category.Functor.html#475" class="Function Operator">&lt;$&gt;</a> <a id="2966" href="Data.Sum.Categorical.html#2952" class="Bound">x</a>

    <a id="2973" href="Data.Sum.Categorical.html#2973" class="Function">mapA</a> <a id="2978" class="Symbol">:</a> <a id="2980" class="Symbol">∀</a> <a id="2982" class="Symbol">{</a><a id="2983" href="Data.Sum.Categorical.html#2983" class="Bound">A</a> <a id="2985" href="Data.Sum.Categorical.html#2985" class="Bound">B</a><a id="2986" class="Symbol">}</a> <a id="2988" class="Symbol">→</a> <a id="2990" class="Symbol">(</a><a id="2991" href="Data.Sum.Categorical.html#2983" class="Bound">A</a> <a id="2993" class="Symbol">→</a> <a id="2995" href="Data.Sum.Categorical.html#2773" class="Bound">F</a> <a id="2997" href="Data.Sum.Categorical.html#2985" class="Bound">B</a><a id="2998" class="Symbol">)</a> <a id="3000" class="Symbol">→</a> <a id="3002" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a> <a id="3007" href="Data.Sum.Categorical.html#2983" class="Bound">A</a> <a id="3009" class="Symbol">→</a> <a id="3011" href="Data.Sum.Categorical.html#2773" class="Bound">F</a> <a id="3013" class="Symbol">(</a><a id="3014" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a> <a id="3019" href="Data.Sum.Categorical.html#2985" class="Bound">B</a><a id="3020" class="Symbol">)</a>
    <a id="3026" href="Data.Sum.Categorical.html#2973" class="Function">mapA</a> <a id="3031" href="Data.Sum.Categorical.html#3031" class="Bound">f</a> <a id="3033" class="Symbol">=</a> <a id="3035" href="Data.Sum.Categorical.html#2849" class="Function">sequenceA</a> <a id="3045" href="Function.html#769" class="Function Operator">∘</a> <a id="3047" href="Data.Sum.Base.html#1288" class="Function">map₁</a> <a id="3052" href="Data.Sum.Categorical.html#3031" class="Bound">f</a>

    <a id="3059" href="Data.Sum.Categorical.html#3059" class="Function">forA</a> <a id="3064" class="Symbol">:</a> <a id="3066" class="Symbol">∀</a> <a id="3068" class="Symbol">{</a><a id="3069" href="Data.Sum.Categorical.html#3069" class="Bound">A</a> <a id="3071" href="Data.Sum.Categorical.html#3071" class="Bound">B</a><a id="3072" class="Symbol">}</a> <a id="3074" class="Symbol">→</a> <a id="3076" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a> <a id="3081" href="Data.Sum.Categorical.html#3069" class="Bound">A</a> <a id="3083" class="Symbol">→</a> <a id="3085" class="Symbol">(</a><a id="3086" href="Data.Sum.Categorical.html#3069" class="Bound">A</a> <a id="3088" class="Symbol">→</a> <a id="3090" href="Data.Sum.Categorical.html#2773" class="Bound">F</a> <a id="3092" href="Data.Sum.Categorical.html#3071" class="Bound">B</a><a id="3093" class="Symbol">)</a> <a id="3095" class="Symbol">→</a> <a id="3097" href="Data.Sum.Categorical.html#2773" class="Bound">F</a> <a id="3099" class="Symbol">(</a><a id="3100" href="Data.Sum.Categorical.html#2184" class="Function">Sumᵣ</a> <a id="3105" href="Data.Sum.Categorical.html#3071" class="Bound">B</a><a id="3106" class="Symbol">)</a>
    <a id="3112" href="Data.Sum.Categorical.html#3059" class="Function">forA</a> <a id="3117" class="Symbol">=</a> <a id="3119" href="Function.html#1536" class="Function">flip</a> <a id="3124" href="Data.Sum.Categorical.html#2973" class="Function">mapA</a>

  <a id="3132" class="Keyword">module</a> <a id="3139" href="Data.Sum.Categorical.html#3139" class="Module">_</a> <a id="3141" class="Symbol">{</a><a id="3142" href="Data.Sum.Categorical.html#3142" class="Bound">M</a><a id="3143" class="Symbol">}</a> <a id="3145" class="Symbol">(</a><a id="3146" href="Data.Sum.Categorical.html#3146" class="Bound">Mon</a> <a id="3150" class="Symbol">:</a> <a id="3152" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="3161" class="Symbol">{</a><a id="3162" href="Data.Sum.Categorical.html#2148" class="Bound">a</a> <a id="3164" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="3166" href="Data.Sum.Categorical.html#2160" class="Bound">b</a><a id="3167" class="Symbol">}</a> <a id="3169" href="Data.Sum.Categorical.html#3142" class="Bound">M</a><a id="3170" class="Symbol">)</a> <a id="3172" class="Keyword">where</a>

    <a id="3183" class="Keyword">private</a> <a id="3191" href="Data.Sum.Categorical.html#3191" class="Function">App</a> <a id="3195" class="Symbol">=</a> <a id="3197" href="Category.Monad.Indexed.html#1104" class="Function">RawMonad.rawIApplicative</a> <a id="3222" href="Data.Sum.Categorical.html#3146" class="Bound">Mon</a>

    <a id="3231" href="Data.Sum.Categorical.html#3231" class="Function">sequenceM</a> <a id="3241" class="Symbol">=</a> <a id="3243" href="Data.Sum.Categorical.html#2849" class="Function">sequenceA</a> <a id="3253" href="Data.Sum.Categorical.html#3191" class="Function">App</a>
    <a id="3261" href="Data.Sum.Categorical.html#3261" class="Function">mapM</a> <a id="3266" class="Symbol">=</a> <a id="3268" href="Data.Sum.Categorical.html#2973" class="Function">mapA</a> <a id="3273" href="Data.Sum.Categorical.html#3191" class="Function">App</a>
    <a id="3281" href="Data.Sum.Categorical.html#3281" class="Function">forM</a> <a id="3286" class="Symbol">=</a> <a id="3288" href="Data.Sum.Categorical.html#3059" class="Function">forA</a> <a id="3293" href="Data.Sum.Categorical.html#3191" class="Function">App</a>

<a id="3298" class="Comment">------------------------------------------------------------------------</a>
<a id="3371" class="Comment">-- Examples</a>

<a id="3384" class="Comment">-- Note that these examples are simple unit tests, because the type</a>
<a id="3452" class="Comment">-- checker verifies them.</a>

<a id="3479" class="Keyword">private</a>
  <a id="3489" class="Keyword">module</a> <a id="Examplesₗ"></a><a id="3496" href="Data.Sum.Categorical.html#3496" class="Module">Examplesₗ</a> <a id="3506" class="Symbol">{</a><a id="3507" href="Data.Sum.Categorical.html#3507" class="Bound">a</a> <a id="3509" href="Data.Sum.Categorical.html#3509" class="Bound">b</a><a id="3510" class="Symbol">}</a> <a id="3512" class="Symbol">{</a><a id="3513" href="Data.Sum.Categorical.html#3513" class="Bound">A</a> <a id="3515" class="Symbol">:</a> <a id="3517" class="PrimitiveType">Set</a> <a id="3521" href="Data.Sum.Categorical.html#3507" class="Bound">a</a><a id="3522" class="Symbol">}</a> <a id="3524" class="Symbol">{</a><a id="3525" href="Data.Sum.Categorical.html#3525" class="Bound">B</a> <a id="3527" class="Symbol">:</a> <a id="3529" class="PrimitiveType">Set</a> <a id="3533" href="Data.Sum.Categorical.html#3509" class="Bound">b</a><a id="3534" class="Symbol">}</a> <a id="3536" class="Keyword">where</a>

    <a id="3547" class="Keyword">open</a> <a id="3552" class="Keyword">import</a> <a id="3559" href="Agda.Builtin.Equality.html" class="Module">Agda.Builtin.Equality</a>
    <a id="3585" class="Keyword">open</a> <a id="3590" class="Keyword">import</a> <a id="3597" href="Function.html" class="Module">Function</a>
    <a id="3610" class="Keyword">module</a> <a id="Sₗ"></a><a id="3617" href="Data.Sum.Categorical.html#3617" class="Module">Sₗ</a> <a id="3620" class="Symbol">=</a> <a id="3622" href="Data.Sum.Categorical.html#788" class="Module">Sumₗ</a> <a id="3627" href="Data.Sum.Categorical.html#3513" class="Bound">A</a> <a id="3629" href="Data.Sum.Categorical.html#3509" class="Bound">b</a>

    <a id="3636" class="Keyword">open</a> <a id="3641" href="Category.Functor.html#385" class="Module">RawFunctor</a> <a id="3652" href="Data.Sum.Categorical.html#883" class="Function">Sₗ.functor</a>

    <a id="3668" class="Comment">-- This type to the right of ⊎ needs to be a &quot;lifted&quot; version of (B : Set b)</a>
    <a id="3749" class="Comment">-- that lives in the universe (Set (a ⊔ b)).</a>
    <a id="Examplesₗ.fmapId"></a><a id="3798" href="Data.Sum.Categorical.html#3798" class="Function">fmapId</a> <a id="3805" class="Symbol">:</a> <a id="3807" class="Symbol">(</a><a id="3808" href="Data.Sum.Categorical.html#3808" class="Bound">x</a> <a id="3810" class="Symbol">:</a> <a id="3812" href="Data.Sum.Categorical.html#3513" class="Bound">A</a> <a id="3814" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="3816" class="Symbol">(</a><a id="3817" href="Level.html#358" class="Record">Lift</a> <a id="3822" href="Data.Sum.Categorical.html#3507" class="Bound">a</a> <a id="3824" href="Data.Sum.Categorical.html#3525" class="Bound">B</a><a id="3825" class="Symbol">))</a> <a id="3828" class="Symbol">→</a> <a id="3830" class="Symbol">(</a><a id="3831" href="Function.html#1068" class="Function">id</a> <a id="3834" href="Category.Functor.html#475" class="Function Operator">&lt;$&gt;</a> <a id="3838" href="Data.Sum.Categorical.html#3808" class="Bound">x</a><a id="3839" class="Symbol">)</a> <a id="3841" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="3843" href="Data.Sum.Categorical.html#3808" class="Bound">x</a>
    <a id="3849" href="Data.Sum.Categorical.html#3798" class="Function">fmapId</a> <a id="3856" class="Symbol">(</a><a id="3857" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="3862" href="Data.Sum.Categorical.html#3862" class="Bound">x</a><a id="3863" class="Symbol">)</a> <a id="3865" class="Symbol">=</a> <a id="3867" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
    <a id="3876" href="Data.Sum.Categorical.html#3798" class="Function">fmapId</a> <a id="3883" class="Symbol">(</a><a id="3884" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="3889" href="Data.Sum.Categorical.html#3889" class="Bound">y</a><a id="3890" class="Symbol">)</a> <a id="3892" class="Symbol">=</a> <a id="3894" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>


    <a id="3905" class="Keyword">open</a> <a id="3910" href="Category.Monad.html#774" class="Module">RawMonad</a>   <a id="3921" href="Data.Sum.Categorical.html#1321" class="Function">Sₗ.monad</a>

    <a id="3935" class="Comment">-- Now, let&#39;s show that &quot;return&quot; is a unit for &gt;&gt;=. We use Lift in exactly</a>
    <a id="4014" class="Comment">-- the same way as above. The data (x : B) then needs to be &quot;lifted&quot; to</a>
    <a id="4090" class="Comment">-- this new type (Lift B).</a>
    <a id="Examplesₗ.returnUnitL"></a><a id="4121" href="Data.Sum.Categorical.html#4121" class="Function">returnUnitL</a> <a id="4133" class="Symbol">:</a> <a id="4135" class="Symbol">∀</a> <a id="4137" class="Symbol">{</a><a id="4138" href="Data.Sum.Categorical.html#4138" class="Bound">x</a> <a id="4140" class="Symbol">:</a> <a id="4142" href="Data.Sum.Categorical.html#3525" class="Bound">B</a><a id="4143" class="Symbol">}</a> <a id="4145" class="Symbol">{</a><a id="4146" href="Data.Sum.Categorical.html#4146" class="Bound">f</a> <a id="4148" class="Symbol">:</a> <a id="4150" href="Level.html#358" class="Record">Lift</a> <a id="4155" href="Data.Sum.Categorical.html#3507" class="Bound">a</a> <a id="4157" href="Data.Sum.Categorical.html#3525" class="Bound">B</a> <a id="4159" class="Symbol">→</a> <a id="4161" href="Data.Sum.Categorical.html#3513" class="Bound">A</a> <a id="4163" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="4165" class="Symbol">(</a><a id="4166" href="Level.html#358" class="Record">Lift</a> <a id="4171" href="Data.Sum.Categorical.html#3507" class="Bound">a</a> <a id="4173" href="Data.Sum.Categorical.html#3525" class="Bound">B</a><a id="4174" class="Symbol">)}</a>
                  <a id="4195" class="Symbol">→</a> <a id="4197" class="Symbol">((</a><a id="4199" href="Category.Monad.Indexed.html#533" class="Function">return</a> <a id="4206" class="Symbol">(</a><a id="4207" href="Level.html#415" class="InductiveConstructor">lift</a> <a id="4212" href="Data.Sum.Categorical.html#4138" class="Bound">x</a><a id="4213" class="Symbol">))</a> <a id="4216" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4220" href="Data.Sum.Categorical.html#4146" class="Bound">f</a><a id="4221" class="Symbol">)</a> <a id="4223" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="4225" href="Data.Sum.Categorical.html#4146" class="Bound">f</a> <a id="4227" class="Symbol">(</a><a id="4228" href="Level.html#415" class="InductiveConstructor">lift</a> <a id="4233" href="Data.Sum.Categorical.html#4138" class="Bound">x</a><a id="4234" class="Symbol">)</a>
    <a id="4240" href="Data.Sum.Categorical.html#4121" class="Function">returnUnitL</a> <a id="4252" class="Symbol">=</a> <a id="4254" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>

    <a id="Examplesₗ.returnUnitR"></a><a id="4264" href="Data.Sum.Categorical.html#4264" class="Function">returnUnitR</a> <a id="4276" class="Symbol">:</a> <a id="4278" class="Symbol">(</a><a id="4279" href="Data.Sum.Categorical.html#4279" class="Bound">x</a> <a id="4281" class="Symbol">:</a> <a id="4283" href="Data.Sum.Categorical.html#3513" class="Bound">A</a> <a id="4285" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="4287" class="Symbol">(</a><a id="4288" href="Level.html#358" class="Record">Lift</a> <a id="4293" href="Data.Sum.Categorical.html#3507" class="Bound">a</a> <a id="4295" href="Data.Sum.Categorical.html#3525" class="Bound">B</a><a id="4296" class="Symbol">))</a> <a id="4299" class="Symbol">→</a> <a id="4301" class="Symbol">(</a><a id="4302" href="Data.Sum.Categorical.html#4279" class="Bound">x</a> <a id="4304" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4308" href="Category.Monad.Indexed.html#533" class="Function">return</a><a id="4314" class="Symbol">)</a> <a id="4316" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="4318" href="Data.Sum.Categorical.html#4279" class="Bound">x</a>
    <a id="4324" href="Data.Sum.Categorical.html#4264" class="Function">returnUnitR</a> <a id="4336" class="Symbol">(</a><a id="4337" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="4342" class="Symbol">_)</a> <a id="4345" class="Symbol">=</a> <a id="4347" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
    <a id="4356" href="Data.Sum.Categorical.html#4264" class="Function">returnUnitR</a> <a id="4368" class="Symbol">(</a><a id="4369" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="4374" class="Symbol">_)</a> <a id="4377" class="Symbol">=</a> <a id="4379" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>

    <a id="4389" class="Comment">-- And another (limited version of a) monad law...</a>
    <a id="Examplesₗ.bindCompose"></a><a id="4444" href="Data.Sum.Categorical.html#4444" class="Function">bindCompose</a> <a id="4456" class="Symbol">:</a> <a id="4458" class="Symbol">∀</a> <a id="4460" class="Symbol">{</a><a id="4461" href="Data.Sum.Categorical.html#4461" class="Bound">f</a> <a id="4463" href="Data.Sum.Categorical.html#4463" class="Bound">g</a> <a id="4465" class="Symbol">:</a> <a id="4467" href="Level.html#358" class="Record">Lift</a> <a id="4472" href="Data.Sum.Categorical.html#3507" class="Bound">a</a> <a id="4474" href="Data.Sum.Categorical.html#3525" class="Bound">B</a> <a id="4476" class="Symbol">→</a> <a id="4478" href="Data.Sum.Categorical.html#3513" class="Bound">A</a> <a id="4480" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="4482" class="Symbol">(</a><a id="4483" href="Level.html#358" class="Record">Lift</a> <a id="4488" href="Data.Sum.Categorical.html#3507" class="Bound">a</a> <a id="4490" href="Data.Sum.Categorical.html#3525" class="Bound">B</a><a id="4491" class="Symbol">)}</a>
                <a id="4510" class="Symbol">→</a> <a id="4512" class="Symbol">(</a><a id="4513" href="Data.Sum.Categorical.html#4513" class="Bound">x</a> <a id="4515" class="Symbol">:</a> <a id="4517" href="Data.Sum.Categorical.html#3513" class="Bound">A</a> <a id="4519" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="4521" class="Symbol">(</a><a id="4522" href="Level.html#358" class="Record">Lift</a> <a id="4527" href="Data.Sum.Categorical.html#3507" class="Bound">a</a> <a id="4529" href="Data.Sum.Categorical.html#3525" class="Bound">B</a><a id="4530" class="Symbol">))</a>
                <a id="4549" class="Symbol">→</a> <a id="4551" class="Symbol">((</a><a id="4553" href="Data.Sum.Categorical.html#4513" class="Bound">x</a> <a id="4555" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4559" href="Data.Sum.Categorical.html#4461" class="Bound">f</a><a id="4560" class="Symbol">)</a> <a id="4562" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4566" href="Data.Sum.Categorical.html#4463" class="Bound">g</a><a id="4567" class="Symbol">)</a> <a id="4569" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="4571" class="Symbol">(</a><a id="4572" href="Data.Sum.Categorical.html#4513" class="Bound">x</a> <a id="4574" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4578" class="Symbol">(λ</a> <a id="4581" href="Data.Sum.Categorical.html#4581" class="Bound">y</a> <a id="4583" class="Symbol">→</a> <a id="4585" class="Symbol">(</a><a id="4586" href="Data.Sum.Categorical.html#4461" class="Bound">f</a> <a id="4588" href="Data.Sum.Categorical.html#4581" class="Bound">y</a> <a id="4590" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4594" href="Data.Sum.Categorical.html#4463" class="Bound">g</a><a id="4595" class="Symbol">)))</a>
    <a id="4603" href="Data.Sum.Categorical.html#4444" class="Function">bindCompose</a> <a id="4615" class="Symbol">(</a><a id="4616" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="4621" href="Data.Sum.Categorical.html#4621" class="Bound">x</a><a id="4622" class="Symbol">)</a> <a id="4624" class="Symbol">=</a> <a id="4626" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
    <a id="4635" href="Data.Sum.Categorical.html#4444" class="Function">bindCompose</a> <a id="4647" class="Symbol">(</a><a id="4648" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="4653" href="Data.Sum.Categorical.html#4653" class="Bound">y</a><a id="4654" class="Symbol">)</a> <a id="4656" class="Symbol">=</a> <a id="4658" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
</pre></body></html>