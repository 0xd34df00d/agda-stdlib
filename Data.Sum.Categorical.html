<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Sum.Categorical</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Universe-sensitive functor and monad instances for the Sum type.</a>
<a id="174" class="Comment">------------------------------------------------------------------------</a>

<a id="248" class="Keyword">module</a> <a id="255" href="Data.Sum.Categorical.html" class="Module">Data.Sum.Categorical</a> <a id="276" class="Keyword">where</a>

<a id="283" class="Keyword">open</a> <a id="288" class="Keyword">import</a> <a id="295" href="Level.html" class="Module">Level</a>
<a id="301" class="Keyword">open</a> <a id="306" class="Keyword">import</a> <a id="313" href="Data.Sum.html" class="Module">Data.Sum</a>
<a id="322" class="Keyword">open</a> <a id="327" class="Keyword">import</a> <a id="334" href="Category.Functor.html" class="Module">Category.Functor</a>
<a id="351" class="Keyword">open</a> <a id="356" class="Keyword">import</a> <a id="363" href="Category.Applicative.html" class="Module">Category.Applicative</a>
<a id="384" class="Keyword">open</a> <a id="389" class="Keyword">import</a> <a id="396" href="Category.Monad.html" class="Module">Category.Monad</a>
<a id="411" class="Keyword">open</a> <a id="416" class="Keyword">import</a> <a id="423" href="Category.Monad.Identity.html" class="Module">Category.Monad.Identity</a>
<a id="447" class="Keyword">open</a> <a id="452" class="Keyword">import</a> <a id="459" href="Function.html" class="Module">Function</a>

<a id="469" class="Comment">-- To minimize the universe level of the RawFunctor, we require that elements of</a>
<a id="550" class="Comment">-- B are &quot;lifted&quot; to a copy of B at a higher universe level (a ⊔ b). See the</a>
<a id="627" class="Comment">-- examples for how this is done.</a>

<a id="662" class="Comment">------------------------------------------------------------------------</a>
<a id="735" class="Comment">-- Left-biased monad instance for _⊎_</a>

<a id="774" class="Keyword">module</a> <a id="Sumₗ"></a><a id="781" href="Data.Sum.Categorical.html#781" class="Module">Sumₗ</a> <a id="786" class="Symbol">{</a><a id="787" href="Data.Sum.Categorical.html#787" class="Bound">a</a><a id="788" class="Symbol">}</a> <a id="790" class="Symbol">(</a><a id="791" href="Data.Sum.Categorical.html#791" class="Bound">A</a> <a id="793" class="Symbol">:</a> <a id="795" class="PrimitiveType">Set</a> <a id="799" href="Data.Sum.Categorical.html#787" class="Bound">a</a><a id="800" class="Symbol">)</a> <a id="802" class="Symbol">(</a><a id="803" href="Data.Sum.Categorical.html#803" class="Bound">b</a> <a id="805" class="Symbol">:</a> <a id="807" href="Agda.Primitive.html#408" class="Postulate">Level</a><a id="812" class="Symbol">)</a> <a id="814" class="Keyword">where</a>

  <a id="Sumₗ.Sumₗ"></a><a id="823" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a> <a id="828" class="Symbol">:</a> <a id="830" class="PrimitiveType">Set</a> <a id="834" class="Symbol">(</a><a id="835" href="Data.Sum.Categorical.html#787" class="Bound">a</a> <a id="837" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="839" href="Data.Sum.Categorical.html#803" class="Bound">b</a><a id="840" class="Symbol">)</a> <a id="842" class="Symbol">→</a> <a id="844" class="PrimitiveType">Set</a> <a id="848" class="Symbol">(</a><a id="849" href="Data.Sum.Categorical.html#787" class="Bound">a</a> <a id="851" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="853" href="Data.Sum.Categorical.html#803" class="Bound">b</a><a id="854" class="Symbol">)</a>
  <a id="858" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a> <a id="863" href="Data.Sum.Categorical.html#863" class="Bound">B</a> <a id="865" class="Symbol">=</a> <a id="867" href="Data.Sum.Categorical.html#791" class="Bound">A</a> <a id="869" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="871" href="Data.Sum.Categorical.html#863" class="Bound">B</a>

  <a id="Sumₗ.functor"></a><a id="876" href="Data.Sum.Categorical.html#876" class="Function">functor</a> <a id="884" class="Symbol">:</a> <a id="886" href="Category.Functor.html#385" class="Record">RawFunctor</a> <a id="897" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a>
  <a id="904" href="Data.Sum.Categorical.html#876" class="Function">functor</a> <a id="912" class="Symbol">=</a> <a id="914" class="Keyword">record</a> <a id="921" class="Symbol">{</a> <a id="923" class="Field Operator">_&lt;$&gt;_</a> <a id="929" class="Symbol">=</a> <a id="931" href="Data.Sum.Base.html#1394" class="Function">map₂</a> <a id="936" class="Symbol">}</a>

  <a id="Sumₗ.applicative"></a><a id="941" href="Data.Sum.Categorical.html#941" class="Function">applicative</a> <a id="953" class="Symbol">:</a> <a id="955" href="Category.Applicative.html#382" class="Function">RawApplicative</a> <a id="970" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a>
  <a id="977" href="Data.Sum.Categorical.html#941" class="Function">applicative</a> <a id="989" class="Symbol">=</a> <a id="991" class="Keyword">record</a>
    <a id="1002" class="Symbol">{</a> <a id="1004" class="Field">pure</a> <a id="1009" class="Symbol">=</a> <a id="1011" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a>
    <a id="1020" class="Symbol">;</a> <a id="1022" class="Field Operator">_⊛_</a> <a id="1026" class="Symbol">=</a> <a id="1028" href="Data.Sum.Base.html#927" class="Function Operator">[</a> <a id="1030" href="Function.html#1109" class="Function">const</a> <a id="1036" href="Function.html#769" class="Function Operator">∘</a> <a id="1038" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1043" href="Data.Sum.Base.html#927" class="Function Operator">,</a> <a id="1045" href="Data.Sum.Base.html#1394" class="Function">map₂</a> <a id="1050" href="Data.Sum.Base.html#927" class="Function Operator">]′</a>
    <a id="1057" class="Symbol">}</a>

  <a id="1062" class="Comment">-- The monad instance also requires some mucking about with universe levels.</a>
  <a id="Sumₗ.monadT"></a><a id="1141" href="Data.Sum.Categorical.html#1141" class="Function">monadT</a> <a id="1148" class="Symbol">:</a> <a id="1150" class="Symbol">∀</a> <a id="1152" class="Symbol">{</a><a id="1153" href="Data.Sum.Categorical.html#1153" class="Bound">M</a><a id="1154" class="Symbol">}</a> <a id="1156" class="Symbol">→</a> <a id="1158" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="1167" href="Data.Sum.Categorical.html#1153" class="Bound">M</a> <a id="1169" class="Symbol">→</a> <a id="1171" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="1180" class="Symbol">(</a><a id="1181" href="Data.Sum.Categorical.html#1153" class="Bound">M</a> <a id="1183" href="Function.html#958" class="Function Operator">∘′</a> <a id="1186" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a><a id="1190" class="Symbol">)</a>
  <a id="1194" href="Data.Sum.Categorical.html#1141" class="Function">monadT</a> <a id="1201" href="Data.Sum.Categorical.html#1201" class="Bound">M</a> <a id="1203" class="Symbol">=</a> <a id="1205" class="Keyword">record</a>
    <a id="1216" class="Symbol">{</a> <a id="1218" class="Field">return</a> <a id="1225" class="Symbol">=</a> <a id="1227" href="Category.Applicative.Indexed.html#747" class="Function">M.pure</a> <a id="1234" href="Function.html#769" class="Function Operator">∘</a> <a id="1236" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a>
    <a id="1245" class="Symbol">;</a> <a id="1247" class="Field Operator">_&gt;&gt;=_</a>  <a id="1254" class="Symbol">=</a> <a id="1256" class="Symbol">λ</a> <a id="1258" href="Data.Sum.Categorical.html#1258" class="Bound">ma</a> <a id="1261" href="Data.Sum.Categorical.html#1261" class="Bound">f</a> <a id="1263" class="Symbol">→</a> <a id="1265" href="Data.Sum.Categorical.html#1258" class="Bound">ma</a> <a id="1268" href="Category.Monad.Indexed.html#568" class="Field Operator">M.&gt;&gt;=</a> <a id="1274" href="Data.Sum.Base.html#927" class="Function Operator">[</a> <a id="1276" href="Category.Applicative.Indexed.html#747" class="Function">M.pure</a> <a id="1283" href="Function.html#958" class="Function Operator">∘′</a> <a id="1286" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1291" href="Data.Sum.Base.html#927" class="Function Operator">,</a> <a id="1293" href="Data.Sum.Categorical.html#1261" class="Bound">f</a> <a id="1295" href="Data.Sum.Base.html#927" class="Function Operator">]′</a>
    <a id="1302" class="Symbol">}</a> <a id="1304" class="Keyword">where</a> <a id="1310" class="Keyword">module</a> <a id="M"></a><a id="1317" href="Data.Sum.Categorical.html#1317" class="Module">M</a> <a id="1319" class="Symbol">=</a> <a id="1321" href="Category.Monad.html#652" class="Module">RawMonad</a> <a id="1330" href="Data.Sum.Categorical.html#1201" class="Bound">M</a>

  <a id="Sumₗ.monad"></a><a id="1335" href="Data.Sum.Categorical.html#1335" class="Function">monad</a> <a id="1341" class="Symbol">:</a> <a id="1343" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="1352" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a>
  <a id="1359" href="Data.Sum.Categorical.html#1335" class="Function">monad</a> <a id="1365" class="Symbol">=</a> <a id="1367" href="Data.Sum.Categorical.html#1141" class="Function">monadT</a> <a id="1374" href="Category.Monad.Identity.html#356" class="Function">IdentityMonad</a>

<a id="1389" class="Comment">------------------------------------------------------------------------</a>
<a id="1462" class="Comment">-- Get access to other monadic functions</a>

  <a id="1506" class="Keyword">module</a> <a id="1513" href="Data.Sum.Categorical.html#1513" class="Module">_</a> <a id="1515" class="Symbol">{</a><a id="1516" href="Data.Sum.Categorical.html#1516" class="Bound">F</a><a id="1517" class="Symbol">}</a> <a id="1519" class="Symbol">(</a><a id="1520" href="Data.Sum.Categorical.html#1520" class="Bound">App</a> <a id="1524" class="Symbol">:</a> <a id="1526" href="Category.Applicative.html#382" class="Function">RawApplicative</a> <a id="1541" class="Symbol">{</a><a id="1542" href="Data.Sum.Categorical.html#787" class="Bound">a</a> <a id="1544" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1546" href="Data.Sum.Categorical.html#803" class="Bound">b</a><a id="1547" class="Symbol">}</a> <a id="1549" href="Data.Sum.Categorical.html#1516" class="Bound">F</a><a id="1550" class="Symbol">)</a> <a id="1552" class="Keyword">where</a>

    <a id="1563" class="Keyword">open</a> <a id="1568" href="Category.Applicative.html#494" class="Module">RawApplicative</a> <a id="1583" href="Data.Sum.Categorical.html#1520" class="Bound">App</a>

    <a id="1592" href="Data.Sum.Categorical.html#1592" class="Function">sequenceA</a> <a id="1602" class="Symbol">:</a> <a id="1604" class="Symbol">∀</a> <a id="1606" class="Symbol">{</a><a id="1607" href="Data.Sum.Categorical.html#1607" class="Bound">A</a><a id="1608" class="Symbol">}</a> <a id="1610" class="Symbol">→</a> <a id="1612" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a> <a id="1617" class="Symbol">(</a><a id="1618" href="Data.Sum.Categorical.html#1516" class="Bound">F</a> <a id="1620" href="Data.Sum.Categorical.html#1607" class="Bound">A</a><a id="1621" class="Symbol">)</a> <a id="1623" class="Symbol">→</a> <a id="1625" href="Data.Sum.Categorical.html#1516" class="Bound">F</a> <a id="1627" class="Symbol">(</a><a id="1628" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a> <a id="1633" href="Data.Sum.Categorical.html#1607" class="Bound">A</a><a id="1634" class="Symbol">)</a>
    <a id="1640" href="Data.Sum.Categorical.html#1592" class="Function">sequenceA</a> <a id="1650" class="Symbol">(</a><a id="1651" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1656" href="Data.Sum.Categorical.html#1656" class="Bound">a</a><a id="1657" class="Symbol">)</a> <a id="1659" class="Symbol">=</a> <a id="1661" href="Category.Applicative.Indexed.html#747" class="Field">pure</a> <a id="1666" class="Symbol">(</a><a id="1667" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1672" href="Data.Sum.Categorical.html#1656" class="Bound">a</a><a id="1673" class="Symbol">)</a>
    <a id="1679" href="Data.Sum.Categorical.html#1592" class="Function">sequenceA</a> <a id="1689" class="Symbol">(</a><a id="1690" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="1695" href="Data.Sum.Categorical.html#1695" class="Bound">x</a><a id="1696" class="Symbol">)</a> <a id="1698" class="Symbol">=</a> <a id="1700" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="1705" href="Category.Functor.html#475" class="Function Operator">&lt;$&gt;</a> <a id="1709" href="Data.Sum.Categorical.html#1695" class="Bound">x</a>

    <a id="1716" href="Data.Sum.Categorical.html#1716" class="Function">mapA</a> <a id="1721" class="Symbol">:</a> <a id="1723" class="Symbol">∀</a> <a id="1725" class="Symbol">{</a><a id="1726" href="Data.Sum.Categorical.html#1726" class="Bound">A</a> <a id="1728" href="Data.Sum.Categorical.html#1728" class="Bound">B</a><a id="1729" class="Symbol">}</a> <a id="1731" class="Symbol">→</a> <a id="1733" class="Symbol">(</a><a id="1734" href="Data.Sum.Categorical.html#1726" class="Bound">A</a> <a id="1736" class="Symbol">→</a> <a id="1738" href="Data.Sum.Categorical.html#1516" class="Bound">F</a> <a id="1740" href="Data.Sum.Categorical.html#1728" class="Bound">B</a><a id="1741" class="Symbol">)</a> <a id="1743" class="Symbol">→</a> <a id="1745" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a> <a id="1750" href="Data.Sum.Categorical.html#1726" class="Bound">A</a> <a id="1752" class="Symbol">→</a> <a id="1754" href="Data.Sum.Categorical.html#1516" class="Bound">F</a> <a id="1756" class="Symbol">(</a><a id="1757" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a> <a id="1762" href="Data.Sum.Categorical.html#1728" class="Bound">B</a><a id="1763" class="Symbol">)</a>
    <a id="1769" href="Data.Sum.Categorical.html#1716" class="Function">mapA</a> <a id="1774" href="Data.Sum.Categorical.html#1774" class="Bound">f</a> <a id="1776" class="Symbol">=</a> <a id="1778" href="Data.Sum.Categorical.html#1592" class="Function">sequenceA</a> <a id="1788" href="Function.html#769" class="Function Operator">∘</a> <a id="1790" href="Data.Sum.Base.html#1394" class="Function">map₂</a> <a id="1795" href="Data.Sum.Categorical.html#1774" class="Bound">f</a>

    <a id="1802" href="Data.Sum.Categorical.html#1802" class="Function">forA</a> <a id="1807" class="Symbol">:</a> <a id="1809" class="Symbol">∀</a> <a id="1811" class="Symbol">{</a><a id="1812" href="Data.Sum.Categorical.html#1812" class="Bound">A</a> <a id="1814" href="Data.Sum.Categorical.html#1814" class="Bound">B</a><a id="1815" class="Symbol">}</a> <a id="1817" class="Symbol">→</a> <a id="1819" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a> <a id="1824" href="Data.Sum.Categorical.html#1812" class="Bound">A</a> <a id="1826" class="Symbol">→</a> <a id="1828" class="Symbol">(</a><a id="1829" href="Data.Sum.Categorical.html#1812" class="Bound">A</a> <a id="1831" class="Symbol">→</a> <a id="1833" href="Data.Sum.Categorical.html#1516" class="Bound">F</a> <a id="1835" href="Data.Sum.Categorical.html#1814" class="Bound">B</a><a id="1836" class="Symbol">)</a> <a id="1838" class="Symbol">→</a> <a id="1840" href="Data.Sum.Categorical.html#1516" class="Bound">F</a> <a id="1842" class="Symbol">(</a><a id="1843" href="Data.Sum.Categorical.html#823" class="Function">Sumₗ</a> <a id="1848" href="Data.Sum.Categorical.html#1814" class="Bound">B</a><a id="1849" class="Symbol">)</a>
    <a id="1855" href="Data.Sum.Categorical.html#1802" class="Function">forA</a> <a id="1860" class="Symbol">=</a> <a id="1862" href="Function.html#1536" class="Function">flip</a> <a id="1867" href="Data.Sum.Categorical.html#1716" class="Function">mapA</a>

  <a id="1875" class="Keyword">module</a> <a id="1882" href="Data.Sum.Categorical.html#1882" class="Module">_</a> <a id="1884" class="Symbol">{</a><a id="1885" href="Data.Sum.Categorical.html#1885" class="Bound">M</a><a id="1886" class="Symbol">}</a> <a id="1888" class="Symbol">(</a><a id="1889" href="Data.Sum.Categorical.html#1889" class="Bound">Mon</a> <a id="1893" class="Symbol">:</a> <a id="1895" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="1904" class="Symbol">{</a><a id="1905" href="Data.Sum.Categorical.html#787" class="Bound">a</a> <a id="1907" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1909" href="Data.Sum.Categorical.html#803" class="Bound">b</a><a id="1910" class="Symbol">}</a> <a id="1912" href="Data.Sum.Categorical.html#1885" class="Bound">M</a><a id="1913" class="Symbol">)</a> <a id="1915" class="Keyword">where</a>

    <a id="1926" class="Keyword">private</a> <a id="1934" href="Data.Sum.Categorical.html#1934" class="Function">App</a> <a id="1938" class="Symbol">=</a> <a id="1940" href="Category.Monad.Indexed.html#1104" class="Function">RawMonad.rawIApplicative</a> <a id="1965" href="Data.Sum.Categorical.html#1889" class="Bound">Mon</a>

    <a id="1974" href="Data.Sum.Categorical.html#1974" class="Function">sequenceM</a> <a id="1984" class="Symbol">=</a> <a id="1986" href="Data.Sum.Categorical.html#1592" class="Function">sequenceA</a> <a id="1996" href="Data.Sum.Categorical.html#1934" class="Function">App</a>
    <a id="2004" href="Data.Sum.Categorical.html#2004" class="Function">mapM</a> <a id="2009" class="Symbol">=</a> <a id="2011" href="Data.Sum.Categorical.html#1716" class="Function">mapA</a> <a id="2016" href="Data.Sum.Categorical.html#1934" class="Function">App</a>
    <a id="2024" href="Data.Sum.Categorical.html#2024" class="Function">forM</a> <a id="2029" class="Symbol">=</a> <a id="2031" href="Data.Sum.Categorical.html#1802" class="Function">forA</a> <a id="2036" href="Data.Sum.Categorical.html#1934" class="Function">App</a>

<a id="2041" class="Comment">------------------------------------------------------------------------</a>
<a id="2114" class="Comment">-- Right-biased monad instance for _⊎_</a>

<a id="2154" class="Keyword">module</a> <a id="Sumᵣ"></a><a id="2161" href="Data.Sum.Categorical.html#2161" class="Module">Sumᵣ</a> <a id="2166" class="Symbol">(</a><a id="2167" href="Data.Sum.Categorical.html#2167" class="Bound">a</a> <a id="2169" class="Symbol">:</a> <a id="2171" href="Agda.Primitive.html#408" class="Postulate">Level</a><a id="2176" class="Symbol">)</a> <a id="2178" class="Symbol">{</a><a id="2179" href="Data.Sum.Categorical.html#2179" class="Bound">b</a><a id="2180" class="Symbol">}</a> <a id="2182" class="Symbol">(</a><a id="2183" href="Data.Sum.Categorical.html#2183" class="Bound">B</a> <a id="2185" class="Symbol">:</a> <a id="2187" class="PrimitiveType">Set</a> <a id="2191" href="Data.Sum.Categorical.html#2179" class="Bound">b</a><a id="2192" class="Symbol">)</a> <a id="2194" class="Keyword">where</a>

  <a id="Sumᵣ.Sumᵣ"></a><a id="2203" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a> <a id="2208" class="Symbol">:</a> <a id="2210" class="PrimitiveType">Set</a> <a id="2214" class="Symbol">(</a><a id="2215" href="Data.Sum.Categorical.html#2167" class="Bound">a</a> <a id="2217" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="2219" href="Data.Sum.Categorical.html#2179" class="Bound">b</a><a id="2220" class="Symbol">)</a> <a id="2222" class="Symbol">→</a> <a id="2224" class="PrimitiveType">Set</a> <a id="2228" class="Symbol">(</a><a id="2229" href="Data.Sum.Categorical.html#2167" class="Bound">a</a> <a id="2231" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="2233" href="Data.Sum.Categorical.html#2179" class="Bound">b</a><a id="2234" class="Symbol">)</a>
  <a id="2238" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a> <a id="2243" href="Data.Sum.Categorical.html#2243" class="Bound">A</a> <a id="2245" class="Symbol">=</a> <a id="2247" href="Data.Sum.Categorical.html#2243" class="Bound">A</a> <a id="2249" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="2251" href="Data.Sum.Categorical.html#2183" class="Bound">B</a>

  <a id="Sumᵣ.functor"></a><a id="2256" href="Data.Sum.Categorical.html#2256" class="Function">functor</a> <a id="2264" class="Symbol">:</a> <a id="2266" href="Category.Functor.html#385" class="Record">RawFunctor</a> <a id="2277" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a>
  <a id="2284" href="Data.Sum.Categorical.html#2256" class="Function">functor</a> <a id="2292" class="Symbol">=</a> <a id="2294" class="Keyword">record</a> <a id="2301" class="Symbol">{</a> <a id="2303" class="Field Operator">_&lt;$&gt;_</a> <a id="2309" class="Symbol">=</a> <a id="2311" href="Data.Sum.Base.html#1288" class="Function">map₁</a> <a id="2316" class="Symbol">}</a>

  <a id="Sumᵣ.applicative"></a><a id="2321" href="Data.Sum.Categorical.html#2321" class="Function">applicative</a> <a id="2333" class="Symbol">:</a> <a id="2335" href="Category.Applicative.html#382" class="Function">RawApplicative</a> <a id="2350" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a>
  <a id="2357" href="Data.Sum.Categorical.html#2321" class="Function">applicative</a> <a id="2369" class="Symbol">=</a> <a id="2371" class="Keyword">record</a>
    <a id="2382" class="Symbol">{</a> <a id="2384" class="Field">pure</a> <a id="2389" class="Symbol">=</a> <a id="2391" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a>
    <a id="2400" class="Symbol">;</a> <a id="2402" class="Field Operator">_⊛_</a> <a id="2406" class="Symbol">=</a> <a id="2408" href="Data.Sum.Base.html#927" class="Function Operator">[</a> <a id="2410" href="Data.Sum.Base.html#1288" class="Function">map₁</a> <a id="2415" href="Data.Sum.Base.html#927" class="Function Operator">,</a> <a id="2417" href="Function.html#1109" class="Function">const</a> <a id="2423" href="Function.html#769" class="Function Operator">∘</a> <a id="2425" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="2430" href="Data.Sum.Base.html#927" class="Function Operator">]′</a>
    <a id="2437" class="Symbol">}</a>

  <a id="Sumᵣ.monadT"></a><a id="2442" href="Data.Sum.Categorical.html#2442" class="Function">monadT</a> <a id="2449" class="Symbol">:</a> <a id="2451" class="Symbol">∀</a> <a id="2453" class="Symbol">{</a><a id="2454" href="Data.Sum.Categorical.html#2454" class="Bound">M</a><a id="2455" class="Symbol">}</a> <a id="2457" class="Symbol">→</a> <a id="2459" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="2468" href="Data.Sum.Categorical.html#2454" class="Bound">M</a> <a id="2470" class="Symbol">→</a> <a id="2472" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="2481" class="Symbol">(</a><a id="2482" href="Data.Sum.Categorical.html#2454" class="Bound">M</a> <a id="2484" href="Function.html#958" class="Function Operator">∘′</a> <a id="2487" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a><a id="2491" class="Symbol">)</a>
  <a id="2495" href="Data.Sum.Categorical.html#2442" class="Function">monadT</a> <a id="2502" href="Data.Sum.Categorical.html#2502" class="Bound">M</a> <a id="2504" class="Symbol">=</a> <a id="2506" class="Keyword">record</a>
    <a id="2517" class="Symbol">{</a> <a id="2519" class="Field">return</a> <a id="2526" class="Symbol">=</a> <a id="2528" href="Category.Applicative.Indexed.html#747" class="Function">M.pure</a> <a id="2535" href="Function.html#958" class="Function Operator">∘′</a> <a id="2538" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a>
    <a id="2547" class="Symbol">;</a> <a id="2549" class="Field Operator">_&gt;&gt;=_</a>  <a id="2556" class="Symbol">=</a> <a id="2558" class="Symbol">λ</a> <a id="2560" href="Data.Sum.Categorical.html#2560" class="Bound">ma</a> <a id="2563" href="Data.Sum.Categorical.html#2563" class="Bound">f</a> <a id="2565" class="Symbol">→</a> <a id="2567" href="Data.Sum.Categorical.html#2560" class="Bound">ma</a> <a id="2570" href="Category.Monad.Indexed.html#568" class="Field Operator">M.&gt;&gt;=</a> <a id="2576" href="Data.Sum.Base.html#927" class="Function Operator">[</a> <a id="2578" href="Data.Sum.Categorical.html#2563" class="Bound">f</a> <a id="2580" href="Data.Sum.Base.html#927" class="Function Operator">,</a> <a id="2582" href="Category.Applicative.Indexed.html#747" class="Function">M.pure</a> <a id="2589" href="Function.html#958" class="Function Operator">∘′</a> <a id="2592" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="2597" href="Data.Sum.Base.html#927" class="Function Operator">]′</a>
    <a id="2604" class="Symbol">}</a> <a id="2606" class="Keyword">where</a> <a id="2612" class="Keyword">module</a> <a id="M"></a><a id="2619" href="Data.Sum.Categorical.html#2619" class="Module">M</a> <a id="2621" class="Symbol">=</a> <a id="2623" href="Category.Monad.html#652" class="Module">RawMonad</a> <a id="2632" href="Data.Sum.Categorical.html#2502" class="Bound">M</a>

  <a id="Sumᵣ.monad"></a><a id="2637" href="Data.Sum.Categorical.html#2637" class="Function">monad</a> <a id="2643" class="Symbol">:</a> <a id="2645" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="2654" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a>
  <a id="2661" href="Data.Sum.Categorical.html#2637" class="Function">monad</a> <a id="2667" class="Symbol">=</a> <a id="2669" href="Data.Sum.Categorical.html#2442" class="Function">monadT</a> <a id="2676" href="Category.Monad.Identity.html#356" class="Function">IdentityMonad</a>

<a id="2691" class="Comment">------------------------------------------------------------------------</a>
<a id="2764" class="Comment">-- Get access to other monadic functions</a>

  <a id="2808" class="Keyword">module</a> <a id="2815" href="Data.Sum.Categorical.html#2815" class="Module">_</a> <a id="2817" class="Symbol">{</a><a id="2818" href="Data.Sum.Categorical.html#2818" class="Bound">F</a><a id="2819" class="Symbol">}</a> <a id="2821" class="Symbol">(</a><a id="2822" href="Data.Sum.Categorical.html#2822" class="Bound">App</a> <a id="2826" class="Symbol">:</a> <a id="2828" href="Category.Applicative.html#382" class="Function">RawApplicative</a> <a id="2843" class="Symbol">{</a><a id="2844" href="Data.Sum.Categorical.html#2167" class="Bound">a</a> <a id="2846" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="2848" href="Data.Sum.Categorical.html#2179" class="Bound">b</a><a id="2849" class="Symbol">}</a> <a id="2851" href="Data.Sum.Categorical.html#2818" class="Bound">F</a><a id="2852" class="Symbol">)</a> <a id="2854" class="Keyword">where</a>

    <a id="2865" class="Keyword">open</a> <a id="2870" href="Category.Applicative.html#494" class="Module">RawApplicative</a> <a id="2885" href="Data.Sum.Categorical.html#2822" class="Bound">App</a>

    <a id="2894" href="Data.Sum.Categorical.html#2894" class="Function">sequenceA</a> <a id="2904" class="Symbol">:</a> <a id="2906" class="Symbol">∀</a> <a id="2908" class="Symbol">{</a><a id="2909" href="Data.Sum.Categorical.html#2909" class="Bound">A</a><a id="2910" class="Symbol">}</a> <a id="2912" class="Symbol">→</a> <a id="2914" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a> <a id="2919" class="Symbol">(</a><a id="2920" href="Data.Sum.Categorical.html#2818" class="Bound">F</a> <a id="2922" href="Data.Sum.Categorical.html#2909" class="Bound">A</a><a id="2923" class="Symbol">)</a> <a id="2925" class="Symbol">→</a> <a id="2927" href="Data.Sum.Categorical.html#2818" class="Bound">F</a> <a id="2929" class="Symbol">(</a><a id="2930" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a> <a id="2935" href="Data.Sum.Categorical.html#2909" class="Bound">A</a><a id="2936" class="Symbol">)</a>
    <a id="2942" href="Data.Sum.Categorical.html#2894" class="Function">sequenceA</a> <a id="2952" class="Symbol">(</a><a id="2953" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="2958" href="Data.Sum.Categorical.html#2958" class="Bound">a</a><a id="2959" class="Symbol">)</a> <a id="2961" class="Symbol">=</a> <a id="2963" href="Category.Applicative.Indexed.html#747" class="Field">pure</a> <a id="2968" class="Symbol">(</a><a id="2969" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="2974" href="Data.Sum.Categorical.html#2958" class="Bound">a</a><a id="2975" class="Symbol">)</a>
    <a id="2981" href="Data.Sum.Categorical.html#2894" class="Function">sequenceA</a> <a id="2991" class="Symbol">(</a><a id="2992" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="2997" href="Data.Sum.Categorical.html#2997" class="Bound">x</a><a id="2998" class="Symbol">)</a> <a id="3000" class="Symbol">=</a> <a id="3002" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="3007" href="Category.Functor.html#475" class="Function Operator">&lt;$&gt;</a> <a id="3011" href="Data.Sum.Categorical.html#2997" class="Bound">x</a>

    <a id="3018" href="Data.Sum.Categorical.html#3018" class="Function">mapA</a> <a id="3023" class="Symbol">:</a> <a id="3025" class="Symbol">∀</a> <a id="3027" class="Symbol">{</a><a id="3028" href="Data.Sum.Categorical.html#3028" class="Bound">A</a> <a id="3030" href="Data.Sum.Categorical.html#3030" class="Bound">B</a><a id="3031" class="Symbol">}</a> <a id="3033" class="Symbol">→</a> <a id="3035" class="Symbol">(</a><a id="3036" href="Data.Sum.Categorical.html#3028" class="Bound">A</a> <a id="3038" class="Symbol">→</a> <a id="3040" href="Data.Sum.Categorical.html#2818" class="Bound">F</a> <a id="3042" href="Data.Sum.Categorical.html#3030" class="Bound">B</a><a id="3043" class="Symbol">)</a> <a id="3045" class="Symbol">→</a> <a id="3047" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a> <a id="3052" href="Data.Sum.Categorical.html#3028" class="Bound">A</a> <a id="3054" class="Symbol">→</a> <a id="3056" href="Data.Sum.Categorical.html#2818" class="Bound">F</a> <a id="3058" class="Symbol">(</a><a id="3059" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a> <a id="3064" href="Data.Sum.Categorical.html#3030" class="Bound">B</a><a id="3065" class="Symbol">)</a>
    <a id="3071" href="Data.Sum.Categorical.html#3018" class="Function">mapA</a> <a id="3076" href="Data.Sum.Categorical.html#3076" class="Bound">f</a> <a id="3078" class="Symbol">=</a> <a id="3080" href="Data.Sum.Categorical.html#2894" class="Function">sequenceA</a> <a id="3090" href="Function.html#769" class="Function Operator">∘</a> <a id="3092" href="Data.Sum.Base.html#1288" class="Function">map₁</a> <a id="3097" href="Data.Sum.Categorical.html#3076" class="Bound">f</a>

    <a id="3104" href="Data.Sum.Categorical.html#3104" class="Function">forA</a> <a id="3109" class="Symbol">:</a> <a id="3111" class="Symbol">∀</a> <a id="3113" class="Symbol">{</a><a id="3114" href="Data.Sum.Categorical.html#3114" class="Bound">A</a> <a id="3116" href="Data.Sum.Categorical.html#3116" class="Bound">B</a><a id="3117" class="Symbol">}</a> <a id="3119" class="Symbol">→</a> <a id="3121" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a> <a id="3126" href="Data.Sum.Categorical.html#3114" class="Bound">A</a> <a id="3128" class="Symbol">→</a> <a id="3130" class="Symbol">(</a><a id="3131" href="Data.Sum.Categorical.html#3114" class="Bound">A</a> <a id="3133" class="Symbol">→</a> <a id="3135" href="Data.Sum.Categorical.html#2818" class="Bound">F</a> <a id="3137" href="Data.Sum.Categorical.html#3116" class="Bound">B</a><a id="3138" class="Symbol">)</a> <a id="3140" class="Symbol">→</a> <a id="3142" href="Data.Sum.Categorical.html#2818" class="Bound">F</a> <a id="3144" class="Symbol">(</a><a id="3145" href="Data.Sum.Categorical.html#2203" class="Function">Sumᵣ</a> <a id="3150" href="Data.Sum.Categorical.html#3116" class="Bound">B</a><a id="3151" class="Symbol">)</a>
    <a id="3157" href="Data.Sum.Categorical.html#3104" class="Function">forA</a> <a id="3162" class="Symbol">=</a> <a id="3164" href="Function.html#1536" class="Function">flip</a> <a id="3169" href="Data.Sum.Categorical.html#3018" class="Function">mapA</a>

  <a id="3177" class="Keyword">module</a> <a id="3184" href="Data.Sum.Categorical.html#3184" class="Module">_</a> <a id="3186" class="Symbol">{</a><a id="3187" href="Data.Sum.Categorical.html#3187" class="Bound">M</a><a id="3188" class="Symbol">}</a> <a id="3190" class="Symbol">(</a><a id="3191" href="Data.Sum.Categorical.html#3191" class="Bound">Mon</a> <a id="3195" class="Symbol">:</a> <a id="3197" href="Category.Monad.html#360" class="Function">RawMonad</a> <a id="3206" class="Symbol">{</a><a id="3207" href="Data.Sum.Categorical.html#2167" class="Bound">a</a> <a id="3209" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="3211" href="Data.Sum.Categorical.html#2179" class="Bound">b</a><a id="3212" class="Symbol">}</a> <a id="3214" href="Data.Sum.Categorical.html#3187" class="Bound">M</a><a id="3215" class="Symbol">)</a> <a id="3217" class="Keyword">where</a>

    <a id="3228" class="Keyword">private</a> <a id="3236" href="Data.Sum.Categorical.html#3236" class="Function">App</a> <a id="3240" class="Symbol">=</a> <a id="3242" href="Category.Monad.Indexed.html#1104" class="Function">RawMonad.rawIApplicative</a> <a id="3267" href="Data.Sum.Categorical.html#3191" class="Bound">Mon</a>

    <a id="3276" href="Data.Sum.Categorical.html#3276" class="Function">sequenceM</a> <a id="3286" class="Symbol">=</a> <a id="3288" href="Data.Sum.Categorical.html#2894" class="Function">sequenceA</a> <a id="3298" href="Data.Sum.Categorical.html#3236" class="Function">App</a>
    <a id="3306" href="Data.Sum.Categorical.html#3306" class="Function">mapM</a> <a id="3311" class="Symbol">=</a> <a id="3313" href="Data.Sum.Categorical.html#3018" class="Function">mapA</a> <a id="3318" href="Data.Sum.Categorical.html#3236" class="Function">App</a>
    <a id="3326" href="Data.Sum.Categorical.html#3326" class="Function">forM</a> <a id="3331" class="Symbol">=</a> <a id="3333" href="Data.Sum.Categorical.html#3104" class="Function">forA</a> <a id="3338" href="Data.Sum.Categorical.html#3236" class="Function">App</a>

<a id="3343" class="Comment">------------------------------------------------------------------------</a>
<a id="3416" class="Comment">-- Examples</a>

<a id="3429" class="Comment">-- Note that these examples are simple unit tests, because the type</a>
<a id="3497" class="Comment">-- checker verifies them.</a>

<a id="3524" class="Keyword">private</a>
  <a id="3534" class="Keyword">module</a> <a id="Examplesₗ"></a><a id="3541" href="Data.Sum.Categorical.html#3541" class="Module">Examplesₗ</a> <a id="3551" class="Symbol">{</a><a id="3552" href="Data.Sum.Categorical.html#3552" class="Bound">a</a> <a id="3554" href="Data.Sum.Categorical.html#3554" class="Bound">b</a><a id="3555" class="Symbol">}</a> <a id="3557" class="Symbol">{</a><a id="3558" href="Data.Sum.Categorical.html#3558" class="Bound">A</a> <a id="3560" class="Symbol">:</a> <a id="3562" class="PrimitiveType">Set</a> <a id="3566" href="Data.Sum.Categorical.html#3552" class="Bound">a</a><a id="3567" class="Symbol">}</a> <a id="3569" class="Symbol">{</a><a id="3570" href="Data.Sum.Categorical.html#3570" class="Bound">B</a> <a id="3572" class="Symbol">:</a> <a id="3574" class="PrimitiveType">Set</a> <a id="3578" href="Data.Sum.Categorical.html#3554" class="Bound">b</a><a id="3579" class="Symbol">}</a> <a id="3581" class="Keyword">where</a>

    <a id="3592" class="Keyword">open</a> <a id="3597" class="Keyword">import</a> <a id="3604" href="Agda.Builtin.Equality.html" class="Module">Agda.Builtin.Equality</a>
    <a id="3630" class="Keyword">open</a> <a id="3635" class="Keyword">import</a> <a id="3642" href="Function.html" class="Module">Function</a>
    <a id="3655" class="Keyword">module</a> <a id="Sₗ"></a><a id="3662" href="Data.Sum.Categorical.html#3662" class="Module">Sₗ</a> <a id="3665" class="Symbol">=</a> <a id="3667" href="Data.Sum.Categorical.html#781" class="Module">Sumₗ</a> <a id="3672" href="Data.Sum.Categorical.html#3558" class="Bound">A</a> <a id="3674" href="Data.Sum.Categorical.html#3554" class="Bound">b</a>

    <a id="3681" class="Keyword">open</a> <a id="3686" href="Category.Functor.html#385" class="Module">RawFunctor</a> <a id="3697" href="Data.Sum.Categorical.html#876" class="Function">Sₗ.functor</a>

    <a id="3713" class="Comment">-- This type to the right of ⊎ needs to be a &quot;lifted&quot; version of (B : Set b)</a>
    <a id="3794" class="Comment">-- that lives in the universe (Set (a ⊔ b)).</a>
    <a id="Examplesₗ.fmapId"></a><a id="3843" href="Data.Sum.Categorical.html#3843" class="Function">fmapId</a> <a id="3850" class="Symbol">:</a> <a id="3852" class="Symbol">(</a><a id="3853" href="Data.Sum.Categorical.html#3853" class="Bound">x</a> <a id="3855" class="Symbol">:</a> <a id="3857" href="Data.Sum.Categorical.html#3558" class="Bound">A</a> <a id="3859" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="3861" class="Symbol">(</a><a id="3862" href="Level.html#579" class="Record">Lift</a> <a id="3867" href="Data.Sum.Categorical.html#3552" class="Bound">a</a> <a id="3869" href="Data.Sum.Categorical.html#3570" class="Bound">B</a><a id="3870" class="Symbol">))</a> <a id="3873" class="Symbol">→</a> <a id="3875" class="Symbol">(</a><a id="3876" href="Function.html#1068" class="Function">id</a> <a id="3879" href="Category.Functor.html#475" class="Function Operator">&lt;$&gt;</a> <a id="3883" href="Data.Sum.Categorical.html#3853" class="Bound">x</a><a id="3884" class="Symbol">)</a> <a id="3886" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="3888" href="Data.Sum.Categorical.html#3853" class="Bound">x</a>
    <a id="3894" href="Data.Sum.Categorical.html#3843" class="Function">fmapId</a> <a id="3901" class="Symbol">(</a><a id="3902" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="3907" href="Data.Sum.Categorical.html#3907" class="Bound">x</a><a id="3908" class="Symbol">)</a> <a id="3910" class="Symbol">=</a> <a id="3912" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
    <a id="3921" href="Data.Sum.Categorical.html#3843" class="Function">fmapId</a> <a id="3928" class="Symbol">(</a><a id="3929" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="3934" href="Data.Sum.Categorical.html#3934" class="Bound">y</a><a id="3935" class="Symbol">)</a> <a id="3937" class="Symbol">=</a> <a id="3939" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>


    <a id="3950" class="Keyword">open</a> <a id="3955" href="Category.Monad.html#652" class="Module">RawMonad</a>   <a id="3966" href="Data.Sum.Categorical.html#1335" class="Function">Sₗ.monad</a>

    <a id="3980" class="Comment">-- Now, let&#39;s show that &quot;return&quot; is a unit for &gt;&gt;=. We use Lift in exactly</a>
    <a id="4059" class="Comment">-- the same way as above. The data (x : B) then needs to be &quot;lifted&quot; to</a>
    <a id="4135" class="Comment">-- this new type (Lift B).</a>
    <a id="Examplesₗ.returnUnitL"></a><a id="4166" href="Data.Sum.Categorical.html#4166" class="Function">returnUnitL</a> <a id="4178" class="Symbol">:</a> <a id="4180" class="Symbol">∀</a> <a id="4182" class="Symbol">{</a><a id="4183" href="Data.Sum.Categorical.html#4183" class="Bound">x</a> <a id="4185" class="Symbol">:</a> <a id="4187" href="Data.Sum.Categorical.html#3570" class="Bound">B</a><a id="4188" class="Symbol">}</a> <a id="4190" class="Symbol">{</a><a id="4191" href="Data.Sum.Categorical.html#4191" class="Bound">f</a> <a id="4193" class="Symbol">:</a> <a id="4195" href="Level.html#579" class="Record">Lift</a> <a id="4200" href="Data.Sum.Categorical.html#3552" class="Bound">a</a> <a id="4202" href="Data.Sum.Categorical.html#3570" class="Bound">B</a> <a id="4204" class="Symbol">→</a> <a id="4206" href="Data.Sum.Categorical.html#3558" class="Bound">A</a> <a id="4208" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="4210" class="Symbol">(</a><a id="4211" href="Level.html#579" class="Record">Lift</a> <a id="4216" href="Data.Sum.Categorical.html#3552" class="Bound">a</a> <a id="4218" href="Data.Sum.Categorical.html#3570" class="Bound">B</a><a id="4219" class="Symbol">)}</a>
                  <a id="4240" class="Symbol">→</a> <a id="4242" class="Symbol">((</a><a id="4244" href="Category.Monad.Indexed.html#533" class="Function">return</a> <a id="4251" class="Symbol">(</a><a id="4252" href="Level.html#636" class="InductiveConstructor">lift</a> <a id="4257" href="Data.Sum.Categorical.html#4183" class="Bound">x</a><a id="4258" class="Symbol">))</a> <a id="4261" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4265" href="Data.Sum.Categorical.html#4191" class="Bound">f</a><a id="4266" class="Symbol">)</a> <a id="4268" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="4270" href="Data.Sum.Categorical.html#4191" class="Bound">f</a> <a id="4272" class="Symbol">(</a><a id="4273" href="Level.html#636" class="InductiveConstructor">lift</a> <a id="4278" href="Data.Sum.Categorical.html#4183" class="Bound">x</a><a id="4279" class="Symbol">)</a>
    <a id="4285" href="Data.Sum.Categorical.html#4166" class="Function">returnUnitL</a> <a id="4297" class="Symbol">=</a> <a id="4299" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>

    <a id="Examplesₗ.returnUnitR"></a><a id="4309" href="Data.Sum.Categorical.html#4309" class="Function">returnUnitR</a> <a id="4321" class="Symbol">:</a> <a id="4323" class="Symbol">(</a><a id="4324" href="Data.Sum.Categorical.html#4324" class="Bound">x</a> <a id="4326" class="Symbol">:</a> <a id="4328" href="Data.Sum.Categorical.html#3558" class="Bound">A</a> <a id="4330" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="4332" class="Symbol">(</a><a id="4333" href="Level.html#579" class="Record">Lift</a> <a id="4338" href="Data.Sum.Categorical.html#3552" class="Bound">a</a> <a id="4340" href="Data.Sum.Categorical.html#3570" class="Bound">B</a><a id="4341" class="Symbol">))</a> <a id="4344" class="Symbol">→</a> <a id="4346" class="Symbol">(</a><a id="4347" href="Data.Sum.Categorical.html#4324" class="Bound">x</a> <a id="4349" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4353" href="Category.Monad.Indexed.html#533" class="Function">return</a><a id="4359" class="Symbol">)</a> <a id="4361" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="4363" href="Data.Sum.Categorical.html#4324" class="Bound">x</a>
    <a id="4369" href="Data.Sum.Categorical.html#4309" class="Function">returnUnitR</a> <a id="4381" class="Symbol">(</a><a id="4382" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="4387" class="Symbol">_)</a> <a id="4390" class="Symbol">=</a> <a id="4392" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
    <a id="4401" href="Data.Sum.Categorical.html#4309" class="Function">returnUnitR</a> <a id="4413" class="Symbol">(</a><a id="4414" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="4419" class="Symbol">_)</a> <a id="4422" class="Symbol">=</a> <a id="4424" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>

    <a id="4434" class="Comment">-- And another (limited version of a) monad law...</a>
    <a id="Examplesₗ.bindCompose"></a><a id="4489" href="Data.Sum.Categorical.html#4489" class="Function">bindCompose</a> <a id="4501" class="Symbol">:</a> <a id="4503" class="Symbol">∀</a> <a id="4505" class="Symbol">{</a><a id="4506" href="Data.Sum.Categorical.html#4506" class="Bound">f</a> <a id="4508" href="Data.Sum.Categorical.html#4508" class="Bound">g</a> <a id="4510" class="Symbol">:</a> <a id="4512" href="Level.html#579" class="Record">Lift</a> <a id="4517" href="Data.Sum.Categorical.html#3552" class="Bound">a</a> <a id="4519" href="Data.Sum.Categorical.html#3570" class="Bound">B</a> <a id="4521" class="Symbol">→</a> <a id="4523" href="Data.Sum.Categorical.html#3558" class="Bound">A</a> <a id="4525" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="4527" class="Symbol">(</a><a id="4528" href="Level.html#579" class="Record">Lift</a> <a id="4533" href="Data.Sum.Categorical.html#3552" class="Bound">a</a> <a id="4535" href="Data.Sum.Categorical.html#3570" class="Bound">B</a><a id="4536" class="Symbol">)}</a>
                <a id="4555" class="Symbol">→</a> <a id="4557" class="Symbol">(</a><a id="4558" href="Data.Sum.Categorical.html#4558" class="Bound">x</a> <a id="4560" class="Symbol">:</a> <a id="4562" href="Data.Sum.Categorical.html#3558" class="Bound">A</a> <a id="4564" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="4566" class="Symbol">(</a><a id="4567" href="Level.html#579" class="Record">Lift</a> <a id="4572" href="Data.Sum.Categorical.html#3552" class="Bound">a</a> <a id="4574" href="Data.Sum.Categorical.html#3570" class="Bound">B</a><a id="4575" class="Symbol">))</a>
                <a id="4594" class="Symbol">→</a> <a id="4596" class="Symbol">((</a><a id="4598" href="Data.Sum.Categorical.html#4558" class="Bound">x</a> <a id="4600" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4604" href="Data.Sum.Categorical.html#4506" class="Bound">f</a><a id="4605" class="Symbol">)</a> <a id="4607" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4611" href="Data.Sum.Categorical.html#4508" class="Bound">g</a><a id="4612" class="Symbol">)</a> <a id="4614" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="4616" class="Symbol">(</a><a id="4617" href="Data.Sum.Categorical.html#4558" class="Bound">x</a> <a id="4619" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4623" class="Symbol">(λ</a> <a id="4626" href="Data.Sum.Categorical.html#4626" class="Bound">y</a> <a id="4628" class="Symbol">→</a> <a id="4630" class="Symbol">(</a><a id="4631" href="Data.Sum.Categorical.html#4506" class="Bound">f</a> <a id="4633" href="Data.Sum.Categorical.html#4626" class="Bound">y</a> <a id="4635" href="Category.Monad.Indexed.html#568" class="Function Operator">&gt;&gt;=</a> <a id="4639" href="Data.Sum.Categorical.html#4508" class="Bound">g</a><a id="4640" class="Symbol">)))</a>
    <a id="4648" href="Data.Sum.Categorical.html#4489" class="Function">bindCompose</a> <a id="4660" class="Symbol">(</a><a id="4661" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="4666" href="Data.Sum.Categorical.html#4666" class="Bound">x</a><a id="4667" class="Symbol">)</a> <a id="4669" class="Symbol">=</a> <a id="4671" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
    <a id="4680" href="Data.Sum.Categorical.html#4489" class="Function">bindCompose</a> <a id="4692" class="Symbol">(</a><a id="4693" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="4698" href="Data.Sum.Categorical.html#4698" class="Bound">y</a><a id="4699" class="Symbol">)</a> <a id="4701" class="Symbol">=</a> <a id="4703" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
</pre></body></html>