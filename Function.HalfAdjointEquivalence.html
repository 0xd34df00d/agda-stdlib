<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Function.HalfAdjointEquivalence</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Half adjoint equivalences</a>
<a id="135" class="Comment">------------------------------------------------------------------------</a>

<a id="209" class="Symbol">{-#</a> <a id="213" class="Keyword">OPTIONS</a> <a id="221" class="Option">--without-K</a> <a id="233" class="Symbol">#-}</a>

<a id="238" class="Keyword">module</a> <a id="245" href="Function.HalfAdjointEquivalence.html" class="Module">Function.HalfAdjointEquivalence</a> <a id="277" class="Keyword">where</a>

<a id="284" class="Keyword">open</a> <a id="289" class="Keyword">import</a> <a id="296" href="Function.html" class="Module">Function</a>
<a id="305" class="Keyword">open</a> <a id="310" class="Keyword">import</a> <a id="317" href="Function.Equality.html" class="Module">Function.Equality</a> <a id="335" class="Keyword">using</a> <a id="341" class="Symbol">(</a><a id="342" href="Function.Equality.html#824" class="Field Operator">_⟨$⟩_</a><a id="347" class="Symbol">)</a>
<a id="349" class="Keyword">open</a> <a id="354" class="Keyword">import</a> <a id="361" href="Function.Inverse.html" class="Module">Function.Inverse</a> <a id="378" class="Symbol">as</a> <a id="381" class="Module">Inv</a> <a id="385" class="Keyword">using</a> <a id="391" class="Symbol">(</a><a id="392" href="Function.Inverse.html#2222" class="Function Operator">_↔_</a><a id="395" class="Symbol">;</a> <a id="397" class="Keyword">module</a> <a id="404" href="Function.Inverse.html#1150" class="Module">Inverse</a><a id="411" class="Symbol">)</a>
<a id="413" class="Keyword">open</a> <a id="418" class="Keyword">import</a> <a id="425" href="Level.html" class="Module">Level</a>
<a id="431" class="Keyword">open</a> <a id="436" class="Keyword">import</a> <a id="443" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>

<a id="482" class="Comment">-- Half adjoint equivalences (see the HoTT book).</a>

<a id="533" class="Keyword">record</a> <a id="_≃_"></a><a id="540" href="Function.HalfAdjointEquivalence.html#540" class="Record Operator">_≃_</a> <a id="544" class="Symbol">{</a><a id="545" href="Function.HalfAdjointEquivalence.html#545" class="Bound">a</a> <a id="547" href="Function.HalfAdjointEquivalence.html#547" class="Bound">b</a><a id="548" class="Symbol">}</a> <a id="550" class="Symbol">(</a><a id="551" href="Function.HalfAdjointEquivalence.html#551" class="Bound">A</a> <a id="553" class="Symbol">:</a> <a id="555" class="PrimitiveType">Set</a> <a id="559" href="Function.HalfAdjointEquivalence.html#545" class="Bound">a</a><a id="560" class="Symbol">)</a> <a id="562" class="Symbol">(</a><a id="563" href="Function.HalfAdjointEquivalence.html#563" class="Bound">B</a> <a id="565" class="Symbol">:</a> <a id="567" class="PrimitiveType">Set</a> <a id="571" href="Function.HalfAdjointEquivalence.html#547" class="Bound">b</a><a id="572" class="Symbol">)</a> <a id="574" class="Symbol">:</a> <a id="576" class="PrimitiveType">Set</a> <a id="580" class="Symbol">(</a><a id="581" href="Function.HalfAdjointEquivalence.html#545" class="Bound">a</a> <a id="583" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="585" href="Function.HalfAdjointEquivalence.html#547" class="Bound">b</a><a id="586" class="Symbol">)</a> <a id="588" class="Keyword">where</a>
  <a id="596" class="Keyword">field</a>
    <a id="_≃_.to"></a><a id="606" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a>               <a id="623" class="Symbol">:</a> <a id="625" href="Function.HalfAdjointEquivalence.html#551" class="Bound">A</a> <a id="627" class="Symbol">→</a> <a id="629" href="Function.HalfAdjointEquivalence.html#563" class="Bound">B</a>
    <a id="_≃_.from"></a><a id="635" href="Function.HalfAdjointEquivalence.html#635" class="Field">from</a>             <a id="652" class="Symbol">:</a> <a id="654" href="Function.HalfAdjointEquivalence.html#563" class="Bound">B</a> <a id="656" class="Symbol">→</a> <a id="658" href="Function.HalfAdjointEquivalence.html#551" class="Bound">A</a>
    <a id="_≃_.left-inverse-of"></a><a id="664" href="Function.HalfAdjointEquivalence.html#664" class="Field">left-inverse-of</a>  <a id="681" class="Symbol">:</a> <a id="683" class="Symbol">∀</a> <a id="685" href="Function.HalfAdjointEquivalence.html#685" class="Bound">x</a> <a id="687" class="Symbol">→</a> <a id="689" href="Function.HalfAdjointEquivalence.html#635" class="Field">from</a> <a id="694" class="Symbol">(</a><a id="695" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="698" href="Function.HalfAdjointEquivalence.html#685" class="Bound">x</a><a id="699" class="Symbol">)</a> <a id="701" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="703" href="Function.HalfAdjointEquivalence.html#685" class="Bound">x</a>
    <a id="_≃_.right-inverse-of"></a><a id="709" href="Function.HalfAdjointEquivalence.html#709" class="Field">right-inverse-of</a> <a id="726" class="Symbol">:</a> <a id="728" class="Symbol">∀</a> <a id="730" href="Function.HalfAdjointEquivalence.html#730" class="Bound">x</a> <a id="732" class="Symbol">→</a> <a id="734" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="737" class="Symbol">(</a><a id="738" href="Function.HalfAdjointEquivalence.html#635" class="Field">from</a> <a id="743" href="Function.HalfAdjointEquivalence.html#730" class="Bound">x</a><a id="744" class="Symbol">)</a> <a id="746" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="748" href="Function.HalfAdjointEquivalence.html#730" class="Bound">x</a>
    <a id="_≃_.left-right"></a><a id="754" href="Function.HalfAdjointEquivalence.html#754" class="Field">left-right</a>       <a id="771" class="Symbol">:</a>
      <a id="779" class="Symbol">∀</a> <a id="781" href="Function.HalfAdjointEquivalence.html#781" class="Bound">x</a> <a id="783" class="Symbol">→</a> <a id="785" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="790" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="793" class="Symbol">(</a><a id="794" href="Function.HalfAdjointEquivalence.html#664" class="Field">left-inverse-of</a> <a id="810" href="Function.HalfAdjointEquivalence.html#781" class="Bound">x</a><a id="811" class="Symbol">)</a> <a id="813" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="815" href="Function.HalfAdjointEquivalence.html#709" class="Field">right-inverse-of</a> <a id="832" class="Symbol">(</a><a id="833" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="836" href="Function.HalfAdjointEquivalence.html#781" class="Bound">x</a><a id="837" class="Symbol">)</a>

  <a id="842" class="Comment">-- Half adjoint equivalences can be turned into inverses.</a>

  <a id="_≃_.inverse"></a><a id="903" href="Function.HalfAdjointEquivalence.html#903" class="Function">inverse</a> <a id="911" class="Symbol">:</a> <a id="913" href="Function.HalfAdjointEquivalence.html#551" class="Bound">A</a> <a id="915" href="Function.Inverse.html#2222" class="Function Operator">↔</a> <a id="917" href="Function.HalfAdjointEquivalence.html#563" class="Bound">B</a>
  <a id="921" href="Function.HalfAdjointEquivalence.html#903" class="Function">inverse</a> <a id="929" class="Symbol">=</a> <a id="931" href="Function.Inverse.html#2406" class="Function">Inv.inverse</a> <a id="943" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="946" href="Function.HalfAdjointEquivalence.html#635" class="Field">from</a> <a id="951" href="Function.HalfAdjointEquivalence.html#664" class="Field">left-inverse-of</a> <a id="967" href="Function.HalfAdjointEquivalence.html#709" class="Field">right-inverse-of</a>

  <a id="987" class="Comment">-- The forward direction of a half adjoint equivalence is injective.</a>

  <a id="_≃_.injective"></a><a id="1059" href="Function.HalfAdjointEquivalence.html#1059" class="Function">injective</a> <a id="1069" class="Symbol">:</a> <a id="1071" class="Symbol">∀</a> <a id="1073" class="Symbol">{</a><a id="1074" href="Function.HalfAdjointEquivalence.html#1074" class="Bound">x</a> <a id="1076" href="Function.HalfAdjointEquivalence.html#1076" class="Bound">y</a><a id="1077" class="Symbol">}</a> <a id="1079" class="Symbol">→</a> <a id="1081" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="1084" href="Function.HalfAdjointEquivalence.html#1074" class="Bound">x</a> <a id="1086" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1088" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="1091" href="Function.HalfAdjointEquivalence.html#1076" class="Bound">y</a> <a id="1093" class="Symbol">→</a> <a id="1095" href="Function.HalfAdjointEquivalence.html#1074" class="Bound">x</a> <a id="1097" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1099" href="Function.HalfAdjointEquivalence.html#1076" class="Bound">y</a>
  <a id="1103" href="Function.HalfAdjointEquivalence.html#1059" class="Function">injective</a> <a id="1113" class="Symbol">{</a><a id="1114" href="Function.HalfAdjointEquivalence.html#1114" class="Bound">x</a><a id="1115" class="Symbol">}</a> <a id="1117" class="Symbol">{</a><a id="1118" href="Function.HalfAdjointEquivalence.html#1118" class="Bound">y</a><a id="1119" class="Symbol">}</a> <a id="1121" href="Function.HalfAdjointEquivalence.html#1121" class="Bound">to-x≡to-y</a> <a id="1131" class="Symbol">=</a>
    <a id="1137" href="Function.HalfAdjointEquivalence.html#1114" class="Bound">x</a>            <a id="1150" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="1153" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="1157" class="Symbol">(</a><a id="1158" href="Function.HalfAdjointEquivalence.html#664" class="Field">left-inverse-of</a> <a id="1174" class="Symbol">_)</a> <a id="1177" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
    <a id="1183" href="Function.HalfAdjointEquivalence.html#635" class="Field">from</a> <a id="1188" class="Symbol">(</a><a id="1189" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="1192" href="Function.HalfAdjointEquivalence.html#1114" class="Bound">x</a><a id="1193" class="Symbol">)</a>  <a id="1196" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="1199" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="1204" href="Function.HalfAdjointEquivalence.html#635" class="Field">from</a> <a id="1209" href="Function.HalfAdjointEquivalence.html#1121" class="Bound">to-x≡to-y</a> <a id="1219" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
    <a id="1225" href="Function.HalfAdjointEquivalence.html#635" class="Field">from</a> <a id="1230" class="Symbol">(</a><a id="1231" href="Function.HalfAdjointEquivalence.html#606" class="Field">to</a> <a id="1234" href="Function.HalfAdjointEquivalence.html#1118" class="Bound">y</a><a id="1235" class="Symbol">)</a>  <a id="1238" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="1241" href="Function.HalfAdjointEquivalence.html#664" class="Field">left-inverse-of</a> <a id="1257" class="Symbol">_</a> <a id="1259" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
    <a id="1265" href="Function.HalfAdjointEquivalence.html#1118" class="Bound">y</a>            <a id="1278" href="Relation.Binary.PropositionalEquality.html#4575" class="Function Operator">∎</a>
    <a id="1284" class="Keyword">where</a>
    <a id="1294" class="Keyword">open</a> <a id="1299" href="Relation.Binary.PropositionalEquality.html#4279" class="Module">≡-Reasoning</a>

<a id="1312" class="Comment">-- Inverses can be turned into half adjoint equivalences.</a>
<a id="1370" class="Comment">--</a>
<a id="1373" class="Comment">-- (This proof is based on one in the HoTT book.)</a>

<a id="↔→≃"></a><a id="1424" href="Function.HalfAdjointEquivalence.html#1424" class="Function">↔→≃</a> <a id="1428" class="Symbol">:</a> <a id="1430" class="Symbol">∀</a> <a id="1432" class="Symbol">{</a><a id="1433" href="Function.HalfAdjointEquivalence.html#1433" class="Bound">a</a> <a id="1435" href="Function.HalfAdjointEquivalence.html#1435" class="Bound">b</a><a id="1436" class="Symbol">}</a> <a id="1438" class="Symbol">{</a><a id="1439" href="Function.HalfAdjointEquivalence.html#1439" class="Bound">A</a> <a id="1441" class="Symbol">:</a> <a id="1443" class="PrimitiveType">Set</a> <a id="1447" href="Function.HalfAdjointEquivalence.html#1433" class="Bound">a</a><a id="1448" class="Symbol">}</a> <a id="1450" class="Symbol">{</a><a id="1451" href="Function.HalfAdjointEquivalence.html#1451" class="Bound">B</a> <a id="1453" class="Symbol">:</a> <a id="1455" class="PrimitiveType">Set</a> <a id="1459" href="Function.HalfAdjointEquivalence.html#1435" class="Bound">b</a><a id="1460" class="Symbol">}</a> <a id="1462" class="Symbol">→</a> <a id="1464" href="Function.HalfAdjointEquivalence.html#1439" class="Bound">A</a> <a id="1466" href="Function.Inverse.html#2222" class="Function Operator">↔</a> <a id="1468" href="Function.HalfAdjointEquivalence.html#1451" class="Bound">B</a> <a id="1470" class="Symbol">→</a> <a id="1472" href="Function.HalfAdjointEquivalence.html#1439" class="Bound">A</a> <a id="1474" href="Function.HalfAdjointEquivalence.html#540" class="Record Operator">≃</a> <a id="1476" href="Function.HalfAdjointEquivalence.html#1451" class="Bound">B</a>
<a id="1478" href="Function.HalfAdjointEquivalence.html#1424" class="Function">↔→≃</a> <a id="1482" href="Function.HalfAdjointEquivalence.html#1482" class="Bound">A↔B</a> <a id="1486" class="Symbol">=</a> <a id="1488" class="Keyword">record</a>
  <a id="1497" class="Symbol">{</a> <a id="1499" class="Field">to</a>               <a id="1516" class="Symbol">=</a> <a id="1518" href="Function.Inverse.html#1288" class="Field">to</a>   <a id="1523" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a>
  <a id="1530" class="Symbol">;</a> <a id="1532" class="Field">from</a>             <a id="1549" class="Symbol">=</a> <a id="1551" href="Function.Inverse.html#1315" class="Field">from</a> <a id="1556" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a>
  <a id="1563" class="Symbol">;</a> <a id="1565" class="Field">left-inverse-of</a>  <a id="1582" class="Symbol">=</a> <a id="1584" href="Function.Inverse.html#933" class="Function">left-inverse-of</a>
  <a id="1602" class="Symbol">;</a> <a id="1604" class="Field">right-inverse-of</a> <a id="1621" class="Symbol">=</a> <a id="1623" href="Function.HalfAdjointEquivalence.html#1774" class="Function">right-inverse-of</a>
  <a id="1642" class="Symbol">;</a> <a id="1644" class="Field">left-right</a>       <a id="1661" class="Symbol">=</a> <a id="1663" href="Function.HalfAdjointEquivalence.html#2133" class="Function">left-right</a>
  <a id="1676" class="Symbol">}</a>
  <a id="1680" class="Keyword">where</a>
  <a id="1688" class="Keyword">open</a> <a id="1693" href="Relation.Binary.PropositionalEquality.html#4279" class="Module">≡-Reasoning</a>
  <a id="1707" class="Keyword">open</a> <a id="1712" class="Keyword">module</a> <a id="A↔B"></a><a id="1719" href="Function.HalfAdjointEquivalence.html#1719" class="Module">A↔B</a> <a id="1723" class="Symbol">=</a> <a id="1725" href="Function.Inverse.html#1150" class="Module">Inverse</a> <a id="1733" href="Function.HalfAdjointEquivalence.html#1482" class="Bound">A↔B</a> <a id="1737" class="Keyword">using</a> <a id="1743" class="Symbol">(</a><a id="1744" href="Function.Inverse.html#1288" class="Field">to</a><a id="1746" class="Symbol">;</a> <a id="1748" href="Function.Inverse.html#1315" class="Field">from</a><a id="1752" class="Symbol">;</a> <a id="1754" href="Function.Inverse.html#933" class="Function">left-inverse-of</a><a id="1769" class="Symbol">)</a>

  <a id="1774" href="Function.HalfAdjointEquivalence.html#1774" class="Function">right-inverse-of</a> <a id="1791" class="Symbol">:</a> <a id="1793" class="Symbol">∀</a> <a id="1795" href="Function.HalfAdjointEquivalence.html#1795" class="Bound">x</a> <a id="1797" class="Symbol">→</a> <a id="1799" href="Function.Inverse.html#1288" class="Field">to</a> <a id="1802" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="1806" class="Symbol">(</a><a id="1807" href="Function.Inverse.html#1315" class="Field">from</a> <a id="1812" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="1816" href="Function.HalfAdjointEquivalence.html#1795" class="Bound">x</a><a id="1817" class="Symbol">)</a> <a id="1819" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1821" href="Function.HalfAdjointEquivalence.html#1795" class="Bound">x</a>
  <a id="1825" href="Function.HalfAdjointEquivalence.html#1774" class="Function">right-inverse-of</a> <a id="1842" href="Function.HalfAdjointEquivalence.html#1842" class="Bound">x</a> <a id="1844" class="Symbol">=</a>
    <a id="1850" href="Function.Inverse.html#1288" class="Field">to</a> <a id="1853" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="1857" class="Symbol">(</a><a id="1858" href="Function.Inverse.html#1315" class="Field">from</a> <a id="1863" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="1867" href="Function.HalfAdjointEquivalence.html#1842" class="Bound">x</a><a id="1868" class="Symbol">)</a>                      <a id="1891" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="1894" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="1898" class="Symbol">(</a><a id="1899" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="1920" class="Symbol">_)</a> <a id="1923" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
    <a id="1929" href="Function.Inverse.html#1288" class="Field">to</a> <a id="1932" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="1936" class="Symbol">(</a><a id="1937" href="Function.Inverse.html#1315" class="Field">from</a> <a id="1942" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="1946" class="Symbol">(</a><a id="1947" href="Function.Inverse.html#1288" class="Field">to</a> <a id="1950" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="1954" class="Symbol">(</a><a id="1955" href="Function.Inverse.html#1315" class="Field">from</a> <a id="1960" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="1964" href="Function.HalfAdjointEquivalence.html#1842" class="Bound">x</a><a id="1965" class="Symbol">)))</a>  <a id="1970" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="1973" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="1978" class="Symbol">(</a><a id="1979" href="Function.Inverse.html#1288" class="Field">to</a> <a id="1982" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="1986" class="Symbol">)</a> <a id="1988" class="Symbol">(</a><a id="1989" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="2005" class="Symbol">_)</a> <a id="2008" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
    <a id="2014" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2017" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="2021" class="Symbol">(</a><a id="2022" href="Function.Inverse.html#1315" class="Field">from</a> <a id="2027" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="2031" href="Function.HalfAdjointEquivalence.html#1842" class="Bound">x</a><a id="2032" class="Symbol">)</a>                      <a id="2055" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="2058" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2079" class="Symbol">_</a> <a id="2081" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
    <a id="2087" href="Function.HalfAdjointEquivalence.html#1842" class="Bound">x</a>                                        <a id="2128" href="Relation.Binary.PropositionalEquality.html#4575" class="Function Operator">∎</a>

  <a id="2133" href="Function.HalfAdjointEquivalence.html#2133" class="Function">left-right</a> <a id="2144" class="Symbol">:</a>
    <a id="2150" class="Symbol">∀</a> <a id="2152" href="Function.HalfAdjointEquivalence.html#2152" class="Bound">x</a> <a id="2154" class="Symbol">→</a>
    <a id="2160" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2165" class="Symbol">(</a><a id="2166" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2169" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="2173" class="Symbol">)</a> <a id="2175" class="Symbol">(</a><a id="2176" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="2192" href="Function.HalfAdjointEquivalence.html#2152" class="Bound">x</a><a id="2193" class="Symbol">)</a> <a id="2195" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="2197" href="Function.HalfAdjointEquivalence.html#1774" class="Function">right-inverse-of</a> <a id="2214" class="Symbol">(</a><a id="2215" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2218" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="2222" href="Function.HalfAdjointEquivalence.html#2152" class="Bound">x</a><a id="2223" class="Symbol">)</a>
  <a id="2227" href="Function.HalfAdjointEquivalence.html#2133" class="Function">left-right</a> <a id="2238" href="Function.HalfAdjointEquivalence.html#2238" class="Bound">x</a> <a id="2240" class="Symbol">=</a>
    <a id="2246" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2251" class="Symbol">(</a><a id="2252" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2255" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="2259" class="Symbol">)</a> <a id="2261" class="Symbol">(</a><a id="2262" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="2278" href="Function.HalfAdjointEquivalence.html#2238" class="Bound">x</a><a id="2279" class="Symbol">)</a>               <a id="2295" href="Relation.Binary.PropositionalEquality.html#4431" class="Function Operator">≡⟨⟩</a>

    <a id="2304" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2310" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a> <a id="2315" class="Symbol">(</a><a id="2316" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2321" class="Symbol">(</a><a id="2322" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2325" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="2329" class="Symbol">)</a> <a id="2331" class="Symbol">(</a><a id="2332" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="2348" class="Symbol">_))</a>  <a id="2353" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="2356" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2361" class="Symbol">(λ</a> <a id="2364" href="Function.HalfAdjointEquivalence.html#2364" class="Bound">p</a> <a id="2366" class="Symbol">→</a> <a id="2368" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2374" href="Function.HalfAdjointEquivalence.html#2364" class="Bound">p</a> <a id="2376" class="Symbol">(</a><a id="2377" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2382" class="Symbol">(</a><a id="2383" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2386" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="2390" class="Symbol">)</a> <a id="2392" class="Symbol">_))</a>
                                                          <a id="2454" class="Symbol">(</a><a id="2455" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="2459" class="Symbol">(</a><a id="2460" href="Relation.Binary.PropositionalEquality.html#6152" class="Function">trans-symˡ</a> <a id="2471" class="Symbol">(</a><a id="2472" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2493" class="Symbol">_)))</a> <a id="2498" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
    <a id="2504" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2510" class="Symbol">(</a><a id="2511" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2517" class="Symbol">(</a><a id="2518" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="2522" class="Symbol">(</a><a id="2523" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2544" class="Symbol">_))</a>
               <a id="2563" class="Symbol">(</a><a id="2564" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2585" class="Symbol">_))</a>
      <a id="2595" class="Symbol">(</a><a id="2596" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2601" class="Symbol">(</a><a id="2602" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2605" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="2609" class="Symbol">)</a> <a id="2611" class="Symbol">(</a><a id="2612" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="2628" class="Symbol">_))</a>           <a id="2642" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="2645" href="Relation.Binary.PropositionalEquality.html#5995" class="Function">trans-assoc</a> <a id="2657" class="Symbol">(</a><a id="2658" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="2662" class="Symbol">(</a><a id="2663" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2684" class="Symbol">_))</a> <a id="2688" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>

    <a id="2695" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2701" class="Symbol">(</a><a id="2702" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="2706" class="Symbol">(</a><a id="2707" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2728" class="Symbol">_))</a>
      <a id="2738" class="Symbol">(</a><a id="2739" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2745" class="Symbol">(</a><a id="2746" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2767" class="Symbol">_)</a>
         <a id="2779" class="Symbol">(</a><a id="2780" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2785" class="Symbol">(</a><a id="2786" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2789" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="2793" class="Symbol">)</a> <a id="2795" class="Symbol">(</a><a id="2796" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="2812" class="Symbol">_)))</a>       <a id="2823" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="2826" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2831" class="Symbol">(</a><a id="2832" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2838" class="Symbol">(</a><a id="2839" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="2843" class="Symbol">(</a><a id="2844" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2865" class="Symbol">_)))</a> <a id="2870" href="Function.HalfAdjointEquivalence.html#3097" class="Function">lemma</a> <a id="2876" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>

    <a id="2883" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2889" class="Symbol">(</a><a id="2890" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="2894" class="Symbol">(</a><a id="2895" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="2916" class="Symbol">_))</a>
      <a id="2926" class="Symbol">(</a><a id="2927" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2933" class="Symbol">(</a><a id="2934" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="2939" class="Symbol">(</a><a id="2940" href="Function.Inverse.html#1288" class="Field">to</a> <a id="2943" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="2947" class="Symbol">)</a> <a id="2949" class="Symbol">(</a><a id="2950" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="2966" class="Symbol">_))</a>
         <a id="2979" class="Symbol">(</a><a id="2980" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="2986" class="Symbol">(</a><a id="2987" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="3008" class="Symbol">_)</a> <a id="3011" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a><a id="3015" class="Symbol">))</a>      <a id="3023" href="Relation.Binary.PropositionalEquality.html#4431" class="Function Operator">≡⟨⟩</a>

    <a id="3032" href="Function.HalfAdjointEquivalence.html#1774" class="Function">right-inverse-of</a> <a id="3049" class="Symbol">(</a><a id="3050" href="Function.Inverse.html#1288" class="Field">to</a> <a id="3053" href="Function.Equality.html#824" class="Field Operator">⟨$⟩</a> <a id="3057" href="Function.HalfAdjointEquivalence.html#2238" class="Bound">x</a><a id="3058" class="Symbol">)</a>                      <a id="3081" href="Relation.Binary.PropositionalEquality.html#4575" class="Function Operator">∎</a>
    <a id="3087" class="Keyword">where</a>
    <a id="3097" href="Function.HalfAdjointEquivalence.html#3097" class="Function">lemma</a> <a id="3103" class="Symbol">=</a>
      <a id="3111" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="3117" class="Symbol">(</a><a id="3118" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="3139" class="Symbol">_)</a>
        <a id="3150" class="Symbol">(</a><a id="3151" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3156" class="Symbol">(</a><a id="3157" href="Function.Inverse.html#1288" class="Field">to</a> <a id="3160" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3164" class="Symbol">)</a> <a id="3166" class="Symbol">(</a><a id="3167" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="3183" class="Symbol">_))</a>             <a id="3199" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="3202" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3207" class="Symbol">(</a><a id="3208" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="3214" class="Symbol">(</a><a id="3215" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="3236" class="Symbol">_))</a> <a id="3240" class="Symbol">(</a><a id="3241" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="3245" class="Symbol">(</a><a id="3246" href="Relation.Binary.PropositionalEquality.html#6364" class="Function">cong-id</a> <a id="3254" class="Symbol">_))</a> <a id="3258" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>

      <a id="3267" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="3273" class="Symbol">(</a><a id="3274" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="3295" class="Symbol">_)</a>
        <a id="3306" class="Symbol">(</a><a id="3307" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3312" href="Function.html#1097" class="Function">id</a> <a id="3315" class="Symbol">(</a><a id="3316" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3321" class="Symbol">(</a><a id="3322" href="Function.Inverse.html#1288" class="Field">to</a> <a id="3325" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3329" class="Symbol">)</a> <a id="3331" class="Symbol">(</a><a id="3332" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="3348" class="Symbol">_)))</a>   <a id="3355" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="3358" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="3362" class="Symbol">(</a><a id="3363" href="Relation.Binary.PropositionalEquality.html#7686" class="Function">naturality</a> <a id="3374" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a><a id="3394" class="Symbol">)</a> <a id="3396" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>

      <a id="3405" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="3411" class="Symbol">(</a><a id="3412" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3417" class="Symbol">((</a><a id="3419" href="Function.Inverse.html#1288" class="Field">to</a> <a id="3422" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3426" class="Symbol">)</a> <a id="3428" href="Function.html#798" class="Function Operator">∘</a> <a id="3430" class="Symbol">(</a><a id="3431" href="Function.Inverse.html#1315" class="Field">from</a> <a id="3436" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3440" class="Symbol">))</a>
                 <a id="3460" class="Symbol">(</a><a id="3461" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3466" class="Symbol">(</a><a id="3467" href="Function.Inverse.html#1288" class="Field">to</a> <a id="3470" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3474" class="Symbol">)</a> <a id="3476" class="Symbol">(</a><a id="3477" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="3493" class="Symbol">_)))</a>
        <a id="3506" class="Symbol">(</a><a id="3507" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="3528" class="Symbol">_)</a>                         <a id="3555" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="3558" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3563" class="Symbol">(λ</a> <a id="3566" href="Function.HalfAdjointEquivalence.html#3566" class="Bound">p</a> <a id="3568" class="Symbol">→</a> <a id="3570" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="3576" href="Function.HalfAdjointEquivalence.html#3566" class="Bound">p</a> <a id="3578" class="Symbol">(</a><a id="3579" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="3600" class="Symbol">_))</a>
                                                              <a id="3666" class="Symbol">(</a><a id="3667" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="3671" class="Symbol">(</a><a id="3672" href="Relation.Binary.PropositionalEquality.html#6455" class="Function">cong-∘</a> <a id="3679" class="Symbol">_))</a> <a id="3683" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
      <a id="3691" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="3697" class="Symbol">(</a><a id="3698" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3703" class="Symbol">((</a><a id="3705" href="Function.Inverse.html#1288" class="Field">to</a> <a id="3708" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3712" class="Symbol">)</a> <a id="3714" href="Function.html#798" class="Function Operator">∘</a> <a id="3716" class="Symbol">(</a><a id="3717" href="Function.Inverse.html#1315" class="Field">from</a> <a id="3722" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3726" class="Symbol">)</a> <a id="3728" href="Function.html#798" class="Function Operator">∘</a> <a id="3730" class="Symbol">(</a><a id="3731" href="Function.Inverse.html#1288" class="Field">to</a> <a id="3734" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3738" class="Symbol">))</a>
                      <a id="3763" class="Symbol">(</a><a id="3764" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="3780" class="Symbol">_))</a>
        <a id="3792" class="Symbol">(</a><a id="3793" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="3814" class="Symbol">_)</a>                         <a id="3841" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="3844" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3849" class="Symbol">(λ</a> <a id="3852" href="Function.HalfAdjointEquivalence.html#3852" class="Bound">p</a> <a id="3854" class="Symbol">→</a> <a id="3856" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="3862" href="Function.HalfAdjointEquivalence.html#3852" class="Bound">p</a> <a id="3864" class="Symbol">(</a><a id="3865" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="3886" class="Symbol">_))</a>
                                                              <a id="3952" class="Symbol">(</a><a id="3953" href="Relation.Binary.PropositionalEquality.html#6455" class="Function">cong-∘</a> <a id="3960" class="Symbol">_)</a> <a id="3963" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
      <a id="3971" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="3977" class="Symbol">(</a><a id="3978" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3983" class="Symbol">(</a><a id="3984" href="Function.Inverse.html#1288" class="Field">to</a> <a id="3987" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="3991" class="Symbol">)</a>
                 <a id="4010" class="Symbol">(</a><a id="4011" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="4016" class="Symbol">((</a><a id="4018" href="Function.Inverse.html#1315" class="Field">from</a> <a id="4023" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="4027" class="Symbol">)</a> <a id="4029" href="Function.html#798" class="Function Operator">∘</a> <a id="4031" class="Symbol">(</a><a id="4032" href="Function.Inverse.html#1288" class="Field">to</a> <a id="4035" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="4039" class="Symbol">))</a>
                    <a id="4062" class="Symbol">(</a><a id="4063" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="4079" class="Symbol">_)))</a>
        <a id="4092" class="Symbol">(</a><a id="4093" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="4114" class="Symbol">_)</a>                         <a id="4141" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="4144" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="4149" class="Symbol">(λ</a> <a id="4152" href="Function.HalfAdjointEquivalence.html#4152" class="Bound">p</a> <a id="4154" class="Symbol">→</a> <a id="4156" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="4162" class="Symbol">(</a><a id="4163" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="4168" class="Symbol">(</a><a id="4169" href="Function.Inverse.html#1288" class="Field">to</a> <a id="4172" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="4176" class="Symbol">)</a> <a id="4178" href="Function.HalfAdjointEquivalence.html#4152" class="Bound">p</a><a id="4179" class="Symbol">)</a> <a id="4181" class="Symbol">_)</a>
                                                              <a id="4246" class="Symbol">(</a><a id="4247" href="Relation.Binary.PropositionalEquality.html#8062" class="Function">cong-≡id</a> <a id="4256" href="Function.Inverse.html#933" class="Function">left-inverse-of</a><a id="4271" class="Symbol">)</a> <a id="4273" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
      <a id="4281" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="4287" class="Symbol">(</a><a id="4288" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="4293" class="Symbol">(</a><a id="4294" href="Function.Inverse.html#1288" class="Field">to</a> <a id="4297" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="4301" class="Symbol">)</a> <a id="4303" class="Symbol">(</a><a id="4304" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="4320" class="Symbol">_))</a>
        <a id="4332" class="Symbol">(</a><a id="4333" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="4354" class="Symbol">_)</a>                         <a id="4381" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">≡⟨</a> <a id="4384" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="4389" class="Symbol">(</a><a id="4390" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="4396" class="Symbol">(</a><a id="4397" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="4402" class="Symbol">(</a><a id="4403" href="Function.Inverse.html#1288" class="Field">to</a> <a id="4406" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="4410" class="Symbol">)</a> <a id="4412" class="Symbol">(</a><a id="4413" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="4429" class="Symbol">_)))</a>
                                                              <a id="4496" class="Symbol">(</a><a id="4497" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="4501" class="Symbol">(</a><a id="4502" href="Relation.Binary.PropositionalEquality.html#5893" class="Function">trans-reflʳ</a> <a id="4514" class="Symbol">_))</a> <a id="4518" href="Relation.Binary.PropositionalEquality.html#4490" class="Function Operator">⟩</a>
      <a id="4526" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="4532" class="Symbol">(</a><a id="4533" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="4538" class="Symbol">(</a><a id="4539" href="Function.Inverse.html#1288" class="Field">to</a> <a id="4542" href="Function.Equality.html#824" class="Field Operator">⟨$⟩_</a><a id="4546" class="Symbol">)</a> <a id="4548" class="Symbol">(</a><a id="4549" href="Function.Inverse.html#933" class="Function">left-inverse-of</a> <a id="4565" class="Symbol">_))</a>
        <a id="4577" class="Symbol">(</a><a id="4578" href="Relation.Binary.PropositionalEquality.Core.html#916" class="Function">trans</a> <a id="4584" class="Symbol">(</a><a id="4585" href="Function.Inverse.html#979" class="Function">A↔B.right-inverse-of</a> <a id="4606" class="Symbol">_)</a> <a id="4609" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a><a id="4613" class="Symbol">)</a>            <a id="4626" href="Relation.Binary.PropositionalEquality.html#4575" class="Function Operator">∎</a>
</pre></body></html>