<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Table.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Table-related properties</a>
<a id="134" class="Comment">------------------------------------------------------------------------</a>

<a id="208" class="Symbol">{-#</a> <a id="212" class="Keyword">OPTIONS</a> <a id="220" class="Option">--without-K</a> <a id="232" class="Option">--safe</a> <a id="239" class="Symbol">#-}</a>

<a id="244" class="Keyword">module</a> <a id="251" href="Data.Table.Properties.html" class="Module">Data.Table.Properties</a> <a id="273" class="Keyword">where</a>

<a id="280" class="Keyword">open</a> <a id="285" class="Keyword">import</a> <a id="292" href="Data.Table.html" class="Module">Data.Table</a>
<a id="303" class="Keyword">open</a> <a id="308" class="Keyword">import</a> <a id="315" href="Data.Table.Relation.Equality.html" class="Module">Data.Table.Relation.Equality</a>

<a id="345" class="Keyword">open</a> <a id="350" class="Keyword">import</a> <a id="357" href="Data.Bool.html" class="Module">Data.Bool</a> <a id="367" class="Keyword">using</a> <a id="373" class="Symbol">(</a><a id="374" href="Agda.Builtin.Bool.html#92" class="InductiveConstructor">true</a><a id="378" class="Symbol">;</a> <a id="380" href="Agda.Builtin.Bool.html#86" class="InductiveConstructor">false</a><a id="385" class="Symbol">;</a> <a id="387" href="Data.Bool.Base.html#835" class="Function Operator">if_then_else_</a><a id="400" class="Symbol">)</a>
<a id="402" class="Keyword">open</a> <a id="407" class="Keyword">import</a> <a id="414" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="423" class="Keyword">using</a> <a id="429" class="Symbol">(</a><a id="430" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="434" class="Symbol">;</a> <a id="436" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a><a id="439" class="Symbol">)</a>
<a id="441" class="Keyword">open</a> <a id="446" class="Keyword">import</a> <a id="453" href="Data.Empty.html" class="Module">Data.Empty</a> <a id="464" class="Keyword">using</a> <a id="470" class="Symbol">(</a><a id="471" href="Data.Empty.html#396" class="Function">⊥-elim</a><a id="477" class="Symbol">)</a>
<a id="479" class="Keyword">open</a> <a id="484" class="Keyword">import</a> <a id="491" href="Data.Fin.html" class="Module">Data.Fin</a> <a id="500" class="Keyword">using</a> <a id="506" class="Symbol">(</a><a id="507" href="Data.Fin.Base.html#951" class="Datatype">Fin</a><a id="510" class="Symbol">;</a> <a id="512" href="Data.Fin.Base.html#1004" class="InductiveConstructor">suc</a><a id="515" class="Symbol">;</a> <a id="517" href="Data.Fin.Base.html#973" class="InductiveConstructor">zero</a><a id="521" class="Symbol">;</a> <a id="523" href="Data.Fin.Properties.html#1730" class="Function Operator">_≟_</a><a id="526" class="Symbol">;</a> <a id="528" href="Data.Fin.Base.html#5468" class="Function">punchIn</a><a id="535" class="Symbol">)</a>
<a id="537" class="Keyword">import</a> <a id="544" href="Data.Fin.Properties.html" class="Module">Data.Fin.Properties</a> <a id="564" class="Symbol">as</a> <a id="567" class="Module">FP</a>
<a id="570" class="Keyword">open</a> <a id="575" class="Keyword">import</a> <a id="582" href="Data.Fin.Permutation.html" class="Module">Data.Fin.Permutation</a> <a id="603" class="Symbol">as</a> <a id="606" class="Module">Perm</a> <a id="611" class="Keyword">using</a> <a id="617" class="Symbol">(</a><a id="618" href="Data.Fin.Permutation.html#1365" class="Function">Permutation</a><a id="629" class="Symbol">;</a> <a id="631" href="Data.Fin.Permutation.html#1810" class="Function Operator">_⟨$⟩ʳ_</a><a id="637" class="Symbol">;</a> <a id="639" href="Data.Fin.Permutation.html#1890" class="Function Operator">_⟨$⟩ˡ_</a><a id="645" class="Symbol">)</a>
<a id="647" class="Keyword">open</a> <a id="652" class="Keyword">import</a> <a id="659" href="Data.List.html" class="Module">Data.List</a> <a id="669" class="Symbol">as</a> <a id="672" class="Module">L</a> <a id="674" class="Keyword">using</a> <a id="680" class="Symbol">(</a><a id="681" href="Agda.Builtin.List.html#80" class="Datatype">List</a><a id="685" class="Symbol">;</a> <a id="687" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">_∷_</a><a id="690" class="Symbol">;</a> <a id="692" href="Data.List.Base.html#9851" class="InductiveConstructor">[]</a><a id="694" class="Symbol">)</a>
<a id="696" class="Keyword">open</a> <a id="701" class="Keyword">import</a> <a id="708" href="Data.List.Any.html" class="Module">Data.List.Any</a> <a id="722" class="Keyword">using</a> <a id="728" class="Symbol">(</a><a id="729" href="Data.List.Any.html#947" class="InductiveConstructor">here</a><a id="733" class="Symbol">;</a> <a id="735" href="Data.List.Any.html#1000" class="InductiveConstructor">there</a><a id="740" class="Symbol">;</a> <a id="742" href="Data.List.Any.html#1641" class="Function">index</a><a id="747" class="Symbol">)</a>
<a id="749" class="Keyword">open</a> <a id="754" class="Keyword">import</a> <a id="761" href="Data.List.Membership.Propositional.html" class="Module">Data.List.Membership.Propositional</a> <a id="796" class="Keyword">using</a> <a id="802" class="Symbol">(</a><a id="803" href="Data.List.Membership.Setoid.html#867" class="Function Operator">_∈_</a><a id="806" class="Symbol">)</a>
<a id="808" class="Keyword">open</a> <a id="813" class="Keyword">import</a> <a id="820" href="Data.Product.html" class="Module">Data.Product</a> <a id="833" class="Symbol">as</a> <a id="836" class="Module">Product</a> <a id="844" class="Keyword">using</a> <a id="850" class="Symbol">(</a><a id="851" href="Agda.Builtin.Sigma.html#69" class="Record">Σ</a><a id="852" class="Symbol">;</a> <a id="854" href="Data.Product.html#917" class="Function">∃</a><a id="855" class="Symbol">;</a> <a id="857" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">_,_</a><a id="860" class="Symbol">;</a> <a id="862" href="Agda.Builtin.Sigma.html#155" class="Field">proj₁</a><a id="867" class="Symbol">;</a> <a id="869" href="Agda.Builtin.Sigma.html#167" class="Field">proj₂</a><a id="874" class="Symbol">)</a>
<a id="876" class="Keyword">open</a> <a id="881" class="Keyword">import</a> <a id="888" href="Data.Vec.html" class="Module">Data.Vec</a> <a id="897" class="Symbol">as</a> <a id="900" class="Module">V</a> <a id="902" class="Keyword">using</a> <a id="908" class="Symbol">(</a><a id="909" href="Data.Vec.html#773" class="Datatype">Vec</a><a id="912" class="Symbol">;</a> <a id="914" href="Data.Vec.html#832" class="InductiveConstructor Operator">_∷_</a><a id="917" class="Symbol">;</a> <a id="919" href="Data.Vec.html#813" class="InductiveConstructor">[]</a><a id="921" class="Symbol">)</a>
<a id="923" class="Keyword">import</a> <a id="930" href="Data.Vec.Properties.html" class="Module">Data.Vec.Properties</a> <a id="950" class="Symbol">as</a> <a id="953" class="Module">VP</a>
<a id="956" class="Keyword">open</a> <a id="961" class="Keyword">import</a> <a id="968" href="Function.html" class="Module">Function</a> <a id="977" class="Keyword">using</a> <a id="983" class="Symbol">(</a><a id="984" href="Function.html#805" class="Function Operator">_∘_</a><a id="987" class="Symbol">;</a> <a id="989" href="Function.html#1572" class="Function">flip</a><a id="993" class="Symbol">)</a>
<a id="995" class="Keyword">open</a> <a id="1000" class="Keyword">import</a> <a id="1007" href="Function.Inverse.html" class="Module">Function.Inverse</a> <a id="1024" class="Keyword">using</a> <a id="1030" class="Symbol">(</a><a id="1031" href="Function.Inverse.html#1157" class="Record">Inverse</a><a id="1038" class="Symbol">)</a>
<a id="1040" class="Keyword">open</a> <a id="1045" class="Keyword">import</a> <a id="1052" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="1090" class="Symbol">as</a> <a id="1093" class="Module">P</a>
  <a id="1097" class="Keyword">using</a> <a id="1103" class="Symbol">(</a><a id="1104" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">_≡_</a><a id="1107" class="Symbol">;</a> <a id="1109" href="Relation.Binary.PropositionalEquality.Core.html#696" class="Function Operator">_≢_</a><a id="1112" class="Symbol">;</a> <a id="1114" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a><a id="1118" class="Symbol">;</a> <a id="1120" href="Relation.Binary.PropositionalEquality.Core.html#874" class="Function">sym</a><a id="1123" class="Symbol">;</a> <a id="1125" href="Relation.Binary.PropositionalEquality.html#1134" class="Function">cong</a><a id="1129" class="Symbol">)</a>
<a id="1131" class="Keyword">open</a> <a id="1136" class="Keyword">import</a> <a id="1143" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="1160" class="Keyword">using</a> <a id="1166" class="Symbol">(</a><a id="1167" href="Relation.Nullary.html#606" class="InductiveConstructor">yes</a><a id="1170" class="Symbol">;</a> <a id="1172" href="Relation.Nullary.html#633" class="InductiveConstructor">no</a><a id="1174" class="Symbol">)</a>
<a id="1176" class="Keyword">open</a> <a id="1181" class="Keyword">import</a> <a id="1188" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="1214" class="Keyword">using</a> <a id="1220" class="Symbol">(</a><a id="1221" href="Relation.Nullary.Negation.html#596" class="Function">contradiction</a><a id="1234" class="Symbol">)</a>


<a id="1238" class="Comment">------------------------------------------------------------------------</a>
<a id="1311" class="Comment">-- select</a>

<a id="1322" class="Keyword">module</a> <a id="1329" href="Data.Table.Properties.html#1329" class="Module">_</a> <a id="1331" class="Symbol">{</a><a id="1332" href="Data.Table.Properties.html#1332" class="Bound">a</a><a id="1333" class="Symbol">}</a> <a id="1335" class="Symbol">{</a><a id="1336" href="Data.Table.Properties.html#1336" class="Bound">A</a> <a id="1338" class="Symbol">:</a> <a id="1340" class="PrimitiveType">Set</a> <a id="1344" href="Data.Table.Properties.html#1332" class="Bound">a</a><a id="1345" class="Symbol">}</a> <a id="1347" class="Keyword">where</a>

  <a id="1356" class="Comment">-- Selecting from any table is the same as selecting from a constant table.</a>

  <a id="1435" href="Data.Table.Properties.html#1435" class="Function">select-const</a> <a id="1448" class="Symbol">:</a> <a id="1450" class="Symbol">∀</a> <a id="1452" class="Symbol">{</a><a id="1453" href="Data.Table.Properties.html#1453" class="Bound">n</a><a id="1454" class="Symbol">}</a> <a id="1456" class="Symbol">(</a><a id="1457" href="Data.Table.Properties.html#1457" class="Bound">z</a> <a id="1459" class="Symbol">:</a> <a id="1461" href="Data.Table.Properties.html#1336" class="Bound">A</a><a id="1462" class="Symbol">)</a> <a id="1464" class="Symbol">(</a><a id="1465" href="Data.Table.Properties.html#1465" class="Bound">i</a> <a id="1467" class="Symbol">:</a> <a id="1469" href="Data.Fin.Base.html#951" class="Datatype">Fin</a> <a id="1473" href="Data.Table.Properties.html#1453" class="Bound">n</a><a id="1474" class="Symbol">)</a> <a id="1476" href="Data.Table.Properties.html#1476" class="Bound">t</a> <a id="1478" class="Symbol">→</a>
                 <a id="1497" href="Data.Table.html#1252" class="Function">select</a> <a id="1504" href="Data.Table.Properties.html#1457" class="Bound">z</a> <a id="1506" href="Data.Table.Properties.html#1465" class="Bound">i</a> <a id="1508" href="Data.Table.Properties.html#1476" class="Bound">t</a> <a id="1510" href="Relation.Binary.html#2019" class="Function Operator">≗</a> <a id="1512" href="Data.Table.html#1252" class="Function">select</a> <a id="1519" href="Data.Table.Properties.html#1457" class="Bound">z</a> <a id="1521" href="Data.Table.Properties.html#1465" class="Bound">i</a> <a id="1523" class="Symbol">(</a><a id="1524" href="Data.Table.Base.html#2220" class="Function">replicate</a> <a id="1534" class="Symbol">(</a><a id="1535" href="Data.Table.Base.html#650" class="Field">lookup</a> <a id="1542" href="Data.Table.Properties.html#1476" class="Bound">t</a> <a id="1544" href="Data.Table.Properties.html#1465" class="Bound">i</a><a id="1545" class="Symbol">))</a>
  <a id="1550" href="Data.Table.Properties.html#1435" class="Function">select-const</a> <a id="1563" href="Data.Table.Properties.html#1563" class="Bound">z</a> <a id="1565" href="Data.Table.Properties.html#1565" class="Bound">i</a> <a id="1567" href="Data.Table.Properties.html#1567" class="Bound">t</a> <a id="1569" href="Data.Table.Properties.html#1569" class="Bound">j</a> <a id="1571" class="Keyword">with</a> <a id="1576" href="Data.Table.Properties.html#1569" class="Bound">j</a> <a id="1578" href="Data.Fin.Properties.html#1730" class="Function Operator">≟</a> <a id="1580" href="Data.Table.Properties.html#1565" class="Bound">i</a>
  <a id="1584" class="Symbol">...</a> <a id="1588" class="Symbol">|</a> <a id="1590" href="Relation.Nullary.html#606" class="InductiveConstructor">yes</a> <a id="1594" class="Symbol">_</a> <a id="1596" class="Symbol">=</a> <a id="1598" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
  <a id="1605" class="Symbol">...</a> <a id="1609" class="Symbol">|</a> <a id="1611" href="Relation.Nullary.html#633" class="InductiveConstructor">no</a>  <a id="1615" class="Symbol">_</a> <a id="1617" class="Symbol">=</a> <a id="1619" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>

  <a id="1627" class="Comment">-- Selecting an element from a table then looking it up is the same as looking</a>
  <a id="1708" class="Comment">-- up the index in the original table</a>

  <a id="1749" href="Data.Table.Properties.html#1749" class="Function">select-lookup</a> <a id="1763" class="Symbol">:</a> <a id="1765" class="Symbol">∀</a> <a id="1767" class="Symbol">{</a><a id="1768" href="Data.Table.Properties.html#1768" class="Bound">n</a> <a id="1770" href="Data.Table.Properties.html#1770" class="Bound">x</a> <a id="1772" href="Data.Table.Properties.html#1772" class="Bound">i</a><a id="1773" class="Symbol">}</a> <a id="1775" class="Symbol">(</a><a id="1776" href="Data.Table.Properties.html#1776" class="Bound">t</a> <a id="1778" class="Symbol">:</a> <a id="1780" href="Data.Table.Base.html#581" class="Record">Table</a> <a id="1786" href="Data.Table.Properties.html#1336" class="Bound">A</a> <a id="1788" href="Data.Table.Properties.html#1768" class="Bound">n</a><a id="1789" class="Symbol">)</a> <a id="1791" class="Symbol">→</a>
                  <a id="1811" href="Data.Table.Base.html#650" class="Field">lookup</a> <a id="1818" class="Symbol">(</a><a id="1819" href="Data.Table.html#1252" class="Function">select</a> <a id="1826" href="Data.Table.Properties.html#1770" class="Bound">x</a> <a id="1828" href="Data.Table.Properties.html#1772" class="Bound">i</a> <a id="1830" href="Data.Table.Properties.html#1776" class="Bound">t</a><a id="1831" class="Symbol">)</a> <a id="1833" href="Data.Table.Properties.html#1772" class="Bound">i</a> <a id="1835" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1837" href="Data.Table.Base.html#650" class="Field">lookup</a> <a id="1844" href="Data.Table.Properties.html#1776" class="Bound">t</a> <a id="1846" href="Data.Table.Properties.html#1772" class="Bound">i</a>
  <a id="1850" href="Data.Table.Properties.html#1749" class="Function">select-lookup</a> <a id="1864" class="Symbol">{</a><a id="1865" class="Argument">i</a> <a id="1867" class="Symbol">=</a> <a id="1869" href="Data.Table.Properties.html#1869" class="Bound">i</a><a id="1870" class="Symbol">}</a> <a id="1872" href="Data.Table.Properties.html#1872" class="Bound">t</a> <a id="1874" class="Keyword">with</a> <a id="1879" href="Data.Table.Properties.html#1869" class="Bound">i</a> <a id="1881" href="Data.Fin.Properties.html#1730" class="Function Operator">≟</a> <a id="1883" href="Data.Table.Properties.html#1869" class="Bound">i</a>
  <a id="1887" class="Symbol">...</a> <a id="1891" class="Symbol">|</a> <a id="1893" href="Relation.Nullary.html#606" class="InductiveConstructor">yes</a> <a id="1897" class="Symbol">_</a>  <a id="1900" class="Symbol">=</a> <a id="1902" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
  <a id="1909" class="Symbol">...</a> <a id="1913" class="Symbol">|</a> <a id="1915" href="Relation.Nullary.html#633" class="InductiveConstructor">no</a> <a id="1918" href="Data.Table.Properties.html#1918" class="Bound">i≢i</a> <a id="1922" class="Symbol">=</a> <a id="1924" href="Relation.Nullary.Negation.html#596" class="Function">contradiction</a> <a id="1938" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a> <a id="1943" href="Data.Table.Properties.html#1918" class="Bound">i≢i</a>

  <a id="1950" class="Comment">-- Selecting an element from a table then removing the same element produces a</a>
  <a id="2031" class="Comment">-- constant table</a>

  <a id="2052" href="Data.Table.Properties.html#2052" class="Function">select-remove</a> <a id="2066" class="Symbol">:</a> <a id="2068" class="Symbol">∀</a> <a id="2070" class="Symbol">{</a><a id="2071" href="Data.Table.Properties.html#2071" class="Bound">n</a> <a id="2073" href="Data.Table.Properties.html#2073" class="Bound">x</a><a id="2074" class="Symbol">}</a> <a id="2076" href="Data.Table.Properties.html#2076" class="Bound">i</a> <a id="2078" class="Symbol">(</a><a id="2079" href="Data.Table.Properties.html#2079" class="Bound">t</a> <a id="2081" class="Symbol">:</a> <a id="2083" href="Data.Table.Base.html#581" class="Record">Table</a> <a id="2089" href="Data.Table.Properties.html#1336" class="Bound">A</a> <a id="2091" class="Symbol">(</a><a id="2092" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2096" href="Data.Table.Properties.html#2071" class="Bound">n</a><a id="2097" class="Symbol">))</a> <a id="2100" class="Symbol">→</a>
                  <a id="2120" href="Data.Table.Base.html#1043" class="Function">remove</a> <a id="2127" href="Data.Table.Properties.html#2076" class="Bound">i</a> <a id="2129" class="Symbol">(</a><a id="2130" href="Data.Table.html#1252" class="Function">select</a> <a id="2137" href="Data.Table.Properties.html#2073" class="Bound">x</a> <a id="2139" href="Data.Table.Properties.html#2076" class="Bound">i</a> <a id="2141" href="Data.Table.Properties.html#2079" class="Bound">t</a><a id="2142" class="Symbol">)</a> <a id="2144" href="Relation.Binary.html#2019" class="Function Operator">≗</a> <a id="2146" href="Data.Table.Base.html#2220" class="Function">replicate</a> <a id="2156" class="Symbol">{</a><a id="2157" class="Argument">n</a> <a id="2159" class="Symbol">=</a> <a id="2161" href="Data.Table.Properties.html#2071" class="Bound">n</a><a id="2162" class="Symbol">}</a> <a id="2164" href="Data.Table.Properties.html#2073" class="Bound">x</a>
  <a id="2168" href="Data.Table.Properties.html#2052" class="Function">select-remove</a> <a id="2182" href="Data.Table.Properties.html#2182" class="Bound">i</a> <a id="2184" href="Data.Table.Properties.html#2184" class="Bound">t</a> <a id="2186" href="Data.Table.Properties.html#2186" class="Bound">j</a> <a id="2188" class="Keyword">with</a> <a id="2193" href="Data.Fin.Base.html#5468" class="Function">punchIn</a> <a id="2201" href="Data.Table.Properties.html#2182" class="Bound">i</a> <a id="2203" href="Data.Table.Properties.html#2186" class="Bound">j</a> <a id="2205" href="Data.Fin.Properties.html#1730" class="Function Operator">≟</a> <a id="2207" href="Data.Table.Properties.html#2182" class="Bound">i</a>
  <a id="2211" class="Symbol">...</a> <a id="2215" class="Symbol">|</a> <a id="2217" href="Relation.Nullary.html#606" class="InductiveConstructor">yes</a> <a id="2221" href="Data.Table.Properties.html#2221" class="Bound">p</a> <a id="2223" class="Symbol">=</a> <a id="2225" href="Relation.Nullary.Negation.html#596" class="Function">contradiction</a> <a id="2239" href="Data.Table.Properties.html#2221" class="Bound">p</a> <a id="2241" class="Symbol">(</a><a id="2242" href="Data.Fin.Properties.html#12608" class="Function">FP.punchInᵢ≢i</a> <a id="2256" class="Symbol">_</a> <a id="2258" class="Symbol">_)</a>
  <a id="2263" class="Symbol">...</a> <a id="2267" class="Symbol">|</a> <a id="2269" href="Relation.Nullary.html#633" class="InductiveConstructor">no</a> <a id="2272" href="Data.Table.Properties.html#2272" class="Bound">¬p</a> <a id="2275" class="Symbol">=</a> <a id="2277" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>


<a id="2284" class="Comment">------------------------------------------------------------------------</a>
<a id="2357" class="Comment">-- permute</a>

  <a id="2371" class="Comment">-- Removing an index &#39;i&#39; from a table permuted with &#39;π&#39; is the same as</a>
  <a id="2444" class="Comment">-- removing the element, then permuting with &#39;π&#39; minus &#39;i&#39;.</a>

  <a id="2507" href="Data.Table.Properties.html#2507" class="Function">remove-permute</a> <a id="2522" class="Symbol">:</a> <a id="2524" class="Symbol">∀</a> <a id="2526" class="Symbol">{</a><a id="2527" href="Data.Table.Properties.html#2527" class="Bound">m</a> <a id="2529" href="Data.Table.Properties.html#2529" class="Bound">n</a><a id="2530" class="Symbol">}</a> <a id="2532" class="Symbol">(</a><a id="2533" href="Data.Table.Properties.html#2533" class="Bound">π</a> <a id="2535" class="Symbol">:</a> <a id="2537" href="Data.Fin.Permutation.html#1365" class="Function">Permutation</a> <a id="2549" class="Symbol">(</a><a id="2550" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2554" href="Data.Table.Properties.html#2527" class="Bound">m</a><a id="2555" class="Symbol">)</a> <a id="2557" class="Symbol">(</a><a id="2558" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2562" href="Data.Table.Properties.html#2529" class="Bound">n</a><a id="2563" class="Symbol">))</a>
                   <a id="2585" href="Data.Table.Properties.html#2585" class="Bound">i</a> <a id="2587" class="Symbol">(</a><a id="2588" href="Data.Table.Properties.html#2588" class="Bound">t</a> <a id="2590" class="Symbol">:</a> <a id="2592" href="Data.Table.Base.html#581" class="Record">Table</a> <a id="2598" href="Data.Table.Properties.html#1336" class="Bound">A</a> <a id="2600" class="Symbol">(</a><a id="2601" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2605" href="Data.Table.Properties.html#2529" class="Bound">n</a><a id="2606" class="Symbol">))</a> <a id="2609" class="Symbol">→</a>
                   <a id="2630" href="Data.Table.Base.html#1043" class="Function">remove</a> <a id="2637" class="Symbol">(</a><a id="2638" href="Data.Table.Properties.html#2533" class="Bound">π</a> <a id="2640" href="Data.Fin.Permutation.html#1890" class="Function Operator">⟨$⟩ˡ</a> <a id="2645" href="Data.Table.Properties.html#2585" class="Bound">i</a><a id="2646" class="Symbol">)</a> <a id="2648" class="Symbol">(</a><a id="2649" href="Data.Table.html#1030" class="Function">permute</a> <a id="2657" href="Data.Table.Properties.html#2533" class="Bound">π</a> <a id="2659" href="Data.Table.Properties.html#2588" class="Bound">t</a><a id="2660" class="Symbol">)</a>
                   <a id="2681" href="Relation.Binary.html#2019" class="Function Operator">≗</a> <a id="2683" href="Data.Table.html#1030" class="Function">permute</a> <a id="2691" class="Symbol">(</a><a id="2692" href="Data.Fin.Permutation.html#3253" class="Function">Perm.remove</a> <a id="2704" class="Symbol">(</a><a id="2705" href="Data.Table.Properties.html#2533" class="Bound">π</a> <a id="2707" href="Data.Fin.Permutation.html#1890" class="Function Operator">⟨$⟩ˡ</a> <a id="2712" href="Data.Table.Properties.html#2585" class="Bound">i</a><a id="2713" class="Symbol">)</a> <a id="2715" href="Data.Table.Properties.html#2533" class="Bound">π</a><a id="2716" class="Symbol">)</a> <a id="2718" class="Symbol">(</a><a id="2719" href="Data.Table.Base.html#1043" class="Function">remove</a> <a id="2726" href="Data.Table.Properties.html#2585" class="Bound">i</a> <a id="2728" href="Data.Table.Properties.html#2588" class="Bound">t</a><a id="2729" class="Symbol">)</a>
  <a id="2733" href="Data.Table.Properties.html#2507" class="Function">remove-permute</a> <a id="2748" href="Data.Table.Properties.html#2748" class="Bound">π</a> <a id="2750" href="Data.Table.Properties.html#2750" class="Bound">i</a> <a id="2752" href="Data.Table.Properties.html#2752" class="Bound">t</a> <a id="2754" href="Data.Table.Properties.html#2754" class="Bound">j</a> <a id="2756" class="Symbol">=</a> <a id="2758" href="Relation.Binary.PropositionalEquality.html#1134" class="Function">P.cong</a> <a id="2765" class="Symbol">(</a><a id="2766" href="Data.Table.Base.html#650" class="Field">lookup</a> <a id="2773" href="Data.Table.Properties.html#2752" class="Bound">t</a><a id="2774" class="Symbol">)</a> <a id="2776" class="Symbol">(</a><a id="2777" href="Data.Fin.Permutation.html#5750" class="Function">Perm.punchIn-permute′</a> <a id="2799" href="Data.Table.Properties.html#2748" class="Bound">π</a> <a id="2801" href="Data.Table.Properties.html#2750" class="Bound">i</a> <a id="2803" href="Data.Table.Properties.html#2754" class="Bound">j</a><a id="2804" class="Symbol">)</a>

<a id="2807" class="Comment">------------------------------------------------------------------------</a>
<a id="2880" class="Comment">-- fromList</a>

<a id="2893" class="Keyword">module</a> <a id="2900" href="Data.Table.Properties.html#2900" class="Module">_</a> <a id="2902" class="Symbol">{</a><a id="2903" href="Data.Table.Properties.html#2903" class="Bound">a</a><a id="2904" class="Symbol">}</a> <a id="2906" class="Symbol">{</a><a id="2907" href="Data.Table.Properties.html#2907" class="Bound">A</a> <a id="2909" class="Symbol">:</a> <a id="2911" class="PrimitiveType">Set</a> <a id="2915" href="Data.Table.Properties.html#2903" class="Bound">a</a><a id="2916" class="Symbol">}</a> <a id="2918" class="Keyword">where</a>

  <a id="2927" href="Data.Table.Properties.html#2927" class="Function">fromList-∈</a> <a id="2938" class="Symbol">:</a> <a id="2940" class="Symbol">∀</a> <a id="2942" class="Symbol">{</a><a id="2943" href="Data.Table.Properties.html#2943" class="Bound">xs</a> <a id="2946" class="Symbol">:</a> <a id="2948" href="Agda.Builtin.List.html#80" class="Datatype">List</a> <a id="2953" href="Data.Table.Properties.html#2907" class="Bound">A</a><a id="2954" class="Symbol">}</a> <a id="2956" class="Symbol">(</a><a id="2957" href="Data.Table.Properties.html#2957" class="Bound">i</a> <a id="2959" class="Symbol">:</a> <a id="2961" href="Data.Fin.Base.html#951" class="Datatype">Fin</a> <a id="2965" class="Symbol">(</a><a id="2966" href="Data.List.Base.html#4454" class="Function">L.length</a> <a id="2975" href="Data.Table.Properties.html#2943" class="Bound">xs</a><a id="2977" class="Symbol">))</a> <a id="2980" class="Symbol">→</a> <a id="2982" href="Data.Table.Base.html#650" class="Field">lookup</a> <a id="2989" class="Symbol">(</a><a id="2990" href="Data.Table.Base.html#2507" class="Function">fromList</a> <a id="2999" href="Data.Table.Properties.html#2943" class="Bound">xs</a><a id="3001" class="Symbol">)</a> <a id="3003" href="Data.Table.Properties.html#2957" class="Bound">i</a> <a id="3005" href="Data.List.Membership.Setoid.html#867" class="Function Operator">∈</a> <a id="3007" href="Data.Table.Properties.html#2943" class="Bound">xs</a>
  <a id="3012" href="Data.Table.Properties.html#2927" class="Function">fromList-∈</a> <a id="3023" class="Symbol">{</a><a id="3024" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a><a id="3026" class="Symbol">}</a>     <a id="3032" class="Symbol">()</a>
  <a id="3037" href="Data.Table.Properties.html#2927" class="Function">fromList-∈</a> <a id="3048" class="Symbol">{</a><a id="3049" href="Data.Table.Properties.html#3049" class="Bound">x</a> <a id="3051" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3053" href="Data.Table.Properties.html#3053" class="Bound">xs</a><a id="3055" class="Symbol">}</a> <a id="3057" href="Data.Fin.Base.html#973" class="InductiveConstructor">zero</a>    <a id="3065" class="Symbol">=</a> <a id="3067" href="Data.List.Any.html#947" class="InductiveConstructor">here</a> <a id="3072" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
  <a id="3079" href="Data.Table.Properties.html#2927" class="Function">fromList-∈</a> <a id="3090" class="Symbol">{</a><a id="3091" href="Data.Table.Properties.html#3091" class="Bound">x</a> <a id="3093" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3095" href="Data.Table.Properties.html#3095" class="Bound">xs</a><a id="3097" class="Symbol">}</a> <a id="3099" class="Symbol">(</a><a id="3100" href="Data.Fin.Base.html#1004" class="InductiveConstructor">suc</a> <a id="3104" href="Data.Table.Properties.html#3104" class="Bound">i</a><a id="3105" class="Symbol">)</a> <a id="3107" class="Symbol">=</a> <a id="3109" href="Data.List.Any.html#1000" class="InductiveConstructor">there</a> <a id="3115" class="Symbol">(</a><a id="3116" href="Data.Table.Properties.html#2927" class="Function">fromList-∈</a> <a id="3127" href="Data.Table.Properties.html#3104" class="Bound">i</a><a id="3128" class="Symbol">)</a>

  <a id="3133" href="Data.Table.Properties.html#3133" class="Function">index-fromList-∈</a> <a id="3150" class="Symbol">:</a> <a id="3152" class="Symbol">∀</a> <a id="3154" class="Symbol">{</a><a id="3155" href="Data.Table.Properties.html#3155" class="Bound">xs</a> <a id="3158" href="Data.Table.Properties.html#3158" class="Bound">i</a><a id="3159" class="Symbol">}</a> <a id="3161" class="Symbol">→</a> <a id="3163" href="Data.List.Any.html#1641" class="Function">index</a> <a id="3169" class="Symbol">(</a><a id="3170" href="Data.Table.Properties.html#2927" class="Function">fromList-∈</a> <a id="3181" class="Symbol">{</a><a id="3182" href="Data.Table.Properties.html#3155" class="Bound">xs</a><a id="3184" class="Symbol">}</a> <a id="3186" href="Data.Table.Properties.html#3158" class="Bound">i</a><a id="3187" class="Symbol">)</a> <a id="3189" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="3191" href="Data.Table.Properties.html#3158" class="Bound">i</a>
  <a id="3195" href="Data.Table.Properties.html#3133" class="Function">index-fromList-∈</a> <a id="3212" class="Symbol">{</a><a id="3213" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a><a id="3215" class="Symbol">}</a>     <a id="3221" class="Symbol">{()}</a>
  <a id="3228" href="Data.Table.Properties.html#3133" class="Function">index-fromList-∈</a> <a id="3245" class="Symbol">{</a><a id="3246" href="Data.Table.Properties.html#3246" class="Bound">x</a> <a id="3248" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3250" href="Data.Table.Properties.html#3250" class="Bound">xs</a><a id="3252" class="Symbol">}</a> <a id="3254" class="Symbol">{</a><a id="3255" href="Data.Fin.Base.html#973" class="InductiveConstructor">zero</a><a id="3259" class="Symbol">}</a>  <a id="3262" class="Symbol">=</a> <a id="3264" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
  <a id="3271" href="Data.Table.Properties.html#3133" class="Function">index-fromList-∈</a> <a id="3288" class="Symbol">{</a><a id="3289" href="Data.Table.Properties.html#3289" class="Bound">x</a> <a id="3291" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3293" href="Data.Table.Properties.html#3293" class="Bound">xs</a><a id="3295" class="Symbol">}</a> <a id="3297" class="Symbol">{</a><a id="3298" href="Data.Fin.Base.html#1004" class="InductiveConstructor">suc</a> <a id="3302" href="Data.Table.Properties.html#3302" class="Bound">i</a><a id="3303" class="Symbol">}</a> <a id="3305" class="Symbol">=</a> <a id="3307" href="Relation.Binary.PropositionalEquality.html#1134" class="Function">cong</a> <a id="3312" href="Data.Fin.Base.html#1004" class="InductiveConstructor">suc</a> <a id="3316" href="Data.Table.Properties.html#3133" class="Function">index-fromList-∈</a>

  <a id="3336" href="Data.Table.Properties.html#3336" class="Function">fromList-index</a> <a id="3351" class="Symbol">:</a> <a id="3353" class="Symbol">∀</a> <a id="3355" class="Symbol">{</a><a id="3356" href="Data.Table.Properties.html#3356" class="Bound">xs</a><a id="3358" class="Symbol">}</a> <a id="3360" class="Symbol">{</a><a id="3361" href="Data.Table.Properties.html#3361" class="Bound">x</a> <a id="3363" class="Symbol">:</a> <a id="3365" href="Data.Table.Properties.html#2907" class="Bound">A</a><a id="3366" class="Symbol">}</a> <a id="3368" class="Symbol">(</a><a id="3369" href="Data.Table.Properties.html#3369" class="Bound">x∈xs</a> <a id="3374" class="Symbol">:</a> <a id="3376" href="Data.Table.Properties.html#3361" class="Bound">x</a> <a id="3378" href="Data.List.Membership.Setoid.html#867" class="Function Operator">∈</a> <a id="3380" href="Data.Table.Properties.html#3356" class="Bound">xs</a><a id="3382" class="Symbol">)</a> <a id="3384" class="Symbol">→</a> <a id="3386" href="Data.Table.Base.html#650" class="Field">lookup</a> <a id="3393" class="Symbol">(</a><a id="3394" href="Data.Table.Base.html#2507" class="Function">fromList</a> <a id="3403" href="Data.Table.Properties.html#3356" class="Bound">xs</a><a id="3405" class="Symbol">)</a> <a id="3407" class="Symbol">(</a><a id="3408" href="Data.List.Any.html#1641" class="Function">index</a> <a id="3414" href="Data.Table.Properties.html#3369" class="Bound">x∈xs</a><a id="3418" class="Symbol">)</a> <a id="3420" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="3422" href="Data.Table.Properties.html#3361" class="Bound">x</a>
  <a id="3426" href="Data.Table.Properties.html#3336" class="Function">fromList-index</a> <a id="3441" class="Symbol">(</a><a id="3442" href="Data.List.Any.html#947" class="InductiveConstructor">here</a> <a id="3447" href="Data.Table.Properties.html#3447" class="Bound">px</a><a id="3449" class="Symbol">)</a>    <a id="3454" class="Symbol">=</a> <a id="3456" href="Relation.Binary.PropositionalEquality.Core.html#874" class="Function">sym</a> <a id="3460" href="Data.Table.Properties.html#3447" class="Bound">px</a>
  <a id="3465" href="Data.Table.Properties.html#3336" class="Function">fromList-index</a> <a id="3480" class="Symbol">(</a><a id="3481" href="Data.List.Any.html#1000" class="InductiveConstructor">there</a> <a id="3487" href="Data.Table.Properties.html#3487" class="Bound">x∈xs</a><a id="3491" class="Symbol">)</a> <a id="3493" class="Symbol">=</a> <a id="3495" href="Data.Table.Properties.html#3336" class="Function">fromList-index</a> <a id="3510" href="Data.Table.Properties.html#3487" class="Bound">x∈xs</a>


<a id="3517" class="Comment">------------------------------------------------------------------------</a>
<a id="3590" class="Comment">-- There exists an isomorphism between tables and vectors.</a>

<a id="3650" class="Keyword">module</a> <a id="3657" href="Data.Table.Properties.html#3657" class="Module">_</a> <a id="3659" class="Symbol">{</a><a id="3660" href="Data.Table.Properties.html#3660" class="Bound">a</a> <a id="3662" href="Data.Table.Properties.html#3662" class="Bound">n</a><a id="3663" class="Symbol">}</a> <a id="3665" class="Symbol">{</a><a id="3666" href="Data.Table.Properties.html#3666" class="Bound">A</a> <a id="3668" class="Symbol">:</a> <a id="3670" class="PrimitiveType">Set</a> <a id="3674" href="Data.Table.Properties.html#3660" class="Bound">a</a><a id="3675" class="Symbol">}</a> <a id="3677" class="Keyword">where</a>

  <a id="3686" href="Data.Table.Properties.html#3686" class="Function">↔Vec</a> <a id="3691" class="Symbol">:</a> <a id="3693" href="Function.Inverse.html#1157" class="Record">Inverse</a> <a id="3701" class="Symbol">(</a><a id="3702" href="Data.Table.Relation.Equality.html#806" class="Function">≡-setoid</a> <a id="3711" href="Data.Table.Properties.html#3666" class="Bound">A</a> <a id="3713" href="Data.Table.Properties.html#3662" class="Bound">n</a><a id="3714" class="Symbol">)</a> <a id="3716" class="Symbol">(</a><a id="3717" href="Relation.Binary.PropositionalEquality.html#1518" class="Function">P.setoid</a> <a id="3726" class="Symbol">(</a><a id="3727" href="Data.Vec.html#773" class="Datatype">Vec</a> <a id="3731" href="Data.Table.Properties.html#3666" class="Bound">A</a> <a id="3733" href="Data.Table.Properties.html#3662" class="Bound">n</a><a id="3734" class="Symbol">))</a>
  <a id="3739" href="Data.Table.Properties.html#3686" class="Function">↔Vec</a> <a id="3744" class="Symbol">=</a> <a id="3746" class="Keyword">record</a>
    <a id="3757" class="Symbol">{</a> <a id="3759" class="Field">to</a> <a id="3762" class="Symbol">=</a> <a id="3764" class="Keyword">record</a> <a id="3771" class="Symbol">{</a> <a id="3773" class="Field Operator">_⟨$⟩_</a> <a id="3779" class="Symbol">=</a> <a id="3781" href="Data.Table.Base.html#2680" class="Function">toVec</a> <a id="3787" class="Symbol">;</a> <a id="3789" class="Field">cong</a> <a id="3794" class="Symbol">=</a> <a id="3796" href="Data.Vec.Properties.html#20951" class="Function">VP.tabulate-cong</a> <a id="3813" class="Symbol">}</a>
    <a id="3819" class="Symbol">;</a> <a id="3821" class="Field">from</a> <a id="3826" class="Symbol">=</a> <a id="3828" href="Relation.Binary.PropositionalEquality.html#3194" class="Function">P.→-to-⟶</a> <a id="3837" href="Data.Table.Base.html#2600" class="Function">fromVec</a>
    <a id="3849" class="Symbol">;</a> <a id="3851" class="Field">inverse-of</a> <a id="3862" class="Symbol">=</a> <a id="3864" class="Keyword">record</a>
      <a id="3877" class="Symbol">{</a> <a id="3879" class="Field">left-inverse-of</a>  <a id="3896" class="Symbol">=</a> <a id="3898" href="Data.Vec.Properties.html#20303" class="Function">VP.lookup∘tabulate</a> <a id="3917" href="Function.html#805" class="Function Operator">∘</a> <a id="3919" href="Data.Table.Base.html#650" class="Field">lookup</a>
      <a id="3932" class="Symbol">;</a> <a id="3934" class="Field">right-inverse-of</a> <a id="3951" class="Symbol">=</a> <a id="3953" href="Data.Vec.Properties.html#20507" class="Function">VP.tabulate∘lookup</a>
      <a id="3978" class="Symbol">}</a>
    <a id="3984" class="Symbol">}</a>

<a id="3987" class="Comment">------------------------------------------------------------------------</a>
<a id="4060" class="Comment">-- Other</a>

<a id="4070" class="Keyword">module</a> <a id="4077" href="Data.Table.Properties.html#4077" class="Module">_</a> <a id="4079" class="Symbol">{</a><a id="4080" href="Data.Table.Properties.html#4080" class="Bound">a</a><a id="4081" class="Symbol">}</a> <a id="4083" class="Symbol">{</a><a id="4084" href="Data.Table.Properties.html#4084" class="Bound">A</a> <a id="4086" class="Symbol">:</a> <a id="4088" class="PrimitiveType">Set</a> <a id="4092" href="Data.Table.Properties.html#4080" class="Bound">a</a><a id="4093" class="Symbol">}</a> <a id="4095" class="Keyword">where</a>

  <a id="4104" href="Data.Table.Properties.html#4104" class="Function">lookup∈</a> <a id="4112" class="Symbol">:</a> <a id="4114" class="Symbol">∀</a> <a id="4116" class="Symbol">{</a><a id="4117" href="Data.Table.Properties.html#4117" class="Bound">xs</a> <a id="4120" class="Symbol">:</a> <a id="4122" href="Agda.Builtin.List.html#80" class="Datatype">List</a> <a id="4127" href="Data.Table.Properties.html#4084" class="Bound">A</a><a id="4128" class="Symbol">}</a> <a id="4130" class="Symbol">(</a><a id="4131" href="Data.Table.Properties.html#4131" class="Bound">i</a> <a id="4133" class="Symbol">:</a> <a id="4135" href="Data.Fin.Base.html#951" class="Datatype">Fin</a> <a id="4139" class="Symbol">(</a><a id="4140" href="Data.List.Base.html#4454" class="Function">L.length</a> <a id="4149" href="Data.Table.Properties.html#4117" class="Bound">xs</a><a id="4151" class="Symbol">))</a> <a id="4154" class="Symbol">→</a> <a id="4156" href="Data.Product.html#917" class="Function">∃</a> <a id="4158" class="Symbol">λ</a> <a id="4160" href="Data.Table.Properties.html#4160" class="Bound">x</a> <a id="4162" class="Symbol">→</a> <a id="4164" href="Data.Table.Properties.html#4160" class="Bound">x</a> <a id="4166" href="Data.List.Membership.Setoid.html#867" class="Function Operator">∈</a> <a id="4168" href="Data.Table.Properties.html#4117" class="Bound">xs</a>
  <a id="4173" href="Data.Table.Properties.html#4104" class="Function">lookup∈</a> <a id="4181" href="Data.Table.Properties.html#4181" class="Bound">i</a> <a id="4183" class="Symbol">=</a> <a id="4185" class="Symbol">_</a> <a id="4187" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="4189" href="Data.Table.Properties.html#2927" class="Function">fromList-∈</a> <a id="4200" href="Data.Table.Properties.html#4181" class="Bound">i</a>
</pre></body></html>