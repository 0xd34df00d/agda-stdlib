<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Table.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Table-related properties</a>
<a id="134" class="Comment">------------------------------------------------------------------------</a>

<a id="208" class="Symbol">{-#</a> <a id="212" class="Keyword">OPTIONS</a> <a id="220" class="Option">--without-K</a> <a id="232" class="Symbol">#-}</a>

<a id="237" class="Keyword">module</a> <a id="244" href="Data.Table.Properties.html" class="Module">Data.Table.Properties</a> <a id="266" class="Keyword">where</a>

<a id="273" class="Keyword">open</a> <a id="278" class="Keyword">import</a> <a id="285" href="Data.Table.html" class="Module">Data.Table</a>
<a id="296" class="Keyword">open</a> <a id="301" class="Keyword">import</a> <a id="308" href="Data.Table.Relation.Equality.html" class="Module">Data.Table.Relation.Equality</a>

<a id="338" class="Keyword">open</a> <a id="343" class="Keyword">import</a> <a id="350" href="Data.Bool.html" class="Module">Data.Bool</a> <a id="360" class="Keyword">using</a> <a id="366" class="Symbol">(</a><a id="367" href="Agda.Builtin.Bool.html#92" class="InductiveConstructor">true</a><a id="371" class="Symbol">;</a> <a id="373" href="Agda.Builtin.Bool.html#86" class="InductiveConstructor">false</a><a id="378" class="Symbol">;</a> <a id="380" href="Data.Bool.Base.html#828" class="Function Operator">if_then_else_</a><a id="393" class="Symbol">)</a>
<a id="395" class="Keyword">open</a> <a id="400" class="Keyword">import</a> <a id="407" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="416" class="Keyword">using</a> <a id="422" class="Symbol">(</a><a id="423" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="427" class="Symbol">;</a> <a id="429" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a><a id="432" class="Symbol">)</a>
<a id="434" class="Keyword">open</a> <a id="439" class="Keyword">import</a> <a id="446" href="Data.Empty.html" class="Module">Data.Empty</a> <a id="457" class="Keyword">using</a> <a id="463" class="Symbol">(</a><a id="464" href="Data.Empty.html#389" class="Function">⊥-elim</a><a id="470" class="Symbol">)</a>
<a id="472" class="Keyword">open</a> <a id="477" class="Keyword">import</a> <a id="484" href="Data.Fin.html" class="Module">Data.Fin</a> <a id="493" class="Keyword">using</a> <a id="499" class="Symbol">(</a><a id="500" href="Data.Fin.Base.html#944" class="Datatype">Fin</a><a id="503" class="Symbol">;</a> <a id="505" href="Data.Fin.Base.html#997" class="InductiveConstructor">suc</a><a id="508" class="Symbol">;</a> <a id="510" href="Data.Fin.Base.html#966" class="InductiveConstructor">zero</a><a id="514" class="Symbol">;</a> <a id="516" href="Data.Fin.Properties.html#1723" class="Function Operator">_≟_</a><a id="519" class="Symbol">;</a> <a id="521" href="Data.Fin.Base.html#5461" class="Function">punchIn</a><a id="528" class="Symbol">)</a>
<a id="530" class="Keyword">import</a> <a id="537" href="Data.Fin.Properties.html" class="Module">Data.Fin.Properties</a> <a id="557" class="Symbol">as</a> <a id="560" class="Module">FP</a>
<a id="563" class="Keyword">open</a> <a id="568" class="Keyword">import</a> <a id="575" href="Data.Fin.Permutation.html" class="Module">Data.Fin.Permutation</a> <a id="596" class="Symbol">as</a> <a id="599" class="Module">Perm</a> <a id="604" class="Keyword">using</a> <a id="610" class="Symbol">(</a><a id="611" href="Data.Fin.Permutation.html#1358" class="Function">Permutation</a><a id="622" class="Symbol">;</a> <a id="624" href="Data.Fin.Permutation.html#1803" class="Function Operator">_⟨$⟩ʳ_</a><a id="630" class="Symbol">;</a> <a id="632" href="Data.Fin.Permutation.html#1883" class="Function Operator">_⟨$⟩ˡ_</a><a id="638" class="Symbol">)</a>
<a id="640" class="Keyword">open</a> <a id="645" class="Keyword">import</a> <a id="652" href="Data.List.html" class="Module">Data.List</a> <a id="662" class="Symbol">as</a> <a id="665" class="Module">L</a> <a id="667" class="Keyword">using</a> <a id="673" class="Symbol">(</a><a id="674" href="Agda.Builtin.List.html#80" class="Datatype">List</a><a id="678" class="Symbol">;</a> <a id="680" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">_∷_</a><a id="683" class="Symbol">;</a> <a id="685" href="Data.List.Base.html#9372" class="InductiveConstructor">[]</a><a id="687" class="Symbol">)</a>
<a id="689" class="Keyword">open</a> <a id="694" class="Keyword">import</a> <a id="701" href="Data.List.Any.html" class="Module">Data.List.Any</a> <a id="715" class="Keyword">using</a> <a id="721" class="Symbol">(</a><a id="722" href="Data.List.Any.html#940" class="InductiveConstructor">here</a><a id="726" class="Symbol">;</a> <a id="728" href="Data.List.Any.html#993" class="InductiveConstructor">there</a><a id="733" class="Symbol">;</a> <a id="735" href="Data.List.Any.html#1634" class="Function">index</a><a id="740" class="Symbol">)</a>
<a id="742" class="Keyword">open</a> <a id="747" class="Keyword">import</a> <a id="754" href="Data.List.Membership.Propositional.html" class="Module">Data.List.Membership.Propositional</a> <a id="789" class="Keyword">using</a> <a id="795" class="Symbol">(</a><a id="796" href="Data.List.Membership.Setoid.html#860" class="Function Operator">_∈_</a><a id="799" class="Symbol">)</a>
<a id="801" class="Keyword">open</a> <a id="806" class="Keyword">import</a> <a id="813" href="Data.Product.html" class="Module">Data.Product</a> <a id="826" class="Symbol">as</a> <a id="829" class="Module">Product</a> <a id="837" class="Keyword">using</a> <a id="843" class="Symbol">(</a><a id="844" href="Agda.Builtin.Sigma.html#69" class="Record">Σ</a><a id="845" class="Symbol">;</a> <a id="847" href="Data.Product.html#910" class="Function">∃</a><a id="848" class="Symbol">;</a> <a id="850" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">_,_</a><a id="853" class="Symbol">;</a> <a id="855" href="Agda.Builtin.Sigma.html#155" class="Field">proj₁</a><a id="860" class="Symbol">;</a> <a id="862" href="Agda.Builtin.Sigma.html#167" class="Field">proj₂</a><a id="867" class="Symbol">)</a>
<a id="869" class="Keyword">open</a> <a id="874" class="Keyword">import</a> <a id="881" href="Data.Vec.html" class="Module">Data.Vec</a> <a id="890" class="Symbol">as</a> <a id="893" class="Module">V</a> <a id="895" class="Keyword">using</a> <a id="901" class="Symbol">(</a><a id="902" href="Data.Vec.html#766" class="Datatype">Vec</a><a id="905" class="Symbol">;</a> <a id="907" href="Data.Vec.html#825" class="InductiveConstructor Operator">_∷_</a><a id="910" class="Symbol">;</a> <a id="912" href="Data.Vec.html#806" class="InductiveConstructor">[]</a><a id="914" class="Symbol">)</a>
<a id="916" class="Keyword">import</a> <a id="923" href="Data.Vec.Properties.html" class="Module">Data.Vec.Properties</a> <a id="943" class="Symbol">as</a> <a id="946" class="Module">VP</a>
<a id="949" class="Keyword">open</a> <a id="954" class="Keyword">import</a> <a id="961" href="Function.html" class="Module">Function</a> <a id="970" class="Keyword">using</a> <a id="976" class="Symbol">(</a><a id="977" href="Function.html#798" class="Function Operator">_∘_</a><a id="980" class="Symbol">;</a> <a id="982" href="Function.html#1565" class="Function">flip</a><a id="986" class="Symbol">)</a>
<a id="988" class="Keyword">open</a> <a id="993" class="Keyword">import</a> <a id="1000" href="Function.Inverse.html" class="Module">Function.Inverse</a> <a id="1017" class="Keyword">using</a> <a id="1023" class="Symbol">(</a><a id="1024" href="Function.Inverse.html#1150" class="Record">Inverse</a><a id="1031" class="Symbol">)</a>
<a id="1033" class="Keyword">open</a> <a id="1038" class="Keyword">import</a> <a id="1045" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="1083" class="Symbol">as</a> <a id="1086" class="Module">P</a>
  <a id="1090" class="Keyword">using</a> <a id="1096" class="Symbol">(</a><a id="1097" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">_≡_</a><a id="1100" class="Symbol">;</a> <a id="1102" href="Relation.Binary.PropositionalEquality.Core.html#689" class="Function Operator">_≢_</a><a id="1105" class="Symbol">;</a> <a id="1107" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a><a id="1111" class="Symbol">;</a> <a id="1113" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a><a id="1116" class="Symbol">;</a> <a id="1118" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a><a id="1122" class="Symbol">)</a>
<a id="1124" class="Keyword">open</a> <a id="1129" class="Keyword">import</a> <a id="1136" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="1153" class="Keyword">using</a> <a id="1159" class="Symbol">(</a><a id="1160" href="Relation.Nullary.html#599" class="InductiveConstructor">yes</a><a id="1163" class="Symbol">;</a> <a id="1165" href="Relation.Nullary.html#626" class="InductiveConstructor">no</a><a id="1167" class="Symbol">)</a>
<a id="1169" class="Keyword">open</a> <a id="1174" class="Keyword">import</a> <a id="1181" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="1207" class="Keyword">using</a> <a id="1213" class="Symbol">(</a><a id="1214" href="Relation.Nullary.Negation.html#589" class="Function">contradiction</a><a id="1227" class="Symbol">)</a>


<a id="1231" class="Comment">------------------------------------------------------------------------</a>
<a id="1304" class="Comment">-- select</a>

<a id="1315" class="Keyword">module</a> <a id="1322" href="Data.Table.Properties.html#1322" class="Module">_</a> <a id="1324" class="Symbol">{</a><a id="1325" href="Data.Table.Properties.html#1325" class="Bound">a</a><a id="1326" class="Symbol">}</a> <a id="1328" class="Symbol">{</a><a id="1329" href="Data.Table.Properties.html#1329" class="Bound">A</a> <a id="1331" class="Symbol">:</a> <a id="1333" class="PrimitiveType">Set</a> <a id="1337" href="Data.Table.Properties.html#1325" class="Bound">a</a><a id="1338" class="Symbol">}</a> <a id="1340" class="Keyword">where</a>

  <a id="1349" class="Comment">-- Selecting from any table is the same as selecting from a constant table.</a>

  <a id="1428" href="Data.Table.Properties.html#1428" class="Function">select-const</a> <a id="1441" class="Symbol">:</a> <a id="1443" class="Symbol">∀</a> <a id="1445" class="Symbol">{</a><a id="1446" href="Data.Table.Properties.html#1446" class="Bound">n</a><a id="1447" class="Symbol">}</a> <a id="1449" class="Symbol">(</a><a id="1450" href="Data.Table.Properties.html#1450" class="Bound">z</a> <a id="1452" class="Symbol">:</a> <a id="1454" href="Data.Table.Properties.html#1329" class="Bound">A</a><a id="1455" class="Symbol">)</a> <a id="1457" class="Symbol">(</a><a id="1458" href="Data.Table.Properties.html#1458" class="Bound">i</a> <a id="1460" class="Symbol">:</a> <a id="1462" href="Data.Fin.Base.html#944" class="Datatype">Fin</a> <a id="1466" href="Data.Table.Properties.html#1446" class="Bound">n</a><a id="1467" class="Symbol">)</a> <a id="1469" href="Data.Table.Properties.html#1469" class="Bound">t</a> <a id="1471" class="Symbol">→</a>
                 <a id="1490" href="Data.Table.html#1245" class="Function">select</a> <a id="1497" href="Data.Table.Properties.html#1450" class="Bound">z</a> <a id="1499" href="Data.Table.Properties.html#1458" class="Bound">i</a> <a id="1501" href="Data.Table.Properties.html#1469" class="Bound">t</a> <a id="1503" href="Relation.Binary.html#2012" class="Function Operator">≗</a> <a id="1505" href="Data.Table.html#1245" class="Function">select</a> <a id="1512" href="Data.Table.Properties.html#1450" class="Bound">z</a> <a id="1514" href="Data.Table.Properties.html#1458" class="Bound">i</a> <a id="1516" class="Symbol">(</a><a id="1517" href="Data.Table.Base.html#2213" class="Function">replicate</a> <a id="1527" class="Symbol">(</a><a id="1528" href="Data.Table.Base.html#643" class="Field">lookup</a> <a id="1535" href="Data.Table.Properties.html#1469" class="Bound">t</a> <a id="1537" href="Data.Table.Properties.html#1458" class="Bound">i</a><a id="1538" class="Symbol">))</a>
  <a id="1543" href="Data.Table.Properties.html#1428" class="Function">select-const</a> <a id="1556" href="Data.Table.Properties.html#1556" class="Bound">z</a> <a id="1558" href="Data.Table.Properties.html#1558" class="Bound">i</a> <a id="1560" href="Data.Table.Properties.html#1560" class="Bound">t</a> <a id="1562" href="Data.Table.Properties.html#1562" class="Bound">j</a> <a id="1564" class="Keyword">with</a> <a id="1569" href="Data.Table.Properties.html#1562" class="Bound">j</a> <a id="1571" href="Data.Fin.Properties.html#1723" class="Function Operator">≟</a> <a id="1573" href="Data.Table.Properties.html#1558" class="Bound">i</a>
  <a id="1577" class="Symbol">...</a> <a id="1581" class="Symbol">|</a> <a id="1583" href="Relation.Nullary.html#599" class="InductiveConstructor">yes</a> <a id="1587" class="Symbol">_</a> <a id="1589" class="Symbol">=</a> <a id="1591" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
  <a id="1598" class="Symbol">...</a> <a id="1602" class="Symbol">|</a> <a id="1604" href="Relation.Nullary.html#626" class="InductiveConstructor">no</a>  <a id="1608" class="Symbol">_</a> <a id="1610" class="Symbol">=</a> <a id="1612" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>

  <a id="1620" class="Comment">-- Selecting an element from a table then looking it up is the same as looking</a>
  <a id="1701" class="Comment">-- up the index in the original table</a>

  <a id="1742" href="Data.Table.Properties.html#1742" class="Function">select-lookup</a> <a id="1756" class="Symbol">:</a> <a id="1758" class="Symbol">∀</a> <a id="1760" class="Symbol">{</a><a id="1761" href="Data.Table.Properties.html#1761" class="Bound">n</a> <a id="1763" href="Data.Table.Properties.html#1763" class="Bound">x</a> <a id="1765" href="Data.Table.Properties.html#1765" class="Bound">i</a><a id="1766" class="Symbol">}</a> <a id="1768" class="Symbol">(</a><a id="1769" href="Data.Table.Properties.html#1769" class="Bound">t</a> <a id="1771" class="Symbol">:</a> <a id="1773" href="Data.Table.Base.html#574" class="Record">Table</a> <a id="1779" href="Data.Table.Properties.html#1329" class="Bound">A</a> <a id="1781" href="Data.Table.Properties.html#1761" class="Bound">n</a><a id="1782" class="Symbol">)</a> <a id="1784" class="Symbol">→</a>
                  <a id="1804" href="Data.Table.Base.html#643" class="Field">lookup</a> <a id="1811" class="Symbol">(</a><a id="1812" href="Data.Table.html#1245" class="Function">select</a> <a id="1819" href="Data.Table.Properties.html#1763" class="Bound">x</a> <a id="1821" href="Data.Table.Properties.html#1765" class="Bound">i</a> <a id="1823" href="Data.Table.Properties.html#1769" class="Bound">t</a><a id="1824" class="Symbol">)</a> <a id="1826" href="Data.Table.Properties.html#1765" class="Bound">i</a> <a id="1828" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1830" href="Data.Table.Base.html#643" class="Field">lookup</a> <a id="1837" href="Data.Table.Properties.html#1769" class="Bound">t</a> <a id="1839" href="Data.Table.Properties.html#1765" class="Bound">i</a>
  <a id="1843" href="Data.Table.Properties.html#1742" class="Function">select-lookup</a> <a id="1857" class="Symbol">{</a><a id="1858" class="Argument">i</a> <a id="1860" class="Symbol">=</a> <a id="1862" href="Data.Table.Properties.html#1862" class="Bound">i</a><a id="1863" class="Symbol">}</a> <a id="1865" href="Data.Table.Properties.html#1865" class="Bound">t</a> <a id="1867" class="Keyword">with</a> <a id="1872" href="Data.Table.Properties.html#1862" class="Bound">i</a> <a id="1874" href="Data.Fin.Properties.html#1723" class="Function Operator">≟</a> <a id="1876" href="Data.Table.Properties.html#1862" class="Bound">i</a>
  <a id="1880" class="Symbol">...</a> <a id="1884" class="Symbol">|</a> <a id="1886" href="Relation.Nullary.html#599" class="InductiveConstructor">yes</a> <a id="1890" class="Symbol">_</a>  <a id="1893" class="Symbol">=</a> <a id="1895" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
  <a id="1902" class="Symbol">...</a> <a id="1906" class="Symbol">|</a> <a id="1908" href="Relation.Nullary.html#626" class="InductiveConstructor">no</a> <a id="1911" href="Data.Table.Properties.html#1911" class="Bound">i≢i</a> <a id="1915" class="Symbol">=</a> <a id="1917" href="Relation.Nullary.Negation.html#589" class="Function">contradiction</a> <a id="1931" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a> <a id="1936" href="Data.Table.Properties.html#1911" class="Bound">i≢i</a>

  <a id="1943" class="Comment">-- Selecting an element from a table then removing the same element produces a</a>
  <a id="2024" class="Comment">-- constant table</a>

  <a id="2045" href="Data.Table.Properties.html#2045" class="Function">select-remove</a> <a id="2059" class="Symbol">:</a> <a id="2061" class="Symbol">∀</a> <a id="2063" class="Symbol">{</a><a id="2064" href="Data.Table.Properties.html#2064" class="Bound">n</a> <a id="2066" href="Data.Table.Properties.html#2066" class="Bound">x</a><a id="2067" class="Symbol">}</a> <a id="2069" href="Data.Table.Properties.html#2069" class="Bound">i</a> <a id="2071" class="Symbol">(</a><a id="2072" href="Data.Table.Properties.html#2072" class="Bound">t</a> <a id="2074" class="Symbol">:</a> <a id="2076" href="Data.Table.Base.html#574" class="Record">Table</a> <a id="2082" href="Data.Table.Properties.html#1329" class="Bound">A</a> <a id="2084" class="Symbol">(</a><a id="2085" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2089" href="Data.Table.Properties.html#2064" class="Bound">n</a><a id="2090" class="Symbol">))</a> <a id="2093" class="Symbol">→</a>
                  <a id="2113" href="Data.Table.Base.html#1036" class="Function">remove</a> <a id="2120" href="Data.Table.Properties.html#2069" class="Bound">i</a> <a id="2122" class="Symbol">(</a><a id="2123" href="Data.Table.html#1245" class="Function">select</a> <a id="2130" href="Data.Table.Properties.html#2066" class="Bound">x</a> <a id="2132" href="Data.Table.Properties.html#2069" class="Bound">i</a> <a id="2134" href="Data.Table.Properties.html#2072" class="Bound">t</a><a id="2135" class="Symbol">)</a> <a id="2137" href="Relation.Binary.html#2012" class="Function Operator">≗</a> <a id="2139" href="Data.Table.Base.html#2213" class="Function">replicate</a> <a id="2149" class="Symbol">{</a><a id="2150" class="Argument">n</a> <a id="2152" class="Symbol">=</a> <a id="2154" href="Data.Table.Properties.html#2064" class="Bound">n</a><a id="2155" class="Symbol">}</a> <a id="2157" href="Data.Table.Properties.html#2066" class="Bound">x</a>
  <a id="2161" href="Data.Table.Properties.html#2045" class="Function">select-remove</a> <a id="2175" href="Data.Table.Properties.html#2175" class="Bound">i</a> <a id="2177" href="Data.Table.Properties.html#2177" class="Bound">t</a> <a id="2179" href="Data.Table.Properties.html#2179" class="Bound">j</a> <a id="2181" class="Keyword">with</a> <a id="2186" href="Data.Fin.Base.html#5461" class="Function">punchIn</a> <a id="2194" href="Data.Table.Properties.html#2175" class="Bound">i</a> <a id="2196" href="Data.Table.Properties.html#2179" class="Bound">j</a> <a id="2198" href="Data.Fin.Properties.html#1723" class="Function Operator">≟</a> <a id="2200" href="Data.Table.Properties.html#2175" class="Bound">i</a>
  <a id="2204" class="Symbol">...</a> <a id="2208" class="Symbol">|</a> <a id="2210" href="Relation.Nullary.html#599" class="InductiveConstructor">yes</a> <a id="2214" href="Data.Table.Properties.html#2214" class="Bound">p</a> <a id="2216" class="Symbol">=</a> <a id="2218" href="Relation.Nullary.Negation.html#589" class="Function">contradiction</a> <a id="2232" href="Data.Table.Properties.html#2214" class="Bound">p</a> <a id="2234" class="Symbol">(</a><a id="2235" href="Data.Fin.Properties.html#12601" class="Function">FP.punchInᵢ≢i</a> <a id="2249" class="Symbol">_</a> <a id="2251" class="Symbol">_)</a>
  <a id="2256" class="Symbol">...</a> <a id="2260" class="Symbol">|</a> <a id="2262" href="Relation.Nullary.html#626" class="InductiveConstructor">no</a> <a id="2265" href="Data.Table.Properties.html#2265" class="Bound">¬p</a> <a id="2268" class="Symbol">=</a> <a id="2270" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>


<a id="2277" class="Comment">------------------------------------------------------------------------</a>
<a id="2350" class="Comment">-- permute</a>

  <a id="2364" class="Comment">-- Removing an index &#39;i&#39; from a table permuted with &#39;π&#39; is the same as</a>
  <a id="2437" class="Comment">-- removing the element, then permuting with &#39;π&#39; minus &#39;i&#39;.</a>

  <a id="2500" href="Data.Table.Properties.html#2500" class="Function">remove-permute</a> <a id="2515" class="Symbol">:</a> <a id="2517" class="Symbol">∀</a> <a id="2519" class="Symbol">{</a><a id="2520" href="Data.Table.Properties.html#2520" class="Bound">m</a> <a id="2522" href="Data.Table.Properties.html#2522" class="Bound">n</a><a id="2523" class="Symbol">}</a> <a id="2525" class="Symbol">(</a><a id="2526" href="Data.Table.Properties.html#2526" class="Bound">π</a> <a id="2528" class="Symbol">:</a> <a id="2530" href="Data.Fin.Permutation.html#1358" class="Function">Permutation</a> <a id="2542" class="Symbol">(</a><a id="2543" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2547" href="Data.Table.Properties.html#2520" class="Bound">m</a><a id="2548" class="Symbol">)</a> <a id="2550" class="Symbol">(</a><a id="2551" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2555" href="Data.Table.Properties.html#2522" class="Bound">n</a><a id="2556" class="Symbol">))</a>
                   <a id="2578" href="Data.Table.Properties.html#2578" class="Bound">i</a> <a id="2580" class="Symbol">(</a><a id="2581" href="Data.Table.Properties.html#2581" class="Bound">t</a> <a id="2583" class="Symbol">:</a> <a id="2585" href="Data.Table.Base.html#574" class="Record">Table</a> <a id="2591" href="Data.Table.Properties.html#1329" class="Bound">A</a> <a id="2593" class="Symbol">(</a><a id="2594" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2598" href="Data.Table.Properties.html#2522" class="Bound">n</a><a id="2599" class="Symbol">))</a> <a id="2602" class="Symbol">→</a>
                   <a id="2623" href="Data.Table.Base.html#1036" class="Function">remove</a> <a id="2630" class="Symbol">(</a><a id="2631" href="Data.Table.Properties.html#2526" class="Bound">π</a> <a id="2633" href="Data.Fin.Permutation.html#1883" class="Function Operator">⟨$⟩ˡ</a> <a id="2638" href="Data.Table.Properties.html#2578" class="Bound">i</a><a id="2639" class="Symbol">)</a> <a id="2641" class="Symbol">(</a><a id="2642" href="Data.Table.html#1023" class="Function">permute</a> <a id="2650" href="Data.Table.Properties.html#2526" class="Bound">π</a> <a id="2652" href="Data.Table.Properties.html#2581" class="Bound">t</a><a id="2653" class="Symbol">)</a>
                   <a id="2674" href="Relation.Binary.html#2012" class="Function Operator">≗</a> <a id="2676" href="Data.Table.html#1023" class="Function">permute</a> <a id="2684" class="Symbol">(</a><a id="2685" href="Data.Fin.Permutation.html#3246" class="Function">Perm.remove</a> <a id="2697" class="Symbol">(</a><a id="2698" href="Data.Table.Properties.html#2526" class="Bound">π</a> <a id="2700" href="Data.Fin.Permutation.html#1883" class="Function Operator">⟨$⟩ˡ</a> <a id="2705" href="Data.Table.Properties.html#2578" class="Bound">i</a><a id="2706" class="Symbol">)</a> <a id="2708" href="Data.Table.Properties.html#2526" class="Bound">π</a><a id="2709" class="Symbol">)</a> <a id="2711" class="Symbol">(</a><a id="2712" href="Data.Table.Base.html#1036" class="Function">remove</a> <a id="2719" href="Data.Table.Properties.html#2578" class="Bound">i</a> <a id="2721" href="Data.Table.Properties.html#2581" class="Bound">t</a><a id="2722" class="Symbol">)</a>
  <a id="2726" href="Data.Table.Properties.html#2500" class="Function">remove-permute</a> <a id="2741" href="Data.Table.Properties.html#2741" class="Bound">π</a> <a id="2743" href="Data.Table.Properties.html#2743" class="Bound">i</a> <a id="2745" href="Data.Table.Properties.html#2745" class="Bound">t</a> <a id="2747" href="Data.Table.Properties.html#2747" class="Bound">j</a> <a id="2749" class="Symbol">=</a> <a id="2751" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">P.cong</a> <a id="2758" class="Symbol">(</a><a id="2759" href="Data.Table.Base.html#643" class="Field">lookup</a> <a id="2766" href="Data.Table.Properties.html#2745" class="Bound">t</a><a id="2767" class="Symbol">)</a> <a id="2769" class="Symbol">(</a><a id="2770" href="Data.Fin.Permutation.html#5743" class="Function">Perm.punchIn-permute′</a> <a id="2792" href="Data.Table.Properties.html#2741" class="Bound">π</a> <a id="2794" href="Data.Table.Properties.html#2743" class="Bound">i</a> <a id="2796" href="Data.Table.Properties.html#2747" class="Bound">j</a><a id="2797" class="Symbol">)</a>

<a id="2800" class="Comment">------------------------------------------------------------------------</a>
<a id="2873" class="Comment">-- fromList</a>

<a id="2886" class="Keyword">module</a> <a id="2893" href="Data.Table.Properties.html#2893" class="Module">_</a> <a id="2895" class="Symbol">{</a><a id="2896" href="Data.Table.Properties.html#2896" class="Bound">a</a><a id="2897" class="Symbol">}</a> <a id="2899" class="Symbol">{</a><a id="2900" href="Data.Table.Properties.html#2900" class="Bound">A</a> <a id="2902" class="Symbol">:</a> <a id="2904" class="PrimitiveType">Set</a> <a id="2908" href="Data.Table.Properties.html#2896" class="Bound">a</a><a id="2909" class="Symbol">}</a> <a id="2911" class="Keyword">where</a>

  <a id="2920" href="Data.Table.Properties.html#2920" class="Function">fromList-∈</a> <a id="2931" class="Symbol">:</a> <a id="2933" class="Symbol">∀</a> <a id="2935" class="Symbol">{</a><a id="2936" href="Data.Table.Properties.html#2936" class="Bound">xs</a> <a id="2939" class="Symbol">:</a> <a id="2941" href="Agda.Builtin.List.html#80" class="Datatype">List</a> <a id="2946" href="Data.Table.Properties.html#2900" class="Bound">A</a><a id="2947" class="Symbol">}</a> <a id="2949" class="Symbol">(</a><a id="2950" href="Data.Table.Properties.html#2950" class="Bound">i</a> <a id="2952" class="Symbol">:</a> <a id="2954" href="Data.Fin.Base.html#944" class="Datatype">Fin</a> <a id="2958" class="Symbol">(</a><a id="2959" href="Data.List.Base.html#4049" class="Function">L.length</a> <a id="2968" href="Data.Table.Properties.html#2936" class="Bound">xs</a><a id="2970" class="Symbol">))</a> <a id="2973" class="Symbol">→</a> <a id="2975" href="Data.Table.Base.html#643" class="Field">lookup</a> <a id="2982" class="Symbol">(</a><a id="2983" href="Data.Table.Base.html#2500" class="Function">fromList</a> <a id="2992" href="Data.Table.Properties.html#2936" class="Bound">xs</a><a id="2994" class="Symbol">)</a> <a id="2996" href="Data.Table.Properties.html#2950" class="Bound">i</a> <a id="2998" href="Data.List.Membership.Setoid.html#860" class="Function Operator">∈</a> <a id="3000" href="Data.Table.Properties.html#2936" class="Bound">xs</a>
  <a id="3005" href="Data.Table.Properties.html#2920" class="Function">fromList-∈</a> <a id="3016" class="Symbol">{</a><a id="3017" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a><a id="3019" class="Symbol">}</a>     <a id="3025" class="Symbol">()</a>
  <a id="3030" href="Data.Table.Properties.html#2920" class="Function">fromList-∈</a> <a id="3041" class="Symbol">{</a><a id="3042" href="Data.Table.Properties.html#3042" class="Bound">x</a> <a id="3044" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3046" href="Data.Table.Properties.html#3046" class="Bound">xs</a><a id="3048" class="Symbol">}</a> <a id="3050" href="Data.Fin.Base.html#966" class="InductiveConstructor">zero</a>    <a id="3058" class="Symbol">=</a> <a id="3060" href="Data.List.Any.html#940" class="InductiveConstructor">here</a> <a id="3065" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
  <a id="3072" href="Data.Table.Properties.html#2920" class="Function">fromList-∈</a> <a id="3083" class="Symbol">{</a><a id="3084" href="Data.Table.Properties.html#3084" class="Bound">x</a> <a id="3086" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3088" href="Data.Table.Properties.html#3088" class="Bound">xs</a><a id="3090" class="Symbol">}</a> <a id="3092" class="Symbol">(</a><a id="3093" href="Data.Fin.Base.html#997" class="InductiveConstructor">suc</a> <a id="3097" href="Data.Table.Properties.html#3097" class="Bound">i</a><a id="3098" class="Symbol">)</a> <a id="3100" class="Symbol">=</a> <a id="3102" href="Data.List.Any.html#993" class="InductiveConstructor">there</a> <a id="3108" class="Symbol">(</a><a id="3109" href="Data.Table.Properties.html#2920" class="Function">fromList-∈</a> <a id="3120" href="Data.Table.Properties.html#3097" class="Bound">i</a><a id="3121" class="Symbol">)</a>

  <a id="3126" href="Data.Table.Properties.html#3126" class="Function">index-fromList-∈</a> <a id="3143" class="Symbol">:</a> <a id="3145" class="Symbol">∀</a> <a id="3147" class="Symbol">{</a><a id="3148" href="Data.Table.Properties.html#3148" class="Bound">xs</a> <a id="3151" href="Data.Table.Properties.html#3151" class="Bound">i</a><a id="3152" class="Symbol">}</a> <a id="3154" class="Symbol">→</a> <a id="3156" href="Data.List.Any.html#1634" class="Function">index</a> <a id="3162" class="Symbol">(</a><a id="3163" href="Data.Table.Properties.html#2920" class="Function">fromList-∈</a> <a id="3174" class="Symbol">{</a><a id="3175" href="Data.Table.Properties.html#3148" class="Bound">xs</a><a id="3177" class="Symbol">}</a> <a id="3179" href="Data.Table.Properties.html#3151" class="Bound">i</a><a id="3180" class="Symbol">)</a> <a id="3182" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="3184" href="Data.Table.Properties.html#3151" class="Bound">i</a>
  <a id="3188" href="Data.Table.Properties.html#3126" class="Function">index-fromList-∈</a> <a id="3205" class="Symbol">{</a><a id="3206" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a><a id="3208" class="Symbol">}</a>     <a id="3214" class="Symbol">{()}</a>
  <a id="3221" href="Data.Table.Properties.html#3126" class="Function">index-fromList-∈</a> <a id="3238" class="Symbol">{</a><a id="3239" href="Data.Table.Properties.html#3239" class="Bound">x</a> <a id="3241" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3243" href="Data.Table.Properties.html#3243" class="Bound">xs</a><a id="3245" class="Symbol">}</a> <a id="3247" class="Symbol">{</a><a id="3248" href="Data.Fin.Base.html#966" class="InductiveConstructor">zero</a><a id="3252" class="Symbol">}</a>  <a id="3255" class="Symbol">=</a> <a id="3257" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
  <a id="3264" href="Data.Table.Properties.html#3126" class="Function">index-fromList-∈</a> <a id="3281" class="Symbol">{</a><a id="3282" href="Data.Table.Properties.html#3282" class="Bound">x</a> <a id="3284" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3286" href="Data.Table.Properties.html#3286" class="Bound">xs</a><a id="3288" class="Symbol">}</a> <a id="3290" class="Symbol">{</a><a id="3291" href="Data.Fin.Base.html#997" class="InductiveConstructor">suc</a> <a id="3295" href="Data.Table.Properties.html#3295" class="Bound">i</a><a id="3296" class="Symbol">}</a> <a id="3298" class="Symbol">=</a> <a id="3300" href="Relation.Binary.PropositionalEquality.html#1127" class="Function">cong</a> <a id="3305" href="Data.Fin.Base.html#997" class="InductiveConstructor">suc</a> <a id="3309" href="Data.Table.Properties.html#3126" class="Function">index-fromList-∈</a>

  <a id="3329" href="Data.Table.Properties.html#3329" class="Function">fromList-index</a> <a id="3344" class="Symbol">:</a> <a id="3346" class="Symbol">∀</a> <a id="3348" class="Symbol">{</a><a id="3349" href="Data.Table.Properties.html#3349" class="Bound">xs</a><a id="3351" class="Symbol">}</a> <a id="3353" class="Symbol">{</a><a id="3354" href="Data.Table.Properties.html#3354" class="Bound">x</a> <a id="3356" class="Symbol">:</a> <a id="3358" href="Data.Table.Properties.html#2900" class="Bound">A</a><a id="3359" class="Symbol">}</a> <a id="3361" class="Symbol">(</a><a id="3362" href="Data.Table.Properties.html#3362" class="Bound">x∈xs</a> <a id="3367" class="Symbol">:</a> <a id="3369" href="Data.Table.Properties.html#3354" class="Bound">x</a> <a id="3371" href="Data.List.Membership.Setoid.html#860" class="Function Operator">∈</a> <a id="3373" href="Data.Table.Properties.html#3349" class="Bound">xs</a><a id="3375" class="Symbol">)</a> <a id="3377" class="Symbol">→</a> <a id="3379" href="Data.Table.Base.html#643" class="Field">lookup</a> <a id="3386" class="Symbol">(</a><a id="3387" href="Data.Table.Base.html#2500" class="Function">fromList</a> <a id="3396" href="Data.Table.Properties.html#3349" class="Bound">xs</a><a id="3398" class="Symbol">)</a> <a id="3400" class="Symbol">(</a><a id="3401" href="Data.List.Any.html#1634" class="Function">index</a> <a id="3407" href="Data.Table.Properties.html#3362" class="Bound">x∈xs</a><a id="3411" class="Symbol">)</a> <a id="3413" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="3415" href="Data.Table.Properties.html#3354" class="Bound">x</a>
  <a id="3419" href="Data.Table.Properties.html#3329" class="Function">fromList-index</a> <a id="3434" class="Symbol">(</a><a id="3435" href="Data.List.Any.html#940" class="InductiveConstructor">here</a> <a id="3440" href="Data.Table.Properties.html#3440" class="Bound">px</a><a id="3442" class="Symbol">)</a>    <a id="3447" class="Symbol">=</a> <a id="3449" href="Relation.Binary.PropositionalEquality.Core.html#867" class="Function">sym</a> <a id="3453" href="Data.Table.Properties.html#3440" class="Bound">px</a>
  <a id="3458" href="Data.Table.Properties.html#3329" class="Function">fromList-index</a> <a id="3473" class="Symbol">(</a><a id="3474" href="Data.List.Any.html#993" class="InductiveConstructor">there</a> <a id="3480" href="Data.Table.Properties.html#3480" class="Bound">x∈xs</a><a id="3484" class="Symbol">)</a> <a id="3486" class="Symbol">=</a> <a id="3488" href="Data.Table.Properties.html#3329" class="Function">fromList-index</a> <a id="3503" href="Data.Table.Properties.html#3480" class="Bound">x∈xs</a>


<a id="3510" class="Comment">------------------------------------------------------------------------</a>
<a id="3583" class="Comment">-- There exists an isomorphism between tables and vectors.</a>

<a id="3643" class="Keyword">module</a> <a id="3650" href="Data.Table.Properties.html#3650" class="Module">_</a> <a id="3652" class="Symbol">{</a><a id="3653" href="Data.Table.Properties.html#3653" class="Bound">a</a> <a id="3655" href="Data.Table.Properties.html#3655" class="Bound">n</a><a id="3656" class="Symbol">}</a> <a id="3658" class="Symbol">{</a><a id="3659" href="Data.Table.Properties.html#3659" class="Bound">A</a> <a id="3661" class="Symbol">:</a> <a id="3663" class="PrimitiveType">Set</a> <a id="3667" href="Data.Table.Properties.html#3653" class="Bound">a</a><a id="3668" class="Symbol">}</a> <a id="3670" class="Keyword">where</a>

  <a id="3679" href="Data.Table.Properties.html#3679" class="Function">↔Vec</a> <a id="3684" class="Symbol">:</a> <a id="3686" href="Function.Inverse.html#1150" class="Record">Inverse</a> <a id="3694" class="Symbol">(</a><a id="3695" href="Data.Table.Relation.Equality.html#799" class="Function">≡-setoid</a> <a id="3704" href="Data.Table.Properties.html#3659" class="Bound">A</a> <a id="3706" href="Data.Table.Properties.html#3655" class="Bound">n</a><a id="3707" class="Symbol">)</a> <a id="3709" class="Symbol">(</a><a id="3710" href="Relation.Binary.PropositionalEquality.html#1511" class="Function">P.setoid</a> <a id="3719" class="Symbol">(</a><a id="3720" href="Data.Vec.html#766" class="Datatype">Vec</a> <a id="3724" href="Data.Table.Properties.html#3659" class="Bound">A</a> <a id="3726" href="Data.Table.Properties.html#3655" class="Bound">n</a><a id="3727" class="Symbol">))</a>
  <a id="3732" href="Data.Table.Properties.html#3679" class="Function">↔Vec</a> <a id="3737" class="Symbol">=</a> <a id="3739" class="Keyword">record</a>
    <a id="3750" class="Symbol">{</a> <a id="3752" class="Field">to</a> <a id="3755" class="Symbol">=</a> <a id="3757" class="Keyword">record</a> <a id="3764" class="Symbol">{</a> <a id="3766" class="Field Operator">_⟨$⟩_</a> <a id="3772" class="Symbol">=</a> <a id="3774" href="Data.Table.Base.html#2673" class="Function">toVec</a> <a id="3780" class="Symbol">;</a> <a id="3782" class="Field">cong</a> <a id="3787" class="Symbol">=</a> <a id="3789" href="Data.Vec.Properties.html#20944" class="Function">VP.tabulate-cong</a> <a id="3806" class="Symbol">}</a>
    <a id="3812" class="Symbol">;</a> <a id="3814" class="Field">from</a> <a id="3819" class="Symbol">=</a> <a id="3821" href="Relation.Binary.PropositionalEquality.html#3187" class="Function">P.→-to-⟶</a> <a id="3830" href="Data.Table.Base.html#2593" class="Function">fromVec</a>
    <a id="3842" class="Symbol">;</a> <a id="3844" class="Field">inverse-of</a> <a id="3855" class="Symbol">=</a> <a id="3857" class="Keyword">record</a>
      <a id="3870" class="Symbol">{</a> <a id="3872" class="Field">left-inverse-of</a>  <a id="3889" class="Symbol">=</a> <a id="3891" href="Data.Vec.Properties.html#20296" class="Function">VP.lookup∘tabulate</a> <a id="3910" href="Function.html#798" class="Function Operator">∘</a> <a id="3912" href="Data.Table.Base.html#643" class="Field">lookup</a>
      <a id="3925" class="Symbol">;</a> <a id="3927" class="Field">right-inverse-of</a> <a id="3944" class="Symbol">=</a> <a id="3946" href="Data.Vec.Properties.html#20500" class="Function">VP.tabulate∘lookup</a>
      <a id="3971" class="Symbol">}</a>
    <a id="3977" class="Symbol">}</a>

<a id="3980" class="Comment">------------------------------------------------------------------------</a>
<a id="4053" class="Comment">-- Other</a>

<a id="4063" class="Keyword">module</a> <a id="4070" href="Data.Table.Properties.html#4070" class="Module">_</a> <a id="4072" class="Symbol">{</a><a id="4073" href="Data.Table.Properties.html#4073" class="Bound">a</a><a id="4074" class="Symbol">}</a> <a id="4076" class="Symbol">{</a><a id="4077" href="Data.Table.Properties.html#4077" class="Bound">A</a> <a id="4079" class="Symbol">:</a> <a id="4081" class="PrimitiveType">Set</a> <a id="4085" href="Data.Table.Properties.html#4073" class="Bound">a</a><a id="4086" class="Symbol">}</a> <a id="4088" class="Keyword">where</a>

  <a id="4097" href="Data.Table.Properties.html#4097" class="Function">lookup∈</a> <a id="4105" class="Symbol">:</a> <a id="4107" class="Symbol">∀</a> <a id="4109" class="Symbol">{</a><a id="4110" href="Data.Table.Properties.html#4110" class="Bound">xs</a> <a id="4113" class="Symbol">:</a> <a id="4115" href="Agda.Builtin.List.html#80" class="Datatype">List</a> <a id="4120" href="Data.Table.Properties.html#4077" class="Bound">A</a><a id="4121" class="Symbol">}</a> <a id="4123" class="Symbol">(</a><a id="4124" href="Data.Table.Properties.html#4124" class="Bound">i</a> <a id="4126" class="Symbol">:</a> <a id="4128" href="Data.Fin.Base.html#944" class="Datatype">Fin</a> <a id="4132" class="Symbol">(</a><a id="4133" href="Data.List.Base.html#4049" class="Function">L.length</a> <a id="4142" href="Data.Table.Properties.html#4110" class="Bound">xs</a><a id="4144" class="Symbol">))</a> <a id="4147" class="Symbol">→</a> <a id="4149" href="Data.Product.html#910" class="Function">∃</a> <a id="4151" class="Symbol">λ</a> <a id="4153" href="Data.Table.Properties.html#4153" class="Bound">x</a> <a id="4155" class="Symbol">→</a> <a id="4157" href="Data.Table.Properties.html#4153" class="Bound">x</a> <a id="4159" href="Data.List.Membership.Setoid.html#860" class="Function Operator">∈</a> <a id="4161" href="Data.Table.Properties.html#4110" class="Bound">xs</a>
  <a id="4166" href="Data.Table.Properties.html#4097" class="Function">lookup∈</a> <a id="4174" href="Data.Table.Properties.html#4174" class="Bound">i</a> <a id="4176" class="Symbol">=</a> <a id="4178" class="Symbol">_</a> <a id="4180" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="4182" href="Data.Table.Properties.html#2920" class="Function">fromList-∈</a> <a id="4193" href="Data.Table.Properties.html#4174" class="Bound">i</a>
</pre></body></html>