<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Operations.Semiring</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Some defined operations (multiplication by natural number and</a>
<a id="171" class="Comment">-- exponentiation)</a>
<a id="190" class="Comment">------------------------------------------------------------------------</a>

<a id="264" class="Symbol">{-#</a> <a id="268" class="Keyword">OPTIONS</a> <a id="276" class="Option">--without-K</a> <a id="288" class="Symbol">#-}</a>

<a id="293" class="Keyword">open</a> <a id="298" class="Keyword">import</a> <a id="305" href="Algebra.html" class="Module">Algebra</a>

<a id="314" class="Keyword">module</a> <a id="321" href="Algebra.Operations.Semiring.html" class="Module">Algebra.Operations.Semiring</a> <a id="349" class="Symbol">{</a><a id="350" href="Algebra.Operations.Semiring.html#350" class="Bound">s₁</a> <a id="353" href="Algebra.Operations.Semiring.html#353" class="Bound">s₂</a><a id="355" class="Symbol">}</a> <a id="357" class="Symbol">(</a><a id="358" href="Algebra.Operations.Semiring.html#358" class="Bound">S</a> <a id="360" class="Symbol">:</a> <a id="362" href="Algebra.html#8818" class="Record">Semiring</a> <a id="371" href="Algebra.Operations.Semiring.html#350" class="Bound">s₁</a> <a id="374" href="Algebra.Operations.Semiring.html#353" class="Bound">s₂</a><a id="376" class="Symbol">)</a> <a id="378" class="Keyword">where</a>

<a id="385" class="Keyword">import</a> <a id="392" href="Algebra.Operations.CommutativeMonoid.html" class="Module">Algebra.Operations.CommutativeMonoid</a> <a id="429" class="Symbol">as</a> <a id="432" class="Module">MonoidOperations</a>
<a id="449" class="Keyword">open</a> <a id="454" class="Keyword">import</a> <a id="461" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
  <a id="477" class="Keyword">using</a> <a id="483" class="Symbol">(</a><a id="484" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="488" class="Symbol">;</a> <a id="490" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a><a id="493" class="Symbol">;</a> <a id="495" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a><a id="496" class="Symbol">)</a> <a id="498" class="Keyword">renaming</a> <a id="507" class="Symbol">(</a><a id="508" href="Agda.Builtin.Nat.html#230" class="Primitive Operator">_+_</a> <a id="512" class="Symbol">to</a> <a id="515" href="Agda.Builtin.Nat.html#230" class="Primitive Operator">_ℕ+_</a><a id="519" class="Symbol">;</a> <a id="521" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">_*_</a> <a id="525" class="Symbol">to</a> <a id="528" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">_ℕ*_</a><a id="532" class="Symbol">)</a>
<a id="534" class="Keyword">open</a> <a id="539" class="Keyword">import</a> <a id="546" href="Data.Product.html" class="Module">Data.Product</a> <a id="559" class="Keyword">using</a> <a id="565" class="Symbol">(</a><a id="566" class="Keyword">module</a> <a id="573" href="Data.Product.html#594" class="Module">Σ</a><a id="574" class="Symbol">)</a>
<a id="576" class="Keyword">open</a> <a id="581" class="Keyword">import</a> <a id="588" href="Function.html" class="Module">Function</a> <a id="597" class="Keyword">using</a> <a id="603" class="Symbol">(</a><a id="604" href="Function.html#1920" class="Function Operator">_$_</a><a id="607" class="Symbol">)</a>
<a id="609" class="Keyword">open</a> <a id="614" class="Keyword">import</a> <a id="621" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="637" class="Keyword">open</a> <a id="642" class="Keyword">import</a> <a id="649" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="687" class="Symbol">as</a> <a id="690" class="Module">P</a> <a id="692" class="Keyword">using</a> <a id="698" class="Symbol">(</a><a id="699" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">_≡_</a><a id="702" class="Symbol">)</a>

<a id="705" class="Keyword">open</a> <a id="710" href="Algebra.html#8818" class="Module">Semiring</a> <a id="719" href="Algebra.Operations.Semiring.html#358" class="Bound">S</a> <a id="721" class="Keyword">renaming</a> <a id="730" class="Symbol">(</a>zero <a id="736" class="Symbol">to</a> *-zero<a id="745" class="Symbol">)</a>
<a id="747" class="Keyword">open</a> <a id="752" class="Keyword">import</a> <a id="759" href="Relation.Binary.EqReasoning.html" class="Module">Relation.Binary.EqReasoning</a> <a id="787" href="Algebra.Structures.html#928" class="Function">setoid</a>

<a id="795" class="Comment">------------------------------------------------------------------------</a>
<a id="868" class="Comment">-- Operations</a>

<a id="883" class="Comment">-- Re-export all monoid operations and proofs</a>
<a id="929" class="Keyword">open</a> <a id="934" href="Algebra.Operations.CommutativeMonoid.html" class="Module">MonoidOperations</a> <a id="951" href="Algebra.html#8198" class="Function">+-commutativeMonoid</a> <a id="971" class="Keyword">public</a>

<a id="979" class="Comment">-- Exponentiation.</a>
<a id="998" class="Keyword">infixr</a> <a id="1005" class="Number">9</a> <a id="1007" href="Algebra.Operations.Semiring.html#1011" class="Function Operator">_^_</a>
<a id="_^_"></a><a id="1011" href="Algebra.Operations.Semiring.html#1011" class="Function Operator">_^_</a> <a id="1015" class="Symbol">:</a> <a id="1017" href="Algebra.html#8914" class="Field">Carrier</a> <a id="1025" class="Symbol">→</a> <a id="1027" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a> <a id="1029" class="Symbol">→</a> <a id="1031" href="Algebra.html#8914" class="Field">Carrier</a>
<a id="1039" href="Algebra.Operations.Semiring.html#1039" class="Bound">x</a> <a id="1041" href="Algebra.Operations.Semiring.html#1011" class="Function Operator">^</a> <a id="1043" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>  <a id="1049" class="Symbol">=</a> <a id="1051" href="Algebra.html#9051" class="Field">1#</a>
<a id="1054" href="Algebra.Operations.Semiring.html#1054" class="Bound">x</a> <a id="1056" href="Algebra.Operations.Semiring.html#1011" class="Function Operator">^</a> <a id="1058" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1062" href="Algebra.Operations.Semiring.html#1062" class="Bound">n</a> <a id="1064" class="Symbol">=</a> <a id="1066" href="Algebra.Operations.Semiring.html#1054" class="Bound">x</a> <a id="1068" href="Algebra.html#8997" class="Field Operator">*</a> <a id="1070" href="Algebra.Operations.Semiring.html#1054" class="Bound">x</a> <a id="1072" href="Algebra.Operations.Semiring.html#1011" class="Function Operator">^</a> <a id="1074" href="Algebra.Operations.Semiring.html#1062" class="Bound">n</a>

<a id="1077" class="Comment">------------------------------------------------------------------------</a>
<a id="1150" class="Comment">-- Properties of _×_</a>

<a id="1172" class="Comment">-- _× 1# is homomorphic with respect to _ℕ*_/_*_.</a>

<a id="×1-homo-*"></a><a id="1223" href="Algebra.Operations.Semiring.html#1223" class="Function">×1-homo-*</a> <a id="1233" class="Symbol">:</a> <a id="1235" class="Symbol">∀</a> <a id="1237" href="Algebra.Operations.Semiring.html#1237" class="Bound">m</a> <a id="1239" href="Algebra.Operations.Semiring.html#1239" class="Bound">n</a> <a id="1241" class="Symbol">→</a> <a id="1243" class="Symbol">(</a><a id="1244" href="Algebra.Operations.Semiring.html#1237" class="Bound">m</a> <a id="1246" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">ℕ*</a> <a id="1249" href="Algebra.Operations.Semiring.html#1239" class="Bound">n</a><a id="1250" class="Symbol">)</a> <a id="1252" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1254" href="Algebra.html#9051" class="Field">1#</a> <a id="1257" href="Algebra.html#8937" class="Field Operator">≈</a> <a id="1259" class="Symbol">(</a><a id="1260" href="Algebra.Operations.Semiring.html#1237" class="Bound">m</a> <a id="1262" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1264" href="Algebra.html#9051" class="Field">1#</a><a id="1266" class="Symbol">)</a> <a id="1268" href="Algebra.html#8997" class="Field Operator">*</a> <a id="1270" class="Symbol">(</a><a id="1271" href="Algebra.Operations.Semiring.html#1239" class="Bound">n</a> <a id="1273" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1275" href="Algebra.html#9051" class="Field">1#</a><a id="1277" class="Symbol">)</a>
<a id="1279" href="Algebra.Operations.Semiring.html#1223" class="Function">×1-homo-*</a> <a id="1289" class="Number">0</a> <a id="1291" href="Algebra.Operations.Semiring.html#1291" class="Bound">n</a> <a id="1293" class="Symbol">=</a> <a id="1295" href="Relation.Binary.PreorderReasoning.html#1257" class="Function Operator">begin</a>
  <a id="1303" href="Algebra.html#9026" class="Field">0#</a>             <a id="1318" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1321" href="Relation.Binary.Core.html#5646" class="Function">sym</a> <a id="1325" class="Symbol">(</a><a id="1326" href="Agda.Builtin.Sigma.html#155" class="Field">Σ.proj₁</a> <a id="1334" href="Algebra.Structures.html#6713" class="Function">*-zero</a> <a id="1341" class="Symbol">(</a><a id="1342" href="Algebra.Operations.Semiring.html#1291" class="Bound">n</a> <a id="1344" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1346" href="Algebra.html#9051" class="Field">1#</a><a id="1348" class="Symbol">))</a> <a id="1351" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1355" href="Algebra.html#9026" class="Field">0#</a> <a id="1358" href="Algebra.html#8997" class="Field Operator">*</a> <a id="1360" class="Symbol">(</a><a id="1361" href="Algebra.Operations.Semiring.html#1291" class="Bound">n</a> <a id="1363" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1365" href="Algebra.html#9051" class="Field">1#</a><a id="1367" class="Symbol">)</a>  <a id="1370" href="Relation.Binary.PreorderReasoning.html#1710" class="Function Operator">∎</a>
<a id="1372" href="Algebra.Operations.Semiring.html#1223" class="Function">×1-homo-*</a> <a id="1382" class="Symbol">(</a><a id="1383" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1387" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a><a id="1388" class="Symbol">)</a> <a id="1390" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1392" class="Symbol">=</a> <a id="1394" href="Relation.Binary.PreorderReasoning.html#1257" class="Function Operator">begin</a>
  <a id="1402" class="Symbol">(</a><a id="1403" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1405" href="Agda.Builtin.Nat.html#230" class="Primitive Operator">ℕ+</a> <a id="1408" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a> <a id="1410" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">ℕ*</a> <a id="1413" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a><a id="1414" class="Symbol">)</a> <a id="1416" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1418" href="Algebra.html#9051" class="Field">1#</a>                  <a id="1438" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1441" href="Algebra.Operations.CommutativeMonoid.html#2207" class="Function">×-homo-+</a> <a id="1450" href="Algebra.html#9051" class="Field">1#</a> <a id="1453" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1455" class="Symbol">(</a><a id="1456" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a> <a id="1458" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">ℕ*</a> <a id="1461" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a><a id="1462" class="Symbol">)</a> <a id="1464" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1468" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1470" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1472" href="Algebra.html#9051" class="Field">1#</a> <a id="1475" href="Algebra.html#8968" class="Field Operator">+</a> <a id="1477" class="Symbol">(</a><a id="1478" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a> <a id="1480" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">ℕ*</a> <a id="1483" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a><a id="1484" class="Symbol">)</a> <a id="1486" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1488" href="Algebra.html#9051" class="Field">1#</a>              <a id="1504" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1507" href="Algebra.Structures.html#853" class="Function">+-cong</a> <a id="1514" href="Relation.Binary.Core.html#5620" class="Function">refl</a> <a id="1519" class="Symbol">(</a><a id="1520" href="Algebra.Operations.Semiring.html#1223" class="Function">×1-homo-*</a> <a id="1530" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a> <a id="1532" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a><a id="1533" class="Symbol">)</a> <a id="1535" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1539" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1541" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1543" href="Algebra.html#9051" class="Field">1#</a> <a id="1546" href="Algebra.html#8968" class="Field Operator">+</a> <a id="1548" class="Symbol">(</a><a id="1549" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a> <a id="1551" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1553" href="Algebra.html#9051" class="Field">1#</a><a id="1555" class="Symbol">)</a> <a id="1557" href="Algebra.html#8997" class="Field Operator">*</a> <a id="1559" class="Symbol">(</a><a id="1560" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1562" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1564" href="Algebra.html#9051" class="Field">1#</a><a id="1566" class="Symbol">)</a>         <a id="1576" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1579" href="Relation.Binary.Core.html#5646" class="Function">sym</a> <a id="1583" class="Symbol">(</a><a id="1584" href="Algebra.Structures.html#853" class="Function">+-cong</a> <a id="1591" class="Symbol">(</a><a id="1592" href="Algebra.Structures.html#1695" class="Function">*-identityˡ</a> <a id="1604" class="Symbol">_)</a> <a id="1607" href="Relation.Binary.Core.html#5620" class="Function">refl</a><a id="1611" class="Symbol">)</a> <a id="1613" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1617" href="Algebra.html#9051" class="Field">1#</a> <a id="1620" href="Algebra.html#8997" class="Field Operator">*</a> <a id="1622" class="Symbol">(</a><a id="1623" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1625" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1627" href="Algebra.html#9051" class="Field">1#</a><a id="1629" class="Symbol">)</a> <a id="1631" href="Algebra.html#8968" class="Field Operator">+</a> <a id="1633" class="Symbol">(</a><a id="1634" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a> <a id="1636" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1638" href="Algebra.html#9051" class="Field">1#</a><a id="1640" class="Symbol">)</a> <a id="1642" href="Algebra.html#8997" class="Field Operator">*</a> <a id="1644" class="Symbol">(</a><a id="1645" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1647" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1649" href="Algebra.html#9051" class="Field">1#</a><a id="1651" class="Symbol">)</a>  <a id="1654" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1657" href="Relation.Binary.Core.html#5646" class="Function">sym</a> <a id="1661" class="Symbol">(</a><a id="1662" href="Algebra.Structures.html#5842" class="Function">distribʳ</a> <a id="1671" class="Symbol">(</a><a id="1672" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1674" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1676" href="Algebra.html#9051" class="Field">1#</a><a id="1678" class="Symbol">)</a> <a id="1680" href="Algebra.html#9051" class="Field">1#</a> <a id="1683" class="Symbol">(</a><a id="1684" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a> <a id="1686" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1688" href="Algebra.html#9051" class="Field">1#</a><a id="1690" class="Symbol">))</a> <a id="1693" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="1697" class="Symbol">(</a><a id="1698" href="Algebra.html#9051" class="Field">1#</a> <a id="1701" href="Algebra.html#8968" class="Field Operator">+</a> <a id="1703" href="Algebra.Operations.Semiring.html#1387" class="Bound">m</a> <a id="1705" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1707" href="Algebra.html#9051" class="Field">1#</a><a id="1709" class="Symbol">)</a> <a id="1711" href="Algebra.html#8997" class="Field Operator">*</a> <a id="1713" class="Symbol">(</a><a id="1714" href="Algebra.Operations.Semiring.html#1390" class="Bound">n</a> <a id="1716" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a> <a id="1718" href="Algebra.html#9051" class="Field">1#</a><a id="1720" class="Symbol">)</a>             <a id="1734" href="Relation.Binary.PreorderReasoning.html#1710" class="Function Operator">∎</a>

<a id="1737" class="Comment">------------------------------------------------------------------------</a>
<a id="1810" class="Comment">-- Properties of _×′_</a>

<a id="1833" class="Comment">-- _×′ 1# is homomorphic with respect to _ℕ*_/_*_.</a>

<a id="×′1-homo-*"></a><a id="1885" href="Algebra.Operations.Semiring.html#1885" class="Function">×′1-homo-*</a> <a id="1896" class="Symbol">:</a> <a id="1898" class="Symbol">∀</a> <a id="1900" href="Algebra.Operations.Semiring.html#1900" class="Bound">m</a> <a id="1902" href="Algebra.Operations.Semiring.html#1902" class="Bound">n</a> <a id="1904" class="Symbol">→</a> <a id="1906" class="Symbol">(</a><a id="1907" href="Algebra.Operations.Semiring.html#1900" class="Bound">m</a> <a id="1909" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">ℕ*</a> <a id="1912" href="Algebra.Operations.Semiring.html#1902" class="Bound">n</a><a id="1913" class="Symbol">)</a> <a id="1915" href="Algebra.Operations.CommutativeMonoid.html#1404" class="Function Operator">×′</a> <a id="1918" href="Algebra.html#9051" class="Field">1#</a> <a id="1921" href="Algebra.html#8937" class="Field Operator">≈</a> <a id="1923" class="Symbol">(</a><a id="1924" href="Algebra.Operations.Semiring.html#1900" class="Bound">m</a> <a id="1926" href="Algebra.Operations.CommutativeMonoid.html#1404" class="Function Operator">×′</a> <a id="1929" href="Algebra.html#9051" class="Field">1#</a><a id="1931" class="Symbol">)</a> <a id="1933" href="Algebra.html#8997" class="Field Operator">*</a> <a id="1935" class="Symbol">(</a><a id="1936" href="Algebra.Operations.Semiring.html#1902" class="Bound">n</a> <a id="1938" href="Algebra.Operations.CommutativeMonoid.html#1404" class="Function Operator">×′</a> <a id="1941" href="Algebra.html#9051" class="Field">1#</a><a id="1943" class="Symbol">)</a>
<a id="1945" href="Algebra.Operations.Semiring.html#1885" class="Function">×′1-homo-*</a> <a id="1956" href="Algebra.Operations.Semiring.html#1956" class="Bound">m</a> <a id="1958" href="Algebra.Operations.Semiring.html#1958" class="Bound">n</a> <a id="1960" class="Symbol">=</a> <a id="1962" href="Relation.Binary.PreorderReasoning.html#1257" class="Function Operator">begin</a>
  <a id="1970" class="Symbol">(</a><a id="1971" href="Algebra.Operations.Semiring.html#1956" class="Bound">m</a> <a id="1973" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">ℕ*</a> <a id="1976" href="Algebra.Operations.Semiring.html#1958" class="Bound">n</a><a id="1977" class="Symbol">)</a> <a id="1979" href="Algebra.Operations.CommutativeMonoid.html#1404" class="Function Operator">×′</a> <a id="1982" href="Algebra.html#9051" class="Field">1#</a>         <a id="1993" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="1996" href="Relation.Binary.Core.html#5646" class="Function">sym</a> <a id="2000" href="Function.html#1920" class="Function Operator">$</a> <a id="2002" href="Algebra.Operations.CommutativeMonoid.html#2749" class="Function">×≈×′</a> <a id="2007" class="Symbol">(</a><a id="2008" href="Algebra.Operations.Semiring.html#1956" class="Bound">m</a> <a id="2010" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">ℕ*</a> <a id="2013" href="Algebra.Operations.Semiring.html#1958" class="Bound">n</a><a id="2014" class="Symbol">)</a> <a id="2016" href="Algebra.html#9051" class="Field">1#</a> <a id="2019" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="2023" class="Symbol">(</a><a id="2024" href="Algebra.Operations.Semiring.html#1956" class="Bound">m</a> <a id="2026" href="Agda.Builtin.Nat.html#433" class="Primitive Operator">ℕ*</a> <a id="2029" href="Algebra.Operations.Semiring.html#1958" class="Bound">n</a><a id="2030" class="Symbol">)</a> <a id="2032" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a>  <a id="2035" href="Algebra.html#9051" class="Field">1#</a>         <a id="2046" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="2049" href="Algebra.Operations.Semiring.html#1223" class="Function">×1-homo-*</a> <a id="2059" href="Algebra.Operations.Semiring.html#1956" class="Bound">m</a> <a id="2061" href="Algebra.Operations.Semiring.html#1958" class="Bound">n</a> <a id="2063" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="2067" class="Symbol">(</a><a id="2068" href="Algebra.Operations.Semiring.html#1956" class="Bound">m</a> <a id="2070" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a>  <a id="2073" href="Algebra.html#9051" class="Field">1#</a><a id="2075" class="Symbol">)</a> <a id="2077" href="Algebra.html#8997" class="Field Operator">*</a> <a id="2079" class="Symbol">(</a><a id="2080" href="Algebra.Operations.Semiring.html#1958" class="Bound">n</a> <a id="2082" href="Algebra.Operations.CommutativeMonoid.html#1229" class="Function Operator">×</a>  <a id="2085" href="Algebra.html#9051" class="Field">1#</a><a id="2087" class="Symbol">)</a>  <a id="2090" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">≈⟨</a> <a id="2093" href="Algebra.Structures.html#853" class="Function">*-cong</a> <a id="2100" class="Symbol">(</a><a id="2101" href="Algebra.Operations.CommutativeMonoid.html#2749" class="Function">×≈×′</a> <a id="2106" href="Algebra.Operations.Semiring.html#1956" class="Bound">m</a> <a id="2108" href="Algebra.html#9051" class="Field">1#</a><a id="2110" class="Symbol">)</a> <a id="2112" class="Symbol">(</a><a id="2113" href="Algebra.Operations.CommutativeMonoid.html#2749" class="Function">×≈×′</a> <a id="2118" href="Algebra.Operations.Semiring.html#1958" class="Bound">n</a> <a id="2120" href="Algebra.html#9051" class="Field">1#</a><a id="2122" class="Symbol">)</a> <a id="2124" href="Relation.Binary.PreorderReasoning.html#1323" class="Function Operator">⟩</a>
  <a id="2128" class="Symbol">(</a><a id="2129" href="Algebra.Operations.Semiring.html#1956" class="Bound">m</a> <a id="2131" href="Algebra.Operations.CommutativeMonoid.html#1404" class="Function Operator">×′</a> <a id="2134" href="Algebra.html#9051" class="Field">1#</a><a id="2136" class="Symbol">)</a> <a id="2138" href="Algebra.html#8997" class="Field Operator">*</a> <a id="2140" class="Symbol">(</a><a id="2141" href="Algebra.Operations.Semiring.html#1958" class="Bound">n</a> <a id="2143" href="Algebra.Operations.CommutativeMonoid.html#1404" class="Function Operator">×′</a> <a id="2146" href="Algebra.html#9051" class="Field">1#</a><a id="2148" class="Symbol">)</a>  <a id="2151" href="Relation.Binary.PreorderReasoning.html#1710" class="Function Operator">∎</a>

<a id="2154" class="Comment">------------------------------------------------------------------------</a>
<a id="2227" class="Comment">-- Properties of _^_</a>

<a id="2249" class="Comment">-- _^_ preserves equality.</a>

<a id="^-congˡ"></a><a id="2277" href="Algebra.Operations.Semiring.html#2277" class="Function">^-congˡ</a> <a id="2285" class="Symbol">:</a> <a id="2287" class="Symbol">∀</a> <a id="2289" href="Algebra.Operations.Semiring.html#2289" class="Bound">n</a> <a id="2291" class="Symbol">→</a> <a id="2293" class="Symbol">(</a><a id="2294" href="Algebra.Operations.Semiring.html#1011" class="Function Operator">_^</a> <a id="2297" href="Algebra.Operations.Semiring.html#2289" class="Bound">n</a><a id="2298" class="Symbol">)</a> <a id="2300" href="Relation.Binary.Core.html#1593" class="Function Operator">Preserves</a> <a id="2310" href="Algebra.html#8937" class="Field Operator">_≈_</a> <a id="2314" href="Relation.Binary.Core.html#1593" class="Function Operator">⟶</a> <a id="2316" href="Algebra.html#8937" class="Field Operator">_≈_</a>
<a id="2320" href="Algebra.Operations.Semiring.html#2277" class="Function">^-congˡ</a> <a id="2328" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>    <a id="2336" href="Algebra.Operations.Semiring.html#2336" class="Bound">x≈y</a> <a id="2340" class="Symbol">=</a> <a id="2342" href="Relation.Binary.Core.html#5620" class="Function">refl</a>
<a id="2347" href="Algebra.Operations.Semiring.html#2277" class="Function">^-congˡ</a> <a id="2355" class="Symbol">(</a><a id="2356" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2360" href="Algebra.Operations.Semiring.html#2360" class="Bound">n</a><a id="2361" class="Symbol">)</a> <a id="2363" href="Algebra.Operations.Semiring.html#2363" class="Bound">x≈y</a> <a id="2367" class="Symbol">=</a> <a id="2369" href="Algebra.Structures.html#853" class="Function">*-cong</a> <a id="2376" href="Algebra.Operations.Semiring.html#2363" class="Bound">x≈y</a> <a id="2380" class="Symbol">(</a><a id="2381" href="Algebra.Operations.Semiring.html#2277" class="Function">^-congˡ</a> <a id="2389" href="Algebra.Operations.Semiring.html#2360" class="Bound">n</a> <a id="2391" href="Algebra.Operations.Semiring.html#2363" class="Bound">x≈y</a><a id="2394" class="Symbol">)</a>

<a id="^-cong"></a><a id="2397" href="Algebra.Operations.Semiring.html#2397" class="Function">^-cong</a> <a id="2404" class="Symbol">:</a> <a id="2406" href="Algebra.Operations.Semiring.html#1011" class="Function Operator">_^_</a> <a id="2410" href="Relation.Binary.Core.html#1736" class="Function Operator">Preserves₂</a> <a id="2421" href="Algebra.html#8937" class="Field Operator">_≈_</a> <a id="2425" href="Relation.Binary.Core.html#1736" class="Function Operator">⟶</a> <a id="2427" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">_≡_</a> <a id="2431" href="Relation.Binary.Core.html#1736" class="Function Operator">⟶</a> <a id="2433" href="Algebra.html#8937" class="Field Operator">_≈_</a>
<a id="2437" href="Algebra.Operations.Semiring.html#2397" class="Function">^-cong</a> <a id="2444" class="Symbol">{</a><a id="2445" class="Argument">v</a> <a id="2447" class="Symbol">=</a> <a id="2449" href="Algebra.Operations.Semiring.html#2449" class="Bound">n</a><a id="2450" class="Symbol">}</a> <a id="2452" href="Algebra.Operations.Semiring.html#2452" class="Bound">x≈y</a> <a id="2456" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a> <a id="2463" class="Symbol">=</a> <a id="2465" href="Algebra.Operations.Semiring.html#2277" class="Function">^-congˡ</a> <a id="2473" href="Algebra.Operations.Semiring.html#2449" class="Bound">n</a> <a id="2475" href="Algebra.Operations.Semiring.html#2452" class="Bound">x≈y</a>
</pre></body></html>