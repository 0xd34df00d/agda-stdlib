<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.DifferenceVec</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Vectors with fast append</a>
<a id="134" class="Comment">------------------------------------------------------------------------</a>

<a id="208" class="Symbol">{-#</a> <a id="212" class="Keyword">OPTIONS</a> <a id="220" class="Option">--without-K</a> <a id="232" class="Symbol">#-}</a>

<a id="237" class="Keyword">module</a> <a id="244" href="Data.DifferenceVec.html" class="Module">Data.DifferenceVec</a> <a id="263" class="Keyword">where</a>

<a id="270" class="Keyword">open</a> <a id="275" class="Keyword">import</a> <a id="282" href="Data.DifferenceNat.html" class="Module">Data.DifferenceNat</a>
<a id="301" class="Keyword">open</a> <a id="306" class="Keyword">import</a> <a id="313" href="Data.Vec.html" class="Module">Data.Vec</a> <a id="322" class="Symbol">as</a> <a id="325" class="Module">V</a> <a id="327" class="Keyword">using</a> <a id="333" class="Symbol">(</a><a id="334" href="Data.Vec.html#766" class="Datatype">Vec</a><a id="337" class="Symbol">)</a>
<a id="339" class="Keyword">open</a> <a id="344" class="Keyword">import</a> <a id="351" href="Function.html" class="Module">Function</a>
<a id="360" class="Keyword">import</a> <a id="367" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="381" class="Symbol">as</a> <a id="384" class="Module">N</a>

<a id="387" class="Keyword">infixr</a> <a id="394" class="Number">5</a> <a id="396" href="Data.DifferenceVec.html#543" class="Function Operator">_∷_</a> <a id="400" href="Data.DifferenceVec.html#699" class="Function Operator">_++_</a>

<a id="DiffVec"></a><a id="406" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="414" class="Symbol">:</a> <a id="416" class="Symbol">∀</a> <a id="418" class="Symbol">{</a><a id="419" href="Data.DifferenceVec.html#419" class="Bound">ℓ</a><a id="420" class="Symbol">}</a> <a id="422" class="Symbol">→</a> <a id="424" class="PrimitiveType">Set</a> <a id="428" href="Data.DifferenceVec.html#419" class="Bound">ℓ</a> <a id="430" class="Symbol">→</a> <a id="432" href="Data.DifferenceNat.html#398" class="Function">Diffℕ</a> <a id="438" class="Symbol">→</a> <a id="440" class="PrimitiveType">Set</a> <a id="444" href="Data.DifferenceVec.html#419" class="Bound">ℓ</a>
<a id="446" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="454" href="Data.DifferenceVec.html#454" class="Bound">A</a> <a id="456" href="Data.DifferenceVec.html#456" class="Bound">m</a> <a id="458" class="Symbol">=</a> <a id="460" class="Symbol">∀</a> <a id="462" class="Symbol">{</a><a id="463" href="Data.DifferenceVec.html#463" class="Bound">n</a><a id="464" class="Symbol">}</a> <a id="466" class="Symbol">→</a> <a id="468" href="Data.Vec.html#766" class="Datatype">Vec</a> <a id="472" href="Data.DifferenceVec.html#454" class="Bound">A</a> <a id="474" href="Data.DifferenceVec.html#463" class="Bound">n</a> <a id="476" class="Symbol">→</a> <a id="478" href="Data.Vec.html#766" class="Datatype">Vec</a> <a id="482" href="Data.DifferenceVec.html#454" class="Bound">A</a> <a id="484" class="Symbol">(</a><a id="485" href="Data.DifferenceVec.html#456" class="Bound">m</a> <a id="487" href="Data.DifferenceVec.html#463" class="Bound">n</a><a id="488" class="Symbol">)</a>

<a id="[]"></a><a id="491" href="Data.DifferenceVec.html#491" class="Function">[]</a> <a id="494" class="Symbol">:</a> <a id="496" class="Symbol">∀</a> <a id="498" class="Symbol">{</a><a id="499" href="Data.DifferenceVec.html#499" class="Bound">a</a><a id="500" class="Symbol">}</a> <a id="502" class="Symbol">{</a><a id="503" href="Data.DifferenceVec.html#503" class="Bound">A</a> <a id="505" class="Symbol">:</a> <a id="507" class="PrimitiveType">Set</a> <a id="511" href="Data.DifferenceVec.html#499" class="Bound">a</a><a id="512" class="Symbol">}</a> <a id="514" class="Symbol">→</a> <a id="516" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="524" href="Data.DifferenceVec.html#503" class="Bound">A</a> <a id="526" href="Data.DifferenceNat.html#425" class="Function">0#</a>
<a id="529" href="Data.DifferenceVec.html#491" class="Function">[]</a> <a id="532" class="Symbol">=</a> <a id="534" class="Symbol">λ</a> <a id="536" href="Data.DifferenceVec.html#536" class="Bound">k</a> <a id="538" class="Symbol">→</a> <a id="540" href="Data.DifferenceVec.html#536" class="Bound">k</a>

<a id="_∷_"></a><a id="543" href="Data.DifferenceVec.html#543" class="Function Operator">_∷_</a> <a id="547" class="Symbol">:</a> <a id="549" class="Symbol">∀</a> <a id="551" class="Symbol">{</a><a id="552" href="Data.DifferenceVec.html#552" class="Bound">a</a><a id="553" class="Symbol">}</a> <a id="555" class="Symbol">{</a><a id="556" href="Data.DifferenceVec.html#556" class="Bound">A</a> <a id="558" class="Symbol">:</a> <a id="560" class="PrimitiveType">Set</a> <a id="564" href="Data.DifferenceVec.html#552" class="Bound">a</a><a id="565" class="Symbol">}</a> <a id="567" class="Symbol">{</a><a id="568" href="Data.DifferenceVec.html#568" class="Bound">n</a><a id="569" class="Symbol">}</a> <a id="571" class="Symbol">→</a> <a id="573" href="Data.DifferenceVec.html#556" class="Bound">A</a> <a id="575" class="Symbol">→</a> <a id="577" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="585" href="Data.DifferenceVec.html#556" class="Bound">A</a> <a id="587" href="Data.DifferenceVec.html#568" class="Bound">n</a> <a id="589" class="Symbol">→</a> <a id="591" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="599" href="Data.DifferenceVec.html#556" class="Bound">A</a> <a id="601" class="Symbol">(</a><a id="602" href="Data.DifferenceNat.html#450" class="Function">suc</a> <a id="606" href="Data.DifferenceVec.html#568" class="Bound">n</a><a id="607" class="Symbol">)</a>
<a id="609" href="Data.DifferenceVec.html#609" class="Bound">x</a> <a id="611" href="Data.DifferenceVec.html#543" class="Function Operator">∷</a> <a id="613" href="Data.DifferenceVec.html#613" class="Bound">xs</a> <a id="616" class="Symbol">=</a> <a id="618" class="Symbol">λ</a> <a id="620" href="Data.DifferenceVec.html#620" class="Bound">k</a> <a id="622" class="Symbol">→</a> <a id="624" href="Data.Vec.html#825" class="InductiveConstructor Operator">V._∷_</a> <a id="630" href="Data.DifferenceVec.html#609" class="Bound">x</a> <a id="632" class="Symbol">(</a><a id="633" href="Data.DifferenceVec.html#613" class="Bound">xs</a> <a id="636" href="Data.DifferenceVec.html#620" class="Bound">k</a><a id="637" class="Symbol">)</a>

<a id="[_]"></a><a id="640" href="Data.DifferenceVec.html#640" class="Function Operator">[_]</a> <a id="644" class="Symbol">:</a> <a id="646" class="Symbol">∀</a> <a id="648" class="Symbol">{</a><a id="649" href="Data.DifferenceVec.html#649" class="Bound">a</a><a id="650" class="Symbol">}</a> <a id="652" class="Symbol">{</a><a id="653" href="Data.DifferenceVec.html#653" class="Bound">A</a> <a id="655" class="Symbol">:</a> <a id="657" class="PrimitiveType">Set</a> <a id="661" href="Data.DifferenceVec.html#649" class="Bound">a</a><a id="662" class="Symbol">}</a> <a id="664" class="Symbol">→</a> <a id="666" href="Data.DifferenceVec.html#653" class="Bound">A</a> <a id="668" class="Symbol">→</a> <a id="670" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="678" href="Data.DifferenceVec.html#653" class="Bound">A</a> <a id="680" href="Data.DifferenceNat.html#497" class="Function">1#</a>
<a id="683" href="Data.DifferenceVec.html#640" class="Function Operator">[</a> <a id="685" href="Data.DifferenceVec.html#685" class="Bound">x</a> <a id="687" href="Data.DifferenceVec.html#640" class="Function Operator">]</a> <a id="689" class="Symbol">=</a> <a id="691" href="Data.DifferenceVec.html#685" class="Bound">x</a> <a id="693" href="Data.DifferenceVec.html#543" class="Function Operator">∷</a> <a id="695" href="Data.DifferenceVec.html#491" class="Function">[]</a>

<a id="_++_"></a><a id="699" href="Data.DifferenceVec.html#699" class="Function Operator">_++_</a> <a id="704" class="Symbol">:</a> <a id="706" class="Symbol">∀</a> <a id="708" class="Symbol">{</a><a id="709" href="Data.DifferenceVec.html#709" class="Bound">a</a><a id="710" class="Symbol">}</a> <a id="712" class="Symbol">{</a><a id="713" href="Data.DifferenceVec.html#713" class="Bound">A</a> <a id="715" class="Symbol">:</a> <a id="717" class="PrimitiveType">Set</a> <a id="721" href="Data.DifferenceVec.html#709" class="Bound">a</a><a id="722" class="Symbol">}</a> <a id="724" class="Symbol">{</a><a id="725" href="Data.DifferenceVec.html#725" class="Bound">m</a> <a id="727" href="Data.DifferenceVec.html#727" class="Bound">n</a><a id="728" class="Symbol">}</a> <a id="730" class="Symbol">→</a>
       <a id="739" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="747" href="Data.DifferenceVec.html#713" class="Bound">A</a> <a id="749" href="Data.DifferenceVec.html#725" class="Bound">m</a> <a id="751" class="Symbol">→</a> <a id="753" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="761" href="Data.DifferenceVec.html#713" class="Bound">A</a> <a id="763" href="Data.DifferenceVec.html#727" class="Bound">n</a> <a id="765" class="Symbol">→</a> <a id="767" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="775" href="Data.DifferenceVec.html#713" class="Bound">A</a> <a id="777" class="Symbol">(</a><a id="778" href="Data.DifferenceVec.html#725" class="Bound">m</a> <a id="780" href="Data.DifferenceNat.html#521" class="Function Operator">+</a> <a id="782" href="Data.DifferenceVec.html#727" class="Bound">n</a><a id="783" class="Symbol">)</a>
<a id="785" href="Data.DifferenceVec.html#785" class="Bound">xs</a> <a id="788" href="Data.DifferenceVec.html#699" class="Function Operator">++</a> <a id="791" href="Data.DifferenceVec.html#791" class="Bound">ys</a> <a id="794" class="Symbol">=</a> <a id="796" class="Symbol">λ</a> <a id="798" href="Data.DifferenceVec.html#798" class="Bound">k</a> <a id="800" class="Symbol">→</a> <a id="802" href="Data.DifferenceVec.html#785" class="Bound">xs</a> <a id="805" class="Symbol">(</a><a id="806" href="Data.DifferenceVec.html#791" class="Bound">ys</a> <a id="809" href="Data.DifferenceVec.html#798" class="Bound">k</a><a id="810" class="Symbol">)</a>

<a id="toVec"></a><a id="813" href="Data.DifferenceVec.html#813" class="Function">toVec</a> <a id="819" class="Symbol">:</a> <a id="821" class="Symbol">∀</a> <a id="823" class="Symbol">{</a><a id="824" href="Data.DifferenceVec.html#824" class="Bound">a</a><a id="825" class="Symbol">}</a> <a id="827" class="Symbol">{</a><a id="828" href="Data.DifferenceVec.html#828" class="Bound">A</a> <a id="830" class="Symbol">:</a> <a id="832" class="PrimitiveType">Set</a> <a id="836" href="Data.DifferenceVec.html#824" class="Bound">a</a><a id="837" class="Symbol">}</a> <a id="839" class="Symbol">{</a><a id="840" href="Data.DifferenceVec.html#840" class="Bound">n</a><a id="841" class="Symbol">}</a> <a id="843" class="Symbol">→</a> <a id="845" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="853" href="Data.DifferenceVec.html#828" class="Bound">A</a> <a id="855" href="Data.DifferenceVec.html#840" class="Bound">n</a> <a id="857" class="Symbol">→</a> <a id="859" href="Data.Vec.html#766" class="Datatype">Vec</a> <a id="863" href="Data.DifferenceVec.html#828" class="Bound">A</a> <a id="865" class="Symbol">(</a><a id="866" href="Data.DifferenceNat.html#572" class="Function">toℕ</a> <a id="870" href="Data.DifferenceVec.html#840" class="Bound">n</a><a id="871" class="Symbol">)</a>
<a id="873" href="Data.DifferenceVec.html#813" class="Function">toVec</a> <a id="879" href="Data.DifferenceVec.html#879" class="Bound">xs</a> <a id="882" class="Symbol">=</a> <a id="884" href="Data.DifferenceVec.html#879" class="Bound">xs</a> <a id="887" href="Data.Vec.html#806" class="InductiveConstructor">V.[]</a>

<a id="893" class="Comment">-- fromVec xs is linear in the length of xs.</a>

<a id="fromVec"></a><a id="939" href="Data.DifferenceVec.html#939" class="Function">fromVec</a> <a id="947" class="Symbol">:</a> <a id="949" class="Symbol">∀</a> <a id="951" class="Symbol">{</a><a id="952" href="Data.DifferenceVec.html#952" class="Bound">a</a><a id="953" class="Symbol">}</a> <a id="955" class="Symbol">{</a><a id="956" href="Data.DifferenceVec.html#956" class="Bound">A</a> <a id="958" class="Symbol">:</a> <a id="960" class="PrimitiveType">Set</a> <a id="964" href="Data.DifferenceVec.html#952" class="Bound">a</a><a id="965" class="Symbol">}</a> <a id="967" class="Symbol">{</a><a id="968" href="Data.DifferenceVec.html#968" class="Bound">n</a><a id="969" class="Symbol">}</a> <a id="971" class="Symbol">→</a> <a id="973" href="Data.Vec.html#766" class="Datatype">Vec</a> <a id="977" href="Data.DifferenceVec.html#956" class="Bound">A</a> <a id="979" href="Data.DifferenceVec.html#968" class="Bound">n</a> <a id="981" class="Symbol">→</a> <a id="983" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="991" href="Data.DifferenceVec.html#956" class="Bound">A</a> <a id="993" class="Symbol">(</a><a id="994" href="Data.DifferenceNat.html#641" class="Function">fromℕ</a> <a id="1000" href="Data.DifferenceVec.html#968" class="Bound">n</a><a id="1001" class="Symbol">)</a>
<a id="1003" href="Data.DifferenceVec.html#939" class="Function">fromVec</a> <a id="1011" href="Data.DifferenceVec.html#1011" class="Bound">xs</a> <a id="1014" class="Symbol">=</a> <a id="1016" class="Symbol">λ</a> <a id="1018" href="Data.DifferenceVec.html#1018" class="Bound">k</a> <a id="1020" class="Symbol">→</a> <a id="1022" href="Data.DifferenceVec.html#1011" class="Bound">xs</a> <a id="1025" href="Function.html#2541" class="Function Operator">⟨</a> <a id="1027" href="Data.Vec.html#2288" class="Function Operator">V._++_</a> <a id="1034" href="Function.html#2541" class="Function Operator">⟩</a> <a id="1036" href="Data.DifferenceVec.html#1018" class="Bound">k</a>

<a id="head"></a><a id="1039" href="Data.DifferenceVec.html#1039" class="Function">head</a> <a id="1044" class="Symbol">:</a> <a id="1046" class="Symbol">∀</a> <a id="1048" class="Symbol">{</a><a id="1049" href="Data.DifferenceVec.html#1049" class="Bound">a</a><a id="1050" class="Symbol">}</a> <a id="1052" class="Symbol">{</a><a id="1053" href="Data.DifferenceVec.html#1053" class="Bound">A</a> <a id="1055" class="Symbol">:</a> <a id="1057" class="PrimitiveType">Set</a> <a id="1061" href="Data.DifferenceVec.html#1049" class="Bound">a</a><a id="1062" class="Symbol">}</a> <a id="1064" class="Symbol">{</a><a id="1065" href="Data.DifferenceVec.html#1065" class="Bound">n</a><a id="1066" class="Symbol">}</a> <a id="1068" class="Symbol">→</a> <a id="1070" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="1078" href="Data.DifferenceVec.html#1053" class="Bound">A</a> <a id="1080" class="Symbol">(</a><a id="1081" href="Data.DifferenceNat.html#450" class="Function">suc</a> <a id="1085" href="Data.DifferenceVec.html#1065" class="Bound">n</a><a id="1086" class="Symbol">)</a> <a id="1088" class="Symbol">→</a> <a id="1090" href="Data.DifferenceVec.html#1053" class="Bound">A</a>
<a id="1092" href="Data.DifferenceVec.html#1039" class="Function">head</a> <a id="1097" href="Data.DifferenceVec.html#1097" class="Bound">xs</a> <a id="1100" class="Symbol">=</a> <a id="1102" href="Data.Vec.html#1233" class="Function">V.head</a> <a id="1109" class="Symbol">(</a><a id="1110" href="Data.DifferenceVec.html#813" class="Function">toVec</a> <a id="1116" href="Data.DifferenceVec.html#1097" class="Bound">xs</a><a id="1118" class="Symbol">)</a>

<a id="tail"></a><a id="1121" href="Data.DifferenceVec.html#1121" class="Function">tail</a> <a id="1126" class="Symbol">:</a> <a id="1128" class="Symbol">∀</a> <a id="1130" class="Symbol">{</a><a id="1131" href="Data.DifferenceVec.html#1131" class="Bound">a</a><a id="1132" class="Symbol">}</a> <a id="1134" class="Symbol">{</a><a id="1135" href="Data.DifferenceVec.html#1135" class="Bound">A</a> <a id="1137" class="Symbol">:</a> <a id="1139" class="PrimitiveType">Set</a> <a id="1143" href="Data.DifferenceVec.html#1131" class="Bound">a</a><a id="1144" class="Symbol">}</a> <a id="1146" class="Symbol">{</a><a id="1147" href="Data.DifferenceVec.html#1147" class="Bound">n</a><a id="1148" class="Symbol">}</a> <a id="1150" class="Symbol">→</a> <a id="1152" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="1160" href="Data.DifferenceVec.html#1135" class="Bound">A</a> <a id="1162" class="Symbol">(</a><a id="1163" href="Data.DifferenceNat.html#450" class="Function">suc</a> <a id="1167" href="Data.DifferenceVec.html#1147" class="Bound">n</a><a id="1168" class="Symbol">)</a> <a id="1170" class="Symbol">→</a> <a id="1172" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="1180" href="Data.DifferenceVec.html#1135" class="Bound">A</a> <a id="1182" href="Data.DifferenceVec.html#1147" class="Bound">n</a>
<a id="1184" href="Data.DifferenceVec.html#1121" class="Function">tail</a> <a id="1189" href="Data.DifferenceVec.html#1189" class="Bound">xs</a> <a id="1192" class="Symbol">=</a> <a id="1194" class="Symbol">λ</a> <a id="1196" href="Data.DifferenceVec.html#1196" class="Bound">k</a> <a id="1198" class="Symbol">→</a> <a id="1200" href="Data.Vec.html#1299" class="Function">V.tail</a> <a id="1207" class="Symbol">(</a><a id="1208" href="Data.DifferenceVec.html#1189" class="Bound">xs</a> <a id="1211" href="Data.DifferenceVec.html#1196" class="Bound">k</a><a id="1212" class="Symbol">)</a>

<a id="take"></a><a id="1215" href="Data.DifferenceVec.html#1215" class="Function">take</a> <a id="1220" class="Symbol">:</a> <a id="1222" class="Symbol">∀</a> <a id="1224" class="Symbol">{</a><a id="1225" href="Data.DifferenceVec.html#1225" class="Bound">a</a><a id="1226" class="Symbol">}</a> <a id="1228" class="Symbol">{</a><a id="1229" href="Data.DifferenceVec.html#1229" class="Bound">A</a> <a id="1231" class="Symbol">:</a> <a id="1233" class="PrimitiveType">Set</a> <a id="1237" href="Data.DifferenceVec.html#1225" class="Bound">a</a><a id="1238" class="Symbol">}</a> <a id="1240" href="Data.DifferenceVec.html#1240" class="Bound">m</a> <a id="1242" class="Symbol">{</a><a id="1243" href="Data.DifferenceVec.html#1243" class="Bound">n</a><a id="1244" class="Symbol">}</a> <a id="1246" class="Symbol">→</a>
       <a id="1255" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="1263" href="Data.DifferenceVec.html#1229" class="Bound">A</a> <a id="1265" class="Symbol">(</a><a id="1266" href="Data.DifferenceNat.html#641" class="Function">fromℕ</a> <a id="1272" href="Data.DifferenceVec.html#1240" class="Bound">m</a> <a id="1274" href="Data.DifferenceNat.html#521" class="Function Operator">+</a> <a id="1276" href="Data.DifferenceVec.html#1243" class="Bound">n</a><a id="1277" class="Symbol">)</a> <a id="1279" class="Symbol">→</a> <a id="1281" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="1289" href="Data.DifferenceVec.html#1229" class="Bound">A</a> <a id="1291" class="Symbol">(</a><a id="1292" href="Data.DifferenceNat.html#641" class="Function">fromℕ</a> <a id="1298" href="Data.DifferenceVec.html#1240" class="Bound">m</a><a id="1299" class="Symbol">)</a>
<a id="1301" href="Data.DifferenceVec.html#1215" class="Function">take</a> <a id="1306" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">N.zero</a>    <a id="1316" href="Data.DifferenceVec.html#1316" class="Bound">xs</a> <a id="1319" class="Symbol">=</a> <a id="1321" href="Data.DifferenceVec.html#491" class="Function">[]</a>
<a id="1324" href="Data.DifferenceVec.html#1215" class="Function">take</a> <a id="1329" class="Symbol">(</a><a id="1330" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">N.suc</a> <a id="1336" href="Data.DifferenceVec.html#1336" class="Bound">m</a><a id="1337" class="Symbol">)</a> <a id="1339" href="Data.DifferenceVec.html#1339" class="Bound">xs</a> <a id="1342" class="Symbol">=</a> <a id="1344" href="Data.DifferenceVec.html#1039" class="Function">head</a> <a id="1349" href="Data.DifferenceVec.html#1339" class="Bound">xs</a> <a id="1352" href="Data.DifferenceVec.html#543" class="Function Operator">∷</a> <a id="1354" href="Data.DifferenceVec.html#1215" class="Function">take</a> <a id="1359" href="Data.DifferenceVec.html#1336" class="Bound">m</a> <a id="1361" class="Symbol">(</a><a id="1362" href="Data.DifferenceVec.html#1121" class="Function">tail</a> <a id="1367" href="Data.DifferenceVec.html#1339" class="Bound">xs</a><a id="1369" class="Symbol">)</a>

<a id="drop"></a><a id="1372" href="Data.DifferenceVec.html#1372" class="Function">drop</a> <a id="1377" class="Symbol">:</a> <a id="1379" class="Symbol">∀</a> <a id="1381" class="Symbol">{</a><a id="1382" href="Data.DifferenceVec.html#1382" class="Bound">a</a><a id="1383" class="Symbol">}</a> <a id="1385" class="Symbol">{</a><a id="1386" href="Data.DifferenceVec.html#1386" class="Bound">A</a> <a id="1388" class="Symbol">:</a> <a id="1390" class="PrimitiveType">Set</a> <a id="1394" href="Data.DifferenceVec.html#1382" class="Bound">a</a><a id="1395" class="Symbol">}</a> <a id="1397" href="Data.DifferenceVec.html#1397" class="Bound">m</a> <a id="1399" class="Symbol">{</a><a id="1400" href="Data.DifferenceVec.html#1400" class="Bound">n</a><a id="1401" class="Symbol">}</a> <a id="1403" class="Symbol">→</a>
       <a id="1412" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="1420" href="Data.DifferenceVec.html#1386" class="Bound">A</a> <a id="1422" class="Symbol">(</a><a id="1423" href="Data.DifferenceNat.html#641" class="Function">fromℕ</a> <a id="1429" href="Data.DifferenceVec.html#1397" class="Bound">m</a> <a id="1431" href="Data.DifferenceNat.html#521" class="Function Operator">+</a> <a id="1433" href="Data.DifferenceVec.html#1400" class="Bound">n</a><a id="1434" class="Symbol">)</a> <a id="1436" class="Symbol">→</a> <a id="1438" href="Data.DifferenceVec.html#406" class="Function">DiffVec</a> <a id="1446" href="Data.DifferenceVec.html#1386" class="Bound">A</a> <a id="1448" href="Data.DifferenceVec.html#1400" class="Bound">n</a>
<a id="1450" href="Data.DifferenceVec.html#1372" class="Function">drop</a> <a id="1455" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">N.zero</a>    <a id="1465" href="Data.DifferenceVec.html#1465" class="Bound">xs</a> <a id="1468" class="Symbol">=</a> <a id="1470" href="Data.DifferenceVec.html#1465" class="Bound">xs</a>
<a id="1473" href="Data.DifferenceVec.html#1372" class="Function">drop</a> <a id="1478" class="Symbol">(</a><a id="1479" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">N.suc</a> <a id="1485" href="Data.DifferenceVec.html#1485" class="Bound">m</a><a id="1486" class="Symbol">)</a> <a id="1488" href="Data.DifferenceVec.html#1488" class="Bound">xs</a> <a id="1491" class="Symbol">=</a> <a id="1493" href="Data.DifferenceVec.html#1372" class="Function">drop</a> <a id="1498" href="Data.DifferenceVec.html#1485" class="Bound">m</a> <a id="1500" class="Symbol">(</a><a id="1501" href="Data.DifferenceVec.html#1121" class="Function">tail</a> <a id="1506" href="Data.DifferenceVec.html#1488" class="Bound">xs</a><a id="1508" class="Symbol">)</a>
</pre></body></html>