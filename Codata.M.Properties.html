<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.M.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Properties of operations on M-types</a>
<a id="145" class="Comment">------------------------------------------------------------------------</a>

<a id="219" class="Symbol">{-#</a> <a id="223" class="Keyword">OPTIONS</a> <a id="231" class="Option">--without-K</a> <a id="243" class="Option">--safe</a> <a id="250" class="Symbol">#-}</a>

<a id="255" class="Keyword">module</a> <a id="262" href="Codata.M.Properties.html" class="Module">Codata.M.Properties</a> <a id="282" class="Keyword">where</a>

<a id="289" class="Keyword">open</a> <a id="294" class="Keyword">import</a> <a id="301" href="Level.html" class="Module">Level</a>
<a id="307" class="Keyword">open</a> <a id="312" class="Keyword">import</a> <a id="319" href="Size.html" class="Module">Size</a>
<a id="324" class="Keyword">open</a> <a id="329" class="Keyword">import</a> <a id="336" href="Codata.Thunk.html" class="Module">Codata.Thunk</a> <a id="349" class="Keyword">using</a> <a id="355" class="Symbol">(</a><a id="356" href="Codata.Thunk.html#454" class="Record">Thunk</a><a id="361" class="Symbol">;</a> <a id="363" href="Codata.Thunk.html#530" class="Field">force</a><a id="368" class="Symbol">)</a>
<a id="370" class="Keyword">open</a> <a id="375" class="Keyword">import</a> <a id="382" href="Codata.M.html" class="Module">Codata.M</a>
<a id="391" class="Keyword">open</a> <a id="396" class="Keyword">import</a> <a id="403" href="Codata.M.Bisimilarity.html" class="Module">Codata.M.Bisimilarity</a>
<a id="425" class="Keyword">open</a> <a id="430" class="Keyword">import</a> <a id="437" href="Data.Container.Core.html" class="Module">Data.Container.Core</a> <a id="457" class="Symbol">as</a> <a id="460" class="Module">C</a> <a id="462" class="Keyword">hiding</a> <a id="469" class="Symbol">(</a><a id="470" href="Data.Container.Core.html#875" class="Function">map</a><a id="473" class="Symbol">)</a>
<a id="475" class="Keyword">import</a> <a id="482" href="Data.Container.Morphism.html" class="Module">Data.Container.Morphism</a> <a id="506" class="Symbol">as</a> <a id="509" class="Module">Mp</a>
<a id="512" class="Keyword">open</a> <a id="517" class="Keyword">import</a> <a id="524" href="Data.Product.html" class="Module">Data.Product</a> <a id="537" class="Symbol">as</a> <a id="540" class="Module">Prod</a> <a id="545" class="Keyword">using</a> <a id="551" class="Symbol">(</a><a id="552" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">_,_</a><a id="555" class="Symbol">)</a>
<a id="557" class="Keyword">open</a> <a id="562" class="Keyword">import</a> <a id="569" href="Data.Product.Properties.html" class="Module">Data.Product.Properties</a>
<a id="593" class="Keyword">open</a> <a id="598" class="Keyword">import</a> <a id="605" href="Function.html" class="Module">Function</a>
<a id="614" class="Keyword">import</a> <a id="621" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="659" class="Symbol">as</a> <a id="662" class="Module">P</a>

<a id="665" class="Keyword">open</a> <a id="670" class="Keyword">import</a> <a id="677" href="Data.Container.Relation.Binary.Pointwise.html" class="Module">Data.Container.Relation.Binary.Pointwise</a> <a id="718" class="Keyword">using</a> <a id="724" class="Symbol">(</a><a id="725" href="Data.Container.Relation.Binary.Pointwise.html#861" class="InductiveConstructor Operator">_,_</a><a id="728" class="Symbol">)</a>
<a id="730" class="Keyword">import</a> <a id="737" href="Data.Container.Relation.Binary.Equality.Setoid.html" class="Module">Data.Container.Relation.Binary.Equality.Setoid</a> <a id="784" class="Symbol">as</a> <a id="787" class="Module">EqSetoid</a>

<a id="797" class="Keyword">private</a> <a id="805" class="Keyword">module</a> <a id="Eq"></a><a id="812" href="Codata.M.Properties.html#812" class="Module">Eq</a> <a id="815" class="Symbol">{</a><a id="816" href="Codata.M.Properties.html#816" class="Bound">a</a><a id="817" class="Symbol">}</a> <a id="819" class="Symbol">(</a><a id="820" href="Codata.M.Properties.html#820" class="Bound">A</a> <a id="822" class="Symbol">:</a> <a id="824" class="PrimitiveType">Set</a> <a id="828" href="Codata.M.Properties.html#816" class="Bound">a</a><a id="829" class="Symbol">)</a> <a id="831" class="Symbol">=</a> <a id="833" href="Data.Container.Relation.Binary.Equality.Setoid.html" class="Module">EqSetoid</a> <a id="842" class="Symbol">(</a><a id="843" href="Relation.Binary.PropositionalEquality.html#1508" class="Function">P.setoid</a> <a id="852" href="Codata.M.Properties.html#820" class="Bound">A</a><a id="853" class="Symbol">)</a>
<a id="855" class="Keyword">open</a> <a id="860" href="Codata.M.Properties.html#812" class="Module">Eq</a> <a id="863" class="Keyword">using</a> <a id="869" class="Symbol">(</a><a id="870" href="Data.Container.Relation.Binary.Equality.Setoid.html#843" class="Function">Eq</a><a id="872" class="Symbol">)</a>

<a id="875" class="Keyword">module</a> <a id="882" href="Codata.M.Properties.html#882" class="Module">_</a> <a id="884" class="Symbol">{</a><a id="885" href="Codata.M.Properties.html#885" class="Bound">s</a> <a id="887" href="Codata.M.Properties.html#887" class="Bound">p</a><a id="888" class="Symbol">}</a> <a id="890" class="Symbol">{</a><a id="891" href="Codata.M.Properties.html#891" class="Bound">C</a> <a id="893" class="Symbol">:</a> <a id="895" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="905" href="Codata.M.Properties.html#885" class="Bound">s</a> <a id="907" href="Codata.M.Properties.html#887" class="Bound">p</a><a id="908" class="Symbol">}</a> <a id="910" class="Keyword">where</a>

  <a id="919" href="Codata.M.Properties.html#919" class="Function">map-id</a> <a id="926" class="Symbol">:</a> <a id="928" class="Symbol">∀</a> <a id="930" class="Symbol">{</a><a id="931" href="Codata.M.Properties.html#931" class="Bound">i</a><a id="932" class="Symbol">}</a> <a id="934" href="Codata.M.Properties.html#934" class="Bound">c</a> <a id="936" class="Symbol">→</a> <a id="938" href="Codata.M.Bisimilarity.html#611" class="Datatype">Bisim</a> <a id="944" href="Codata.M.Properties.html#891" class="Bound">C</a> <a id="946" href="Codata.M.Properties.html#931" class="Bound">i</a> <a id="948" class="Symbol">(</a><a id="949" href="Codata.M.html#847" class="Function">map</a> <a id="953" class="Symbol">(</a><a id="954" href="Data.Container.Morphism.html#375" class="Function">Mp.id</a> <a id="960" href="Codata.M.Properties.html#891" class="Bound">C</a><a id="961" class="Symbol">)</a> <a id="963" href="Codata.M.Properties.html#934" class="Bound">c</a><a id="964" class="Symbol">)</a> <a id="966" href="Codata.M.Properties.html#934" class="Bound">c</a>
  <a id="970" href="Codata.M.Properties.html#919" class="Function">map-id</a> <a id="977" class="Symbol">(</a><a id="978" href="Codata.M.html#508" class="InductiveConstructor">inf</a> <a id="982" class="Symbol">(</a><a id="983" href="Codata.M.Properties.html#983" class="Bound">s</a> <a id="985" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="987" href="Codata.M.Properties.html#987" class="Bound">f</a><a id="988" class="Symbol">))</a> <a id="991" class="Symbol">=</a> <a id="993" href="Codata.M.Bisimilarity.html#684" class="InductiveConstructor">inf</a> <a id="997" class="Symbol">(</a><a id="998" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a> <a id="1005" href="Data.Container.Relation.Binary.Pointwise.html#861" class="InductiveConstructor Operator">,</a> <a id="1007" class="Symbol">λ</a> <a id="1009" class="Keyword">where</a> <a id="1015" href="Codata.M.Properties.html#1015" class="Bound">p</a> <a id="1017" class="Symbol">.</a><a id="1018" href="Codata.Thunk.html#530" class="Field">force</a> <a id="1024" class="Symbol">→</a> <a id="1026" href="Codata.M.Properties.html#919" class="Function">map-id</a> <a id="1033" class="Symbol">(</a><a id="1034" href="Codata.M.Properties.html#987" class="Bound">f</a> <a id="1036" href="Codata.M.Properties.html#1015" class="Bound">p</a> <a id="1038" class="Symbol">.</a><a id="1039" href="Codata.Thunk.html#530" class="Field">force</a><a id="1044" class="Symbol">))</a>

<a id="1048" class="Keyword">module</a> <a id="1055" href="Codata.M.Properties.html#1055" class="Module">_</a> <a id="1057" class="Symbol">{</a><a id="1058" href="Codata.M.Properties.html#1058" class="Bound">s₁</a> <a id="1061" href="Codata.M.Properties.html#1061" class="Bound">s₂</a> <a id="1064" href="Codata.M.Properties.html#1064" class="Bound">p₁</a> <a id="1067" href="Codata.M.Properties.html#1067" class="Bound">p₂</a><a id="1069" class="Symbol">}</a> <a id="1071" class="Symbol">{</a><a id="1072" href="Codata.M.Properties.html#1072" class="Bound">C₁</a> <a id="1075" class="Symbol">:</a> <a id="1077" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1087" href="Codata.M.Properties.html#1058" class="Bound">s₁</a> <a id="1090" href="Codata.M.Properties.html#1064" class="Bound">p₁</a><a id="1092" class="Symbol">}</a> <a id="1094" class="Symbol">{</a><a id="1095" href="Codata.M.Properties.html#1095" class="Bound">C₂</a> <a id="1098" class="Symbol">:</a> <a id="1100" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1110" href="Codata.M.Properties.html#1061" class="Bound">s₂</a> <a id="1113" href="Codata.M.Properties.html#1067" class="Bound">p₂</a><a id="1115" class="Symbol">}</a> <a id="1117" class="Keyword">where</a>

  <a id="1126" href="Codata.M.Properties.html#1126" class="Function">map-cong</a> <a id="1135" class="Symbol">:</a> <a id="1137" class="Symbol">∀</a> <a id="1139" class="Symbol">{</a><a id="1140" href="Codata.M.Properties.html#1140" class="Bound">i</a><a id="1141" class="Symbol">}</a> <a id="1143" class="Symbol">{</a><a id="1144" href="Codata.M.Properties.html#1144" class="Bound">f</a> <a id="1146" href="Codata.M.Properties.html#1146" class="Bound">g</a> <a id="1148" class="Symbol">:</a> <a id="1150" href="Codata.M.Properties.html#1072" class="Bound">C₁</a> <a id="1153" href="Data.Container.Core.html#1030" class="Record Operator">⇒</a> <a id="1155" href="Codata.M.Properties.html#1095" class="Bound">C₂</a><a id="1157" class="Symbol">}</a> <a id="1159" class="Symbol">→</a>
             <a id="1174" class="Symbol">(∀</a> <a id="1177" class="Symbol">{</a><a id="1178" href="Codata.M.Properties.html#1178" class="Bound">X</a><a id="1179" class="Symbol">}</a> <a id="1181" href="Codata.M.Properties.html#1181" class="Bound">t</a> <a id="1183" class="Symbol">→</a> <a id="1185" href="Data.Container.Relation.Binary.Equality.Setoid.html#843" class="Function">Eq</a> <a id="1188" href="Codata.M.Properties.html#1178" class="Bound">X</a> <a id="1190" href="Codata.M.Properties.html#1095" class="Bound">C₂</a> <a id="1193" class="Symbol">(</a><a id="1194" href="Data.Container.Core.html#1262" class="Function Operator">⟪</a> <a id="1196" href="Codata.M.Properties.html#1144" class="Bound">f</a> <a id="1198" href="Data.Container.Core.html#1262" class="Function Operator">⟫</a> <a id="1200" href="Codata.M.Properties.html#1181" class="Bound">t</a><a id="1201" class="Symbol">)</a> <a id="1203" class="Symbol">(</a><a id="1204" href="Data.Container.Core.html#1262" class="Function Operator">⟪</a> <a id="1206" href="Codata.M.Properties.html#1146" class="Bound">g</a> <a id="1208" href="Data.Container.Core.html#1262" class="Function Operator">⟫</a> <a id="1210" href="Codata.M.Properties.html#1181" class="Bound">t</a><a id="1211" class="Symbol">))</a> <a id="1214" class="Symbol">→</a>
             <a id="1229" class="Symbol">∀</a> <a id="1231" href="Codata.M.Properties.html#1231" class="Bound">c₁</a> <a id="1234" class="Symbol">→</a> <a id="1236" href="Codata.M.Bisimilarity.html#611" class="Datatype">Bisim</a> <a id="1242" href="Codata.M.Properties.html#1095" class="Bound">C₂</a> <a id="1245" href="Codata.M.Properties.html#1140" class="Bound">i</a> <a id="1247" class="Symbol">(</a><a id="1248" href="Codata.M.html#847" class="Function">map</a> <a id="1252" href="Codata.M.Properties.html#1144" class="Bound">f</a> <a id="1254" href="Codata.M.Properties.html#1231" class="Bound">c₁</a><a id="1256" class="Symbol">)</a> <a id="1258" class="Symbol">(</a><a id="1259" href="Codata.M.html#847" class="Function">map</a> <a id="1263" href="Codata.M.Properties.html#1146" class="Bound">g</a> <a id="1265" href="Codata.M.Properties.html#1231" class="Bound">c₁</a><a id="1267" class="Symbol">)</a>
  <a id="1271" href="Codata.M.Properties.html#1126" class="Function">map-cong</a> <a id="1280" class="Symbol">{</a><a id="1281" class="Argument">f</a> <a id="1283" class="Symbol">=</a> <a id="1285" href="Codata.M.Properties.html#1285" class="Bound">f</a><a id="1286" class="Symbol">}</a> <a id="1288" class="Symbol">{</a><a id="1289" href="Codata.M.Properties.html#1289" class="Bound">g</a><a id="1290" class="Symbol">}</a> <a id="1292" href="Codata.M.Properties.html#1292" class="Bound">f≗g</a> <a id="1296" class="Symbol">(</a><a id="1297" href="Codata.M.html#508" class="InductiveConstructor">inf</a> <a id="1301" href="Codata.M.Properties.html#1301" class="Bound">t</a><a id="1302" class="Symbol">@(</a><a id="1304" href="Codata.M.Properties.html#1304" class="Bound">s</a> <a id="1306" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1308" href="Codata.M.Properties.html#1308" class="Bound">n</a><a id="1309" class="Symbol">))</a> <a id="1312" class="Keyword">with</a> <a id="1317" href="Codata.M.Properties.html#1292" class="Bound">f≗g</a> <a id="1321" href="Codata.M.Properties.html#1301" class="Bound">t</a>
  <a id="1325" class="Symbol">...</a> <a id="1329" class="Symbol">|</a> <a id="1331" href="Codata.M.Properties.html#1331" class="Bound">eqs</a> <a id="1335" href="Data.Container.Relation.Binary.Pointwise.html#861" class="InductiveConstructor Operator">,</a> <a id="1337" href="Codata.M.Properties.html#1337" class="Bound">eqf</a> <a id="1341" class="Symbol">=</a> <a id="1343" href="Codata.M.Bisimilarity.html#684" class="InductiveConstructor">inf</a> <a id="1347" class="Symbol">(</a><a id="1348" href="Codata.M.Properties.html#1331" class="Bound">eqs</a> <a id="1352" href="Data.Container.Relation.Binary.Pointwise.html#861" class="InductiveConstructor Operator">,</a> <a id="1354" class="Symbol">λ</a> <a id="1356" class="Keyword">where</a>
     <a id="1367" href="Codata.M.Properties.html#1367" class="Bound">p</a> <a id="1369" class="Symbol">.</a><a id="1370" href="Codata.Thunk.html#530" class="Field">force</a> <a id="1376" class="Symbol">{</a><a id="1377" href="Codata.M.Properties.html#1377" class="Bound">j</a><a id="1378" class="Symbol">}</a> <a id="1380" class="Symbol">→</a> <a id="1382" href="Relation.Binary.PropositionalEquality.Core.html#978" class="Function">P.subst</a> <a id="1390" class="Symbol">(λ</a> <a id="1393" href="Codata.M.Properties.html#1393" class="Bound">t</a> <a id="1395" class="Symbol">→</a> <a id="1397" href="Codata.M.Bisimilarity.html#611" class="Datatype">Bisim</a> <a id="1403" href="Codata.M.Properties.html#1095" class="Bound">C₂</a> <a id="1406" href="Codata.M.Properties.html#1377" class="Bound">j</a> <a id="1408" class="Symbol">(</a><a id="1409" href="Codata.M.html#847" class="Function">map</a> <a id="1413" class="Bound">f</a> <a id="1415" class="Symbol">(</a><a id="1416" class="Bound">n</a> <a id="1418" class="Symbol">(</a><a id="1419" href="Data.Container.Core.html#1202" class="Field">position</a> <a id="1428" class="Bound">f</a> <a id="1430" href="Codata.M.Properties.html#1367" class="Bound">p</a><a id="1431" class="Symbol">)</a> <a id="1433" class="Symbol">.</a><a id="1434" href="Codata.Thunk.html#530" class="Field">force</a><a id="1439" class="Symbol">))</a>
                                              <a id="1488" class="Symbol">(</a><a id="1489" href="Codata.M.html#847" class="Function">map</a> <a id="1493" class="Bound">g</a> <a id="1495" class="Symbol">(</a><a id="1496" href="Codata.M.Properties.html#1393" class="Bound">t</a> <a id="1498" class="Symbol">.</a><a id="1499" href="Codata.Thunk.html#530" class="Field">force</a><a id="1504" class="Symbol">)))</a>
                    <a id="1528" class="Symbol">(</a><a id="1529" href="Codata.M.Properties.html#1337" class="Bound">eqf</a> <a id="1533" href="Codata.M.Properties.html#1367" class="Bound">p</a><a id="1534" class="Symbol">)</a>
                    <a id="1556" class="Symbol">(</a><a id="1557" href="Codata.M.Properties.html#1126" class="Function">map-cong</a> <a id="1566" class="Bound">f≗g</a> <a id="1570" class="Symbol">(</a><a id="1571" class="Bound">n</a> <a id="1573" class="Symbol">(</a><a id="1574" href="Data.Container.Core.html#1202" class="Field">position</a> <a id="1583" class="Bound">f</a> <a id="1585" href="Codata.M.Properties.html#1367" class="Bound">p</a><a id="1586" class="Symbol">)</a> <a id="1588" class="Symbol">.</a><a id="1589" href="Codata.Thunk.html#530" class="Field">force</a><a id="1594" class="Symbol">)))</a>

<a id="1599" class="Keyword">module</a> <a id="1606" href="Codata.M.Properties.html#1606" class="Module">_</a> <a id="1608" class="Symbol">{</a><a id="1609" href="Codata.M.Properties.html#1609" class="Bound">s₁</a> <a id="1612" href="Codata.M.Properties.html#1612" class="Bound">s₂</a> <a id="1615" href="Codata.M.Properties.html#1615" class="Bound">s₃</a> <a id="1618" href="Codata.M.Properties.html#1618" class="Bound">p₁</a> <a id="1621" href="Codata.M.Properties.html#1621" class="Bound">p₂</a> <a id="1624" href="Codata.M.Properties.html#1624" class="Bound">p₃</a><a id="1626" class="Symbol">}</a> <a id="1628" class="Symbol">{</a><a id="1629" href="Codata.M.Properties.html#1629" class="Bound">C₁</a> <a id="1632" class="Symbol">:</a> <a id="1634" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1644" href="Codata.M.Properties.html#1609" class="Bound">s₁</a> <a id="1647" href="Codata.M.Properties.html#1618" class="Bound">p₁</a><a id="1649" class="Symbol">}</a>
         <a id="1660" class="Symbol">{</a><a id="1661" href="Codata.M.Properties.html#1661" class="Bound">C₂</a> <a id="1664" class="Symbol">:</a> <a id="1666" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1676" href="Codata.M.Properties.html#1612" class="Bound">s₂</a> <a id="1679" href="Codata.M.Properties.html#1621" class="Bound">p₂</a><a id="1681" class="Symbol">}</a> <a id="1683" class="Symbol">{</a><a id="1684" href="Codata.M.Properties.html#1684" class="Bound">C₃</a> <a id="1687" class="Symbol">:</a> <a id="1689" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1699" href="Codata.M.Properties.html#1615" class="Bound">s₃</a> <a id="1702" href="Codata.M.Properties.html#1624" class="Bound">p₃</a><a id="1704" class="Symbol">}</a> <a id="1706" class="Keyword">where</a>

  <a id="1715" href="Codata.M.Properties.html#1715" class="Function">map-compose</a> <a id="1727" class="Symbol">:</a> <a id="1729" class="Symbol">∀</a> <a id="1731" class="Symbol">{</a><a id="1732" href="Codata.M.Properties.html#1732" class="Bound">i</a><a id="1733" class="Symbol">}</a> <a id="1735" class="Symbol">{</a><a id="1736" href="Codata.M.Properties.html#1736" class="Bound">g</a> <a id="1738" class="Symbol">:</a> <a id="1740" href="Codata.M.Properties.html#1661" class="Bound">C₂</a> <a id="1743" href="Data.Container.Core.html#1030" class="Record Operator">⇒</a> <a id="1745" href="Codata.M.Properties.html#1684" class="Bound">C₃</a><a id="1747" class="Symbol">}</a> <a id="1749" class="Symbol">{</a><a id="1750" href="Codata.M.Properties.html#1750" class="Bound">f</a> <a id="1752" class="Symbol">:</a> <a id="1754" href="Codata.M.Properties.html#1629" class="Bound">C₁</a> <a id="1757" href="Data.Container.Core.html#1030" class="Record Operator">⇒</a> <a id="1759" href="Codata.M.Properties.html#1661" class="Bound">C₂</a><a id="1761" class="Symbol">}</a> <a id="1763" href="Codata.M.Properties.html#1763" class="Bound">c₁</a> <a id="1766" class="Symbol">→</a>
                <a id="1784" href="Codata.M.Bisimilarity.html#611" class="Datatype">Bisim</a> <a id="1790" href="Codata.M.Properties.html#1684" class="Bound">C₃</a> <a id="1793" href="Codata.M.Properties.html#1732" class="Bound">i</a> <a id="1795" class="Symbol">(</a><a id="1796" href="Codata.M.html#847" class="Function">map</a> <a id="1800" class="Symbol">(</a><a id="1801" href="Codata.M.Properties.html#1736" class="Bound">g</a> <a id="1803" href="Data.Container.Morphism.html#571" class="Function Operator">Mp.∘</a> <a id="1808" href="Codata.M.Properties.html#1750" class="Bound">f</a><a id="1809" class="Symbol">)</a> <a id="1811" href="Codata.M.Properties.html#1763" class="Bound">c₁</a><a id="1813" class="Symbol">)</a> <a id="1815" class="Symbol">(</a><a id="1816" href="Codata.M.html#847" class="Function">map</a> <a id="1820" href="Codata.M.Properties.html#1736" class="Bound">g</a> <a id="1822" href="Function.html#2024" class="Function Operator">$′</a> <a id="1825" href="Codata.M.html#847" class="Function">map</a> <a id="1829" href="Codata.M.Properties.html#1750" class="Bound">f</a> <a id="1831" href="Codata.M.Properties.html#1763" class="Bound">c₁</a><a id="1833" class="Symbol">)</a>
  <a id="1837" href="Codata.M.Properties.html#1715" class="Function">map-compose</a> <a id="1849" class="Symbol">(</a><a id="1850" href="Codata.M.html#508" class="InductiveConstructor">inf</a> <a id="1854" class="Symbol">(</a><a id="1855" href="Codata.M.Properties.html#1855" class="Bound">s</a> <a id="1857" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1859" href="Codata.M.Properties.html#1859" class="Bound">f</a><a id="1860" class="Symbol">))</a> <a id="1863" class="Symbol">=</a> <a id="1865" href="Codata.M.Bisimilarity.html#684" class="InductiveConstructor">inf</a> <a id="1869" class="Symbol">(</a><a id="1870" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a> <a id="1877" href="Data.Container.Relation.Binary.Pointwise.html#861" class="InductiveConstructor Operator">,</a> <a id="1879" class="Symbol">λ</a> <a id="1881" class="Keyword">where</a> <a id="1887" href="Codata.M.Properties.html#1887" class="Bound">p</a> <a id="1889" class="Symbol">.</a><a id="1890" href="Codata.Thunk.html#530" class="Field">force</a> <a id="1896" class="Symbol">→</a> <a id="1898" href="Codata.M.Properties.html#1715" class="Function">map-compose</a> <a id="1910" class="Symbol">(</a><a id="1911" href="Codata.M.Properties.html#1859" class="Bound">f</a> <a id="1913" class="Symbol">_</a> <a id="1915" class="Symbol">.</a><a id="1916" href="Codata.Thunk.html#530" class="Field">force</a><a id="1921" class="Symbol">))</a>


<a id="1926" class="Keyword">module</a> <a id="1933" href="Codata.M.Properties.html#1933" class="Module">_</a> <a id="1935" class="Symbol">{</a><a id="1936" href="Codata.M.Properties.html#1936" class="Bound">s₁</a> <a id="1939" href="Codata.M.Properties.html#1939" class="Bound">s₂</a> <a id="1942" href="Codata.M.Properties.html#1942" class="Bound">p₁</a> <a id="1945" href="Codata.M.Properties.html#1945" class="Bound">p₂</a> <a id="1948" href="Codata.M.Properties.html#1948" class="Bound">s</a><a id="1949" class="Symbol">}</a> <a id="1951" class="Symbol">{</a><a id="1952" href="Codata.M.Properties.html#1952" class="Bound">C₁</a> <a id="1955" class="Symbol">:</a> <a id="1957" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1967" href="Codata.M.Properties.html#1936" class="Bound">s₁</a> <a id="1970" href="Codata.M.Properties.html#1942" class="Bound">p₁</a><a id="1972" class="Symbol">}</a> <a id="1974" class="Symbol">{</a><a id="1975" href="Codata.M.Properties.html#1975" class="Bound">C₂</a> <a id="1978" class="Symbol">:</a> <a id="1980" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1990" href="Codata.M.Properties.html#1939" class="Bound">s₂</a> <a id="1993" href="Codata.M.Properties.html#1945" class="Bound">p₂</a><a id="1995" class="Symbol">}</a>
         <a id="2006" class="Symbol">{</a><a id="2007" href="Codata.M.Properties.html#2007" class="Bound">S</a> <a id="2009" class="Symbol">:</a> <a id="2011" class="PrimitiveType">Set</a> <a id="2015" href="Codata.M.Properties.html#1948" class="Bound">s</a><a id="2016" class="Symbol">}</a> <a id="2018" class="Symbol">{</a><a id="2019" href="Codata.M.Properties.html#2019" class="Bound">alg</a> <a id="2023" class="Symbol">:</a> <a id="2025" href="Codata.M.Properties.html#2007" class="Bound">S</a> <a id="2027" class="Symbol">→</a> <a id="2029" href="Data.Container.Core.html#750" class="Function Operator">⟦</a> <a id="2031" href="Codata.M.Properties.html#1952" class="Bound">C₁</a> <a id="2034" href="Data.Container.Core.html#750" class="Function Operator">⟧</a> <a id="2036" href="Codata.M.Properties.html#2007" class="Bound">S</a><a id="2037" class="Symbol">}</a> <a id="2039" class="Symbol">{</a><a id="2040" href="Codata.M.Properties.html#2040" class="Bound">f</a> <a id="2042" class="Symbol">:</a> <a id="2044" href="Codata.M.Properties.html#1952" class="Bound">C₁</a> <a id="2047" href="Data.Container.Core.html#1030" class="Record Operator">⇒</a> <a id="2049" href="Codata.M.Properties.html#1975" class="Bound">C₂</a><a id="2051" class="Symbol">}</a> <a id="2053" class="Keyword">where</a>

  <a id="2062" href="Codata.M.Properties.html#2062" class="Function">map-unfold</a> <a id="2073" class="Symbol">:</a> <a id="2075" class="Symbol">∀</a> <a id="2077" class="Symbol">{</a><a id="2078" href="Codata.M.Properties.html#2078" class="Bound">i</a><a id="2079" class="Symbol">}</a> <a id="2081" href="Codata.M.Properties.html#2081" class="Bound">s</a> <a id="2083" class="Symbol">→</a> <a id="2085" href="Codata.M.Bisimilarity.html#611" class="Datatype">Bisim</a> <a id="2091" href="Codata.M.Properties.html#1975" class="Bound">C₂</a> <a id="2094" href="Codata.M.Properties.html#2078" class="Bound">i</a> <a id="2096" class="Symbol">(</a><a id="2097" href="Codata.M.html#847" class="Function">map</a> <a id="2101" href="Codata.M.Properties.html#2040" class="Bound">f</a> <a id="2103" class="Symbol">(</a><a id="2104" href="Codata.M.html#1073" class="Function">unfold</a> <a id="2111" href="Codata.M.Properties.html#2019" class="Bound">alg</a> <a id="2115" href="Codata.M.Properties.html#2081" class="Bound">s</a><a id="2116" class="Symbol">))</a>
                                    <a id="2155" class="Symbol">(</a><a id="2156" href="Codata.M.html#1073" class="Function">unfold</a> <a id="2163" class="Symbol">(</a><a id="2164" href="Data.Container.Core.html#1262" class="Function Operator">⟪</a> <a id="2166" href="Codata.M.Properties.html#2040" class="Bound">f</a> <a id="2168" href="Data.Container.Core.html#1262" class="Function Operator">⟫</a> <a id="2170" href="Function.html#994" class="Function Operator">∘′</a> <a id="2173" href="Codata.M.Properties.html#2019" class="Bound">alg</a><a id="2176" class="Symbol">)</a> <a id="2178" href="Codata.M.Properties.html#2081" class="Bound">s</a><a id="2179" class="Symbol">)</a>
  <a id="2183" href="Codata.M.Properties.html#2062" class="Function">map-unfold</a> <a id="2194" href="Codata.M.Properties.html#2194" class="Bound">s</a> <a id="2196" class="Symbol">=</a> <a id="2198" href="Codata.M.Bisimilarity.html#684" class="InductiveConstructor">inf</a> <a id="2202" class="Symbol">(</a><a id="2203" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a> <a id="2210" href="Data.Container.Relation.Binary.Pointwise.html#861" class="InductiveConstructor Operator">,</a> <a id="2212" class="Symbol">λ</a> <a id="2214" class="Keyword">where</a> <a id="2220" href="Codata.M.Properties.html#2220" class="Bound">p</a> <a id="2222" class="Symbol">.</a><a id="2223" href="Codata.Thunk.html#530" class="Field">force</a> <a id="2229" class="Symbol">→</a> <a id="2231" href="Codata.M.Properties.html#2062" class="Function">map-unfold</a> <a id="2242" class="Symbol">_)</a>
</pre></body></html>