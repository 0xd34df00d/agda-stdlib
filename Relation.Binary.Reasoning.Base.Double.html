<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Reasoning.Base.Double</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The basic code for equational reasoning with two relations:</a>
<a id="169" class="Comment">-- equality and some other ordering.</a>
<a id="206" class="Comment">------------------------------------------------------------------------</a>
<a id="279" class="Comment">--</a>
<a id="282" class="Comment">-- See `Data.Nat.Properties` or `Relation.Binary.Reasoning.PartialOrder`</a>
<a id="355" class="Comment">-- for examples of how to instantiate this module.</a>

<a id="407" class="Symbol">{-#</a> <a id="411" class="Keyword">OPTIONS</a> <a id="419" class="Option">--without-K</a> <a id="431" class="Option">--safe</a> <a id="438" class="Symbol">#-}</a>

<a id="443" class="Keyword">open</a> <a id="448" class="Keyword">import</a> <a id="455" href="Relation.Binary.html" class="Module">Relation.Binary</a>

<a id="472" class="Keyword">module</a> <a id="479" href="Relation.Binary.Reasoning.Base.Double.html" class="Module">Relation.Binary.Reasoning.Base.Double</a> <a id="517" class="Symbol">{</a><a id="518" href="Relation.Binary.Reasoning.Base.Double.html#518" class="Bound">a</a> <a id="520" href="Relation.Binary.Reasoning.Base.Double.html#520" class="Bound">ℓ₁</a> <a id="523" href="Relation.Binary.Reasoning.Base.Double.html#523" class="Bound">ℓ₂</a><a id="525" class="Symbol">}</a> <a id="527" class="Symbol">{</a><a id="528" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a> <a id="530" class="Symbol">:</a> <a id="532" class="PrimitiveType">Set</a> <a id="536" href="Relation.Binary.Reasoning.Base.Double.html#518" class="Bound">a</a><a id="537" class="Symbol">}</a>
  <a id="541" class="Symbol">{</a><a id="542" href="Relation.Binary.Reasoning.Base.Double.html#542" class="Bound Operator">_≈_</a> <a id="546" class="Symbol">:</a> <a id="548" href="Relation.Binary.Core.html#976" class="Function">Rel</a> <a id="552" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a> <a id="554" href="Relation.Binary.Reasoning.Base.Double.html#520" class="Bound">ℓ₁</a><a id="556" class="Symbol">}</a> <a id="558" class="Symbol">{</a><a id="559" href="Relation.Binary.Reasoning.Base.Double.html#559" class="Bound Operator">_∼_</a> <a id="563" class="Symbol">:</a> <a id="565" href="Relation.Binary.Core.html#976" class="Function">Rel</a> <a id="569" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a> <a id="571" href="Relation.Binary.Reasoning.Base.Double.html#523" class="Bound">ℓ₂</a><a id="573" class="Symbol">}</a> <a id="575" class="Symbol">(</a><a id="576" href="Relation.Binary.Reasoning.Base.Double.html#576" class="Bound">isPreorder</a> <a id="587" class="Symbol">:</a> <a id="589" href="Relation.Binary.html#781" class="Record">IsPreorder</a> <a id="600" href="Relation.Binary.Reasoning.Base.Double.html#542" class="Bound Operator">_≈_</a> <a id="604" href="Relation.Binary.Reasoning.Base.Double.html#559" class="Bound Operator">_∼_</a><a id="607" class="Symbol">)</a>
  <a id="611" class="Keyword">where</a>

<a id="618" class="Keyword">open</a> <a id="623" class="Keyword">import</a> <a id="630" href="Data.Product.html" class="Module">Data.Product</a> <a id="643" class="Keyword">using</a> <a id="649" class="Symbol">(</a><a id="650" href="Agda.Builtin.Sigma.html#155" class="Field">proj₁</a><a id="655" class="Symbol">;</a> <a id="657" href="Agda.Builtin.Sigma.html#167" class="Field">proj₂</a><a id="662" class="Symbol">)</a>
<a id="664" class="Keyword">open</a> <a id="669" class="Keyword">import</a> <a id="676" href="Level.html" class="Module">Level</a> <a id="682" class="Keyword">using</a> <a id="688" class="Symbol">(</a><a id="689" href="Agda.Primitive.html#408" class="Postulate">Level</a><a id="694" class="Symbol">;</a> <a id="696" href="Agda.Primitive.html#657" class="Primitive Operator">_⊔_</a><a id="699" class="Symbol">;</a> <a id="701" href="Level.html#394" class="Record">Lift</a><a id="705" class="Symbol">;</a> <a id="707" href="Level.html#451" class="InductiveConstructor">lift</a><a id="711" class="Symbol">)</a>
<a id="713" class="Keyword">open</a> <a id="718" class="Keyword">import</a> <a id="725" href="Function.html" class="Module">Function</a> <a id="734" class="Keyword">using</a> <a id="740" class="Symbol">(</a><a id="741" href="Function.html#3533" class="Function Operator">case_of_</a><a id="749" class="Symbol">;</a> <a id="751" href="Function.html#1104" class="Function">id</a><a id="753" class="Symbol">)</a>
<a id="755" class="Keyword">open</a> <a id="760" class="Keyword">import</a> <a id="767" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="805" class="Keyword">using</a> <a id="811" class="Symbol">(</a><a id="812" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">_≡_</a><a id="815" class="Symbol">;</a> <a id="817" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a><a id="821" class="Symbol">)</a>
<a id="823" class="Keyword">open</a> <a id="828" class="Keyword">import</a> <a id="835" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="852" class="Keyword">using</a> <a id="858" class="Symbol">(</a><a id="859" href="Relation.Nullary.html#570" class="Datatype">Dec</a><a id="862" class="Symbol">;</a> <a id="864" href="Relation.Nullary.html#606" class="InductiveConstructor">yes</a><a id="867" class="Symbol">;</a> <a id="869" href="Relation.Nullary.html#633" class="InductiveConstructor">no</a><a id="871" class="Symbol">)</a>
<a id="873" class="Keyword">open</a> <a id="878" class="Keyword">import</a> <a id="885" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="912" class="Keyword">using</a> <a id="918" class="Symbol">(</a><a id="919" href="Relation.Nullary.Decidable.html#933" class="Function">True</a><a id="923" class="Symbol">;</a> <a id="925" href="Relation.Nullary.Decidable.html#1091" class="Function">toWitness</a><a id="934" class="Symbol">)</a>

<a id="937" class="Keyword">open</a> <a id="942" href="Relation.Binary.html#781" class="Module">IsPreorder</a> <a id="953" href="Relation.Binary.Reasoning.Base.Double.html#576" class="Bound">isPreorder</a>

<a id="965" class="Comment">------------------------------------------------------------------------</a>
<a id="1038" class="Comment">-- A datatype to hide the current relation type</a>

<a id="1087" class="Keyword">data</a> <a id="_IsRelatedTo_"></a><a id="1092" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">_IsRelatedTo_</a> <a id="1106" class="Symbol">(</a><a id="1107" href="Relation.Binary.Reasoning.Base.Double.html#1107" class="Bound">x</a> <a id="1109" href="Relation.Binary.Reasoning.Base.Double.html#1109" class="Bound">y</a> <a id="1111" class="Symbol">:</a> <a id="1113" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a><a id="1114" class="Symbol">)</a> <a id="1116" class="Symbol">:</a> <a id="1118" class="PrimitiveType">Set</a> <a id="1122" class="Symbol">(</a><a id="1123" href="Relation.Binary.Reasoning.Base.Double.html#520" class="Bound">ℓ₁</a> <a id="1126" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1128" href="Relation.Binary.Reasoning.Base.Double.html#523" class="Bound">ℓ₂</a><a id="1130" class="Symbol">)</a> <a id="1132" class="Keyword">where</a>
  <a id="_IsRelatedTo_.nonstrict"></a><a id="1140" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="1150" class="Symbol">:</a> <a id="1152" class="Symbol">(</a><a id="1153" href="Relation.Binary.Reasoning.Base.Double.html#1153" class="Bound">x∼y</a> <a id="1157" class="Symbol">:</a> <a id="1159" href="Relation.Binary.Reasoning.Base.Double.html#1107" class="Bound">x</a> <a id="1161" href="Relation.Binary.Reasoning.Base.Double.html#559" class="Bound Operator">∼</a> <a id="1163" href="Relation.Binary.Reasoning.Base.Double.html#1109" class="Bound">y</a><a id="1164" class="Symbol">)</a> <a id="1166" class="Symbol">→</a> <a id="1168" href="Relation.Binary.Reasoning.Base.Double.html#1107" class="Bound">x</a> <a id="1170" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="1182" href="Relation.Binary.Reasoning.Base.Double.html#1109" class="Bound">y</a>
  <a id="_IsRelatedTo_.equals"></a><a id="1186" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="1196" class="Symbol">:</a> <a id="1198" class="Symbol">(</a><a id="1199" href="Relation.Binary.Reasoning.Base.Double.html#1199" class="Bound">x≈y</a> <a id="1203" class="Symbol">:</a> <a id="1205" href="Relation.Binary.Reasoning.Base.Double.html#1107" class="Bound">x</a> <a id="1207" href="Relation.Binary.Reasoning.Base.Double.html#542" class="Bound Operator">≈</a> <a id="1209" href="Relation.Binary.Reasoning.Base.Double.html#1109" class="Bound">y</a><a id="1210" class="Symbol">)</a> <a id="1212" class="Symbol">→</a> <a id="1214" href="Relation.Binary.Reasoning.Base.Double.html#1107" class="Bound">x</a> <a id="1216" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="1228" href="Relation.Binary.Reasoning.Base.Double.html#1109" class="Bound">y</a>

<a id="levelOf"></a><a id="1231" href="Relation.Binary.Reasoning.Base.Double.html#1231" class="Function">levelOf</a> <a id="1239" class="Symbol">:</a> <a id="1241" class="Symbol">∀</a> <a id="1243" class="Symbol">{</a><a id="1244" href="Relation.Binary.Reasoning.Base.Double.html#1244" class="Bound">x</a> <a id="1246" href="Relation.Binary.Reasoning.Base.Double.html#1246" class="Bound">y</a><a id="1247" class="Symbol">}</a> <a id="1249" class="Symbol">→</a> <a id="1251" href="Relation.Binary.Reasoning.Base.Double.html#1244" class="Bound">x</a> <a id="1253" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="1265" href="Relation.Binary.Reasoning.Base.Double.html#1246" class="Bound">y</a> <a id="1267" class="Symbol">→</a> <a id="1269" href="Agda.Primitive.html#408" class="Postulate">Level</a>
<a id="1275" href="Relation.Binary.Reasoning.Base.Double.html#1231" class="Function">levelOf</a> <a id="1283" class="Symbol">(</a><a id="1284" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="1294" href="Relation.Binary.Reasoning.Base.Double.html#1294" class="Bound">x∼y</a><a id="1297" class="Symbol">)</a> <a id="1299" class="Symbol">=</a> <a id="1301" href="Relation.Binary.Reasoning.Base.Double.html#523" class="Bound">ℓ₂</a>
<a id="1304" href="Relation.Binary.Reasoning.Base.Double.html#1231" class="Function">levelOf</a> <a id="1312" class="Symbol">(</a><a id="1313" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="1323" href="Relation.Binary.Reasoning.Base.Double.html#1323" class="Bound">x≈y</a><a id="1326" class="Symbol">)</a> <a id="1328" class="Symbol">=</a> <a id="1330" href="Relation.Binary.Reasoning.Base.Double.html#520" class="Bound">ℓ₁</a>

<a id="relOf"></a><a id="1334" href="Relation.Binary.Reasoning.Base.Double.html#1334" class="Function">relOf</a> <a id="1340" class="Symbol">:</a> <a id="1342" class="Symbol">∀</a> <a id="1344" class="Symbol">{</a><a id="1345" href="Relation.Binary.Reasoning.Base.Double.html#1345" class="Bound">x</a> <a id="1347" href="Relation.Binary.Reasoning.Base.Double.html#1347" class="Bound">y</a><a id="1348" class="Symbol">}</a> <a id="1350" class="Symbol">(</a><a id="1351" href="Relation.Binary.Reasoning.Base.Double.html#1351" class="Bound">r</a> <a id="1353" class="Symbol">:</a> <a id="1355" href="Relation.Binary.Reasoning.Base.Double.html#1345" class="Bound">x</a> <a id="1357" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="1369" href="Relation.Binary.Reasoning.Base.Double.html#1347" class="Bound">y</a><a id="1370" class="Symbol">)</a> <a id="1372" class="Symbol">→</a> <a id="1374" href="Relation.Binary.Core.html#976" class="Function">Rel</a> <a id="1378" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a> <a id="1380" class="Symbol">(</a><a id="1381" href="Relation.Binary.Reasoning.Base.Double.html#1231" class="Function">levelOf</a> <a id="1389" href="Relation.Binary.Reasoning.Base.Double.html#1351" class="Bound">r</a><a id="1390" class="Symbol">)</a>
<a id="1392" href="Relation.Binary.Reasoning.Base.Double.html#1334" class="Function">relOf</a> <a id="1398" class="Symbol">(</a><a id="1399" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="1409" href="Relation.Binary.Reasoning.Base.Double.html#1409" class="Bound">x∼y</a><a id="1412" class="Symbol">)</a> <a id="1414" class="Symbol">=</a> <a id="1416" href="Relation.Binary.Reasoning.Base.Double.html#559" class="Bound Operator">_∼_</a>
<a id="1420" href="Relation.Binary.Reasoning.Base.Double.html#1334" class="Function">relOf</a> <a id="1426" class="Symbol">(</a><a id="1427" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="1437" href="Relation.Binary.Reasoning.Base.Double.html#1437" class="Bound">x≈y</a><a id="1440" class="Symbol">)</a> <a id="1442" class="Symbol">=</a> <a id="1444" href="Relation.Binary.Reasoning.Base.Double.html#542" class="Bound Operator">_≈_</a>

<a id="1449" class="Comment">------------------------------------------------------------------------</a>
<a id="1522" class="Comment">-- A record that is used to ensure that the final relation proved by the</a>
<a id="1595" class="Comment">-- chain of reasoning can be converted into the required relation.</a>

<a id="1663" class="Keyword">data</a> <a id="IsEquality"></a><a id="1668" href="Relation.Binary.Reasoning.Base.Double.html#1668" class="Datatype">IsEquality</a> <a id="1679" class="Symbol">{</a><a id="1680" href="Relation.Binary.Reasoning.Base.Double.html#1680" class="Bound">x</a> <a id="1682" href="Relation.Binary.Reasoning.Base.Double.html#1682" class="Bound">y</a><a id="1683" class="Symbol">}</a> <a id="1685" class="Symbol">:</a> <a id="1687" href="Relation.Binary.Reasoning.Base.Double.html#1680" class="Bound">x</a> <a id="1689" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="1701" href="Relation.Binary.Reasoning.Base.Double.html#1682" class="Bound">y</a> <a id="1703" class="Symbol">→</a> <a id="1705" class="PrimitiveType">Set</a> <a id="1709" href="Relation.Binary.Reasoning.Base.Double.html#520" class="Bound">ℓ₁</a> <a id="1712" class="Keyword">where</a>
  <a id="IsEquality.isEquality"></a><a id="1720" href="Relation.Binary.Reasoning.Base.Double.html#1720" class="InductiveConstructor">isEquality</a> <a id="1731" class="Symbol">:</a> <a id="1733" class="Symbol">∀</a> <a id="1735" href="Relation.Binary.Reasoning.Base.Double.html#1735" class="Bound">x≈y</a> <a id="1739" class="Symbol">→</a> <a id="1741" href="Relation.Binary.Reasoning.Base.Double.html#1668" class="Datatype">IsEquality</a> <a id="1752" class="Symbol">(</a><a id="1753" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a> <a id="1760" href="Relation.Binary.Reasoning.Base.Double.html#1735" class="Bound">x≈y</a><a id="1763" class="Symbol">)</a>

<a id="IsEquality?"></a><a id="1766" href="Relation.Binary.Reasoning.Base.Double.html#1766" class="Function">IsEquality?</a> <a id="1778" class="Symbol">:</a> <a id="1780" class="Symbol">∀</a> <a id="1782" class="Symbol">{</a><a id="1783" href="Relation.Binary.Reasoning.Base.Double.html#1783" class="Bound">x</a> <a id="1785" href="Relation.Binary.Reasoning.Base.Double.html#1785" class="Bound">y</a><a id="1786" class="Symbol">}</a> <a id="1788" class="Symbol">(</a><a id="1789" href="Relation.Binary.Reasoning.Base.Double.html#1789" class="Bound">x≲y</a> <a id="1793" class="Symbol">:</a> <a id="1795" href="Relation.Binary.Reasoning.Base.Double.html#1783" class="Bound">x</a> <a id="1797" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="1809" href="Relation.Binary.Reasoning.Base.Double.html#1785" class="Bound">y</a><a id="1810" class="Symbol">)</a> <a id="1812" class="Symbol">→</a> <a id="1814" href="Relation.Nullary.html#570" class="Datatype">Dec</a> <a id="1818" class="Symbol">(</a><a id="1819" href="Relation.Binary.Reasoning.Base.Double.html#1668" class="Datatype">IsEquality</a> <a id="1830" href="Relation.Binary.Reasoning.Base.Double.html#1789" class="Bound">x≲y</a><a id="1833" class="Symbol">)</a>
<a id="1835" href="Relation.Binary.Reasoning.Base.Double.html#1766" class="Function">IsEquality?</a> <a id="1847" class="Symbol">(</a><a id="1848" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="1858" class="Symbol">_)</a> <a id="1861" class="Symbol">=</a> <a id="1863" href="Relation.Nullary.html#633" class="InductiveConstructor">no</a> <a id="1866" class="Symbol">λ()</a>
<a id="1870" href="Relation.Binary.Reasoning.Base.Double.html#1766" class="Function">IsEquality?</a> <a id="1882" class="Symbol">(</a><a id="1883" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a> <a id="1890" href="Relation.Binary.Reasoning.Base.Double.html#1890" class="Bound">x≈y</a><a id="1893" class="Symbol">)</a>  <a id="1896" class="Symbol">=</a> <a id="1898" href="Relation.Nullary.html#606" class="InductiveConstructor">yes</a> <a id="1902" class="Symbol">(</a><a id="1903" href="Relation.Binary.Reasoning.Base.Double.html#1720" class="InductiveConstructor">isEquality</a> <a id="1914" href="Relation.Binary.Reasoning.Base.Double.html#1890" class="Bound">x≈y</a><a id="1917" class="Symbol">)</a>

<a id="extractEquality"></a><a id="1920" href="Relation.Binary.Reasoning.Base.Double.html#1920" class="Function">extractEquality</a> <a id="1936" class="Symbol">:</a> <a id="1938" class="Symbol">∀</a> <a id="1940" class="Symbol">{</a><a id="1941" href="Relation.Binary.Reasoning.Base.Double.html#1941" class="Bound">x</a> <a id="1943" href="Relation.Binary.Reasoning.Base.Double.html#1943" class="Bound">y</a><a id="1944" class="Symbol">}</a> <a id="1946" class="Symbol">{</a><a id="1947" href="Relation.Binary.Reasoning.Base.Double.html#1947" class="Bound">x≲y</a> <a id="1951" class="Symbol">:</a> <a id="1953" href="Relation.Binary.Reasoning.Base.Double.html#1941" class="Bound">x</a> <a id="1955" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="1967" href="Relation.Binary.Reasoning.Base.Double.html#1943" class="Bound">y</a><a id="1968" class="Symbol">}</a> <a id="1970" class="Symbol">→</a> <a id="1972" href="Relation.Binary.Reasoning.Base.Double.html#1668" class="Datatype">IsEquality</a> <a id="1983" href="Relation.Binary.Reasoning.Base.Double.html#1947" class="Bound">x≲y</a> <a id="1987" class="Symbol">→</a> <a id="1989" href="Relation.Binary.Reasoning.Base.Double.html#1941" class="Bound">x</a> <a id="1991" href="Relation.Binary.Reasoning.Base.Double.html#542" class="Bound Operator">≈</a> <a id="1993" href="Relation.Binary.Reasoning.Base.Double.html#1943" class="Bound">y</a>
<a id="1995" href="Relation.Binary.Reasoning.Base.Double.html#1920" class="Function">extractEquality</a> <a id="2011" class="Symbol">(</a><a id="2012" href="Relation.Binary.Reasoning.Base.Double.html#1720" class="InductiveConstructor">isEquality</a> <a id="2023" href="Relation.Binary.Reasoning.Base.Double.html#2023" class="Bound">x≈y</a><a id="2026" class="Symbol">)</a> <a id="2028" class="Symbol">=</a> <a id="2030" href="Relation.Binary.Reasoning.Base.Double.html#2023" class="Bound">x≈y</a>

<a id="2035" class="Comment">------------------------------------------------------------------------</a>
<a id="2108" class="Comment">-- Reasoning combinators</a>

<a id="2134" class="Keyword">infix</a> <a id="2140" class="Number">-1</a> <a id="2143" href="Relation.Binary.Reasoning.Base.Double.html#2215" class="Function Operator">begin_</a> <a id="2150" href="Relation.Binary.Reasoning.Base.Double.html#2329" class="Function Operator">begin-equality_</a>
<a id="2166" class="Keyword">infixr</a> <a id="2173" class="Number">0</a> <a id="2175" href="Relation.Binary.Reasoning.Base.Double.html#2469" class="Function Operator">_∼⟨_⟩_</a> <a id="2182" href="Relation.Binary.Reasoning.Base.Double.html#2654" class="Function Operator">_≈⟨_⟩_</a> <a id="2189" href="Relation.Binary.Reasoning.Base.Double.html#2851" class="Function Operator">_≡⟨_⟩_</a> <a id="2196" href="Relation.Binary.Reasoning.Base.Double.html#3061" class="Function Operator">_≡⟨⟩_</a>
<a id="2202" class="Keyword">infix</a>  <a id="2209" class="Number">1</a> <a id="2211" href="Relation.Binary.Reasoning.Base.Double.html#3136" class="Function Operator">_∎</a>

<a id="begin_"></a><a id="2215" href="Relation.Binary.Reasoning.Base.Double.html#2215" class="Function Operator">begin_</a> <a id="2222" class="Symbol">:</a> <a id="2224" class="Symbol">∀</a> <a id="2226" class="Symbol">{</a><a id="2227" href="Relation.Binary.Reasoning.Base.Double.html#2227" class="Bound">x</a> <a id="2229" href="Relation.Binary.Reasoning.Base.Double.html#2229" class="Bound">y</a><a id="2230" class="Symbol">}</a> <a id="2232" class="Symbol">(</a><a id="2233" href="Relation.Binary.Reasoning.Base.Double.html#2233" class="Bound">r</a> <a id="2235" class="Symbol">:</a> <a id="2237" href="Relation.Binary.Reasoning.Base.Double.html#2227" class="Bound">x</a> <a id="2239" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="2251" href="Relation.Binary.Reasoning.Base.Double.html#2229" class="Bound">y</a><a id="2252" class="Symbol">)</a> <a id="2254" class="Symbol">→</a> <a id="2256" href="Relation.Binary.Reasoning.Base.Double.html#2227" class="Bound">x</a> <a id="2258" href="Relation.Binary.Reasoning.Base.Double.html#559" class="Bound Operator">∼</a> <a id="2260" href="Relation.Binary.Reasoning.Base.Double.html#2229" class="Bound">y</a>
<a id="2262" href="Relation.Binary.Reasoning.Base.Double.html#2215" class="Function Operator">begin</a> <a id="2268" class="Symbol">(</a><a id="2269" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="2279" href="Relation.Binary.Reasoning.Base.Double.html#2279" class="Bound">x∼y</a><a id="2282" class="Symbol">)</a> <a id="2284" class="Symbol">=</a> <a id="2286" href="Relation.Binary.Reasoning.Base.Double.html#2279" class="Bound">x∼y</a>
<a id="2290" href="Relation.Binary.Reasoning.Base.Double.html#2215" class="Function Operator">begin</a> <a id="2296" class="Symbol">(</a><a id="2297" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="2307" href="Relation.Binary.Reasoning.Base.Double.html#2307" class="Bound">x≈y</a><a id="2310" class="Symbol">)</a> <a id="2312" class="Symbol">=</a> <a id="2314" href="Relation.Binary.html#1091" class="Field">reflexive</a> <a id="2324" href="Relation.Binary.Reasoning.Base.Double.html#2307" class="Bound">x≈y</a>

<a id="begin-equality_"></a><a id="2329" href="Relation.Binary.Reasoning.Base.Double.html#2329" class="Function Operator">begin-equality_</a> <a id="2345" class="Symbol">:</a> <a id="2347" class="Symbol">∀</a> <a id="2349" class="Symbol">{</a><a id="2350" href="Relation.Binary.Reasoning.Base.Double.html#2350" class="Bound">x</a> <a id="2352" href="Relation.Binary.Reasoning.Base.Double.html#2352" class="Bound">y</a><a id="2353" class="Symbol">}</a> <a id="2355" class="Symbol">(</a><a id="2356" href="Relation.Binary.Reasoning.Base.Double.html#2356" class="Bound">r</a> <a id="2358" class="Symbol">:</a> <a id="2360" href="Relation.Binary.Reasoning.Base.Double.html#2350" class="Bound">x</a> <a id="2362" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="2374" href="Relation.Binary.Reasoning.Base.Double.html#2352" class="Bound">y</a><a id="2375" class="Symbol">)</a> <a id="2377" class="Symbol">→</a> <a id="2379" class="Symbol">{</a><a id="2380" href="Relation.Binary.Reasoning.Base.Double.html#2380" class="Bound">s</a> <a id="2382" class="Symbol">:</a> <a id="2384" href="Relation.Nullary.Decidable.html#933" class="Function">True</a> <a id="2389" class="Symbol">(</a><a id="2390" href="Relation.Binary.Reasoning.Base.Double.html#1766" class="Function">IsEquality?</a> <a id="2402" href="Relation.Binary.Reasoning.Base.Double.html#2356" class="Bound">r</a><a id="2403" class="Symbol">)}</a> <a id="2406" class="Symbol">→</a> <a id="2408" href="Relation.Binary.Reasoning.Base.Double.html#2350" class="Bound">x</a> <a id="2410" href="Relation.Binary.Reasoning.Base.Double.html#542" class="Bound Operator">≈</a> <a id="2412" href="Relation.Binary.Reasoning.Base.Double.html#2352" class="Bound">y</a>
<a id="2414" href="Relation.Binary.Reasoning.Base.Double.html#2329" class="Function Operator">begin-equality_</a> <a id="2430" href="Relation.Binary.Reasoning.Base.Double.html#2430" class="Bound">r</a> <a id="2432" class="Symbol">{</a><a id="2433" href="Relation.Binary.Reasoning.Base.Double.html#2433" class="Bound">s</a><a id="2434" class="Symbol">}</a> <a id="2436" class="Symbol">=</a> <a id="2438" href="Relation.Binary.Reasoning.Base.Double.html#1920" class="Function">extractEquality</a> <a id="2454" class="Symbol">(</a><a id="2455" href="Relation.Nullary.Decidable.html#1091" class="Function">toWitness</a> <a id="2465" href="Relation.Binary.Reasoning.Base.Double.html#2433" class="Bound">s</a><a id="2466" class="Symbol">)</a>

<a id="_∼⟨_⟩_"></a><a id="2469" href="Relation.Binary.Reasoning.Base.Double.html#2469" class="Function Operator">_∼⟨_⟩_</a> <a id="2476" class="Symbol">:</a> <a id="2478" class="Symbol">∀</a> <a id="2480" class="Symbol">(</a><a id="2481" href="Relation.Binary.Reasoning.Base.Double.html#2481" class="Bound">x</a> <a id="2483" class="Symbol">:</a> <a id="2485" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a><a id="2486" class="Symbol">)</a> <a id="2488" class="Symbol">{</a><a id="2489" href="Relation.Binary.Reasoning.Base.Double.html#2489" class="Bound">y</a> <a id="2491" href="Relation.Binary.Reasoning.Base.Double.html#2491" class="Bound">z</a><a id="2492" class="Symbol">}</a> <a id="2494" class="Symbol">→</a> <a id="2496" href="Relation.Binary.Reasoning.Base.Double.html#2481" class="Bound">x</a> <a id="2498" href="Relation.Binary.Reasoning.Base.Double.html#559" class="Bound Operator">∼</a> <a id="2500" href="Relation.Binary.Reasoning.Base.Double.html#2489" class="Bound">y</a> <a id="2502" class="Symbol">→</a> <a id="2504" href="Relation.Binary.Reasoning.Base.Double.html#2489" class="Bound">y</a> <a id="2506" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="2518" href="Relation.Binary.Reasoning.Base.Double.html#2491" class="Bound">z</a> <a id="2520" class="Symbol">→</a> <a id="2522" href="Relation.Binary.Reasoning.Base.Double.html#2481" class="Bound">x</a> <a id="2524" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="2536" href="Relation.Binary.Reasoning.Base.Double.html#2491" class="Bound">z</a>
<a id="2538" href="Relation.Binary.Reasoning.Base.Double.html#2538" class="Bound">x</a> <a id="2540" href="Relation.Binary.Reasoning.Base.Double.html#2469" class="Function Operator">∼⟨</a> <a id="2543" href="Relation.Binary.Reasoning.Base.Double.html#2543" class="Bound">x∼y</a> <a id="2547" href="Relation.Binary.Reasoning.Base.Double.html#2469" class="Function Operator">⟩</a> <a id="2549" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="2559" href="Relation.Binary.Reasoning.Base.Double.html#2559" class="Bound">y∼z</a> <a id="2563" class="Symbol">=</a> <a id="2565" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="2575" class="Symbol">(</a><a id="2576" href="Relation.Binary.html#1121" class="Field">trans</a> <a id="2582" href="Relation.Binary.Reasoning.Base.Double.html#2543" class="Bound">x∼y</a> <a id="2586" href="Relation.Binary.Reasoning.Base.Double.html#2559" class="Bound">y∼z</a><a id="2589" class="Symbol">)</a>
<a id="2591" href="Relation.Binary.Reasoning.Base.Double.html#2591" class="Bound">x</a> <a id="2593" href="Relation.Binary.Reasoning.Base.Double.html#2469" class="Function Operator">∼⟨</a> <a id="2596" href="Relation.Binary.Reasoning.Base.Double.html#2596" class="Bound">x∼y</a> <a id="2600" href="Relation.Binary.Reasoning.Base.Double.html#2469" class="Function Operator">⟩</a> <a id="2602" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="2612" href="Relation.Binary.Reasoning.Base.Double.html#2612" class="Bound">y≈z</a> <a id="2616" class="Symbol">=</a> <a id="2618" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="2628" class="Symbol">(</a><a id="2629" href="Agda.Builtin.Sigma.html#155" class="Field">proj₁</a> <a id="2635" href="Relation.Binary.html#1420" class="Function">∼-resp-≈</a> <a id="2644" href="Relation.Binary.Reasoning.Base.Double.html#2612" class="Bound">y≈z</a> <a id="2648" href="Relation.Binary.Reasoning.Base.Double.html#2596" class="Bound">x∼y</a><a id="2651" class="Symbol">)</a>

<a id="_≈⟨_⟩_"></a><a id="2654" href="Relation.Binary.Reasoning.Base.Double.html#2654" class="Function Operator">_≈⟨_⟩_</a> <a id="2661" class="Symbol">:</a> <a id="2663" class="Symbol">∀</a> <a id="2665" class="Symbol">(</a><a id="2666" href="Relation.Binary.Reasoning.Base.Double.html#2666" class="Bound">x</a> <a id="2668" class="Symbol">:</a> <a id="2670" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a><a id="2671" class="Symbol">)</a> <a id="2673" class="Symbol">{</a><a id="2674" href="Relation.Binary.Reasoning.Base.Double.html#2674" class="Bound">y</a> <a id="2676" href="Relation.Binary.Reasoning.Base.Double.html#2676" class="Bound">z</a><a id="2677" class="Symbol">}</a> <a id="2679" class="Symbol">→</a> <a id="2681" href="Relation.Binary.Reasoning.Base.Double.html#2666" class="Bound">x</a> <a id="2683" href="Relation.Binary.Reasoning.Base.Double.html#542" class="Bound Operator">≈</a> <a id="2685" href="Relation.Binary.Reasoning.Base.Double.html#2674" class="Bound">y</a> <a id="2687" class="Symbol">→</a> <a id="2689" href="Relation.Binary.Reasoning.Base.Double.html#2674" class="Bound">y</a> <a id="2691" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="2703" href="Relation.Binary.Reasoning.Base.Double.html#2676" class="Bound">z</a> <a id="2705" class="Symbol">→</a> <a id="2707" href="Relation.Binary.Reasoning.Base.Double.html#2666" class="Bound">x</a> <a id="2709" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="2721" href="Relation.Binary.Reasoning.Base.Double.html#2676" class="Bound">z</a>
<a id="2723" href="Relation.Binary.Reasoning.Base.Double.html#2723" class="Bound">x</a> <a id="2725" href="Relation.Binary.Reasoning.Base.Double.html#2654" class="Function Operator">≈⟨</a> <a id="2728" href="Relation.Binary.Reasoning.Base.Double.html#2728" class="Bound">x≈y</a> <a id="2732" href="Relation.Binary.Reasoning.Base.Double.html#2654" class="Function Operator">⟩</a> <a id="2734" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="2744" href="Relation.Binary.Reasoning.Base.Double.html#2744" class="Bound">y∼z</a> <a id="2748" class="Symbol">=</a> <a id="2750" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="2760" class="Symbol">(</a><a id="2761" href="Agda.Builtin.Sigma.html#167" class="Field">proj₂</a> <a id="2767" href="Relation.Binary.html#1420" class="Function">∼-resp-≈</a> <a id="2776" class="Symbol">(</a><a id="2777" href="Relation.Binary.Core.html#5834" class="Function">Eq.sym</a> <a id="2784" href="Relation.Binary.Reasoning.Base.Double.html#2728" class="Bound">x≈y</a><a id="2787" class="Symbol">)</a> <a id="2789" href="Relation.Binary.Reasoning.Base.Double.html#2744" class="Bound">y∼z</a><a id="2792" class="Symbol">)</a>
<a id="2794" href="Relation.Binary.Reasoning.Base.Double.html#2794" class="Bound">x</a> <a id="2796" href="Relation.Binary.Reasoning.Base.Double.html#2654" class="Function Operator">≈⟨</a> <a id="2799" href="Relation.Binary.Reasoning.Base.Double.html#2799" class="Bound">x≈y</a> <a id="2803" href="Relation.Binary.Reasoning.Base.Double.html#2654" class="Function Operator">⟩</a> <a id="2805" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="2815" href="Relation.Binary.Reasoning.Base.Double.html#2815" class="Bound">y≈z</a> <a id="2819" class="Symbol">=</a> <a id="2821" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="2831" class="Symbol">(</a><a id="2832" href="Relation.Binary.Core.html#5860" class="Function">Eq.trans</a> <a id="2841" href="Relation.Binary.Reasoning.Base.Double.html#2799" class="Bound">x≈y</a> <a id="2845" href="Relation.Binary.Reasoning.Base.Double.html#2815" class="Bound">y≈z</a><a id="2848" class="Symbol">)</a>

<a id="_≡⟨_⟩_"></a><a id="2851" href="Relation.Binary.Reasoning.Base.Double.html#2851" class="Function Operator">_≡⟨_⟩_</a> <a id="2858" class="Symbol">:</a> <a id="2860" class="Symbol">∀</a> <a id="2862" class="Symbol">(</a><a id="2863" href="Relation.Binary.Reasoning.Base.Double.html#2863" class="Bound">x</a> <a id="2865" class="Symbol">:</a> <a id="2867" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a><a id="2868" class="Symbol">)</a> <a id="2870" class="Symbol">{</a><a id="2871" href="Relation.Binary.Reasoning.Base.Double.html#2871" class="Bound">y</a> <a id="2873" href="Relation.Binary.Reasoning.Base.Double.html#2873" class="Bound">z</a><a id="2874" class="Symbol">}</a> <a id="2876" class="Symbol">→</a> <a id="2878" href="Relation.Binary.Reasoning.Base.Double.html#2863" class="Bound">x</a> <a id="2880" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="2882" href="Relation.Binary.Reasoning.Base.Double.html#2871" class="Bound">y</a> <a id="2884" class="Symbol">→</a> <a id="2886" href="Relation.Binary.Reasoning.Base.Double.html#2871" class="Bound">y</a> <a id="2888" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="2900" href="Relation.Binary.Reasoning.Base.Double.html#2873" class="Bound">z</a> <a id="2902" class="Symbol">→</a> <a id="2904" href="Relation.Binary.Reasoning.Base.Double.html#2863" class="Bound">x</a> <a id="2906" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="2918" href="Relation.Binary.Reasoning.Base.Double.html#2873" class="Bound">z</a>
<a id="2920" href="Relation.Binary.Reasoning.Base.Double.html#2920" class="Bound">x</a> <a id="2922" href="Relation.Binary.Reasoning.Base.Double.html#2851" class="Function Operator">≡⟨</a> <a id="2925" href="Relation.Binary.Reasoning.Base.Double.html#2925" class="Bound">x≡y</a> <a id="2929" href="Relation.Binary.Reasoning.Base.Double.html#2851" class="Function Operator">⟩</a> <a id="2931" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="2941" href="Relation.Binary.Reasoning.Base.Double.html#2941" class="Bound">y∼z</a> <a id="2945" class="Symbol">=</a> <a id="2947" href="Relation.Binary.Reasoning.Base.Double.html#1140" class="InductiveConstructor">nonstrict</a> <a id="2957" class="Symbol">(</a><a id="2958" href="Function.html#3533" class="Function Operator">case</a> <a id="2963" href="Relation.Binary.Reasoning.Base.Double.html#2925" class="Bound">x≡y</a> <a id="2967" href="Function.html#3533" class="Function Operator">of</a> <a id="2970" class="Symbol">λ</a> <a id="2972" class="Keyword">where</a> <a id="2978" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a> <a id="2983" class="Symbol">→</a> <a id="2985" href="Relation.Binary.Reasoning.Base.Double.html#2941" class="Bound">y∼z</a><a id="2988" class="Symbol">)</a>
<a id="2990" href="Relation.Binary.Reasoning.Base.Double.html#2990" class="Bound">x</a> <a id="2992" href="Relation.Binary.Reasoning.Base.Double.html#2851" class="Function Operator">≡⟨</a> <a id="2995" href="Relation.Binary.Reasoning.Base.Double.html#2995" class="Bound">x≡y</a> <a id="2999" href="Relation.Binary.Reasoning.Base.Double.html#2851" class="Function Operator">⟩</a> <a id="3001" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="3011" href="Relation.Binary.Reasoning.Base.Double.html#3011" class="Bound">y≈z</a> <a id="3015" class="Symbol">=</a> <a id="3017" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a>    <a id="3027" class="Symbol">(</a><a id="3028" href="Function.html#3533" class="Function Operator">case</a> <a id="3033" href="Relation.Binary.Reasoning.Base.Double.html#2995" class="Bound">x≡y</a> <a id="3037" href="Function.html#3533" class="Function Operator">of</a> <a id="3040" class="Symbol">λ</a> <a id="3042" class="Keyword">where</a> <a id="3048" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a> <a id="3053" class="Symbol">→</a> <a id="3055" href="Relation.Binary.Reasoning.Base.Double.html#3011" class="Bound">y≈z</a><a id="3058" class="Symbol">)</a>

<a id="_≡⟨⟩_"></a><a id="3061" href="Relation.Binary.Reasoning.Base.Double.html#3061" class="Function Operator">_≡⟨⟩_</a> <a id="3067" class="Symbol">:</a> <a id="3069" class="Symbol">∀</a> <a id="3071" class="Symbol">(</a><a id="3072" href="Relation.Binary.Reasoning.Base.Double.html#3072" class="Bound">x</a> <a id="3074" class="Symbol">:</a> <a id="3076" href="Relation.Binary.Reasoning.Base.Double.html#528" class="Bound">A</a><a id="3077" class="Symbol">)</a> <a id="3079" class="Symbol">{</a><a id="3080" href="Relation.Binary.Reasoning.Base.Double.html#3080" class="Bound">y</a><a id="3081" class="Symbol">}</a> <a id="3083" class="Symbol">→</a> <a id="3085" href="Relation.Binary.Reasoning.Base.Double.html#3072" class="Bound">x</a> <a id="3087" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="3099" href="Relation.Binary.Reasoning.Base.Double.html#3080" class="Bound">y</a> <a id="3101" class="Symbol">→</a> <a id="3103" href="Relation.Binary.Reasoning.Base.Double.html#3072" class="Bound">x</a> <a id="3105" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="3117" href="Relation.Binary.Reasoning.Base.Double.html#3080" class="Bound">y</a>
<a id="3119" href="Relation.Binary.Reasoning.Base.Double.html#3119" class="Bound">x</a> <a id="3121" href="Relation.Binary.Reasoning.Base.Double.html#3061" class="Function Operator">≡⟨⟩</a> <a id="3125" href="Relation.Binary.Reasoning.Base.Double.html#3125" class="Bound">x≲y</a> <a id="3129" class="Symbol">=</a> <a id="3131" href="Relation.Binary.Reasoning.Base.Double.html#3125" class="Bound">x≲y</a>

<a id="_∎"></a><a id="3136" href="Relation.Binary.Reasoning.Base.Double.html#3136" class="Function Operator">_∎</a> <a id="3139" class="Symbol">:</a> <a id="3141" class="Symbol">∀</a> <a id="3143" href="Relation.Binary.Reasoning.Base.Double.html#3143" class="Bound">x</a> <a id="3145" class="Symbol">→</a> <a id="3147" href="Relation.Binary.Reasoning.Base.Double.html#3143" class="Bound">x</a> <a id="3149" href="Relation.Binary.Reasoning.Base.Double.html#1092" class="Datatype Operator">IsRelatedTo</a> <a id="3161" href="Relation.Binary.Reasoning.Base.Double.html#3143" class="Bound">x</a>
<a id="3163" href="Relation.Binary.Reasoning.Base.Double.html#3163" class="Bound">x</a> <a id="3165" href="Relation.Binary.Reasoning.Base.Double.html#3136" class="Function Operator">∎</a> <a id="3167" class="Symbol">=</a> <a id="3169" href="Relation.Binary.Reasoning.Base.Double.html#1186" class="InductiveConstructor">equals</a> <a id="3176" href="Relation.Binary.Core.html#5808" class="Function">Eq.refl</a>

<a id="3185" class="Comment">------------------------------------------------------------------------</a>
<a id="3258" class="Comment">-- Some examples and tests</a>

<a id="3286" class="Comment">{-
private
  module Examples where
    postulate
      u v w y d e : A
      u≈v : u ≈ v
      v≡w : v ≡ w
      w∼y : w ∼ y
      z≡d : y ≡ d
      d≈e : d ≈ e

    u∼y : u ∼ y
    u∼y = begin
      u ≈⟨ u≈v ⟩
      v ≡⟨ v≡w ⟩
      w ∼⟨ w∼y ⟩
      y ∎

    u≈w : u ≈ w
    u≈w = begin-equality
      u ≈⟨ u≈v ⟩
      v ≡⟨ v≡w ⟩
      w ∎
-}</a>
</pre></body></html>