<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Morphism.RawMagma</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Consequences of an injective morphism between magmas</a>
<a id="162" class="Comment">------------------------------------------------------------------------</a>

<a id="236" class="Symbol">{-#</a> <a id="240" class="Keyword">OPTIONS</a> <a id="248" class="Pragma">--without-K</a> <a id="260" class="Pragma">--safe</a> <a id="267" class="Symbol">#-}</a>

<a id="272" class="Keyword">open</a> <a id="277" class="Keyword">import</a> <a id="284" href="Algebra.html" class="Module">Algebra</a>
<a id="292" class="Keyword">open</a> <a id="297" class="Keyword">import</a> <a id="304" href="Algebra.Morphism.html" class="Module">Algebra.Morphism</a>
<a id="321" class="Keyword">open</a> <a id="326" class="Keyword">import</a> <a id="333" href="Algebra.Structures.html" class="Module">Algebra.Structures</a>
<a id="352" class="Keyword">open</a> <a id="357" class="Keyword">import</a> <a id="364" href="Function.html" class="Module">Function</a>
<a id="373" class="Keyword">open</a> <a id="378" href="Algebra.html#693" class="Module">RawMagma</a> <a id="387" class="Keyword">using</a> <a id="393" class="Symbol">(</a><a id="394" href="Algebra.html#774" class="Field">Carrier</a><a id="401" class="Symbol">;</a> <a id="403" href="Algebra.html#794" class="Field Operator">_≈_</a><a id="406" class="Symbol">)</a>

<a id="409" class="Keyword">module</a> <a id="416" href="Algebra.Morphism.RawMagma.html" class="Module">Algebra.Morphism.RawMagma</a>
  <a id="444" class="Symbol">{</a><a id="445" href="Algebra.Morphism.RawMagma.html#445" class="Bound">a</a> <a id="447" href="Algebra.Morphism.RawMagma.html#447" class="Bound">b</a> <a id="449" href="Algebra.Morphism.RawMagma.html#449" class="Bound">ℓ₁</a> <a id="452" href="Algebra.Morphism.RawMagma.html#452" class="Bound">ℓ₂</a><a id="454" class="Symbol">}</a> <a id="456" class="Symbol">{</a><a id="457" href="Algebra.Morphism.RawMagma.html#457" class="Bound">From</a> <a id="462" class="Symbol">:</a> <a id="464" href="Algebra.html#693" class="Record">RawMagma</a> <a id="473" href="Algebra.Morphism.RawMagma.html#447" class="Bound">b</a> <a id="475" href="Algebra.Morphism.RawMagma.html#452" class="Bound">ℓ₂</a><a id="477" class="Symbol">}</a> <a id="479" class="Symbol">{</a><a id="480" href="Algebra.Morphism.RawMagma.html#480" class="Bound">To</a> <a id="483" class="Symbol">:</a> <a id="485" href="Algebra.html#693" class="Record">RawMagma</a> <a id="494" href="Algebra.Morphism.RawMagma.html#445" class="Bound">a</a> <a id="496" href="Algebra.Morphism.RawMagma.html#449" class="Bound">ℓ₁</a><a id="498" class="Symbol">}</a>
  <a id="502" class="Symbol">{</a><a id="503" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="505" class="Symbol">:</a> <a id="507" href="Algebra.html#774" class="Field">Carrier</a> <a id="515" href="Algebra.Morphism.RawMagma.html#457" class="Bound">From</a> <a id="520" class="Symbol">→</a> <a id="522" href="Algebra.html#774" class="Field">Carrier</a> <a id="530" href="Algebra.Morphism.RawMagma.html#480" class="Bound">To</a><a id="532" class="Symbol">}</a>
  <a id="536" class="Symbol">(</a><a id="537" href="Algebra.Morphism.RawMagma.html#537" class="Bound">f-isRawMagmaMorphism</a> <a id="558" class="Symbol">:</a> <a id="560" href="Algebra.Morphism.html#1430" class="Record">IsRawMagmaMorphism</a> <a id="579" href="Algebra.Morphism.RawMagma.html#457" class="Bound">From</a> <a id="584" href="Algebra.Morphism.RawMagma.html#480" class="Bound">To</a> <a id="587" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a><a id="588" class="Symbol">)</a>
  <a id="592" class="Symbol">(</a><a id="593" href="Algebra.Morphism.RawMagma.html#593" class="Bound">f-injective</a> <a id="605" class="Symbol">:</a> <a id="607" href="Function.Definitions.html#862" class="Function">Injective</a> <a id="617" class="Symbol">(</a><a id="618" href="Algebra.html#794" class="Field Operator">_≈_</a> <a id="622" href="Algebra.Morphism.RawMagma.html#457" class="Bound">From</a><a id="626" class="Symbol">)</a> <a id="628" class="Symbol">(</a><a id="629" href="Algebra.html#794" class="Field Operator">_≈_</a> <a id="633" href="Algebra.Morphism.RawMagma.html#480" class="Bound">To</a><a id="635" class="Symbol">)</a> <a id="637" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a><a id="638" class="Symbol">)</a>
  <a id="642" class="Keyword">where</a>

<a id="649" class="Keyword">open</a> <a id="654" class="Keyword">import</a> <a id="661" href="Algebra.FunctionProperties.html" class="Module">Algebra.FunctionProperties</a>
<a id="688" class="Keyword">open</a> <a id="693" class="Keyword">import</a> <a id="700" href="Data.Product.html" class="Module">Data.Product</a>
<a id="713" class="Keyword">open</a> <a id="718" class="Keyword">import</a> <a id="725" href="Data.Sum.html" class="Module">Data.Sum</a> <a id="734" class="Keyword">using</a> <a id="740" class="Symbol">(</a><a id="741" href="Data.Sum.Base.html#667" class="InductiveConstructor">inj₁</a><a id="745" class="Symbol">;</a> <a id="747" href="Data.Sum.Base.html#692" class="InductiveConstructor">inj₂</a><a id="751" class="Symbol">)</a>
<a id="753" class="Keyword">import</a> <a id="760" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="793" class="Symbol">as</a> <a id="796" class="Module">SetoidReasoning</a>
<a id="812" class="Keyword">import</a> <a id="819" href="Relation.Binary.Reasoning.MultiSetoid.html" class="Module">Relation.Binary.Reasoning.MultiSetoid</a> <a id="857" class="Symbol">as</a> <a id="860" class="Module">MultiSetoidReasoning</a>

<a id="882" class="Keyword">private</a>
  <a id="892" class="Keyword">module</a> <a id="F"></a><a id="899" href="Algebra.Morphism.RawMagma.html#899" class="Module">F</a> <a id="901" class="Symbol">=</a> <a id="903" href="Algebra.html#693" class="Module">RawMagma</a> <a id="912" href="Algebra.Morphism.RawMagma.html#457" class="Bound">From</a>
  <a id="919" class="Keyword">module</a> <a id="T"></a><a id="926" href="Algebra.Morphism.RawMagma.html#926" class="Module">T</a> <a id="928" class="Symbol">=</a> <a id="930" href="Algebra.html#693" class="Module">RawMagma</a> <a id="939" href="Algebra.Morphism.RawMagma.html#480" class="Bound">To</a>

<a id="943" class="Keyword">open</a> <a id="948" href="Algebra.Morphism.html#659" class="Module">Definitions</a> <a id="960" href="Algebra.html#774" class="Function">F.Carrier</a> <a id="970" href="Algebra.html#774" class="Function">T.Carrier</a> <a id="980" href="Algebra.html#794" class="Function Operator">T._≈_</a>
<a id="986" class="Keyword">open</a> <a id="991" href="Algebra.Morphism.RawMagma.html#926" class="Module">T</a> <a id="993" class="Keyword">using</a> <a id="999" class="Symbol">()</a> <a id="1002" class="Keyword">renaming</a> <a id="1011" class="Symbol">(</a><a id="1012" href="Algebra.html#822" class="Function Operator">_∙_</a> <a id="1016" class="Symbol">to</a> <a id="1019" href="Algebra.html#822" class="Function Operator">_⊕_</a><a id="1022" class="Symbol">)</a>
<a id="1024" class="Keyword">open</a> <a id="1029" href="Algebra.Morphism.RawMagma.html#899" class="Module">F</a> <a id="1031" class="Keyword">using</a> <a id="1037" class="Symbol">(</a><a id="1038" href="Algebra.html#822" class="Function Operator">_∙_</a><a id="1041" class="Symbol">)</a>

<a id="1044" class="Keyword">open</a> <a id="1049" href="Algebra.Morphism.html#1430" class="Module">IsRawMagmaMorphism</a> <a id="1068" href="Algebra.Morphism.RawMagma.html#537" class="Bound">f-isRawMagmaMorphism</a>
<a id="1089" class="Keyword">open</a> <a id="1094" href="Relation.Binary.Reasoning.Setoid.html" class="Module">SetoidReasoning</a> <a id="1110" href="Algebra.Structures.html#1153" class="Function">T-setoid</a>

<a id="1120" class="Comment">------------------------------------------------------------------------</a>
<a id="1193" class="Comment">-- Properties</a>

<a id="assoc-homo"></a><a id="1208" href="Algebra.Morphism.RawMagma.html#1208" class="Function">assoc-homo</a> <a id="1219" class="Symbol">:</a> <a id="1221" href="Algebra.FunctionProperties.html#788" class="Function">Associative</a> <a id="1233" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="1239" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="1243" class="Symbol">→</a> <a id="1245" href="Algebra.FunctionProperties.html#788" class="Function">Associative</a> <a id="1257" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="1263" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="1267" href="Algebra.Morphism.RawMagma.html#1208" class="Function">assoc-homo</a> <a id="1278" href="Algebra.Morphism.RawMagma.html#1278" class="Bound">assoc</a> <a id="1284" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1286" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a> <a id="1288" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a> <a id="1290" class="Symbol">=</a> <a id="1292" href="Algebra.Morphism.RawMagma.html#593" class="Bound">f-injective</a> <a id="1304" class="Symbol">(</a><a id="1305" href="Relation.Binary.Reasoning.Base.Single.html#866" class="Function Operator">begin</a>
  <a id="1313" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1315" class="Symbol">((</a><a id="1317" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1319" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1321" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a><a id="1322" class="Symbol">)</a> <a id="1324" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1326" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a><a id="1327" class="Symbol">)</a>    <a id="1332" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a>  <a id="1336" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="1343" class="Symbol">(</a><a id="1344" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1346" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1348" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a><a id="1349" class="Symbol">)</a> <a id="1351" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a> <a id="1353" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="1357" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1359" class="Symbol">(</a><a id="1360" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1362" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1364" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a><a id="1365" class="Symbol">)</a> <a id="1367" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1369" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1371" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a>    <a id="1376" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a>  <a id="1380" href="Algebra.Structures.html#1289" class="Function">T-∙-congʳ</a> <a id="1390" class="Symbol">(</a><a id="1391" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="1398" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1400" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a><a id="1401" class="Symbol">)</a> <a id="1403" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="1407" class="Symbol">(</a><a id="1408" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1410" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1412" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1414" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1416" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a><a id="1417" class="Symbol">)</a> <a id="1419" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1421" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1423" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a>  <a id="1426" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a>  <a id="1430" href="Algebra.Morphism.RawMagma.html#1278" class="Bound">assoc</a> <a id="1436" class="Symbol">(</a><a id="1437" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1439" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a><a id="1440" class="Symbol">)</a> <a id="1442" class="Symbol">(</a><a id="1443" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1445" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a><a id="1446" class="Symbol">)</a> <a id="1448" class="Symbol">(</a><a id="1449" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1451" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a><a id="1452" class="Symbol">)</a> <a id="1454" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="1458" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1460" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1462" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1464" class="Symbol">(</a><a id="1465" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1467" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a> <a id="1469" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1471" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1473" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a><a id="1474" class="Symbol">)</a>  <a id="1477" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">≈˘⟨</a> <a id="1481" href="Algebra.Structures.html#1228" class="Function">T-∙-congˡ</a> <a id="1491" class="Symbol">(</a><a id="1492" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="1499" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a> <a id="1501" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a><a id="1502" class="Symbol">)</a> <a id="1504" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">⟩</a>
  <a id="1508" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1510" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1512" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1514" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1516" class="Symbol">(</a><a id="1517" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a> <a id="1519" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1521" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a><a id="1522" class="Symbol">)</a>    <a id="1527" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">≈˘⟨</a> <a id="1531" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="1538" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1540" class="Symbol">(</a><a id="1541" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a> <a id="1543" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1545" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a><a id="1546" class="Symbol">)</a> <a id="1548" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">⟩</a>
  <a id="1552" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1554" class="Symbol">(</a><a id="1555" href="Algebra.Morphism.RawMagma.html#1284" class="Bound">x</a> <a id="1557" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1559" class="Symbol">(</a><a id="1560" href="Algebra.Morphism.RawMagma.html#1286" class="Bound">y</a> <a id="1562" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1564" href="Algebra.Morphism.RawMagma.html#1288" class="Bound">z</a><a id="1565" class="Symbol">))</a>    <a id="1571" href="Relation.Binary.Reasoning.Base.Single.html#1302" class="Function Operator">∎</a><a id="1572" class="Symbol">)</a>

<a id="comm-homo"></a><a id="1575" href="Algebra.Morphism.RawMagma.html#1575" class="Function">comm-homo</a> <a id="1585" class="Symbol">:</a> <a id="1587" href="Algebra.FunctionProperties.html#875" class="Function">Commutative</a> <a id="1599" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="1605" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="1609" class="Symbol">→</a> <a id="1611" href="Algebra.FunctionProperties.html#875" class="Function">Commutative</a> <a id="1623" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="1629" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="1633" href="Algebra.Morphism.RawMagma.html#1575" class="Function">comm-homo</a> <a id="1643" href="Algebra.Morphism.RawMagma.html#1643" class="Bound">comm</a> <a id="1648" href="Algebra.Morphism.RawMagma.html#1648" class="Bound">x</a> <a id="1650" href="Algebra.Morphism.RawMagma.html#1650" class="Bound">y</a> <a id="1652" class="Symbol">=</a> <a id="1654" href="Algebra.Morphism.RawMagma.html#593" class="Bound">f-injective</a> <a id="1666" class="Symbol">(</a><a id="1667" href="Relation.Binary.Reasoning.Base.Single.html#866" class="Function Operator">begin</a>
  <a id="1675" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1677" class="Symbol">(</a><a id="1678" href="Algebra.Morphism.RawMagma.html#1648" class="Bound">x</a> <a id="1680" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1682" href="Algebra.Morphism.RawMagma.html#1650" class="Bound">y</a><a id="1683" class="Symbol">)</a>  <a id="1686" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a>  <a id="1690" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="1697" href="Algebra.Morphism.RawMagma.html#1648" class="Bound">x</a> <a id="1699" href="Algebra.Morphism.RawMagma.html#1650" class="Bound">y</a> <a id="1701" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="1705" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1707" href="Algebra.Morphism.RawMagma.html#1648" class="Bound">x</a> <a id="1709" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1711" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1713" href="Algebra.Morphism.RawMagma.html#1650" class="Bound">y</a>  <a id="1716" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a>  <a id="1720" href="Algebra.Morphism.RawMagma.html#1643" class="Bound">comm</a> <a id="1725" class="Symbol">(</a><a id="1726" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1728" href="Algebra.Morphism.RawMagma.html#1648" class="Bound">x</a><a id="1729" class="Symbol">)</a> <a id="1731" class="Symbol">(</a><a id="1732" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1734" href="Algebra.Morphism.RawMagma.html#1650" class="Bound">y</a><a id="1735" class="Symbol">)</a> <a id="1737" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="1741" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1743" href="Algebra.Morphism.RawMagma.html#1650" class="Bound">y</a> <a id="1745" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1747" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1749" href="Algebra.Morphism.RawMagma.html#1648" class="Bound">x</a>  <a id="1752" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">≈˘⟨</a> <a id="1756" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="1763" href="Algebra.Morphism.RawMagma.html#1650" class="Bound">y</a> <a id="1765" href="Algebra.Morphism.RawMagma.html#1648" class="Bound">x</a> <a id="1767" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">⟩</a>
  <a id="1771" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1773" class="Symbol">(</a><a id="1774" href="Algebra.Morphism.RawMagma.html#1650" class="Bound">y</a> <a id="1776" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1778" href="Algebra.Morphism.RawMagma.html#1648" class="Bound">x</a><a id="1779" class="Symbol">)</a>  <a id="1782" href="Relation.Binary.Reasoning.Base.Single.html#1302" class="Function Operator">∎</a><a id="1783" class="Symbol">)</a>

<a id="idem-homo"></a><a id="1786" href="Algebra.Morphism.RawMagma.html#1786" class="Function">idem-homo</a> <a id="1796" class="Symbol">:</a> <a id="1798" href="Algebra.FunctionProperties.html#2327" class="Function">Idempotent</a> <a id="1809" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="1815" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="1819" class="Symbol">→</a> <a id="1821" href="Algebra.FunctionProperties.html#2327" class="Function">Idempotent</a> <a id="1832" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="1838" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="1842" href="Algebra.Morphism.RawMagma.html#1786" class="Function">idem-homo</a> <a id="1852" href="Algebra.Morphism.RawMagma.html#1852" class="Bound">idem</a> <a id="1857" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a> <a id="1859" class="Symbol">=</a> <a id="1861" href="Algebra.Morphism.RawMagma.html#593" class="Bound">f-injective</a> <a id="1873" class="Symbol">(</a><a id="1874" href="Relation.Binary.Reasoning.Base.Single.html#866" class="Function Operator">begin</a>
  <a id="1882" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1884" class="Symbol">(</a><a id="1885" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a> <a id="1887" href="Algebra.html#822" class="Function Operator">∙</a> <a id="1889" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a><a id="1890" class="Symbol">)</a>  <a id="1893" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a> <a id="1896" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="1903" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a> <a id="1905" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a> <a id="1907" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="1911" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1913" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a> <a id="1915" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="1917" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1919" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a>  <a id="1922" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a> <a id="1925" href="Algebra.Morphism.RawMagma.html#1852" class="Bound">idem</a> <a id="1930" class="Symbol">(</a><a id="1931" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1933" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a><a id="1934" class="Symbol">)</a> <a id="1936" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="1940" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="1942" href="Algebra.Morphism.RawMagma.html#1857" class="Bound">x</a>        <a id="1951" href="Relation.Binary.Reasoning.Base.Single.html#1302" class="Function Operator">∎</a><a id="1952" class="Symbol">)</a>

<a id="sel-homo"></a><a id="1955" href="Algebra.Morphism.RawMagma.html#1955" class="Function">sel-homo</a> <a id="1964" class="Symbol">:</a> <a id="1966" href="Algebra.FunctionProperties.html#2462" class="Function">Selective</a> <a id="1976" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="1982" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="1986" class="Symbol">→</a> <a id="1988" href="Algebra.FunctionProperties.html#2462" class="Function">Selective</a> <a id="1998" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="2004" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="2008" href="Algebra.Morphism.RawMagma.html#1955" class="Function">sel-homo</a> <a id="2017" href="Algebra.Morphism.RawMagma.html#2017" class="Bound">sel</a> <a id="2021" href="Algebra.Morphism.RawMagma.html#2021" class="Bound">x</a> <a id="2023" href="Algebra.Morphism.RawMagma.html#2023" class="Bound">y</a> <a id="2025" class="Keyword">with</a> <a id="2030" href="Algebra.Morphism.RawMagma.html#2017" class="Bound">sel</a> <a id="2034" class="Symbol">(</a><a id="2035" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2037" href="Algebra.Morphism.RawMagma.html#2021" class="Bound">x</a><a id="2038" class="Symbol">)</a> <a id="2040" class="Symbol">(</a><a id="2041" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2043" href="Algebra.Morphism.RawMagma.html#2023" class="Bound">y</a><a id="2044" class="Symbol">)</a>
<a id="2046" class="Symbol">...</a> <a id="2050" class="Symbol">|</a> <a id="2052" href="Data.Sum.Base.html#667" class="InductiveConstructor">inj₁</a> <a id="2057" href="Algebra.Morphism.RawMagma.html#2057" class="Bound">x⊕y≈x</a> <a id="2063" class="Symbol">=</a> <a id="2065" href="Data.Sum.Base.html#667" class="InductiveConstructor">inj₁</a> <a id="2070" class="Symbol">(</a><a id="2071" href="Algebra.Morphism.RawMagma.html#593" class="Bound">f-injective</a> <a id="2083" class="Symbol">(</a><a id="2084" href="Relation.Binary.Reasoning.Base.Single.html#866" class="Function Operator">begin</a>
  <a id="2092" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2094" class="Symbol">(</a><a id="2095" class="Bound">x</a> <a id="2097" href="Algebra.html#822" class="Function Operator">∙</a> <a id="2099" class="Bound">y</a><a id="2100" class="Symbol">)</a>  <a id="2103" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a> <a id="2106" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="2113" class="Bound">x</a> <a id="2115" class="Bound">y</a> <a id="2117" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="2121" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2123" class="Bound">x</a> <a id="2125" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="2127" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2129" class="Bound">y</a>  <a id="2132" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a> <a id="2135" href="Algebra.Morphism.RawMagma.html#2057" class="Bound">x⊕y≈x</a> <a id="2141" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="2145" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2147" class="Bound">x</a>        <a id="2156" href="Relation.Binary.Reasoning.Base.Single.html#1302" class="Function Operator">∎</a><a id="2157" class="Symbol">))</a>
<a id="2160" class="Symbol">...</a> <a id="2164" class="Symbol">|</a> <a id="2166" href="Data.Sum.Base.html#692" class="InductiveConstructor">inj₂</a> <a id="2171" href="Algebra.Morphism.RawMagma.html#2171" class="Bound">x⊕y≈y</a> <a id="2177" class="Symbol">=</a> <a id="2179" href="Data.Sum.Base.html#692" class="InductiveConstructor">inj₂</a> <a id="2184" class="Symbol">(</a><a id="2185" href="Algebra.Morphism.RawMagma.html#593" class="Bound">f-injective</a> <a id="2197" class="Symbol">(</a><a id="2198" href="Relation.Binary.Reasoning.Base.Single.html#866" class="Function Operator">begin</a>
  <a id="2206" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2208" class="Symbol">(</a><a id="2209" class="Bound">x</a> <a id="2211" href="Algebra.html#822" class="Function Operator">∙</a> <a id="2213" class="Bound">y</a><a id="2214" class="Symbol">)</a>  <a id="2217" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a> <a id="2220" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="2227" class="Bound">x</a> <a id="2229" class="Bound">y</a> <a id="2231" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="2235" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2237" class="Bound">x</a> <a id="2239" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="2241" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2243" class="Bound">y</a>  <a id="2246" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a> <a id="2249" href="Algebra.Morphism.RawMagma.html#2171" class="Bound">x⊕y≈y</a> <a id="2255" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="2259" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2261" class="Bound">y</a>        <a id="2270" href="Relation.Binary.Reasoning.Base.Single.html#1302" class="Function Operator">∎</a><a id="2271" class="Symbol">))</a>

<a id="cancelˡ-homo"></a><a id="2275" href="Algebra.Morphism.RawMagma.html#2275" class="Function">cancelˡ-homo</a> <a id="2288" class="Symbol">:</a> <a id="2290" href="Algebra.FunctionProperties.html#2762" class="Function">LeftCancellative</a> <a id="2307" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="2313" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="2317" class="Symbol">→</a> <a id="2319" href="Algebra.FunctionProperties.html#2762" class="Function">LeftCancellative</a> <a id="2336" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="2342" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="2346" href="Algebra.Morphism.RawMagma.html#2275" class="Function">cancelˡ-homo</a> <a id="2359" href="Algebra.Morphism.RawMagma.html#2359" class="Bound">cancelˡ</a> <a id="2367" href="Algebra.Morphism.RawMagma.html#2367" class="Bound">x</a> <a id="2369" class="Symbol">{</a><a id="2370" href="Algebra.Morphism.RawMagma.html#2370" class="Bound">y</a><a id="2371" class="Symbol">}</a> <a id="2373" class="Symbol">{</a><a id="2374" href="Algebra.Morphism.RawMagma.html#2374" class="Bound">z</a><a id="2375" class="Symbol">}</a> <a id="2377" href="Algebra.Morphism.RawMagma.html#2377" class="Bound">x∙y≈x∙z</a> <a id="2385" class="Symbol">=</a> <a id="2387" href="Algebra.Morphism.RawMagma.html#593" class="Bound">f-injective</a> <a id="2399" class="Symbol">(</a><a id="2400" href="Algebra.Morphism.RawMagma.html#2359" class="Bound">cancelˡ</a> <a id="2408" class="Symbol">(</a><a id="2409" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2411" href="Algebra.Morphism.RawMagma.html#2367" class="Bound">x</a><a id="2412" class="Symbol">)</a> <a id="2414" class="Symbol">(</a><a id="2415" href="Relation.Binary.Reasoning.Base.Single.html#866" class="Function Operator">begin</a>
  <a id="2423" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2425" href="Algebra.Morphism.RawMagma.html#2367" class="Bound">x</a> <a id="2427" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="2429" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2431" href="Algebra.Morphism.RawMagma.html#2370" class="Bound">y</a>  <a id="2434" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">≈˘⟨</a> <a id="2438" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="2445" href="Algebra.Morphism.RawMagma.html#2367" class="Bound">x</a> <a id="2447" href="Algebra.Morphism.RawMagma.html#2370" class="Bound">y</a> <a id="2449" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">⟩</a>
  <a id="2453" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2455" class="Symbol">(</a><a id="2456" href="Algebra.Morphism.RawMagma.html#2367" class="Bound">x</a> <a id="2458" href="Algebra.html#822" class="Function Operator">∙</a> <a id="2460" href="Algebra.Morphism.RawMagma.html#2370" class="Bound">y</a><a id="2461" class="Symbol">)</a>  <a id="2464" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a> <a id="2467" href="Algebra.Morphism.html#1599" class="Function">⟦⟧-cong</a> <a id="2475" href="Algebra.Morphism.RawMagma.html#2377" class="Bound">x∙y≈x∙z</a> <a id="2483" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="2487" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2489" class="Symbol">(</a><a id="2490" href="Algebra.Morphism.RawMagma.html#2367" class="Bound">x</a> <a id="2492" href="Algebra.html#822" class="Function Operator">∙</a> <a id="2494" href="Algebra.Morphism.RawMagma.html#2374" class="Bound">z</a><a id="2495" class="Symbol">)</a>  <a id="2498" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a>  <a id="2502" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="2509" href="Algebra.Morphism.RawMagma.html#2367" class="Bound">x</a> <a id="2511" href="Algebra.Morphism.RawMagma.html#2374" class="Bound">z</a> <a id="2513" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="2517" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2519" href="Algebra.Morphism.RawMagma.html#2367" class="Bound">x</a> <a id="2521" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="2523" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2525" href="Algebra.Morphism.RawMagma.html#2374" class="Bound">z</a>  <a id="2528" href="Relation.Binary.Reasoning.Base.Single.html#1302" class="Function Operator">∎</a><a id="2529" class="Symbol">))</a>

<a id="cancelʳ-homo"></a><a id="2533" href="Algebra.Morphism.RawMagma.html#2533" class="Function">cancelʳ-homo</a> <a id="2546" class="Symbol">:</a> <a id="2548" href="Algebra.FunctionProperties.html#2857" class="Function">RightCancellative</a> <a id="2566" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="2572" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="2576" class="Symbol">→</a> <a id="2578" href="Algebra.FunctionProperties.html#2857" class="Function">RightCancellative</a> <a id="2596" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="2602" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="2606" href="Algebra.Morphism.RawMagma.html#2533" class="Function">cancelʳ-homo</a> <a id="2619" href="Algebra.Morphism.RawMagma.html#2619" class="Bound">cancelʳ</a> <a id="2627" class="Symbol">{</a><a id="2628" href="Algebra.Morphism.RawMagma.html#2628" class="Bound">x</a><a id="2629" class="Symbol">}</a> <a id="2631" href="Algebra.Morphism.RawMagma.html#2631" class="Bound">y</a> <a id="2633" href="Algebra.Morphism.RawMagma.html#2633" class="Bound">z</a> <a id="2635" href="Algebra.Morphism.RawMagma.html#2635" class="Bound">y∙x≈z∙x</a> <a id="2643" class="Symbol">=</a> <a id="2645" href="Algebra.Morphism.RawMagma.html#593" class="Bound">f-injective</a> <a id="2657" class="Symbol">(</a><a id="2658" href="Algebra.Morphism.RawMagma.html#2619" class="Bound">cancelʳ</a> <a id="2666" class="Symbol">(</a><a id="2667" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2669" href="Algebra.Morphism.RawMagma.html#2631" class="Bound">y</a><a id="2670" class="Symbol">)</a> <a id="2672" class="Symbol">(</a><a id="2673" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2675" href="Algebra.Morphism.RawMagma.html#2633" class="Bound">z</a><a id="2676" class="Symbol">)</a> <a id="2678" class="Symbol">(</a><a id="2679" href="Relation.Binary.Reasoning.Base.Single.html#866" class="Function Operator">begin</a>
  <a id="2687" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2689" href="Algebra.Morphism.RawMagma.html#2631" class="Bound">y</a> <a id="2691" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="2693" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2695" href="Algebra.Morphism.RawMagma.html#2628" class="Bound">x</a>  <a id="2698" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">≈˘⟨</a> <a id="2702" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="2709" href="Algebra.Morphism.RawMagma.html#2631" class="Bound">y</a> <a id="2711" href="Algebra.Morphism.RawMagma.html#2628" class="Bound">x</a> <a id="2713" href="Relation.Binary.Reasoning.Setoid.html#955" class="Function Operator">⟩</a>
  <a id="2717" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2719" class="Symbol">(</a><a id="2720" href="Algebra.Morphism.RawMagma.html#2631" class="Bound">y</a> <a id="2722" href="Algebra.html#822" class="Function Operator">∙</a> <a id="2724" href="Algebra.Morphism.RawMagma.html#2628" class="Bound">x</a><a id="2725" class="Symbol">)</a>  <a id="2728" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a> <a id="2731" href="Algebra.Morphism.html#1599" class="Function">⟦⟧-cong</a> <a id="2739" href="Algebra.Morphism.RawMagma.html#2635" class="Bound">y∙x≈z∙x</a> <a id="2747" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="2751" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2753" class="Symbol">(</a><a id="2754" href="Algebra.Morphism.RawMagma.html#2633" class="Bound">z</a> <a id="2756" href="Algebra.html#822" class="Function Operator">∙</a> <a id="2758" href="Algebra.Morphism.RawMagma.html#2628" class="Bound">x</a><a id="2759" class="Symbol">)</a>  <a id="2762" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">≈⟨</a>  <a id="2766" href="Algebra.Morphism.html#1643" class="Function">∙-homo</a> <a id="2773" href="Algebra.Morphism.RawMagma.html#2633" class="Bound">z</a> <a id="2775" href="Algebra.Morphism.RawMagma.html#2628" class="Bound">x</a> <a id="2777" href="Relation.Binary.Reasoning.Base.Single.html#932" class="Function Operator">⟩</a>
  <a id="2781" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2783" href="Algebra.Morphism.RawMagma.html#2633" class="Bound">z</a> <a id="2785" href="Algebra.html#822" class="Function Operator">⊕</a> <a id="2787" href="Algebra.Morphism.RawMagma.html#503" class="Bound">f</a> <a id="2789" href="Algebra.Morphism.RawMagma.html#2628" class="Bound">x</a>  <a id="2792" href="Relation.Binary.Reasoning.Base.Single.html#1302" class="Function Operator">∎</a><a id="2793" class="Symbol">))</a>

<a id="cancel-homo"></a><a id="2797" href="Algebra.Morphism.RawMagma.html#2797" class="Function">cancel-homo</a> <a id="2809" class="Symbol">:</a> <a id="2811" href="Algebra.FunctionProperties.html#2954" class="Function">Cancellative</a> <a id="2824" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="2830" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="2834" class="Symbol">→</a> <a id="2836" href="Algebra.FunctionProperties.html#2954" class="Function">Cancellative</a> <a id="2849" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="2855" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="2859" href="Algebra.Morphism.RawMagma.html#2797" class="Function">cancel-homo</a> <a id="2871" class="Symbol">(</a><a id="2872" href="Algebra.Morphism.RawMagma.html#2872" class="Bound">cancelˡ</a> <a id="2880" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2882" href="Algebra.Morphism.RawMagma.html#2882" class="Bound">cancelʳ</a><a id="2889" class="Symbol">)</a> <a id="2891" class="Symbol">=</a> <a id="2893" href="Algebra.Morphism.RawMagma.html#2275" class="Function">cancelˡ-homo</a> <a id="2906" href="Algebra.Morphism.RawMagma.html#2872" class="Bound">cancelˡ</a> <a id="2914" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2916" href="Algebra.Morphism.RawMagma.html#2533" class="Function">cancelʳ-homo</a> <a id="2929" href="Algebra.Morphism.RawMagma.html#2882" class="Bound">cancelʳ</a>

<a id="2938" class="Comment">------------------------------------------------------------------------</a>
<a id="3011" class="Comment">-- Structures</a>

<a id="isSemigroup-homo"></a><a id="3026" href="Algebra.Morphism.RawMagma.html#3026" class="Function">isSemigroup-homo</a> <a id="3043" class="Symbol">:</a> <a id="3045" href="Algebra.Structures.html#1357" class="Record">IsSemigroup</a> <a id="3057" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="3063" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="3067" class="Symbol">→</a> <a id="3069" href="Algebra.Structures.html#1357" class="Record">IsSemigroup</a> <a id="3081" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="3087" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="3091" href="Algebra.Morphism.RawMagma.html#3026" class="Function">isSemigroup-homo</a> <a id="3108" href="Algebra.Morphism.RawMagma.html#3108" class="Bound">isSemigroup</a> <a id="3120" class="Symbol">=</a> <a id="3122" class="Keyword">record</a>
  <a id="3131" class="Symbol">{</a> <a id="3133" href="Algebra.Structures.html#1413" class="Field">isMagma</a> <a id="3141" class="Symbol">=</a> <a id="3143" href="Algebra.Morphism.html#1523" class="Function">F-isMagma</a>
  <a id="3155" class="Symbol">;</a> <a id="3157" href="Algebra.Structures.html#1437" class="Field">assoc</a>   <a id="3165" class="Symbol">=</a> <a id="3167" href="Algebra.Morphism.RawMagma.html#1208" class="Function">assoc-homo</a> <a id="3178" href="Algebra.Structures.html#1437" class="Field">assoc</a>
  <a id="3186" class="Symbol">}</a> <a id="3188" class="Keyword">where</a> <a id="3194" class="Keyword">open</a> <a id="3199" href="Algebra.Structures.html#1357" class="Module">IsSemigroup</a> <a id="3211" href="Algebra.Morphism.RawMagma.html#3108" class="Bound">isSemigroup</a>

<a id="isBand-homo"></a><a id="3224" href="Algebra.Morphism.RawMagma.html#3224" class="Function">isBand-homo</a> <a id="3236" class="Symbol">:</a> <a id="3238" href="Algebra.Structures.html#1501" class="Record">IsBand</a> <a id="3245" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="3251" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="3255" class="Symbol">→</a> <a id="3257" href="Algebra.Structures.html#1501" class="Record">IsBand</a> <a id="3264" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="3270" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="3274" href="Algebra.Morphism.RawMagma.html#3224" class="Function">isBand-homo</a> <a id="3286" href="Algebra.Morphism.RawMagma.html#3286" class="Bound">isBand</a> <a id="3293" class="Symbol">=</a> <a id="3295" class="Keyword">record</a>
  <a id="3304" class="Symbol">{</a> <a id="3306" href="Algebra.Structures.html#1552" class="Field">isSemigroup</a> <a id="3318" class="Symbol">=</a> <a id="3320" href="Algebra.Morphism.RawMagma.html#3026" class="Function">isSemigroup-homo</a> <a id="3337" href="Algebra.Structures.html#1552" class="Field">isSemigroup</a>
  <a id="3351" class="Symbol">;</a> <a id="3353" href="Algebra.Structures.html#1584" class="Field">idem</a>        <a id="3365" class="Symbol">=</a> <a id="3367" href="Algebra.Morphism.RawMagma.html#1786" class="Function">idem-homo</a> <a id="3377" href="Algebra.Structures.html#1584" class="Field">idem</a>
  <a id="3384" class="Symbol">}</a> <a id="3386" class="Keyword">where</a> <a id="3392" class="Keyword">open</a> <a id="3397" href="Algebra.Structures.html#1501" class="Module">IsBand</a> <a id="3404" href="Algebra.Morphism.RawMagma.html#3286" class="Bound">isBand</a>

<a id="isSemilattice-homo"></a><a id="3412" href="Algebra.Morphism.RawMagma.html#3412" class="Function">isSemilattice-homo</a> <a id="3431" class="Symbol">:</a> <a id="3433" href="Algebra.Structures.html#1659" class="Record">IsSemilattice</a> <a id="3447" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="3453" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="3457" class="Symbol">→</a> <a id="3459" href="Algebra.Structures.html#1659" class="Record">IsSemilattice</a> <a id="3473" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="3479" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="3483" href="Algebra.Morphism.RawMagma.html#3412" class="Function">isSemilattice-homo</a> <a id="3502" href="Algebra.Morphism.RawMagma.html#3502" class="Bound">isSemilattice</a> <a id="3516" class="Symbol">=</a> <a id="3518" class="Keyword">record</a>
  <a id="3527" class="Symbol">{</a> <a id="3529" href="Algebra.Structures.html#1717" class="Field">isBand</a> <a id="3536" class="Symbol">=</a> <a id="3538" href="Algebra.Morphism.RawMagma.html#3224" class="Function">isBand-homo</a> <a id="3550" href="Algebra.Structures.html#1717" class="Field">isBand</a>
  <a id="3559" class="Symbol">;</a> <a id="3561" href="Algebra.Structures.html#1739" class="Field">comm</a>   <a id="3568" class="Symbol">=</a> <a id="3570" href="Algebra.Morphism.RawMagma.html#1575" class="Function">comm-homo</a> <a id="3580" href="Algebra.Structures.html#1739" class="Field">comm</a>
  <a id="3587" class="Symbol">}</a> <a id="3589" class="Keyword">where</a> <a id="3595" class="Keyword">open</a> <a id="3600" href="Algebra.Structures.html#1659" class="Module">IsSemilattice</a> <a id="3614" href="Algebra.Morphism.RawMagma.html#3502" class="Bound">isSemilattice</a>

<a id="isSelectiveMagma-homo"></a><a id="3629" href="Algebra.Morphism.RawMagma.html#3629" class="Function">isSelectiveMagma-homo</a> <a id="3651" class="Symbol">:</a> <a id="3653" href="Algebra.Structures.html#1872" class="Record">IsSelectiveMagma</a> <a id="3670" href="Algebra.html#794" class="Function Operator">T._≈_</a> <a id="3676" href="Algebra.html#822" class="Function Operator">_⊕_</a> <a id="3680" class="Symbol">→</a> <a id="3682" href="Algebra.Structures.html#1872" class="Record">IsSelectiveMagma</a> <a id="3699" href="Algebra.html#794" class="Function Operator">F._≈_</a> <a id="3705" href="Algebra.html#822" class="Function Operator">_∙_</a>
<a id="3709" href="Algebra.Morphism.RawMagma.html#3629" class="Function">isSelectiveMagma-homo</a> <a id="3731" href="Algebra.Morphism.RawMagma.html#3731" class="Bound">isSelMagma</a> <a id="3742" class="Symbol">=</a> <a id="3744" class="Keyword">record</a>
  <a id="3753" class="Symbol">{</a> <a id="3755" href="Algebra.Structures.html#1933" class="Field">isMagma</a> <a id="3763" class="Symbol">=</a> <a id="3765" href="Algebra.Morphism.html#1523" class="Function">F-isMagma</a>
  <a id="3777" class="Symbol">;</a> <a id="3779" href="Algebra.Structures.html#1957" class="Field">sel</a>     <a id="3787" class="Symbol">=</a> <a id="3789" href="Algebra.Morphism.RawMagma.html#1955" class="Function">sel-homo</a> <a id="3798" href="Algebra.Structures.html#1957" class="Field">sel</a>
  <a id="3804" class="Symbol">}</a> <a id="3806" class="Keyword">where</a> <a id="3812" class="Keyword">open</a> <a id="3817" href="Algebra.Structures.html#1872" class="Module">IsSelectiveMagma</a> <a id="3834" href="Algebra.Morphism.RawMagma.html#3731" class="Bound">isSelMagma</a>
</pre></body></html>