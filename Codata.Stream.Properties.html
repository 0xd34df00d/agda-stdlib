<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Stream.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Properties of operations on the Stream type</a>
<a id="153" class="Comment">------------------------------------------------------------------------</a>

<a id="227" class="Symbol">{-#</a> <a id="231" class="Keyword">OPTIONS</a> <a id="239" class="Option">--without-K</a> <a id="251" class="Option">--safe</a> <a id="258" class="Symbol">#-}</a>

<a id="263" class="Keyword">module</a> <a id="270" href="Codata.Stream.Properties.html" class="Module">Codata.Stream.Properties</a> <a id="295" class="Keyword">where</a>

<a id="302" class="Keyword">open</a> <a id="307" class="Keyword">import</a> <a id="314" href="Size.html" class="Module">Size</a>
<a id="319" class="Keyword">open</a> <a id="324" class="Keyword">import</a> <a id="331" href="Codata.Thunk.html" class="Module">Codata.Thunk</a> <a id="344" class="Keyword">using</a> <a id="350" class="Symbol">(</a><a id="351" href="Codata.Thunk.html#454" class="Record">Thunk</a><a id="356" class="Symbol">;</a> <a id="358" href="Codata.Thunk.html#530" class="Field">force</a><a id="363" class="Symbol">)</a>
<a id="365" class="Keyword">open</a> <a id="370" class="Keyword">import</a> <a id="377" href="Codata.Stream.html" class="Module">Codata.Stream</a>
<a id="391" class="Keyword">open</a> <a id="396" class="Keyword">import</a> <a id="403" href="Codata.Stream.Bisimilarity.html" class="Module">Codata.Stream.Bisimilarity</a>

<a id="431" class="Keyword">open</a> <a id="436" class="Keyword">import</a> <a id="443" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
<a id="457" class="Keyword">open</a> <a id="462" class="Keyword">import</a> <a id="469" href="Data.Nat.GeneralisedArithmetic.html" class="Module">Data.Nat.GeneralisedArithmetic</a> <a id="500" class="Keyword">using</a> <a id="506" class="Symbol">(</a><a id="507" href="Data.Nat.GeneralisedArithmetic.html#508" class="Function">fold</a><a id="511" class="Symbol">;</a> <a id="513" href="Data.Nat.GeneralisedArithmetic.html#1555" class="Function">fold-pull</a><a id="522" class="Symbol">)</a>

<a id="525" class="Keyword">import</a> <a id="532" href="Data.Vec.html" class="Module">Data.Vec</a> <a id="541" class="Symbol">as</a> <a id="544" class="Module">Vec</a>
<a id="548" class="Keyword">import</a> <a id="555" href="Data.Product.html" class="Module">Data.Product</a> <a id="568" class="Symbol">as</a> <a id="571" class="Module">Prod</a>

<a id="577" class="Keyword">open</a> <a id="582" class="Keyword">import</a> <a id="589" href="Function.html" class="Module">Function</a>
<a id="598" class="Keyword">open</a> <a id="603" class="Keyword">import</a> <a id="610" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="648" class="Symbol">as</a> <a id="651" class="Module">Eq</a> <a id="654" class="Keyword">using</a> <a id="660" class="Symbol">(</a><a id="661" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">_≡_</a><a id="664" class="Symbol">)</a>

<a id="667" class="Comment">------------------------------------------------------------------------</a>
<a id="740" class="Comment">-- repeat</a>

<a id="751" class="Keyword">module</a> <a id="758" href="Codata.Stream.Properties.html#758" class="Module">_</a> <a id="760" class="Symbol">{</a><a id="761" href="Codata.Stream.Properties.html#761" class="Bound">a</a><a id="762" class="Symbol">}</a> <a id="764" class="Symbol">{</a><a id="765" href="Codata.Stream.Properties.html#765" class="Bound">A</a> <a id="767" class="Symbol">:</a> <a id="769" class="PrimitiveType">Set</a> <a id="773" href="Codata.Stream.Properties.html#761" class="Bound">a</a><a id="774" class="Symbol">}</a> <a id="776" class="Keyword">where</a>

 <a id="784" href="Codata.Stream.Properties.html#784" class="Function">lookup-repeat-identity</a> <a id="807" class="Symbol">:</a> <a id="809" class="Symbol">(</a><a id="810" href="Codata.Stream.Properties.html#810" class="Bound">n</a> <a id="812" class="Symbol">:</a> <a id="814" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a><a id="815" class="Symbol">)</a> <a id="817" class="Symbol">(</a><a id="818" href="Codata.Stream.Properties.html#818" class="Bound">a</a> <a id="820" class="Symbol">:</a> <a id="822" href="Codata.Stream.Properties.html#765" class="Bound">A</a><a id="823" class="Symbol">)</a> <a id="825" class="Symbol">→</a> <a id="827" href="Codata.Stream.html#994" class="Function">lookup</a> <a id="834" href="Codata.Stream.Properties.html#810" class="Bound">n</a> <a id="836" class="Symbol">(</a><a id="837" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="844" href="Codata.Stream.Properties.html#818" class="Bound">a</a><a id="845" class="Symbol">)</a> <a id="847" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="849" href="Codata.Stream.Properties.html#818" class="Bound">a</a>
 <a id="852" href="Codata.Stream.Properties.html#784" class="Function">lookup-repeat-identity</a> <a id="875" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>    <a id="883" href="Codata.Stream.Properties.html#883" class="Bound">a</a> <a id="885" class="Symbol">=</a> <a id="887" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a>
 <a id="896" href="Codata.Stream.Properties.html#784" class="Function">lookup-repeat-identity</a> <a id="919" class="Symbol">(</a><a id="920" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="924" href="Codata.Stream.Properties.html#924" class="Bound">n</a><a id="925" class="Symbol">)</a> <a id="927" href="Codata.Stream.Properties.html#927" class="Bound">a</a> <a id="929" class="Symbol">=</a> <a id="931" href="Codata.Stream.Properties.html#784" class="Function">lookup-repeat-identity</a> <a id="954" href="Codata.Stream.Properties.html#924" class="Bound">n</a> <a id="956" href="Codata.Stream.Properties.html#927" class="Bound">a</a>

 <a id="960" href="Codata.Stream.Properties.html#960" class="Function">take-repeat-identity</a> <a id="981" class="Symbol">:</a> <a id="983" class="Symbol">(</a><a id="984" href="Codata.Stream.Properties.html#984" class="Bound">n</a> <a id="986" class="Symbol">:</a> <a id="988" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a><a id="989" class="Symbol">)</a> <a id="991" class="Symbol">(</a><a id="992" href="Codata.Stream.Properties.html#992" class="Bound">a</a> <a id="994" class="Symbol">:</a> <a id="996" href="Codata.Stream.Properties.html#765" class="Bound">A</a><a id="997" class="Symbol">)</a> <a id="999" class="Symbol">→</a> <a id="1001" href="Codata.Stream.html#1251" class="Function">take</a> <a id="1006" href="Codata.Stream.Properties.html#984" class="Bound">n</a> <a id="1008" class="Symbol">(</a><a id="1009" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="1016" href="Codata.Stream.Properties.html#992" class="Bound">a</a><a id="1017" class="Symbol">)</a> <a id="1019" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1021" href="Data.Vec.html#5840" class="Function">Vec.replicate</a> <a id="1035" href="Codata.Stream.Properties.html#992" class="Bound">a</a>
 <a id="1038" href="Codata.Stream.Properties.html#960" class="Function">take-repeat-identity</a> <a id="1059" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>    <a id="1067" href="Codata.Stream.Properties.html#1067" class="Bound">a</a> <a id="1069" class="Symbol">=</a> <a id="1071" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a>
 <a id="1080" href="Codata.Stream.Properties.html#960" class="Function">take-repeat-identity</a> <a id="1101" class="Symbol">(</a><a id="1102" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1106" href="Codata.Stream.Properties.html#1106" class="Bound">n</a><a id="1107" class="Symbol">)</a> <a id="1109" href="Codata.Stream.Properties.html#1109" class="Bound">a</a> <a id="1111" class="Symbol">=</a> <a id="1113" href="Relation.Binary.PropositionalEquality.html#1134" class="Function">Eq.cong</a> <a id="1121" class="Symbol">(</a><a id="1122" href="Codata.Stream.Properties.html#1109" class="Bound">a</a> <a id="1124" href="Data.Vec.html#832" class="InductiveConstructor Operator">Vec.∷_</a><a id="1130" class="Symbol">)</a> <a id="1132" class="Symbol">(</a><a id="1133" href="Codata.Stream.Properties.html#960" class="Function">take-repeat-identity</a> <a id="1154" href="Codata.Stream.Properties.html#1106" class="Bound">n</a> <a id="1156" href="Codata.Stream.Properties.html#1109" class="Bound">a</a><a id="1157" class="Symbol">)</a>

<a id="1160" class="Keyword">module</a> <a id="1167" href="Codata.Stream.Properties.html#1167" class="Module">_</a> <a id="1169" class="Symbol">{</a><a id="1170" href="Codata.Stream.Properties.html#1170" class="Bound">a</a> <a id="1172" href="Codata.Stream.Properties.html#1172" class="Bound">b</a><a id="1173" class="Symbol">}</a> <a id="1175" class="Symbol">{</a><a id="1176" href="Codata.Stream.Properties.html#1176" class="Bound">A</a> <a id="1178" class="Symbol">:</a> <a id="1180" class="PrimitiveType">Set</a> <a id="1184" href="Codata.Stream.Properties.html#1170" class="Bound">a</a><a id="1185" class="Symbol">}</a> <a id="1187" class="Symbol">{</a><a id="1188" href="Codata.Stream.Properties.html#1188" class="Bound">B</a> <a id="1190" class="Symbol">:</a> <a id="1192" class="PrimitiveType">Set</a> <a id="1196" href="Codata.Stream.Properties.html#1172" class="Bound">b</a><a id="1197" class="Symbol">}</a> <a id="1199" class="Keyword">where</a>

 <a id="1207" href="Codata.Stream.Properties.html#1207" class="Function">map-repeat-commute</a> <a id="1226" class="Symbol">:</a> <a id="1228" class="Symbol">∀</a> <a id="1230" class="Symbol">(</a><a id="1231" href="Codata.Stream.Properties.html#1231" class="Bound">f</a> <a id="1233" class="Symbol">:</a> <a id="1235" href="Codata.Stream.Properties.html#1176" class="Bound">A</a> <a id="1237" class="Symbol">→</a> <a id="1239" href="Codata.Stream.Properties.html#1188" class="Bound">B</a><a id="1240" class="Symbol">)</a> <a id="1242" href="Codata.Stream.Properties.html#1242" class="Bound">a</a> <a id="1244" class="Symbol">{</a><a id="1245" href="Codata.Stream.Properties.html#1245" class="Bound">i</a><a id="1246" class="Symbol">}</a> <a id="1248" class="Symbol">→</a> <a id="1250" href="Codata.Stream.Properties.html#1245" class="Bound">i</a> <a id="1252" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">⊢</a> <a id="1254" href="Codata.Stream.html#2415" class="Function">map</a> <a id="1258" href="Codata.Stream.Properties.html#1231" class="Bound">f</a> <a id="1260" class="Symbol">(</a><a id="1261" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="1268" href="Codata.Stream.Properties.html#1242" class="Bound">a</a><a id="1269" class="Symbol">)</a> <a id="1271" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">≈</a> <a id="1273" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="1280" class="Symbol">(</a><a id="1281" href="Codata.Stream.Properties.html#1231" class="Bound">f</a> <a id="1283" href="Codata.Stream.Properties.html#1242" class="Bound">a</a><a id="1284" class="Symbol">)</a>
 <a id="1287" href="Codata.Stream.Properties.html#1207" class="Function">map-repeat-commute</a> <a id="1306" href="Codata.Stream.Properties.html#1306" class="Bound">f</a> <a id="1308" href="Codata.Stream.Properties.html#1308" class="Bound">a</a> <a id="1310" class="Symbol">=</a> <a id="1312" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a> <a id="1320" href="Codata.Stream.Bisimilarity.html#592" class="InductiveConstructor Operator">∷</a> <a id="1322" class="Symbol">λ</a> <a id="1324" class="Keyword">where</a> <a id="1330" class="Symbol">.</a><a id="1331" href="Codata.Thunk.html#530" class="Field">force</a> <a id="1337" class="Symbol">→</a> <a id="1339" href="Codata.Stream.Properties.html#1207" class="Function">map-repeat-commute</a> <a id="1358" href="Codata.Stream.Properties.html#1306" class="Bound">f</a> <a id="1360" href="Codata.Stream.Properties.html#1308" class="Bound">a</a>

 <a id="1364" href="Codata.Stream.Properties.html#1364" class="Function">repeat-ap-identity</a> <a id="1383" class="Symbol">:</a> <a id="1385" class="Symbol">∀</a> <a id="1387" class="Symbol">(</a><a id="1388" href="Codata.Stream.Properties.html#1388" class="Bound">f</a> <a id="1390" class="Symbol">:</a> <a id="1392" href="Codata.Stream.Properties.html#1176" class="Bound">A</a> <a id="1394" class="Symbol">→</a> <a id="1396" href="Codata.Stream.Properties.html#1188" class="Bound">B</a><a id="1397" class="Symbol">)</a> <a id="1399" href="Codata.Stream.Properties.html#1399" class="Bound">as</a> <a id="1402" class="Symbol">{</a><a id="1403" href="Codata.Stream.Properties.html#1403" class="Bound">i</a><a id="1404" class="Symbol">}</a> <a id="1406" class="Symbol">→</a> <a id="1408" href="Codata.Stream.Properties.html#1403" class="Bound">i</a> <a id="1410" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">⊢</a> <a id="1412" href="Codata.Stream.html#2524" class="Function">ap</a> <a id="1415" class="Symbol">(</a><a id="1416" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="1423" href="Codata.Stream.Properties.html#1388" class="Bound">f</a><a id="1424" class="Symbol">)</a> <a id="1426" href="Codata.Stream.Properties.html#1399" class="Bound">as</a> <a id="1429" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">≈</a> <a id="1431" href="Codata.Stream.html#2415" class="Function">map</a> <a id="1435" href="Codata.Stream.Properties.html#1388" class="Bound">f</a> <a id="1437" href="Codata.Stream.Properties.html#1399" class="Bound">as</a>
 <a id="1441" href="Codata.Stream.Properties.html#1364" class="Function">repeat-ap-identity</a> <a id="1460" href="Codata.Stream.Properties.html#1460" class="Bound">f</a> <a id="1462" class="Symbol">(</a><a id="1463" href="Codata.Stream.Properties.html#1463" class="Bound">a</a> <a id="1465" href="Codata.Stream.html#731" class="InductiveConstructor Operator">∷</a> <a id="1467" href="Codata.Stream.Properties.html#1467" class="Bound">as</a><a id="1469" class="Symbol">)</a> <a id="1471" class="Symbol">=</a> <a id="1473" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a> <a id="1481" href="Codata.Stream.Bisimilarity.html#592" class="InductiveConstructor Operator">∷</a> <a id="1483" class="Symbol">λ</a> <a id="1485" class="Keyword">where</a> <a id="1491" class="Symbol">.</a><a id="1492" href="Codata.Thunk.html#530" class="Field">force</a> <a id="1498" class="Symbol">→</a> <a id="1500" href="Codata.Stream.Properties.html#1364" class="Function">repeat-ap-identity</a> <a id="1519" href="Codata.Stream.Properties.html#1460" class="Bound">f</a> <a id="1521" class="Symbol">(</a><a id="1522" href="Codata.Stream.Properties.html#1467" class="Bound">as</a> <a id="1525" class="Symbol">.</a><a id="1526" href="Codata.Thunk.html#530" class="Field">force</a><a id="1531" class="Symbol">)</a>

 <a id="1535" href="Codata.Stream.Properties.html#1535" class="Function">ap-repeat-identity</a> <a id="1554" class="Symbol">:</a> <a id="1556" class="Symbol">∀</a> <a id="1558" class="Symbol">(</a><a id="1559" href="Codata.Stream.Properties.html#1559" class="Bound">fs</a> <a id="1562" class="Symbol">:</a> <a id="1564" href="Codata.Stream.html#681" class="Datatype">Stream</a> <a id="1571" class="Symbol">(</a><a id="1572" href="Codata.Stream.Properties.html#1176" class="Bound">A</a> <a id="1574" class="Symbol">→</a> <a id="1576" href="Codata.Stream.Properties.html#1188" class="Bound">B</a><a id="1577" class="Symbol">)</a> <a id="1579" href="Agda.Builtin.Size.html#210" class="Postulate">∞</a><a id="1580" class="Symbol">)</a> <a id="1582" class="Symbol">(</a><a id="1583" href="Codata.Stream.Properties.html#1583" class="Bound">a</a> <a id="1585" class="Symbol">:</a> <a id="1587" href="Codata.Stream.Properties.html#1176" class="Bound">A</a><a id="1588" class="Symbol">)</a> <a id="1590" class="Symbol">{</a><a id="1591" href="Codata.Stream.Properties.html#1591" class="Bound">i</a><a id="1592" class="Symbol">}</a> <a id="1594" class="Symbol">→</a> <a id="1596" href="Codata.Stream.Properties.html#1591" class="Bound">i</a> <a id="1598" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">⊢</a> <a id="1600" href="Codata.Stream.html#2524" class="Function">ap</a> <a id="1603" href="Codata.Stream.Properties.html#1559" class="Bound">fs</a> <a id="1606" class="Symbol">(</a><a id="1607" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="1614" href="Codata.Stream.Properties.html#1583" class="Bound">a</a><a id="1615" class="Symbol">)</a> <a id="1617" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">≈</a> <a id="1619" href="Codata.Stream.html#2415" class="Function">map</a> <a id="1623" class="Symbol">(</a><a id="1624" href="Function.html#1927" class="Function Operator">_$</a> <a id="1627" href="Codata.Stream.Properties.html#1583" class="Bound">a</a><a id="1628" class="Symbol">)</a> <a id="1630" href="Codata.Stream.Properties.html#1559" class="Bound">fs</a>
 <a id="1634" href="Codata.Stream.Properties.html#1535" class="Function">ap-repeat-identity</a> <a id="1653" class="Symbol">(</a><a id="1654" href="Codata.Stream.Properties.html#1654" class="Bound">f</a> <a id="1656" href="Codata.Stream.html#731" class="InductiveConstructor Operator">∷</a> <a id="1658" href="Codata.Stream.Properties.html#1658" class="Bound">fs</a><a id="1660" class="Symbol">)</a> <a id="1662" href="Codata.Stream.Properties.html#1662" class="Bound">a</a> <a id="1664" class="Symbol">=</a> <a id="1666" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a> <a id="1674" href="Codata.Stream.Bisimilarity.html#592" class="InductiveConstructor Operator">∷</a> <a id="1676" class="Symbol">λ</a> <a id="1678" class="Keyword">where</a> <a id="1684" class="Symbol">.</a><a id="1685" href="Codata.Thunk.html#530" class="Field">force</a> <a id="1691" class="Symbol">→</a> <a id="1693" href="Codata.Stream.Properties.html#1535" class="Function">ap-repeat-identity</a> <a id="1712" class="Symbol">(</a><a id="1713" href="Codata.Stream.Properties.html#1658" class="Bound">fs</a> <a id="1716" class="Symbol">.</a><a id="1717" href="Codata.Thunk.html#530" class="Field">force</a><a id="1722" class="Symbol">)</a> <a id="1724" href="Codata.Stream.Properties.html#1662" class="Bound">a</a>

 <a id="1728" href="Codata.Stream.Properties.html#1728" class="Function">ap-repeat-commute</a> <a id="1746" class="Symbol">:</a> <a id="1748" class="Symbol">∀</a> <a id="1750" class="Symbol">(</a><a id="1751" href="Codata.Stream.Properties.html#1751" class="Bound">f</a> <a id="1753" class="Symbol">:</a> <a id="1755" href="Codata.Stream.Properties.html#1176" class="Bound">A</a> <a id="1757" class="Symbol">→</a> <a id="1759" href="Codata.Stream.Properties.html#1188" class="Bound">B</a><a id="1760" class="Symbol">)</a> <a id="1762" href="Codata.Stream.Properties.html#1762" class="Bound">a</a> <a id="1764" class="Symbol">{</a><a id="1765" href="Codata.Stream.Properties.html#1765" class="Bound">i</a><a id="1766" class="Symbol">}</a> <a id="1768" class="Symbol">→</a> <a id="1770" href="Codata.Stream.Properties.html#1765" class="Bound">i</a> <a id="1772" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">⊢</a> <a id="1774" href="Codata.Stream.html#2524" class="Function">ap</a> <a id="1777" class="Symbol">(</a><a id="1778" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="1785" href="Codata.Stream.Properties.html#1751" class="Bound">f</a><a id="1786" class="Symbol">)</a> <a id="1788" class="Symbol">(</a><a id="1789" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="1796" href="Codata.Stream.Properties.html#1762" class="Bound">a</a><a id="1797" class="Symbol">)</a> <a id="1799" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">≈</a> <a id="1801" href="Codata.Stream.html#807" class="Function">repeat</a> <a id="1808" class="Symbol">(</a><a id="1809" href="Codata.Stream.Properties.html#1751" class="Bound">f</a> <a id="1811" href="Codata.Stream.Properties.html#1762" class="Bound">a</a><a id="1812" class="Symbol">)</a>
 <a id="1815" href="Codata.Stream.Properties.html#1728" class="Function">ap-repeat-commute</a> <a id="1833" href="Codata.Stream.Properties.html#1833" class="Bound">f</a> <a id="1835" href="Codata.Stream.Properties.html#1835" class="Bound">a</a> <a id="1837" class="Symbol">=</a> <a id="1839" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a> <a id="1847" href="Codata.Stream.Bisimilarity.html#592" class="InductiveConstructor Operator">∷</a> <a id="1849" class="Symbol">λ</a> <a id="1851" class="Keyword">where</a> <a id="1857" class="Symbol">.</a><a id="1858" href="Codata.Thunk.html#530" class="Field">force</a> <a id="1864" class="Symbol">→</a> <a id="1866" href="Codata.Stream.Properties.html#1728" class="Function">ap-repeat-commute</a> <a id="1884" href="Codata.Stream.Properties.html#1833" class="Bound">f</a> <a id="1886" href="Codata.Stream.Properties.html#1835" class="Bound">a</a>


<a id="1890" class="Comment">------------------------------------------------------------------------</a>
<a id="1963" class="Comment">-- Functor laws</a>

<a id="1980" class="Keyword">module</a> <a id="1987" href="Codata.Stream.Properties.html#1987" class="Module">_</a> <a id="1989" class="Symbol">{</a><a id="1990" href="Codata.Stream.Properties.html#1990" class="Bound">a</a><a id="1991" class="Symbol">}</a> <a id="1993" class="Symbol">{</a><a id="1994" href="Codata.Stream.Properties.html#1994" class="Bound">A</a> <a id="1996" class="Symbol">:</a> <a id="1998" class="PrimitiveType">Set</a> <a id="2002" href="Codata.Stream.Properties.html#1990" class="Bound">a</a><a id="2003" class="Symbol">}</a> <a id="2005" class="Keyword">where</a>

 <a id="2013" href="Codata.Stream.Properties.html#2013" class="Function">map-identity</a> <a id="2026" class="Symbol">:</a> <a id="2028" class="Symbol">∀</a> <a id="2030" class="Symbol">(</a><a id="2031" href="Codata.Stream.Properties.html#2031" class="Bound">as</a> <a id="2034" class="Symbol">:</a> <a id="2036" href="Codata.Stream.html#681" class="Datatype">Stream</a> <a id="2043" href="Codata.Stream.Properties.html#1994" class="Bound">A</a> <a id="2045" href="Agda.Builtin.Size.html#210" class="Postulate">∞</a><a id="2046" class="Symbol">)</a> <a id="2048" class="Symbol">{</a><a id="2049" href="Codata.Stream.Properties.html#2049" class="Bound">i</a><a id="2050" class="Symbol">}</a> <a id="2052" class="Symbol">→</a> <a id="2054" href="Codata.Stream.Properties.html#2049" class="Bound">i</a> <a id="2056" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">⊢</a> <a id="2058" href="Codata.Stream.html#2415" class="Function">map</a> <a id="2062" href="Function.html#1104" class="Function">id</a> <a id="2065" href="Codata.Stream.Properties.html#2031" class="Bound">as</a> <a id="2068" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">≈</a> <a id="2070" href="Codata.Stream.Properties.html#2031" class="Bound">as</a>
 <a id="2074" href="Codata.Stream.Properties.html#2013" class="Function">map-identity</a> <a id="2087" class="Symbol">(</a><a id="2088" href="Codata.Stream.Properties.html#2088" class="Bound">a</a> <a id="2090" href="Codata.Stream.html#731" class="InductiveConstructor Operator">∷</a> <a id="2092" href="Codata.Stream.Properties.html#2092" class="Bound">as</a><a id="2094" class="Symbol">)</a> <a id="2096" class="Symbol">=</a> <a id="2098" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a> <a id="2106" href="Codata.Stream.Bisimilarity.html#592" class="InductiveConstructor Operator">∷</a> <a id="2108" class="Symbol">λ</a> <a id="2110" class="Keyword">where</a> <a id="2116" class="Symbol">.</a><a id="2117" href="Codata.Thunk.html#530" class="Field">force</a> <a id="2123" class="Symbol">→</a> <a id="2125" href="Codata.Stream.Properties.html#2013" class="Function">map-identity</a> <a id="2138" class="Symbol">(</a><a id="2139" href="Codata.Stream.Properties.html#2092" class="Bound">as</a> <a id="2142" class="Symbol">.</a><a id="2143" href="Codata.Thunk.html#530" class="Field">force</a><a id="2148" class="Symbol">)</a>

<a id="2151" class="Keyword">module</a> <a id="2158" href="Codata.Stream.Properties.html#2158" class="Module">_</a> <a id="2160" class="Symbol">{</a><a id="2161" href="Codata.Stream.Properties.html#2161" class="Bound">a</a> <a id="2163" href="Codata.Stream.Properties.html#2163" class="Bound">b</a> <a id="2165" href="Codata.Stream.Properties.html#2165" class="Bound">c</a><a id="2166" class="Symbol">}</a> <a id="2168" class="Symbol">{</a><a id="2169" href="Codata.Stream.Properties.html#2169" class="Bound">A</a> <a id="2171" class="Symbol">:</a> <a id="2173" class="PrimitiveType">Set</a> <a id="2177" href="Codata.Stream.Properties.html#2161" class="Bound">a</a><a id="2178" class="Symbol">}</a> <a id="2180" class="Symbol">{</a><a id="2181" href="Codata.Stream.Properties.html#2181" class="Bound">B</a> <a id="2183" class="Symbol">:</a> <a id="2185" class="PrimitiveType">Set</a> <a id="2189" href="Codata.Stream.Properties.html#2163" class="Bound">b</a><a id="2190" class="Symbol">}</a> <a id="2192" class="Symbol">{</a><a id="2193" href="Codata.Stream.Properties.html#2193" class="Bound">C</a> <a id="2195" class="Symbol">:</a> <a id="2197" class="PrimitiveType">Set</a> <a id="2201" href="Codata.Stream.Properties.html#2165" class="Bound">c</a><a id="2202" class="Symbol">}</a> <a id="2204" class="Keyword">where</a>

 <a id="2212" href="Codata.Stream.Properties.html#2212" class="Function">map-map-fusion</a> <a id="2227" class="Symbol">:</a> <a id="2229" class="Symbol">∀</a> <a id="2231" class="Symbol">(</a><a id="2232" href="Codata.Stream.Properties.html#2232" class="Bound">f</a> <a id="2234" class="Symbol">:</a> <a id="2236" href="Codata.Stream.Properties.html#2169" class="Bound">A</a> <a id="2238" class="Symbol">→</a> <a id="2240" href="Codata.Stream.Properties.html#2181" class="Bound">B</a><a id="2241" class="Symbol">)</a> <a id="2243" class="Symbol">(</a><a id="2244" href="Codata.Stream.Properties.html#2244" class="Bound">g</a> <a id="2246" class="Symbol">:</a> <a id="2248" href="Codata.Stream.Properties.html#2181" class="Bound">B</a> <a id="2250" class="Symbol">→</a> <a id="2252" href="Codata.Stream.Properties.html#2193" class="Bound">C</a><a id="2253" class="Symbol">)</a> <a id="2255" href="Codata.Stream.Properties.html#2255" class="Bound">as</a> <a id="2258" class="Symbol">{</a><a id="2259" href="Codata.Stream.Properties.html#2259" class="Bound">i</a><a id="2260" class="Symbol">}</a> <a id="2262" class="Symbol">→</a> <a id="2264" href="Codata.Stream.Properties.html#2259" class="Bound">i</a> <a id="2266" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">⊢</a> <a id="2268" href="Codata.Stream.html#2415" class="Function">map</a> <a id="2272" href="Codata.Stream.Properties.html#2244" class="Bound">g</a> <a id="2274" class="Symbol">(</a><a id="2275" href="Codata.Stream.html#2415" class="Function">map</a> <a id="2279" href="Codata.Stream.Properties.html#2232" class="Bound">f</a> <a id="2281" href="Codata.Stream.Properties.html#2255" class="Bound">as</a><a id="2283" class="Symbol">)</a> <a id="2285" href="Codata.Stream.Bisimilarity.html#1612" class="Function Operator">≈</a> <a id="2287" href="Codata.Stream.html#2415" class="Function">map</a> <a id="2291" class="Symbol">(</a><a id="2292" href="Codata.Stream.Properties.html#2244" class="Bound">g</a> <a id="2294" href="Function.html#805" class="Function Operator">∘</a> <a id="2296" href="Codata.Stream.Properties.html#2232" class="Bound">f</a><a id="2297" class="Symbol">)</a> <a id="2299" href="Codata.Stream.Properties.html#2255" class="Bound">as</a>
 <a id="2303" href="Codata.Stream.Properties.html#2212" class="Function">map-map-fusion</a> <a id="2318" href="Codata.Stream.Properties.html#2318" class="Bound">f</a> <a id="2320" href="Codata.Stream.Properties.html#2320" class="Bound">g</a> <a id="2322" class="Symbol">(</a><a id="2323" href="Codata.Stream.Properties.html#2323" class="Bound">a</a> <a id="2325" href="Codata.Stream.html#731" class="InductiveConstructor Operator">∷</a> <a id="2327" href="Codata.Stream.Properties.html#2327" class="Bound">as</a><a id="2329" class="Symbol">)</a> <a id="2331" class="Symbol">=</a> <a id="2333" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a> <a id="2341" href="Codata.Stream.Bisimilarity.html#592" class="InductiveConstructor Operator">∷</a> <a id="2343" class="Symbol">λ</a> <a id="2345" class="Keyword">where</a> <a id="2351" class="Symbol">.</a><a id="2352" href="Codata.Thunk.html#530" class="Field">force</a> <a id="2358" class="Symbol">→</a> <a id="2360" href="Codata.Stream.Properties.html#2212" class="Function">map-map-fusion</a> <a id="2375" href="Codata.Stream.Properties.html#2318" class="Bound">f</a> <a id="2377" href="Codata.Stream.Properties.html#2320" class="Bound">g</a> <a id="2379" class="Symbol">(</a><a id="2380" href="Codata.Stream.Properties.html#2327" class="Bound">as</a> <a id="2383" class="Symbol">.</a><a id="2384" href="Codata.Thunk.html#530" class="Field">force</a><a id="2389" class="Symbol">)</a>


<a id="2393" class="Comment">------------------------------------------------------------------------</a>
<a id="2466" class="Comment">-- splitAt</a>

<a id="2478" class="Keyword">module</a> <a id="2485" href="Codata.Stream.Properties.html#2485" class="Module">_</a> <a id="2487" class="Symbol">{</a><a id="2488" href="Codata.Stream.Properties.html#2488" class="Bound">a</a> <a id="2490" href="Codata.Stream.Properties.html#2490" class="Bound">b</a><a id="2491" class="Symbol">}</a> <a id="2493" class="Symbol">{</a><a id="2494" href="Codata.Stream.Properties.html#2494" class="Bound">A</a> <a id="2496" class="Symbol">:</a> <a id="2498" class="PrimitiveType">Set</a> <a id="2502" href="Codata.Stream.Properties.html#2488" class="Bound">a</a><a id="2503" class="Symbol">}</a> <a id="2505" class="Symbol">{</a><a id="2506" href="Codata.Stream.Properties.html#2506" class="Bound">B</a> <a id="2508" class="Symbol">:</a> <a id="2510" class="PrimitiveType">Set</a> <a id="2514" href="Codata.Stream.Properties.html#2490" class="Bound">b</a><a id="2515" class="Symbol">}</a> <a id="2517" class="Keyword">where</a>

  <a id="2526" href="Codata.Stream.Properties.html#2526" class="Function">splitAt-map</a> <a id="2538" class="Symbol">:</a> <a id="2540" class="Symbol">∀</a> <a id="2542" href="Codata.Stream.Properties.html#2542" class="Bound">n</a> <a id="2544" class="Symbol">(</a><a id="2545" href="Codata.Stream.Properties.html#2545" class="Bound">f</a> <a id="2547" class="Symbol">:</a> <a id="2549" href="Codata.Stream.Properties.html#2494" class="Bound">A</a> <a id="2551" class="Symbol">→</a> <a id="2553" href="Codata.Stream.Properties.html#2506" class="Bound">B</a><a id="2554" class="Symbol">)</a> <a id="2556" href="Codata.Stream.Properties.html#2556" class="Bound">xs</a> <a id="2559" class="Symbol">→</a>
    <a id="2565" href="Codata.Stream.html#1093" class="Function">splitAt</a> <a id="2573" href="Codata.Stream.Properties.html#2542" class="Bound">n</a> <a id="2575" class="Symbol">(</a><a id="2576" href="Codata.Stream.html#2415" class="Function">map</a> <a id="2580" href="Codata.Stream.Properties.html#2545" class="Bound">f</a> <a id="2582" href="Codata.Stream.Properties.html#2556" class="Bound">xs</a><a id="2584" class="Symbol">)</a> <a id="2586" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="2588" href="Data.Product.html#2190" class="Function">Prod.map</a> <a id="2597" class="Symbol">(</a><a id="2598" href="Data.Vec.html#2489" class="Function">Vec.map</a> <a id="2606" href="Codata.Stream.Properties.html#2545" class="Bound">f</a><a id="2607" class="Symbol">)</a> <a id="2609" class="Symbol">(</a><a id="2610" href="Codata.Stream.html#2415" class="Function">map</a> <a id="2614" href="Codata.Stream.Properties.html#2545" class="Bound">f</a><a id="2615" class="Symbol">)</a> <a id="2617" class="Symbol">(</a><a id="2618" href="Codata.Stream.html#1093" class="Function">splitAt</a> <a id="2626" href="Codata.Stream.Properties.html#2542" class="Bound">n</a> <a id="2628" href="Codata.Stream.Properties.html#2556" class="Bound">xs</a><a id="2630" class="Symbol">)</a>
  <a id="2634" href="Codata.Stream.Properties.html#2526" class="Function">splitAt-map</a> <a id="2646" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>    <a id="2654" href="Codata.Stream.Properties.html#2654" class="Bound">f</a> <a id="2656" href="Codata.Stream.Properties.html#2656" class="Bound">xs</a>       <a id="2665" class="Symbol">=</a> <a id="2667" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a>
  <a id="2677" href="Codata.Stream.Properties.html#2526" class="Function">splitAt-map</a> <a id="2689" class="Symbol">(</a><a id="2690" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2694" href="Codata.Stream.Properties.html#2694" class="Bound">n</a><a id="2695" class="Symbol">)</a> <a id="2697" href="Codata.Stream.Properties.html#2697" class="Bound">f</a> <a id="2699" class="Symbol">(</a><a id="2700" href="Codata.Stream.Properties.html#2700" class="Bound">x</a> <a id="2702" href="Codata.Stream.html#731" class="InductiveConstructor Operator">∷</a> <a id="2704" href="Codata.Stream.Properties.html#2704" class="Bound">xs</a><a id="2706" class="Symbol">)</a> <a id="2708" class="Symbol">=</a>
    <a id="2714" href="Relation.Binary.PropositionalEquality.html#1134" class="Function">Eq.cong</a> <a id="2722" class="Symbol">(</a><a id="2723" href="Data.Product.html#2369" class="Function">Prod.map₁</a> <a id="2733" class="Symbol">(</a><a id="2734" href="Codata.Stream.Properties.html#2697" class="Bound">f</a> <a id="2736" href="Codata.Stream.Properties.html#2700" class="Bound">x</a> <a id="2738" href="Data.Vec.html#832" class="InductiveConstructor Operator">Vec.∷_</a><a id="2744" class="Symbol">))</a> <a id="2747" class="Symbol">(</a><a id="2748" href="Codata.Stream.Properties.html#2526" class="Function">splitAt-map</a> <a id="2760" href="Codata.Stream.Properties.html#2694" class="Bound">n</a> <a id="2762" href="Codata.Stream.Properties.html#2697" class="Bound">f</a> <a id="2764" class="Symbol">(</a><a id="2765" href="Codata.Stream.Properties.html#2704" class="Bound">xs</a> <a id="2768" class="Symbol">.</a><a id="2769" href="Codata.Thunk.html#530" class="Field">force</a><a id="2774" class="Symbol">))</a>

<a id="2778" class="Comment">------------------------------------------------------------------------</a>
<a id="2851" class="Comment">-- iterate</a>

<a id="2863" class="Keyword">module</a> <a id="2870" href="Codata.Stream.Properties.html#2870" class="Module">_</a> <a id="2872" class="Symbol">{</a><a id="2873" href="Codata.Stream.Properties.html#2873" class="Bound">a</a><a id="2874" class="Symbol">}</a> <a id="2876" class="Symbol">{</a><a id="2877" href="Codata.Stream.Properties.html#2877" class="Bound">A</a> <a id="2879" class="Symbol">:</a> <a id="2881" class="PrimitiveType">Set</a> <a id="2885" href="Codata.Stream.Properties.html#2873" class="Bound">a</a><a id="2886" class="Symbol">}</a> <a id="2888" class="Keyword">where</a>

  <a id="2897" href="Codata.Stream.Properties.html#2897" class="Function">lookup-iterate-identity</a> <a id="2921" class="Symbol">:</a> <a id="2923" class="Symbol">∀</a> <a id="2925" href="Codata.Stream.Properties.html#2925" class="Bound">n</a> <a id="2927" href="Codata.Stream.Properties.html#2927" class="Bound">f</a> <a id="2929" class="Symbol">(</a><a id="2930" href="Codata.Stream.Properties.html#2930" class="Bound">a</a> <a id="2932" class="Symbol">:</a> <a id="2934" href="Codata.Stream.Properties.html#2877" class="Bound">A</a><a id="2935" class="Symbol">)</a> <a id="2937" class="Symbol">→</a> <a id="2939" href="Codata.Stream.html#994" class="Function">lookup</a> <a id="2946" href="Codata.Stream.Properties.html#2925" class="Bound">n</a> <a id="2948" class="Symbol">(</a><a id="2949" href="Codata.Stream.html#3189" class="Function">iterate</a> <a id="2957" href="Codata.Stream.Properties.html#2927" class="Bound">f</a> <a id="2959" href="Codata.Stream.Properties.html#2930" class="Bound">a</a><a id="2960" class="Symbol">)</a> <a id="2962" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="2964" href="Data.Nat.GeneralisedArithmetic.html#508" class="Function">fold</a> <a id="2969" href="Codata.Stream.Properties.html#2930" class="Bound">a</a> <a id="2971" href="Codata.Stream.Properties.html#2927" class="Bound">f</a> <a id="2973" href="Codata.Stream.Properties.html#2925" class="Bound">n</a>
  <a id="2977" href="Codata.Stream.Properties.html#2897" class="Function">lookup-iterate-identity</a> <a id="3001" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>     <a id="3010" href="Codata.Stream.Properties.html#3010" class="Bound">f</a> <a id="3012" href="Codata.Stream.Properties.html#3012" class="Bound">a</a> <a id="3014" class="Symbol">=</a> <a id="3016" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a>
  <a id="3026" href="Codata.Stream.Properties.html#2897" class="Function">lookup-iterate-identity</a> <a id="3050" class="Symbol">(</a><a id="3051" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="3055" href="Codata.Stream.Properties.html#3055" class="Bound">n</a><a id="3056" class="Symbol">)</a>  <a id="3059" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3061" href="Codata.Stream.Properties.html#3061" class="Bound">a</a> <a id="3063" class="Symbol">=</a> <a id="3065" href="Relation.Binary.PropositionalEquality.html#4033" class="Function Operator">begin</a>
    <a id="3075" href="Codata.Stream.html#994" class="Function">lookup</a> <a id="3082" class="Symbol">(</a><a id="3083" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="3087" href="Codata.Stream.Properties.html#3055" class="Bound">n</a><a id="3088" class="Symbol">)</a> <a id="3090" class="Symbol">(</a><a id="3091" href="Codata.Stream.html#3189" class="Function">iterate</a> <a id="3099" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3101" href="Codata.Stream.Properties.html#3061" class="Bound">a</a><a id="3102" class="Symbol">)</a> <a id="3104" href="Relation.Binary.PropositionalEquality.html#4091" class="Function Operator">≡⟨⟩</a>
    <a id="3112" href="Codata.Stream.html#994" class="Function">lookup</a> <a id="3119" href="Codata.Stream.Properties.html#3055" class="Bound">n</a> <a id="3121" class="Symbol">(</a><a id="3122" href="Codata.Stream.html#3189" class="Function">iterate</a> <a id="3130" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3132" class="Symbol">(</a><a id="3133" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3135" href="Codata.Stream.Properties.html#3061" class="Bound">a</a><a id="3136" class="Symbol">))</a>   <a id="3141" href="Relation.Binary.PropositionalEquality.html#4150" class="Function Operator">≡⟨</a> <a id="3144" href="Codata.Stream.Properties.html#2897" class="Function">lookup-iterate-identity</a> <a id="3168" href="Codata.Stream.Properties.html#3055" class="Bound">n</a> <a id="3170" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3172" class="Symbol">(</a><a id="3173" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3175" href="Codata.Stream.Properties.html#3061" class="Bound">a</a><a id="3176" class="Symbol">)</a> <a id="3178" href="Relation.Binary.PropositionalEquality.html#4150" class="Function Operator">⟩</a>
    <a id="3184" href="Data.Nat.GeneralisedArithmetic.html#508" class="Function">fold</a> <a id="3189" class="Symbol">(</a><a id="3190" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3192" href="Codata.Stream.Properties.html#3061" class="Bound">a</a><a id="3193" class="Symbol">)</a> <a id="3195" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3197" href="Codata.Stream.Properties.html#3055" class="Bound">n</a>               <a id="3213" href="Relation.Binary.PropositionalEquality.html#4150" class="Function Operator">≡⟨</a> <a id="3216" href="Data.Nat.GeneralisedArithmetic.html#1555" class="Function">fold-pull</a> <a id="3226" class="Symbol">(</a><a id="3227" href="Function.html#1145" class="Function">const</a> <a id="3233" href="Function.html#994" class="Function Operator">∘′</a> <a id="3236" href="Codata.Stream.Properties.html#3059" class="Bound">f</a><a id="3237" class="Symbol">)</a> <a id="3239" class="Symbol">(</a><a id="3240" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3242" href="Codata.Stream.Properties.html#3061" class="Bound">a</a><a id="3243" class="Symbol">)</a> <a id="3245" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a> <a id="3253" class="Symbol">(λ</a> <a id="3256" href="Codata.Stream.Properties.html#3256" class="Bound">_</a> <a id="3258" class="Symbol">→</a> <a id="3260" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">Eq.refl</a><a id="3267" class="Symbol">)</a> <a id="3269" href="Codata.Stream.Properties.html#3055" class="Bound">n</a> <a id="3271" href="Relation.Binary.PropositionalEquality.html#4150" class="Function Operator">⟩</a>
    <a id="3277" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3279" class="Symbol">(</a><a id="3280" href="Data.Nat.GeneralisedArithmetic.html#508" class="Function">fold</a> <a id="3285" href="Codata.Stream.Properties.html#3061" class="Bound">a</a> <a id="3287" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3289" href="Codata.Stream.Properties.html#3055" class="Bound">n</a><a id="3290" class="Symbol">)</a>               <a id="3306" href="Relation.Binary.PropositionalEquality.html#4091" class="Function Operator">≡⟨⟩</a>
    <a id="3314" href="Data.Nat.GeneralisedArithmetic.html#508" class="Function">fold</a> <a id="3319" href="Codata.Stream.Properties.html#3061" class="Bound">a</a> <a id="3321" href="Codata.Stream.Properties.html#3059" class="Bound">f</a> <a id="3323" class="Symbol">(</a><a id="3324" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="3328" href="Codata.Stream.Properties.html#3055" class="Bound">n</a><a id="3329" class="Symbol">)</a>             <a id="3343" href="Relation.Binary.PropositionalEquality.html#4235" class="Function Operator">∎</a> <a id="3345" class="Keyword">where</a> <a id="3351" class="Keyword">open</a> <a id="3356" href="Relation.Binary.PropositionalEquality.html#3939" class="Module">Eq.≡-Reasoning</a>
</pre></body></html>