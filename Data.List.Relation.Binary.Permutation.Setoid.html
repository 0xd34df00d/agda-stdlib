<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.List.Relation.Binary.Permutation.Setoid</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- A definition for the permutation relation using setoid equality</a>
<a id="173" class="Comment">------------------------------------------------------------------------</a>

<a id="247" class="Symbol">{-#</a> <a id="251" class="Keyword">OPTIONS</a> <a id="259" class="Pragma">--without-K</a> <a id="271" class="Pragma">--safe</a> <a id="278" class="Symbol">#-}</a>

<a id="283" class="Keyword">open</a> <a id="288" class="Keyword">import</a> <a id="295" href="Relation.Binary.html" class="Module">Relation.Binary</a>

<a id="312" class="Keyword">module</a> <a id="319" href="Data.List.Relation.Binary.Permutation.Setoid.html" class="Module">Data.List.Relation.Binary.Permutation.Setoid</a>
  <a id="366" class="Symbol">{</a><a id="367" href="Data.List.Relation.Binary.Permutation.Setoid.html#367" class="Bound">b</a><a id="368" class="Symbol">}</a> <a id="370" class="Symbol">{</a><a id="371" href="Data.List.Relation.Binary.Permutation.Setoid.html#371" class="Bound">ℓ</a><a id="372" class="Symbol">}</a> <a id="374" class="Symbol">(</a><a id="375" href="Data.List.Relation.Binary.Permutation.Setoid.html#375" class="Bound">S</a> <a id="377" class="Symbol">:</a> <a id="379" href="Relation.Binary.html#1970" class="Record">Setoid</a> <a id="386" href="Data.List.Relation.Binary.Permutation.Setoid.html#367" class="Bound">b</a> <a id="388" href="Data.List.Relation.Binary.Permutation.Setoid.html#371" class="Bound">ℓ</a><a id="389" class="Symbol">)</a> <a id="391" class="Keyword">where</a>

<a id="398" class="Keyword">open</a> <a id="403" class="Keyword">import</a> <a id="410" href="Data.List.html" class="Module">Data.List</a> <a id="420" class="Keyword">using</a> <a id="426" class="Symbol">(</a><a id="427" href="Agda.Builtin.List.html#121" class="Datatype">List</a><a id="431" class="Symbol">;</a> <a id="433" href="Agda.Builtin.List.html#173" class="InductiveConstructor Operator">_∷_</a><a id="436" class="Symbol">)</a>
<a id="438" class="Keyword">import</a> <a id="445" href="Data.List.Relation.Binary.Permutation.Homogeneous.html" class="Module">Data.List.Relation.Binary.Permutation.Homogeneous</a> <a id="495" class="Symbol">as</a> <a id="498" class="Module">HomogeneousPermutation</a>
<a id="521" class="Keyword">open</a> <a id="526" class="Keyword">import</a> <a id="533" href="Level.html" class="Module">Level</a> <a id="539" class="Keyword">using</a> <a id="545" class="Symbol">(</a><a id="546" href="Agda.Primitive.html#657" class="Primitive Operator">_⊔_</a><a id="549" class="Symbol">)</a>
<a id="551" class="Keyword">import</a> <a id="558" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="591" class="Symbol">as</a> <a id="594" class="Module">EqReasoning</a>

<a id="607" class="Keyword">open</a> <a id="612" href="Relation.Binary.html#1970" class="Module">Setoid</a> <a id="619" href="Data.List.Relation.Binary.Permutation.Setoid.html#375" class="Bound">S</a> <a id="621" class="Keyword">using</a> <a id="627" class="Symbol">(</a>Carrier<a id="635" class="Symbol">;</a> _≈_<a id="640" class="Symbol">;</a> sym<a id="645" class="Symbol">)</a>

<a id="648" class="Keyword">open</a> <a id="653" href="Data.List.Relation.Binary.Permutation.Homogeneous.html" class="Module">HomogeneousPermutation</a>
  <a id="678" class="Keyword">using</a> <a id="684" class="Symbol">(</a><a id="685" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#571" class="InductiveConstructor">refl</a><a id="689" class="Symbol">;</a> <a id="691" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#609" class="InductiveConstructor">prep</a><a id="695" class="Symbol">;</a> <a id="697" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#723" class="InductiveConstructor">swap</a><a id="701" class="Symbol">;</a> <a id="703" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#871" class="InductiveConstructor">trans</a><a id="708" class="Symbol">)</a> <a id="710" class="Keyword">public</a>
<a id="717" class="Keyword">open</a> <a id="722" href="Data.List.Relation.Binary.Permutation.Homogeneous.html" class="Module">HomogeneousPermutation</a>
  <a id="747" class="Keyword">renaming</a> <a id="756" class="Symbol">(</a><a id="757" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1133" class="Function">sym</a> <a id="761" class="Symbol">to</a> <a id="764" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1133" class="Function">Perm-sym</a><a id="772" class="Symbol">;</a>
            <a id="786" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1400" class="Function">isEquivalence</a> <a id="800" class="Symbol">to</a> <a id="803" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1400" class="Function">Perm-isEquivalence</a><a id="821" class="Symbol">;</a>
            <a id="835" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1537" class="Function">setoid</a> <a id="842" class="Symbol">to</a> <a id="845" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1537" class="Function">Perm-setoid</a><a id="856" class="Symbol">)</a>

<a id="859" class="Keyword">infix</a> <a id="865" class="Number">3</a> <a id="867" href="Data.List.Relation.Binary.Permutation.Setoid.html#871" class="Function Operator">_↭_</a>
<a id="_↭_"></a><a id="871" href="Data.List.Relation.Binary.Permutation.Setoid.html#871" class="Function Operator">_↭_</a> <a id="875" class="Symbol">:</a> <a id="877" href="Relation.Binary.Core.html#1071" class="Function">Rel</a> <a id="881" class="Symbol">(</a><a id="882" href="Agda.Builtin.List.html#121" class="Datatype">List</a> <a id="887" href="Relation.Binary.html#2033" class="Field">Carrier</a><a id="894" class="Symbol">)</a> <a id="896" class="Symbol">(</a><a id="897" href="Data.List.Relation.Binary.Permutation.Setoid.html#367" class="Bound">b</a> <a id="899" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="901" href="Data.List.Relation.Binary.Permutation.Setoid.html#371" class="Bound">ℓ</a><a id="902" class="Symbol">)</a>
<a id="904" href="Data.List.Relation.Binary.Permutation.Setoid.html#871" class="Function Operator">_↭_</a> <a id="908" class="Symbol">=</a> <a id="910" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#510" class="Datatype">Permutation</a> <a id="922" href="Relation.Binary.html#2059" class="Field Operator">_≈_</a>

<a id="927" class="Comment">------------------------------------------------------------------------</a>
<a id="1000" class="Comment">-- _↭_ is an equivalence</a>

<a id="↭-refl"></a><a id="1026" href="Data.List.Relation.Binary.Permutation.Setoid.html#1026" class="Function">↭-refl</a> <a id="1033" class="Symbol">:</a> <a id="1035" href="Relation.Binary.Core.html#2321" class="Function">Reflexive</a> <a id="1045" class="Symbol">(</a><a id="1046" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#510" class="Datatype">Permutation</a> <a id="1058" href="Relation.Binary.html#2059" class="Field Operator">_≈_</a><a id="1061" class="Symbol">)</a>
<a id="1063" href="Data.List.Relation.Binary.Permutation.Setoid.html#1026" class="Function">↭-refl</a> <a id="1070" class="Symbol">=</a> <a id="1072" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#571" class="InductiveConstructor">refl</a>

<a id="↭-trans"></a><a id="1078" href="Data.List.Relation.Binary.Permutation.Setoid.html#1078" class="Function">↭-trans</a> <a id="1086" class="Symbol">:</a> <a id="1088" href="Relation.Binary.Core.html#2848" class="Function">Transitive</a> <a id="1099" class="Symbol">(</a><a id="1100" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#510" class="Datatype">Permutation</a> <a id="1112" href="Relation.Binary.html#2059" class="Field Operator">_≈_</a><a id="1115" class="Symbol">)</a>
<a id="1117" href="Data.List.Relation.Binary.Permutation.Setoid.html#1078" class="Function">↭-trans</a> <a id="1125" class="Symbol">=</a> <a id="1127" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#871" class="InductiveConstructor">trans</a>

<a id="↭-sym"></a><a id="1134" href="Data.List.Relation.Binary.Permutation.Setoid.html#1134" class="Function">↭-sym</a> <a id="1140" class="Symbol">:</a> <a id="1142" href="Relation.Binary.Core.html#2480" class="Function">Symmetric</a> <a id="1152" class="Symbol">(</a><a id="1153" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#510" class="Datatype">Permutation</a> <a id="1165" href="Relation.Binary.html#2059" class="Field Operator">_≈_</a><a id="1168" class="Symbol">)</a>
<a id="1170" href="Data.List.Relation.Binary.Permutation.Setoid.html#1134" class="Function">↭-sym</a> <a id="1176" class="Symbol">=</a> <a id="1178" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1133" class="Function">Perm-sym</a> <a id="1187" href="Relation.Binary.Core.html#7046" class="Function">sym</a>

<a id="↭-isEquivalence"></a><a id="1192" href="Data.List.Relation.Binary.Permutation.Setoid.html#1192" class="Function">↭-isEquivalence</a> <a id="1208" class="Symbol">:</a> <a id="1210" href="Relation.Binary.Core.html#6946" class="Record">IsEquivalence</a> <a id="1224" class="Symbol">(</a><a id="1225" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#510" class="Datatype">Permutation</a> <a id="1237" href="Relation.Binary.html#2059" class="Field Operator">_≈_</a><a id="1240" class="Symbol">)</a>
<a id="1242" href="Data.List.Relation.Binary.Permutation.Setoid.html#1192" class="Function">↭-isEquivalence</a> <a id="1258" class="Symbol">=</a> <a id="1260" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1400" class="Function">Perm-isEquivalence</a> <a id="1279" href="Relation.Binary.Core.html#7046" class="Function">sym</a>

<a id="↭-setoid"></a><a id="1284" href="Data.List.Relation.Binary.Permutation.Setoid.html#1284" class="Function">↭-setoid</a> <a id="1293" class="Symbol">:</a> <a id="1295" href="Relation.Binary.html#1970" class="Record">Setoid</a> <a id="1302" class="Symbol">_</a> <a id="1304" class="Symbol">_</a>
<a id="1306" href="Data.List.Relation.Binary.Permutation.Setoid.html#1284" class="Function">↭-setoid</a> <a id="1315" class="Symbol">=</a> <a id="1317" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1537" class="Function">Perm-setoid</a> <a id="1329" class="Symbol">{</a><a id="1330" class="Argument">R</a> <a id="1332" class="Symbol">=</a> <a id="1334" href="Relation.Binary.html#2059" class="Field Operator">_≈_</a><a id="1337" class="Symbol">}</a> <a id="1339" href="Relation.Binary.Core.html#7046" class="Function">sym</a>

<a id="1344" class="Comment">------------------------------------------------------------------------</a>
<a id="1417" class="Comment">-- A reasoning API to chain permutation proofs</a>

<a id="1465" class="Keyword">module</a> <a id="PermutationReasoning"></a><a id="1472" href="Data.List.Relation.Binary.Permutation.Setoid.html#1472" class="Module">PermutationReasoning</a> <a id="1493" class="Keyword">where</a>

  <a id="1502" class="Keyword">open</a> <a id="1507" href="Relation.Binary.Reasoning.Setoid.html" class="Module">EqReasoning</a> <a id="1519" href="Data.List.Relation.Binary.Permutation.Setoid.html#1284" class="Function">↭-setoid</a> <a id="1528" class="Keyword">public</a>
    <a id="1539" class="Keyword">using</a> <a id="1545" class="Symbol">(</a>begin_ <a id="1553" class="Symbol">;</a> _∎ <a id="1558" class="Symbol">;</a> _≡⟨⟩_<a id="1565" class="Symbol">;</a> _≡⟨_⟩_<a id="1573" class="Symbol">)</a>
    <a id="1579" class="Keyword">renaming</a> <a id="1588" class="Symbol">(</a>_≈⟨_⟩_ <a id="1596" class="Symbol">to</a> _↭⟨_⟩_<a id="1605" class="Symbol">)</a>

</pre></body></html>