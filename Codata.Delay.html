<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Delay</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The Delay type and some operations</a>
<a id="144" class="Comment">------------------------------------------------------------------------</a>

<a id="218" class="Keyword">module</a> <a id="225" href="Codata.Delay.html" class="Module">Codata.Delay</a> <a id="238" class="Keyword">where</a>

<a id="245" class="Keyword">open</a> <a id="250" class="Keyword">import</a> <a id="257" href="Size.html" class="Module">Size</a>
<a id="262" class="Keyword">open</a> <a id="267" class="Keyword">import</a> <a id="274" href="Codata.Thunk.html" class="Module">Codata.Thunk</a>
<a id="287" class="Keyword">open</a> <a id="292" class="Keyword">import</a> <a id="299" href="Codata.Conat.html" class="Module">Codata.Conat</a> <a id="312" class="Keyword">hiding</a> <a id="319" class="Symbol">(</a><a id="320" href="Codata.Conat.html#1977" class="Function">extract</a><a id="327" class="Symbol">)</a>

<a id="330" class="Keyword">open</a> <a id="335" class="Keyword">import</a> <a id="342" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="353" class="Keyword">open</a> <a id="358" class="Keyword">import</a> <a id="365" href="Relation.Nullary.html" class="Module">Relation.Nullary</a>
<a id="382" class="Keyword">open</a> <a id="387" class="Keyword">import</a> <a id="394" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
<a id="408" class="Keyword">open</a> <a id="413" class="Keyword">import</a> <a id="420" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="436" class="Keyword">hiding</a> <a id="443" class="Symbol">(</a><a id="444" href="Data.Maybe.Base.html#2071" class="Function">map</a> <a id="448" class="Symbol">;</a> <a id="450" href="Data.Maybe.Base.html#1662" class="Function">fromMaybe</a><a id="459" class="Symbol">)</a>
<a id="461" class="Keyword">open</a> <a id="466" class="Keyword">import</a> <a id="473" href="Data.Product.html" class="Module">Data.Product</a> <a id="486" class="Symbol">as</a> <a id="489" class="Module">P</a> <a id="491" class="Keyword">hiding</a> <a id="498" class="Symbol">(</a><a id="499" href="Data.Product.html#2154" class="Function">map</a> <a id="503" class="Symbol">;</a> <a id="505" href="Data.Product.html#2563" class="Function">zip</a><a id="508" class="Symbol">)</a>
<a id="510" class="Keyword">open</a> <a id="515" class="Keyword">import</a> <a id="522" href="Data.Sum.html" class="Module">Data.Sum</a> <a id="531" class="Symbol">as</a> <a id="534" class="Module">S</a> <a id="536" class="Keyword">hiding</a> <a id="543" class="Symbol">(</a><a id="544" href="Data.Sum.Base.html#1143" class="Function">map</a><a id="547" class="Symbol">)</a>
<a id="549" class="Keyword">open</a> <a id="554" class="Keyword">import</a> <a id="561" href="Data.These.html" class="Module">Data.These</a> <a id="572" class="Symbol">as</a> <a id="575" class="Module">T</a> <a id="577" class="Keyword">hiding</a> <a id="584" class="Symbol">(</a><a id="585" href="Data.These.html#393" class="Function">map</a><a id="588" class="Symbol">)</a>
<a id="590" class="Keyword">open</a> <a id="595" class="Keyword">import</a> <a id="602" href="Function.html" class="Module">Function</a>

<a id="612" class="Comment">------------------------------------------------------------------------</a>
<a id="685" class="Comment">-- Definition</a>

<a id="700" class="Keyword">data</a> <a id="Delay"></a><a id="705" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="711" class="Symbol">{</a><a id="712" href="Codata.Delay.html#712" class="Bound">ℓ</a><a id="713" class="Symbol">}</a> <a id="715" class="Symbol">(</a><a id="716" href="Codata.Delay.html#716" class="Bound">A</a> <a id="718" class="Symbol">:</a> <a id="720" class="PrimitiveType">Set</a> <a id="724" href="Codata.Delay.html#712" class="Bound">ℓ</a><a id="725" class="Symbol">)</a> <a id="727" class="Symbol">(</a><a id="728" href="Codata.Delay.html#728" class="Bound">i</a> <a id="730" class="Symbol">:</a> <a id="732" href="Agda.Builtin.Size.html#114" class="Postulate">Size</a><a id="736" class="Symbol">)</a> <a id="738" class="Symbol">:</a> <a id="740" class="PrimitiveType">Set</a> <a id="744" href="Codata.Delay.html#712" class="Bound">ℓ</a> <a id="746" class="Keyword">where</a>
  <a id="Delay.now"></a><a id="754" href="Codata.Delay.html#754" class="InductiveConstructor">now</a>   <a id="760" class="Symbol">:</a> <a id="762" href="Codata.Delay.html#716" class="Bound">A</a> <a id="764" class="Symbol">→</a> <a id="766" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="772" href="Codata.Delay.html#716" class="Bound">A</a> <a id="774" href="Codata.Delay.html#728" class="Bound">i</a>
  <a id="Delay.later"></a><a id="778" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="784" class="Symbol">:</a> <a id="786" href="Codata.Thunk.html#301" class="Record">Thunk</a> <a id="792" class="Symbol">(</a><a id="793" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="799" href="Codata.Delay.html#716" class="Bound">A</a><a id="800" class="Symbol">)</a> <a id="802" href="Codata.Delay.html#728" class="Bound">i</a> <a id="804" class="Symbol">→</a> <a id="806" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="812" href="Codata.Delay.html#716" class="Bound">A</a> <a id="814" href="Codata.Delay.html#728" class="Bound">i</a>

<a id="817" class="Keyword">module</a> <a id="824" href="Codata.Delay.html#824" class="Module">_</a> <a id="826" class="Symbol">{</a><a id="827" href="Codata.Delay.html#827" class="Bound">ℓ</a><a id="828" class="Symbol">}</a> <a id="830" class="Symbol">{</a><a id="831" href="Codata.Delay.html#831" class="Bound">A</a> <a id="833" class="Symbol">:</a> <a id="835" class="PrimitiveType">Set</a> <a id="839" href="Codata.Delay.html#827" class="Bound">ℓ</a><a id="840" class="Symbol">}</a> <a id="842" class="Keyword">where</a>

 <a id="850" href="Codata.Delay.html#850" class="Function">length</a> <a id="857" class="Symbol">:</a> <a id="859" class="Symbol">∀</a> <a id="861" class="Symbol">{</a><a id="862" href="Codata.Delay.html#862" class="Bound">i</a><a id="863" class="Symbol">}</a> <a id="865" class="Symbol">→</a> <a id="867" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="873" href="Codata.Delay.html#831" class="Bound">A</a> <a id="875" href="Codata.Delay.html#862" class="Bound">i</a> <a id="877" class="Symbol">→</a> <a id="879" href="Codata.Conat.html#477" class="Datatype">Conat</a> <a id="885" href="Codata.Delay.html#862" class="Bound">i</a>
 <a id="888" href="Codata.Delay.html#850" class="Function">length</a> <a id="895" class="Symbol">(</a><a id="896" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="900" class="Symbol">_)</a>   <a id="905" class="Symbol">=</a> <a id="907" href="Codata.Conat.html#508" class="InductiveConstructor">zero</a>
 <a id="913" href="Codata.Delay.html#850" class="Function">length</a> <a id="920" class="Symbol">(</a><a id="921" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="927" href="Codata.Delay.html#927" class="Bound">d</a><a id="928" class="Symbol">)</a> <a id="930" class="Symbol">=</a> <a id="932" href="Codata.Conat.html#525" class="InductiveConstructor">suc</a> <a id="936" class="Symbol">λ</a> <a id="938" class="Keyword">where</a> <a id="944" class="Symbol">.</a><a id="945" href="Codata.Thunk.html#377" class="Field">force</a> <a id="951" class="Symbol">→</a> <a id="953" href="Codata.Delay.html#850" class="Function">length</a> <a id="960" class="Symbol">(</a><a id="961" href="Codata.Delay.html#927" class="Bound">d</a> <a id="963" class="Symbol">.</a><a id="964" href="Codata.Thunk.html#377" class="Field">force</a><a id="969" class="Symbol">)</a>

 <a id="973" href="Codata.Delay.html#973" class="Function">never</a> <a id="979" class="Symbol">:</a> <a id="981" class="Symbol">∀</a> <a id="983" class="Symbol">{</a><a id="984" href="Codata.Delay.html#984" class="Bound">i</a><a id="985" class="Symbol">}</a> <a id="987" class="Symbol">→</a> <a id="989" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="995" href="Codata.Delay.html#831" class="Bound">A</a> <a id="997" href="Codata.Delay.html#984" class="Bound">i</a>
 <a id="1000" href="Codata.Delay.html#973" class="Function">never</a> <a id="1006" class="Symbol">=</a> <a id="1008" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="1014" class="Symbol">λ</a> <a id="1016" class="Keyword">where</a> <a id="1022" class="Symbol">.</a><a id="1023" href="Codata.Thunk.html#377" class="Field">force</a> <a id="1029" class="Symbol">→</a> <a id="1031" href="Codata.Delay.html#973" class="Function">never</a>

 <a id="1039" href="Codata.Delay.html#1039" class="Function">fromMaybe</a> <a id="1049" class="Symbol">:</a> <a id="1051" href="Data.Maybe.Base.html#335" class="Datatype">Maybe</a> <a id="1057" href="Codata.Delay.html#831" class="Bound">A</a> <a id="1059" class="Symbol">→</a> <a id="1061" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1067" href="Codata.Delay.html#831" class="Bound">A</a> <a id="1069" href="Agda.Builtin.Size.html#210" class="Postulate">∞</a>
 <a id="1072" href="Codata.Delay.html#1039" class="Function">fromMaybe</a> <a id="1082" class="Symbol">=</a> <a id="1084" href="Data.Maybe.Base.html#1352" class="Function">maybe</a> <a id="1090" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="1094" href="Codata.Delay.html#973" class="Function">never</a>

 <a id="1102" href="Codata.Delay.html#1102" class="Function">runFor</a> <a id="1109" class="Symbol">:</a> <a id="1111" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a> <a id="1113" class="Symbol">→</a> <a id="1115" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1121" href="Codata.Delay.html#831" class="Bound">A</a> <a id="1123" href="Agda.Builtin.Size.html#210" class="Postulate">∞</a> <a id="1125" class="Symbol">→</a> <a id="1127" href="Data.Maybe.Base.html#335" class="Datatype">Maybe</a> <a id="1133" href="Codata.Delay.html#831" class="Bound">A</a>
 <a id="1136" href="Codata.Delay.html#1102" class="Function">runFor</a> <a id="1143" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>    <a id="1151" href="Codata.Delay.html#1151" class="Bound">d</a>         <a id="1161" class="Symbol">=</a> <a id="1163" href="Data.Maybe.Base.html#403" class="InductiveConstructor">nothing</a>
 <a id="1172" href="Codata.Delay.html#1102" class="Function">runFor</a> <a id="1179" class="Symbol">(</a><a id="1180" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1184" href="Codata.Delay.html#1184" class="Bound">n</a><a id="1185" class="Symbol">)</a> <a id="1187" class="Symbol">(</a><a id="1188" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="1192" href="Codata.Delay.html#1192" class="Bound">a</a><a id="1193" class="Symbol">)</a>   <a id="1197" class="Symbol">=</a> <a id="1199" href="Data.Maybe.Base.html#373" class="InductiveConstructor">just</a> <a id="1204" href="Codata.Delay.html#1192" class="Bound">a</a>
 <a id="1207" href="Codata.Delay.html#1102" class="Function">runFor</a> <a id="1214" class="Symbol">(</a><a id="1215" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1219" href="Codata.Delay.html#1219" class="Bound">n</a><a id="1220" class="Symbol">)</a> <a id="1222" class="Symbol">(</a><a id="1223" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="1229" href="Codata.Delay.html#1229" class="Bound">d</a><a id="1230" class="Symbol">)</a> <a id="1232" class="Symbol">=</a> <a id="1234" href="Codata.Delay.html#1102" class="Function">runFor</a> <a id="1241" href="Codata.Delay.html#1219" class="Bound">n</a> <a id="1243" class="Symbol">(</a><a id="1244" href="Codata.Delay.html#1229" class="Bound">d</a> <a id="1246" class="Symbol">.</a><a id="1247" href="Codata.Thunk.html#377" class="Field">force</a><a id="1252" class="Symbol">)</a>

<a id="1255" class="Keyword">module</a> <a id="1262" href="Codata.Delay.html#1262" class="Module">_</a> <a id="1264" class="Symbol">{</a><a id="1265" href="Codata.Delay.html#1265" class="Bound">ℓ</a> <a id="1267" href="Codata.Delay.html#1267" class="Bound">ℓ′</a><a id="1269" class="Symbol">}</a> <a id="1271" class="Symbol">{</a><a id="1272" href="Codata.Delay.html#1272" class="Bound">A</a> <a id="1274" class="Symbol">:</a> <a id="1276" class="PrimitiveType">Set</a> <a id="1280" href="Codata.Delay.html#1265" class="Bound">ℓ</a><a id="1281" class="Symbol">}</a> <a id="1283" class="Symbol">{</a><a id="1284" href="Codata.Delay.html#1284" class="Bound">B</a> <a id="1286" class="Symbol">:</a> <a id="1288" class="PrimitiveType">Set</a> <a id="1292" href="Codata.Delay.html#1267" class="Bound">ℓ′</a><a id="1294" class="Symbol">}</a> <a id="1296" class="Keyword">where</a>

 <a id="1304" href="Codata.Delay.html#1304" class="Function">map</a> <a id="1308" class="Symbol">:</a> <a id="1310" class="Symbol">(</a><a id="1311" href="Codata.Delay.html#1272" class="Bound">A</a> <a id="1313" class="Symbol">→</a> <a id="1315" href="Codata.Delay.html#1284" class="Bound">B</a><a id="1316" class="Symbol">)</a> <a id="1318" class="Symbol">→</a> <a id="1320" class="Symbol">∀</a> <a id="1322" class="Symbol">{</a><a id="1323" href="Codata.Delay.html#1323" class="Bound">i</a><a id="1324" class="Symbol">}</a> <a id="1326" class="Symbol">→</a> <a id="1328" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1334" href="Codata.Delay.html#1272" class="Bound">A</a> <a id="1336" href="Codata.Delay.html#1323" class="Bound">i</a> <a id="1338" class="Symbol">→</a> <a id="1340" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1346" href="Codata.Delay.html#1284" class="Bound">B</a> <a id="1348" href="Codata.Delay.html#1323" class="Bound">i</a>
 <a id="1351" href="Codata.Delay.html#1304" class="Function">map</a> <a id="1355" href="Codata.Delay.html#1355" class="Bound">f</a> <a id="1357" class="Symbol">(</a><a id="1358" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="1362" href="Codata.Delay.html#1362" class="Bound">a</a><a id="1363" class="Symbol">)</a>   <a id="1367" class="Symbol">=</a> <a id="1369" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="1373" class="Symbol">(</a><a id="1374" href="Codata.Delay.html#1355" class="Bound">f</a> <a id="1376" href="Codata.Delay.html#1362" class="Bound">a</a><a id="1377" class="Symbol">)</a>
 <a id="1380" href="Codata.Delay.html#1304" class="Function">map</a> <a id="1384" href="Codata.Delay.html#1384" class="Bound">f</a> <a id="1386" class="Symbol">(</a><a id="1387" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="1393" href="Codata.Delay.html#1393" class="Bound">d</a><a id="1394" class="Symbol">)</a> <a id="1396" class="Symbol">=</a> <a id="1398" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="1404" class="Symbol">λ</a> <a id="1406" class="Keyword">where</a> <a id="1412" class="Symbol">.</a><a id="1413" href="Codata.Thunk.html#377" class="Field">force</a> <a id="1419" class="Symbol">→</a> <a id="1421" href="Codata.Delay.html#1304" class="Function">map</a> <a id="1425" href="Codata.Delay.html#1384" class="Bound">f</a> <a id="1427" class="Symbol">(</a><a id="1428" href="Codata.Delay.html#1393" class="Bound">d</a> <a id="1430" class="Symbol">.</a><a id="1431" href="Codata.Thunk.html#377" class="Field">force</a><a id="1436" class="Symbol">)</a>

 <a id="1440" href="Codata.Delay.html#1440" class="Function">bind</a> <a id="1445" class="Symbol">:</a> <a id="1447" class="Symbol">∀</a> <a id="1449" class="Symbol">{</a><a id="1450" href="Codata.Delay.html#1450" class="Bound">i</a><a id="1451" class="Symbol">}</a> <a id="1453" class="Symbol">→</a> <a id="1455" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1461" href="Codata.Delay.html#1272" class="Bound">A</a> <a id="1463" href="Codata.Delay.html#1450" class="Bound">i</a> <a id="1465" class="Symbol">→</a> <a id="1467" class="Symbol">(</a><a id="1468" href="Codata.Delay.html#1272" class="Bound">A</a> <a id="1470" class="Symbol">→</a> <a id="1472" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1478" href="Codata.Delay.html#1284" class="Bound">B</a> <a id="1480" href="Codata.Delay.html#1450" class="Bound">i</a><a id="1481" class="Symbol">)</a> <a id="1483" class="Symbol">→</a> <a id="1485" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1491" href="Codata.Delay.html#1284" class="Bound">B</a> <a id="1493" href="Codata.Delay.html#1450" class="Bound">i</a>
 <a id="1496" href="Codata.Delay.html#1440" class="Function">bind</a> <a id="1501" class="Symbol">(</a><a id="1502" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="1506" href="Codata.Delay.html#1506" class="Bound">a</a><a id="1507" class="Symbol">)</a>   <a id="1511" href="Codata.Delay.html#1511" class="Bound">f</a> <a id="1513" class="Symbol">=</a> <a id="1515" href="Codata.Delay.html#1511" class="Bound">f</a> <a id="1517" href="Codata.Delay.html#1506" class="Bound">a</a>
 <a id="1520" href="Codata.Delay.html#1440" class="Function">bind</a> <a id="1525" class="Symbol">(</a><a id="1526" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="1532" href="Codata.Delay.html#1532" class="Bound">d</a><a id="1533" class="Symbol">)</a> <a id="1535" href="Codata.Delay.html#1535" class="Bound">f</a> <a id="1537" class="Symbol">=</a> <a id="1539" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="1545" class="Symbol">λ</a> <a id="1547" class="Keyword">where</a> <a id="1553" class="Symbol">.</a><a id="1554" href="Codata.Thunk.html#377" class="Field">force</a> <a id="1560" class="Symbol">→</a> <a id="1562" href="Codata.Delay.html#1440" class="Function">bind</a> <a id="1567" class="Symbol">(</a><a id="1568" href="Codata.Delay.html#1532" class="Bound">d</a> <a id="1570" class="Symbol">.</a><a id="1571" href="Codata.Thunk.html#377" class="Field">force</a><a id="1576" class="Symbol">)</a> <a id="1578" href="Codata.Delay.html#1535" class="Bound">f</a>

 <a id="1582" href="Codata.Delay.html#1582" class="Function">unfold</a> <a id="1589" class="Symbol">:</a> <a id="1591" class="Symbol">(</a><a id="1592" href="Codata.Delay.html#1272" class="Bound">A</a> <a id="1594" class="Symbol">→</a> <a id="1596" href="Codata.Delay.html#1272" class="Bound">A</a> <a id="1598" href="Data.Sum.Base.html#419" class="Datatype Operator">⊎</a> <a id="1600" href="Codata.Delay.html#1284" class="Bound">B</a><a id="1601" class="Symbol">)</a> <a id="1603" class="Symbol">→</a> <a id="1605" href="Codata.Delay.html#1272" class="Bound">A</a> <a id="1607" class="Symbol">→</a> <a id="1609" class="Symbol">∀</a> <a id="1611" class="Symbol">{</a><a id="1612" href="Codata.Delay.html#1612" class="Bound">i</a><a id="1613" class="Symbol">}</a> <a id="1615" class="Symbol">→</a> <a id="1617" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1623" href="Codata.Delay.html#1284" class="Bound">B</a> <a id="1625" href="Codata.Delay.html#1612" class="Bound">i</a>
 <a id="1628" href="Codata.Delay.html#1582" class="Function">unfold</a> <a id="1635" href="Codata.Delay.html#1635" class="Bound">next</a> <a id="1640" href="Codata.Delay.html#1640" class="Bound">seed</a> <a id="1645" class="Keyword">with</a> <a id="1650" href="Codata.Delay.html#1635" class="Bound">next</a> <a id="1655" href="Codata.Delay.html#1640" class="Bound">seed</a>
 <a id="1661" class="Symbol">...</a> <a id="1665" class="Symbol">|</a> <a id="1667" href="Data.Sum.Base.html#475" class="InductiveConstructor">inj₁</a> <a id="1672" href="Codata.Delay.html#1672" class="Bound">seed′</a> <a id="1678" class="Symbol">=</a> <a id="1680" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="1686" class="Symbol">λ</a> <a id="1688" class="Keyword">where</a> <a id="1694" class="Symbol">.</a><a id="1695" href="Codata.Thunk.html#377" class="Field">force</a> <a id="1701" class="Symbol">→</a> <a id="1703" href="Codata.Delay.html#1582" class="Function">unfold</a> <a id="1710" class="Bound">next</a> <a id="1715" href="Codata.Delay.html#1672" class="Bound">seed′</a>
 <a id="1722" class="Symbol">...</a> <a id="1726" class="Symbol">|</a> <a id="1728" href="Data.Sum.Base.html#500" class="InductiveConstructor">inj₂</a> <a id="1733" href="Codata.Delay.html#1733" class="Bound">b</a>     <a id="1739" class="Symbol">=</a> <a id="1741" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="1745" href="Codata.Delay.html#1733" class="Bound">b</a>

<a id="1748" class="Keyword">module</a> <a id="1755" href="Codata.Delay.html#1755" class="Module">_</a> <a id="1757" class="Symbol">{</a><a id="1758" href="Codata.Delay.html#1758" class="Bound">a</a> <a id="1760" href="Codata.Delay.html#1760" class="Bound">b</a> <a id="1762" href="Codata.Delay.html#1762" class="Bound">c</a><a id="1763" class="Symbol">}</a> <a id="1765" class="Symbol">{</a><a id="1766" href="Codata.Delay.html#1766" class="Bound">A</a> <a id="1768" class="Symbol">:</a> <a id="1770" class="PrimitiveType">Set</a> <a id="1774" href="Codata.Delay.html#1758" class="Bound">a</a><a id="1775" class="Symbol">}</a> <a id="1777" class="Symbol">{</a><a id="1778" href="Codata.Delay.html#1778" class="Bound">B</a> <a id="1780" class="Symbol">:</a> <a id="1782" class="PrimitiveType">Set</a> <a id="1786" href="Codata.Delay.html#1760" class="Bound">b</a><a id="1787" class="Symbol">}</a> <a id="1789" class="Symbol">{</a><a id="1790" href="Codata.Delay.html#1790" class="Bound">C</a> <a id="1792" class="Symbol">:</a> <a id="1794" class="PrimitiveType">Set</a> <a id="1798" href="Codata.Delay.html#1762" class="Bound">c</a><a id="1799" class="Symbol">}</a> <a id="1801" class="Keyword">where</a>

 <a id="1809" href="Codata.Delay.html#1809" class="Function">zipWith</a> <a id="1817" class="Symbol">:</a> <a id="1819" class="Symbol">(</a><a id="1820" href="Codata.Delay.html#1766" class="Bound">A</a> <a id="1822" class="Symbol">→</a> <a id="1824" href="Codata.Delay.html#1778" class="Bound">B</a> <a id="1826" class="Symbol">→</a> <a id="1828" href="Codata.Delay.html#1790" class="Bound">C</a><a id="1829" class="Symbol">)</a> <a id="1831" class="Symbol">→</a> <a id="1833" class="Symbol">∀</a> <a id="1835" class="Symbol">{</a><a id="1836" href="Codata.Delay.html#1836" class="Bound">i</a><a id="1837" class="Symbol">}</a> <a id="1839" class="Symbol">→</a> <a id="1841" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1847" href="Codata.Delay.html#1766" class="Bound">A</a> <a id="1849" href="Codata.Delay.html#1836" class="Bound">i</a> <a id="1851" class="Symbol">→</a> <a id="1853" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1859" href="Codata.Delay.html#1778" class="Bound">B</a> <a id="1861" href="Codata.Delay.html#1836" class="Bound">i</a> <a id="1863" class="Symbol">→</a> <a id="1865" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="1871" href="Codata.Delay.html#1790" class="Bound">C</a> <a id="1873" href="Codata.Delay.html#1836" class="Bound">i</a>
 <a id="1876" href="Codata.Delay.html#1809" class="Function">zipWith</a> <a id="1884" href="Codata.Delay.html#1884" class="Bound">f</a> <a id="1886" class="Symbol">(</a><a id="1887" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="1891" href="Codata.Delay.html#1891" class="Bound">a</a><a id="1892" class="Symbol">)</a>   <a id="1896" href="Codata.Delay.html#1896" class="Bound">d</a>         <a id="1906" class="Symbol">=</a> <a id="1908" href="Codata.Delay.html#1304" class="Function">map</a> <a id="1912" class="Symbol">(</a><a id="1913" href="Codata.Delay.html#1884" class="Bound">f</a> <a id="1915" href="Codata.Delay.html#1891" class="Bound">a</a><a id="1916" class="Symbol">)</a> <a id="1918" href="Codata.Delay.html#1896" class="Bound">d</a>
 <a id="1921" href="Codata.Delay.html#1809" class="CatchallClause Function">zipWith</a><a id="1928" class="CatchallClause"> </a><a id="1929" href="Codata.Delay.html#1929" class="CatchallClause Bound">f</a><a id="1930" class="CatchallClause"> </a><a id="1931" href="Codata.Delay.html#1931" class="CatchallClause Bound">d</a><a id="1932" class="CatchallClause">         </a><a id="1941" class="CatchallClause Symbol">(</a><a id="1942" href="Codata.Delay.html#754" class="CatchallClause InductiveConstructor">now</a><a id="1945" class="CatchallClause"> </a><a id="1946" href="Codata.Delay.html#1946" class="CatchallClause Bound">b</a><a id="1947" class="CatchallClause Symbol">)</a>   <a id="1951" class="Symbol">=</a> <a id="1953" href="Codata.Delay.html#1304" class="Function">map</a> <a id="1957" class="Symbol">(λ</a> <a id="1960" href="Codata.Delay.html#1960" class="Bound">a</a> <a id="1962" class="Symbol">→</a> <a id="1964" href="Codata.Delay.html#1929" class="Bound">f</a> <a id="1966" href="Codata.Delay.html#1960" class="Bound">a</a> <a id="1968" href="Codata.Delay.html#1946" class="Bound">b</a><a id="1969" class="Symbol">)</a> <a id="1971" href="Codata.Delay.html#1931" class="Bound">d</a>
 <a id="1974" href="Codata.Delay.html#1809" class="Function">zipWith</a> <a id="1982" href="Codata.Delay.html#1982" class="Bound">f</a> <a id="1984" class="Symbol">(</a><a id="1985" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="1991" href="Codata.Delay.html#1991" class="Bound">a</a><a id="1992" class="Symbol">)</a> <a id="1994" class="Symbol">(</a><a id="1995" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="2001" href="Codata.Delay.html#2001" class="Bound">b</a><a id="2002" class="Symbol">)</a> <a id="2004" class="Symbol">=</a> <a id="2006" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="2012" class="Symbol">λ</a> <a id="2014" class="Keyword">where</a> <a id="2020" class="Symbol">.</a><a id="2021" href="Codata.Thunk.html#377" class="Field">force</a> <a id="2027" class="Symbol">→</a> <a id="2029" href="Codata.Delay.html#1809" class="Function">zipWith</a> <a id="2037" href="Codata.Delay.html#1982" class="Bound">f</a> <a id="2039" class="Symbol">(</a><a id="2040" href="Codata.Delay.html#1991" class="Bound">a</a> <a id="2042" class="Symbol">.</a><a id="2043" href="Codata.Thunk.html#377" class="Field">force</a><a id="2048" class="Symbol">)</a> <a id="2050" class="Symbol">(</a><a id="2051" href="Codata.Delay.html#2001" class="Bound">b</a> <a id="2053" class="Symbol">.</a><a id="2054" href="Codata.Thunk.html#377" class="Field">force</a><a id="2059" class="Symbol">)</a>

 <a id="2063" href="Codata.Delay.html#2063" class="Function">alignWith</a> <a id="2073" class="Symbol">:</a> <a id="2075" class="Symbol">(</a><a id="2076" href="Data.These.html#260" class="Datatype">These</a> <a id="2082" href="Codata.Delay.html#1766" class="Bound">A</a> <a id="2084" href="Codata.Delay.html#1778" class="Bound">B</a> <a id="2086" class="Symbol">→</a> <a id="2088" href="Codata.Delay.html#1790" class="Bound">C</a><a id="2089" class="Symbol">)</a> <a id="2091" class="Symbol">→</a> <a id="2093" class="Symbol">∀</a> <a id="2095" class="Symbol">{</a><a id="2096" href="Codata.Delay.html#2096" class="Bound">i</a><a id="2097" class="Symbol">}</a> <a id="2099" class="Symbol">→</a> <a id="2101" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2107" href="Codata.Delay.html#1766" class="Bound">A</a> <a id="2109" href="Codata.Delay.html#2096" class="Bound">i</a> <a id="2111" class="Symbol">→</a> <a id="2113" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2119" href="Codata.Delay.html#1778" class="Bound">B</a> <a id="2121" href="Codata.Delay.html#2096" class="Bound">i</a> <a id="2123" class="Symbol">→</a> <a id="2125" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2131" href="Codata.Delay.html#1790" class="Bound">C</a> <a id="2133" href="Codata.Delay.html#2096" class="Bound">i</a>
 <a id="2136" href="Codata.Delay.html#2063" class="Function">alignWith</a> <a id="2146" href="Codata.Delay.html#2146" class="Bound">f</a> <a id="2148" class="Symbol">(</a><a id="2149" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="2153" href="Codata.Delay.html#2153" class="Bound">a</a><a id="2154" class="Symbol">)</a>   <a id="2158" class="Symbol">(</a><a id="2159" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="2163" href="Codata.Delay.html#2163" class="Bound">b</a><a id="2164" class="Symbol">)</a>   <a id="2168" class="Symbol">=</a> <a id="2170" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="2174" class="Symbol">(</a><a id="2175" href="Codata.Delay.html#2146" class="Bound">f</a> <a id="2177" class="Symbol">(</a><a id="2178" href="Data.These.html#366" class="InductiveConstructor">these</a> <a id="2184" href="Codata.Delay.html#2153" class="Bound">a</a> <a id="2186" href="Codata.Delay.html#2163" class="Bound">b</a><a id="2187" class="Symbol">))</a>
 <a id="2191" href="Codata.Delay.html#2063" class="Function">alignWith</a> <a id="2201" href="Codata.Delay.html#2201" class="Bound">f</a> <a id="2203" class="Symbol">(</a><a id="2204" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="2208" href="Codata.Delay.html#2208" class="Bound">a</a><a id="2209" class="Symbol">)</a>   <a id="2213" class="Symbol">(</a><a id="2214" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="2220" class="Symbol">_)</a> <a id="2223" class="Symbol">=</a> <a id="2225" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="2229" class="Symbol">(</a><a id="2230" href="Codata.Delay.html#2201" class="Bound">f</a> <a id="2232" class="Symbol">(</a><a id="2233" href="Data.These.html#318" class="InductiveConstructor">this</a> <a id="2238" href="Codata.Delay.html#2208" class="Bound">a</a><a id="2239" class="Symbol">))</a>
 <a id="2243" href="Codata.Delay.html#2063" class="Function">alignWith</a> <a id="2253" href="Codata.Delay.html#2253" class="Bound">f</a> <a id="2255" class="Symbol">(</a><a id="2256" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="2262" class="Symbol">_)</a> <a id="2265" class="Symbol">(</a><a id="2266" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="2270" href="Codata.Delay.html#2270" class="Bound">b</a><a id="2271" class="Symbol">)</a>   <a id="2275" class="Symbol">=</a> <a id="2277" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="2281" class="Symbol">(</a><a id="2282" href="Codata.Delay.html#2253" class="Bound">f</a> <a id="2284" class="Symbol">(</a><a id="2285" href="Data.These.html#342" class="InductiveConstructor">that</a> <a id="2290" href="Codata.Delay.html#2270" class="Bound">b</a><a id="2291" class="Symbol">))</a>
 <a id="2295" href="Codata.Delay.html#2063" class="Function">alignWith</a> <a id="2305" href="Codata.Delay.html#2305" class="Bound">f</a> <a id="2307" class="Symbol">(</a><a id="2308" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="2314" href="Codata.Delay.html#2314" class="Bound">a</a><a id="2315" class="Symbol">)</a> <a id="2317" class="Symbol">(</a><a id="2318" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="2324" href="Codata.Delay.html#2324" class="Bound">b</a><a id="2325" class="Symbol">)</a> <a id="2327" class="Symbol">=</a> <a id="2329" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="2335" class="Symbol">λ</a> <a id="2337" class="Keyword">where</a>
   <a id="2346" class="Symbol">.</a><a id="2347" href="Codata.Thunk.html#377" class="Field">force</a> <a id="2353" class="Symbol">→</a> <a id="2355" href="Codata.Delay.html#2063" class="Function">alignWith</a> <a id="2365" href="Codata.Delay.html#2305" class="Bound">f</a> <a id="2367" class="Symbol">(</a><a id="2368" href="Codata.Delay.html#2314" class="Bound">a</a> <a id="2370" class="Symbol">.</a><a id="2371" href="Codata.Thunk.html#377" class="Field">force</a><a id="2376" class="Symbol">)</a> <a id="2378" class="Symbol">(</a><a id="2379" href="Codata.Delay.html#2324" class="Bound">b</a> <a id="2381" class="Symbol">.</a><a id="2382" href="Codata.Thunk.html#377" class="Field">force</a><a id="2387" class="Symbol">)</a>

<a id="2390" class="Keyword">module</a> <a id="2397" href="Codata.Delay.html#2397" class="Module">_</a> <a id="2399" class="Symbol">{</a><a id="2400" href="Codata.Delay.html#2400" class="Bound">a</a> <a id="2402" href="Codata.Delay.html#2402" class="Bound">b</a><a id="2403" class="Symbol">}</a> <a id="2405" class="Symbol">{</a><a id="2406" href="Codata.Delay.html#2406" class="Bound">A</a> <a id="2408" class="Symbol">:</a> <a id="2410" class="PrimitiveType">Set</a> <a id="2414" href="Codata.Delay.html#2400" class="Bound">a</a><a id="2415" class="Symbol">}</a> <a id="2417" class="Symbol">{</a><a id="2418" href="Codata.Delay.html#2418" class="Bound">B</a> <a id="2420" class="Symbol">:</a> <a id="2422" class="PrimitiveType">Set</a> <a id="2426" href="Codata.Delay.html#2402" class="Bound">b</a><a id="2427" class="Symbol">}</a> <a id="2429" class="Keyword">where</a>

  <a id="2438" href="Codata.Delay.html#2438" class="Function">zip</a> <a id="2442" class="Symbol">:</a> <a id="2444" class="Symbol">∀</a> <a id="2446" class="Symbol">{</a><a id="2447" href="Codata.Delay.html#2447" class="Bound">i</a><a id="2448" class="Symbol">}</a> <a id="2450" class="Symbol">→</a> <a id="2452" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2458" href="Codata.Delay.html#2406" class="Bound">A</a> <a id="2460" href="Codata.Delay.html#2447" class="Bound">i</a> <a id="2462" class="Symbol">→</a> <a id="2464" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2470" href="Codata.Delay.html#2418" class="Bound">B</a> <a id="2472" href="Codata.Delay.html#2447" class="Bound">i</a> <a id="2474" class="Symbol">→</a> <a id="2476" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2482" class="Symbol">(</a><a id="2483" href="Codata.Delay.html#2406" class="Bound">A</a> <a id="2485" href="Data.Product.html#1353" class="Function Operator">×</a> <a id="2487" href="Codata.Delay.html#2418" class="Bound">B</a><a id="2488" class="Symbol">)</a> <a id="2490" href="Codata.Delay.html#2447" class="Bound">i</a>
  <a id="2494" href="Codata.Delay.html#2438" class="Function">zip</a>   <a id="2500" class="Symbol">=</a> <a id="2502" href="Codata.Delay.html#1809" class="Function">zipWith</a> <a id="2510" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">_,_</a>

  <a id="2517" href="Codata.Delay.html#2517" class="Function">align</a> <a id="2523" class="Symbol">:</a> <a id="2525" class="Symbol">∀</a> <a id="2527" class="Symbol">{</a><a id="2528" href="Codata.Delay.html#2528" class="Bound">i</a><a id="2529" class="Symbol">}</a> <a id="2531" class="Symbol">→</a> <a id="2533" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2539" href="Codata.Delay.html#2406" class="Bound">A</a> <a id="2541" href="Codata.Delay.html#2528" class="Bound">i</a> <a id="2543" class="Symbol">→</a> <a id="2545" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2551" href="Codata.Delay.html#2418" class="Bound">B</a> <a id="2553" href="Codata.Delay.html#2528" class="Bound">i</a> <a id="2555" class="Symbol">→</a> <a id="2557" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2563" class="Symbol">(</a><a id="2564" href="Data.These.html#260" class="Datatype">These</a> <a id="2570" href="Codata.Delay.html#2406" class="Bound">A</a> <a id="2572" href="Codata.Delay.html#2418" class="Bound">B</a><a id="2573" class="Symbol">)</a> <a id="2575" href="Codata.Delay.html#2528" class="Bound">i</a>
  <a id="2579" href="Codata.Delay.html#2517" class="Function">align</a> <a id="2585" class="Symbol">=</a> <a id="2587" href="Codata.Delay.html#2063" class="Function">alignWith</a> <a id="2597" href="Function.html#1068" class="Function">id</a>

<a id="2601" class="Comment">------------------------------------------------------------------------</a>
<a id="2674" class="Comment">-- Finite Delays</a>

<a id="2692" class="Keyword">module</a> <a id="2699" href="Codata.Delay.html#2699" class="Module">_</a> <a id="2701" class="Symbol">{</a><a id="2702" href="Codata.Delay.html#2702" class="Bound">ℓ</a><a id="2703" class="Symbol">}</a> <a id="2705" class="Symbol">{</a><a id="2706" href="Codata.Delay.html#2706" class="Bound">A</a> <a id="2708" class="Symbol">:</a> <a id="2710" class="PrimitiveType">Set</a> <a id="2714" href="Codata.Delay.html#2702" class="Bound">ℓ</a><a id="2715" class="Symbol">}</a> <a id="2717" class="Keyword">where</a>

 <a id="2725" class="Keyword">infix</a> <a id="2731" class="Number">3</a> <a id="2733" href="Codata.Delay.html#2742" class="Datatype Operator">_⇓</a>
 <a id="2737" class="Keyword">data</a> <a id="2742" href="Codata.Delay.html#2742" class="Datatype Operator">_⇓</a> <a id="2745" class="Symbol">:</a> <a id="2747" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="2753" href="Codata.Delay.html#2706" class="Bound">A</a> <a id="2755" href="Agda.Builtin.Size.html#210" class="Postulate">∞</a> <a id="2757" class="Symbol">→</a> <a id="2759" class="PrimitiveType">Set</a> <a id="2763" href="Codata.Delay.html#2702" class="Bound">ℓ</a> <a id="2765" class="Keyword">where</a>
   <a id="2774" href="Codata.Delay.html#2774" class="InductiveConstructor">now</a>   <a id="2780" class="Symbol">:</a> <a id="2782" class="Symbol">∀</a> <a id="2784" href="Codata.Delay.html#2784" class="Bound">a</a> <a id="2786" class="Symbol">→</a> <a id="2788" href="Codata.Delay.html#754" class="InductiveConstructor">now</a> <a id="2792" href="Codata.Delay.html#2784" class="Bound">a</a> <a id="2794" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a>
   <a id="2799" href="Codata.Delay.html#2799" class="InductiveConstructor">later</a> <a id="2805" class="Symbol">:</a> <a id="2807" class="Symbol">∀</a> <a id="2809" class="Symbol">{</a><a id="2810" href="Codata.Delay.html#2810" class="Bound">d</a><a id="2811" class="Symbol">}</a> <a id="2813" class="Symbol">→</a> <a id="2815" href="Codata.Delay.html#2810" class="Bound">d</a> <a id="2817" class="Symbol">.</a><a id="2818" href="Codata.Thunk.html#377" class="Field">force</a> <a id="2824" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a> <a id="2826" class="Symbol">→</a> <a id="2828" href="Codata.Delay.html#778" class="InductiveConstructor">later</a> <a id="2834" href="Codata.Delay.html#2810" class="Bound">d</a> <a id="2836" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a>

 <a id="2840" href="Codata.Delay.html#2840" class="Function">extract</a> <a id="2848" class="Symbol">:</a> <a id="2850" class="Symbol">∀</a> <a id="2852" class="Symbol">{</a><a id="2853" href="Codata.Delay.html#2853" class="Bound">d</a><a id="2854" class="Symbol">}</a> <a id="2856" class="Symbol">→</a> <a id="2858" href="Codata.Delay.html#2853" class="Bound">d</a> <a id="2860" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a> <a id="2862" class="Symbol">→</a> <a id="2864" href="Codata.Delay.html#2706" class="Bound">A</a>
 <a id="2867" href="Codata.Delay.html#2840" class="Function">extract</a> <a id="2875" class="Symbol">(</a><a id="2876" href="Codata.Delay.html#2774" class="InductiveConstructor">now</a> <a id="2880" href="Codata.Delay.html#2880" class="Bound">a</a><a id="2881" class="Symbol">)</a>   <a id="2885" class="Symbol">=</a> <a id="2887" href="Codata.Delay.html#2880" class="Bound">a</a>
 <a id="2890" href="Codata.Delay.html#2840" class="Function">extract</a> <a id="2898" class="Symbol">(</a><a id="2899" href="Codata.Delay.html#2799" class="InductiveConstructor">later</a> <a id="2905" href="Codata.Delay.html#2905" class="Bound">d</a><a id="2906" class="Symbol">)</a> <a id="2908" class="Symbol">=</a> <a id="2910" href="Codata.Delay.html#2840" class="Function">extract</a> <a id="2918" href="Codata.Delay.html#2905" class="Bound">d</a>

 <a id="2922" href="Codata.Delay.html#2922" class="Function">¬never⇓</a> <a id="2930" class="Symbol">:</a> <a id="2932" href="Relation.Nullary.html#464" class="Function Operator">¬</a> <a id="2934" class="Symbol">(</a><a id="2935" href="Codata.Delay.html#973" class="Function">never</a> <a id="2941" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a><a id="2942" class="Symbol">)</a>
 <a id="2945" href="Codata.Delay.html#2922" class="Function">¬never⇓</a> <a id="2953" class="Symbol">(</a><a id="2954" href="Codata.Delay.html#2799" class="InductiveConstructor">later</a> <a id="2960" href="Codata.Delay.html#2960" class="Bound">p</a><a id="2961" class="Symbol">)</a> <a id="2963" class="Symbol">=</a> <a id="2965" href="Codata.Delay.html#2922" class="Function">¬never⇓</a> <a id="2973" href="Codata.Delay.html#2960" class="Bound">p</a>

 <a id="2977" href="Codata.Delay.html#2977" class="Function">length-⇓</a> <a id="2986" class="Symbol">:</a> <a id="2988" class="Symbol">∀</a> <a id="2990" class="Symbol">{</a><a id="2991" href="Codata.Delay.html#2991" class="Bound">d</a><a id="2992" class="Symbol">}</a> <a id="2994" class="Symbol">→</a> <a id="2996" href="Codata.Delay.html#2991" class="Bound">d</a> <a id="2998" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a> <a id="3000" class="Symbol">→</a> <a id="3002" href="Codata.Conat.html#1874" class="Datatype">Finite</a> <a id="3009" class="Symbol">(</a><a id="3010" href="Codata.Delay.html#850" class="Function">length</a> <a id="3017" href="Codata.Delay.html#2991" class="Bound">d</a><a id="3018" class="Symbol">)</a>
 <a id="3021" href="Codata.Delay.html#2977" class="Function">length-⇓</a> <a id="3030" class="Symbol">(</a><a id="3031" href="Codata.Delay.html#2774" class="InductiveConstructor">now</a> <a id="3035" href="Codata.Delay.html#3035" class="Bound">a</a><a id="3036" class="Symbol">)</a>    <a id="3041" class="Symbol">=</a> <a id="3043" href="Codata.Conat.html#1905" class="InductiveConstructor">zero</a>
 <a id="3049" href="Codata.Delay.html#2977" class="Function">length-⇓</a> <a id="3058" class="Symbol">(</a><a id="3059" href="Codata.Delay.html#2799" class="InductiveConstructor">later</a> <a id="3065" href="Codata.Delay.html#3065" class="Bound">d⇓</a><a id="3067" class="Symbol">)</a> <a id="3069" class="Symbol">=</a> <a id="3071" href="Codata.Conat.html#1926" class="InductiveConstructor">suc</a> <a id="3075" class="Symbol">(</a><a id="3076" href="Codata.Delay.html#2977" class="Function">length-⇓</a> <a id="3085" href="Codata.Delay.html#3065" class="Bound">d⇓</a><a id="3087" class="Symbol">)</a>

<a id="3090" class="Keyword">module</a> <a id="3097" href="Codata.Delay.html#3097" class="Module">_</a> <a id="3099" class="Symbol">{</a><a id="3100" href="Codata.Delay.html#3100" class="Bound">ℓ</a> <a id="3102" href="Codata.Delay.html#3102" class="Bound">ℓ′</a><a id="3104" class="Symbol">}</a> <a id="3106" class="Symbol">{</a><a id="3107" href="Codata.Delay.html#3107" class="Bound">A</a> <a id="3109" class="Symbol">:</a> <a id="3111" class="PrimitiveType">Set</a> <a id="3115" href="Codata.Delay.html#3100" class="Bound">ℓ</a><a id="3116" class="Symbol">}</a> <a id="3118" class="Symbol">{</a><a id="3119" href="Codata.Delay.html#3119" class="Bound">B</a> <a id="3121" class="Symbol">:</a> <a id="3123" class="PrimitiveType">Set</a> <a id="3127" href="Codata.Delay.html#3102" class="Bound">ℓ′</a><a id="3129" class="Symbol">}</a> <a id="3131" class="Keyword">where</a>

 <a id="3139" href="Codata.Delay.html#3139" class="Function">map-⇓</a> <a id="3145" class="Symbol">:</a> <a id="3147" class="Symbol">∀</a> <a id="3149" class="Symbol">(</a><a id="3150" href="Codata.Delay.html#3150" class="Bound">f</a> <a id="3152" class="Symbol">:</a> <a id="3154" href="Codata.Delay.html#3107" class="Bound">A</a> <a id="3156" class="Symbol">→</a> <a id="3158" href="Codata.Delay.html#3119" class="Bound">B</a><a id="3159" class="Symbol">)</a> <a id="3161" class="Symbol">{</a><a id="3162" href="Codata.Delay.html#3162" class="Bound">d</a><a id="3163" class="Symbol">}</a> <a id="3165" class="Symbol">→</a> <a id="3167" href="Codata.Delay.html#3162" class="Bound">d</a> <a id="3169" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a> <a id="3171" class="Symbol">→</a> <a id="3173" href="Codata.Delay.html#1304" class="Function">map</a> <a id="3177" href="Codata.Delay.html#3150" class="Bound">f</a> <a id="3179" href="Codata.Delay.html#3162" class="Bound">d</a> <a id="3181" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a>
 <a id="3184" href="Codata.Delay.html#3139" class="Function">map-⇓</a> <a id="3190" href="Codata.Delay.html#3190" class="Bound">f</a> <a id="3192" class="Symbol">(</a><a id="3193" href="Codata.Delay.html#2774" class="InductiveConstructor">now</a> <a id="3197" href="Codata.Delay.html#3197" class="Bound">a</a><a id="3198" class="Symbol">)</a>   <a id="3202" class="Symbol">=</a> <a id="3204" href="Codata.Delay.html#2774" class="InductiveConstructor">now</a> <a id="3208" class="Symbol">(</a><a id="3209" href="Codata.Delay.html#3190" class="Bound">f</a> <a id="3211" href="Codata.Delay.html#3197" class="Bound">a</a><a id="3212" class="Symbol">)</a>
 <a id="3215" href="Codata.Delay.html#3139" class="Function">map-⇓</a> <a id="3221" href="Codata.Delay.html#3221" class="Bound">f</a> <a id="3223" class="Symbol">(</a><a id="3224" href="Codata.Delay.html#2799" class="InductiveConstructor">later</a> <a id="3230" href="Codata.Delay.html#3230" class="Bound">d</a><a id="3231" class="Symbol">)</a> <a id="3233" class="Symbol">=</a> <a id="3235" href="Codata.Delay.html#2799" class="InductiveConstructor">later</a> <a id="3241" class="Symbol">(</a><a id="3242" href="Codata.Delay.html#3139" class="Function">map-⇓</a> <a id="3248" href="Codata.Delay.html#3221" class="Bound">f</a> <a id="3250" href="Codata.Delay.html#3230" class="Bound">d</a><a id="3251" class="Symbol">)</a>

 <a id="3255" href="Codata.Delay.html#3255" class="Function">bind-⇓</a> <a id="3262" class="Symbol">:</a> <a id="3264" class="Symbol">∀</a> <a id="3266" class="Symbol">{</a><a id="3267" href="Codata.Delay.html#3267" class="Bound">m</a><a id="3268" class="Symbol">}</a> <a id="3270" class="Symbol">(</a><a id="3271" href="Codata.Delay.html#3271" class="Bound">m⇓</a> <a id="3274" class="Symbol">:</a> <a id="3276" href="Codata.Delay.html#3267" class="Bound">m</a> <a id="3278" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a><a id="3279" class="Symbol">)</a> <a id="3281" class="Symbol">{</a><a id="3282" href="Codata.Delay.html#3282" class="Bound">f</a> <a id="3284" class="Symbol">:</a> <a id="3286" href="Codata.Delay.html#3107" class="Bound">A</a> <a id="3288" class="Symbol">→</a> <a id="3290" href="Codata.Delay.html#705" class="Datatype">Delay</a> <a id="3296" href="Codata.Delay.html#3119" class="Bound">B</a> <a id="3298" href="Agda.Builtin.Size.html#210" class="Postulate">∞</a><a id="3299" class="Symbol">}</a> <a id="3301" class="Symbol">→</a> <a id="3303" href="Codata.Delay.html#3282" class="Bound">f</a> <a id="3305" class="Symbol">(</a><a id="3306" href="Codata.Delay.html#2840" class="Function">extract</a> <a id="3314" href="Codata.Delay.html#3271" class="Bound">m⇓</a><a id="3316" class="Symbol">)</a> <a id="3318" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a> <a id="3320" class="Symbol">→</a> <a id="3322" href="Codata.Delay.html#1440" class="Function">bind</a> <a id="3327" href="Codata.Delay.html#3267" class="Bound">m</a> <a id="3329" href="Codata.Delay.html#3282" class="Bound">f</a> <a id="3331" href="Codata.Delay.html#2742" class="Datatype Operator">⇓</a>
 <a id="3334" href="Codata.Delay.html#3255" class="Function">bind-⇓</a> <a id="3341" class="Symbol">(</a><a id="3342" href="Codata.Delay.html#2774" class="InductiveConstructor">now</a> <a id="3346" href="Codata.Delay.html#3346" class="Bound">a</a><a id="3347" class="Symbol">)</a>   <a id="3351" href="Codata.Delay.html#3351" class="Bound">fa⇓</a> <a id="3355" class="Symbol">=</a> <a id="3357" href="Codata.Delay.html#3351" class="Bound">fa⇓</a>
 <a id="3362" href="Codata.Delay.html#3255" class="Function">bind-⇓</a> <a id="3369" class="Symbol">(</a><a id="3370" href="Codata.Delay.html#2799" class="InductiveConstructor">later</a> <a id="3376" href="Codata.Delay.html#3376" class="Bound">p</a><a id="3377" class="Symbol">)</a> <a id="3379" href="Codata.Delay.html#3379" class="Bound">fa⇓</a> <a id="3383" class="Symbol">=</a> <a id="3385" href="Codata.Delay.html#2799" class="InductiveConstructor">later</a> <a id="3391" class="Symbol">(</a><a id="3392" href="Codata.Delay.html#3255" class="Function">bind-⇓</a> <a id="3399" href="Codata.Delay.html#3376" class="Bound">p</a> <a id="3401" href="Codata.Delay.html#3379" class="Bound">fa⇓</a><a id="3404" class="Symbol">)</a>
</pre></body></html>