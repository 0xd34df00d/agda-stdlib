<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Fin.Permutation.Components</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Component functions of permutations found in `Data.Fin.Permutation`</a>
<a id="177" class="Comment">------------------------------------------------------------------------</a>

<a id="251" class="Symbol">{-#</a> <a id="255" class="Keyword">OPTIONS</a> <a id="263" class="Pragma">--without-K</a> <a id="275" class="Pragma">--safe</a> <a id="282" class="Symbol">#-}</a>

<a id="287" class="Keyword">module</a> <a id="294" href="Data.Fin.Permutation.Components.html" class="Module">Data.Fin.Permutation.Components</a> <a id="326" class="Keyword">where</a>

<a id="333" class="Keyword">open</a> <a id="338" class="Keyword">import</a> <a id="345" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="360" class="Keyword">using</a> <a id="366" class="Symbol">(</a><a id="367" href="Agda.Builtin.Bool.html#135" class="Datatype">Bool</a><a id="371" class="Symbol">;</a> <a id="373" href="Agda.Builtin.Bool.html#160" class="InductiveConstructor">true</a><a id="377" class="Symbol">;</a> <a id="379" href="Agda.Builtin.Bool.html#154" class="InductiveConstructor">false</a><a id="384" class="Symbol">)</a>
<a id="386" class="Keyword">open</a> <a id="391" class="Keyword">import</a> <a id="398" href="Data.Fin.html" class="Module">Data.Fin</a>
<a id="407" class="Keyword">open</a> <a id="412" class="Keyword">import</a> <a id="419" href="Data.Fin.Properties.html" class="Module">Data.Fin.Properties</a>
<a id="439" class="Keyword">open</a> <a id="444" class="Keyword">import</a> <a id="451" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="460" class="Symbol">as</a> <a id="463" class="Module">ℕ</a> <a id="465" class="Keyword">using</a> <a id="471" class="Symbol">(</a><a id="472" href="Agda.Builtin.Nat.html#183" class="InductiveConstructor">zero</a><a id="476" class="Symbol">;</a> <a id="478" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a><a id="481" class="Symbol">;</a> <a id="483" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">_∸_</a><a id="486" class="Symbol">)</a>
<a id="488" class="Keyword">import</a> <a id="495" href="Data.Nat.Properties.html" class="Module">Data.Nat.Properties</a> <a id="515" class="Symbol">as</a> <a id="518" class="Module">ℕₚ</a>
<a id="521" class="Keyword">open</a> <a id="526" class="Keyword">import</a> <a id="533" href="Data.Product.html" class="Module">Data.Product</a> <a id="546" class="Keyword">using</a> <a id="552" class="Symbol">(</a><a id="553" href="Agda.Builtin.Sigma.html#237" class="Field">proj₂</a><a id="558" class="Symbol">)</a>
<a id="560" class="Keyword">open</a> <a id="565" class="Keyword">import</a> <a id="572" href="Function.Base.html" class="Module">Function.Base</a> <a id="586" class="Keyword">using</a> <a id="592" class="Symbol">(</a><a id="593" href="Function.Base.html#992" class="Function Operator">_∘_</a><a id="596" class="Symbol">)</a>
<a id="598" class="Keyword">open</a> <a id="603" class="Keyword">import</a> <a id="610" href="Relation.Nullary.Reflects.html" class="Module">Relation.Nullary.Reflects</a> <a id="636" class="Keyword">using</a> <a id="642" class="Symbol">(</a><a id="643" href="Relation.Nullary.Reflects.html#1031" class="Function">invert</a><a id="649" class="Symbol">)</a>
<a id="651" class="Keyword">open</a> <a id="656" class="Keyword">import</a> <a id="663" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="680" class="Keyword">using</a> <a id="686" class="Symbol">(</a><a id="687" href="Relation.Nullary.html#1578" class="Field">does</a><a id="691" class="Symbol">;</a> <a id="693" href="Relation.Nullary.html#1556" class="InductiveConstructor Operator">_because_</a><a id="702" class="Symbol">;</a> <a id="704" href="Relation.Nullary.html#1645">yes</a><a id="707" class="Symbol">;</a> <a id="709" href="Relation.Nullary.html#1682">no</a><a id="711" class="Symbol">)</a>
<a id="713" class="Keyword">open</a> <a id="718" class="Keyword">import</a> <a id="725" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="752" class="Keyword">using</a> <a id="758" class="Symbol">(</a><a id="759" href="Relation.Nullary.Decidable.Core.html#2874" class="Function">dec-true</a><a id="767" class="Symbol">;</a> <a id="769" href="Relation.Nullary.Decidable.Core.html#3016" class="Function">dec-false</a><a id="778" class="Symbol">)</a>
<a id="780" class="Keyword">open</a> <a id="785" class="Keyword">import</a> <a id="792" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>
<a id="830" class="Keyword">open</a> <a id="835" class="Keyword">import</a> <a id="842" href="Algebra.Definitions.html" class="Module">Algebra.Definitions</a> <a id="862" class="Keyword">using</a> <a id="868" class="Symbol">(</a><a id="869" href="Algebra.Definitions.html#2976" class="Function">Involutive</a><a id="879" class="Symbol">)</a>
<a id="881" class="Keyword">open</a> <a id="886" href="Relation.Binary.PropositionalEquality.Core.html#2419" class="Module">≡-Reasoning</a>

<a id="899" class="Comment">--------------------------------------------------------------------------------</a>
<a id="980" class="Comment">--  Functions</a>
<a id="994" class="Comment">--------------------------------------------------------------------------------</a>

<a id="1076" class="Comment">-- &#39;tranpose i j&#39; swaps the places of &#39;i&#39; and &#39;j&#39;.</a>

<a id="transpose"></a><a id="1128" href="Data.Fin.Permutation.Components.html#1128" class="Function">transpose</a> <a id="1138" class="Symbol">:</a> <a id="1140" class="Symbol">∀</a> <a id="1142" class="Symbol">{</a><a id="1143" href="Data.Fin.Permutation.Components.html#1143" class="Bound">n</a><a id="1144" class="Symbol">}</a> <a id="1146" class="Symbol">→</a> <a id="1148" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="1152" href="Data.Fin.Permutation.Components.html#1143" class="Bound">n</a> <a id="1154" class="Symbol">→</a> <a id="1156" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="1160" href="Data.Fin.Permutation.Components.html#1143" class="Bound">n</a> <a id="1162" class="Symbol">→</a> <a id="1164" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="1168" href="Data.Fin.Permutation.Components.html#1143" class="Bound">n</a> <a id="1170" class="Symbol">→</a> <a id="1172" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="1176" href="Data.Fin.Permutation.Components.html#1143" class="Bound">n</a>
<a id="1178" href="Data.Fin.Permutation.Components.html#1128" class="Function">transpose</a> <a id="1188" href="Data.Fin.Permutation.Components.html#1188" class="Bound">i</a> <a id="1190" href="Data.Fin.Permutation.Components.html#1190" class="Bound">j</a> <a id="1192" href="Data.Fin.Permutation.Components.html#1192" class="Bound">k</a> <a id="1194" class="Keyword">with</a> <a id="1199" href="Relation.Nullary.html#1578" class="Field">does</a> <a id="1204" class="Symbol">(</a><a id="1205" href="Data.Fin.Permutation.Components.html#1192" class="Bound">k</a> <a id="1207" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="1209" href="Data.Fin.Permutation.Components.html#1188" class="Bound">i</a><a id="1210" class="Symbol">)</a>
<a id="1212" class="Symbol">...</a> <a id="1216" class="Symbol">|</a> <a id="1218" href="Agda.Builtin.Bool.html#160" class="InductiveConstructor">true</a>  <a id="1224" class="Symbol">=</a> <a id="1226" class="Bound">j</a>
<a id="1228" class="Symbol">...</a> <a id="1232" class="Symbol">|</a> <a id="1234" href="Agda.Builtin.Bool.html#154" class="InductiveConstructor">false</a> <a id="1240" class="Keyword">with</a> <a id="1245" href="Relation.Nullary.html#1578" class="Field">does</a> <a id="1250" class="Symbol">(</a><a id="1251" class="Bound">k</a> <a id="1253" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="1255" class="Bound">j</a><a id="1256" class="Symbol">)</a>
<a id="1258" class="Symbol">...</a>   <a id="1264" class="Symbol">|</a> <a id="1266" href="Agda.Builtin.Bool.html#160" class="InductiveConstructor">true</a>  <a id="1272" class="Symbol">=</a> <a id="1274" class="Bound">i</a>
<a id="1276" class="Symbol">...</a>   <a id="1282" class="Symbol">|</a> <a id="1284" href="Agda.Builtin.Bool.html#154" class="InductiveConstructor">false</a> <a id="1290" class="Symbol">=</a> <a id="1292" class="Bound">k</a>

<a id="1295" class="Comment">-- reverse i = n ∸ 1 ∸ i</a>

<a id="reverse"></a><a id="1321" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="1329" class="Symbol">:</a> <a id="1331" class="Symbol">∀</a> <a id="1333" class="Symbol">{</a><a id="1334" href="Data.Fin.Permutation.Components.html#1334" class="Bound">n</a><a id="1335" class="Symbol">}</a> <a id="1337" class="Symbol">→</a> <a id="1339" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="1343" href="Data.Fin.Permutation.Components.html#1334" class="Bound">n</a> <a id="1345" class="Symbol">→</a> <a id="1347" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="1351" href="Data.Fin.Permutation.Components.html#1334" class="Bound">n</a>
<a id="1353" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="1361" class="Symbol">{</a><a id="1362" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="1366" href="Data.Fin.Permutation.Components.html#1366" class="Bound">n</a><a id="1367" class="Symbol">}</a> <a id="1369" href="Data.Fin.Permutation.Components.html#1369" class="Bound">i</a>  <a id="1372" class="Symbol">=</a> <a id="1374" href="Data.Fin.Base.html#3055" class="Function">inject≤</a> <a id="1382" class="Symbol">(</a><a id="1383" href="Data.Fin.Permutation.Components.html#1366" class="Bound">n</a> <a id="1385" href="Data.Fin.Base.html#4922" class="Function Operator">ℕ-</a> <a id="1388" href="Data.Fin.Permutation.Components.html#1369" class="Bound">i</a><a id="1389" class="Symbol">)</a> <a id="1391" class="Symbol">(</a><a id="1392" href="Data.Nat.Properties.html#40995" class="Function">ℕₚ.m∸n≤m</a> <a id="1401" class="Symbol">(</a><a id="1402" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="1406" href="Data.Fin.Permutation.Components.html#1366" class="Bound">n</a><a id="1407" class="Symbol">)</a> <a id="1409" class="Symbol">(</a><a id="1410" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="1414" href="Data.Fin.Permutation.Components.html#1369" class="Bound">i</a><a id="1415" class="Symbol">))</a>

<a id="1419" class="Comment">--------------------------------------------------------------------------------</a>
<a id="1500" class="Comment">--  Properties</a>
<a id="1515" class="Comment">--------------------------------------------------------------------------------</a>

<a id="transpose-inverse"></a><a id="1597" href="Data.Fin.Permutation.Components.html#1597" class="Function">transpose-inverse</a> <a id="1615" class="Symbol">:</a> <a id="1617" class="Symbol">∀</a> <a id="1619" class="Symbol">{</a><a id="1620" href="Data.Fin.Permutation.Components.html#1620" class="Bound">n</a><a id="1621" class="Symbol">}</a> <a id="1623" class="Symbol">(</a><a id="1624" href="Data.Fin.Permutation.Components.html#1624" class="Bound">i</a> <a id="1626" href="Data.Fin.Permutation.Components.html#1626" class="Bound">j</a> <a id="1628" class="Symbol">:</a> <a id="1630" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="1634" href="Data.Fin.Permutation.Components.html#1620" class="Bound">n</a><a id="1635" class="Symbol">)</a> <a id="1637" class="Symbol">{</a><a id="1638" href="Data.Fin.Permutation.Components.html#1638" class="Bound">k</a><a id="1639" class="Symbol">}</a> <a id="1641" class="Symbol">→</a>
                    <a id="1663" href="Data.Fin.Permutation.Components.html#1128" class="Function">transpose</a> <a id="1673" href="Data.Fin.Permutation.Components.html#1624" class="Bound">i</a> <a id="1675" href="Data.Fin.Permutation.Components.html#1626" class="Bound">j</a> <a id="1677" class="Symbol">(</a><a id="1678" href="Data.Fin.Permutation.Components.html#1128" class="Function">transpose</a> <a id="1688" href="Data.Fin.Permutation.Components.html#1626" class="Bound">j</a> <a id="1690" href="Data.Fin.Permutation.Components.html#1624" class="Bound">i</a> <a id="1692" href="Data.Fin.Permutation.Components.html#1638" class="Bound">k</a><a id="1693" class="Symbol">)</a> <a id="1695" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="1697" href="Data.Fin.Permutation.Components.html#1638" class="Bound">k</a>
<a id="1699" href="Data.Fin.Permutation.Components.html#1597" class="Function">transpose-inverse</a> <a id="1717" href="Data.Fin.Permutation.Components.html#1717" class="Bound">i</a> <a id="1719" href="Data.Fin.Permutation.Components.html#1719" class="Bound">j</a> <a id="1721" class="Symbol">{</a><a id="1722" href="Data.Fin.Permutation.Components.html#1722" class="Bound">k</a><a id="1723" class="Symbol">}</a> <a id="1725" class="Keyword">with</a> <a id="1730" href="Data.Fin.Permutation.Components.html#1722" class="Bound">k</a> <a id="1732" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="1734" href="Data.Fin.Permutation.Components.html#1719" class="Bound">j</a>
<a id="1736" class="Symbol">...</a> <a id="1740" class="Symbol">|</a> <a id="1742" href="Agda.Builtin.Bool.html#160" class="InductiveConstructor">true</a>  <a id="1748" href="Relation.Nullary.html#1556" class="InductiveConstructor Operator">because</a> <a id="1756" href="Data.Fin.Permutation.Components.html#1756" class="Bound">[k≡j]</a> <a id="1762" class="Keyword">rewrite</a> <a id="1770" href="Relation.Nullary.Decidable.Core.html#2874" class="Function">dec-true</a> <a id="1779" class="Symbol">(</a><a id="1780" class="Bound">i</a> <a id="1782" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="1784" class="Bound">i</a><a id="1785" class="Symbol">)</a> <a id="1787" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a> <a id="1792" class="Symbol">=</a> <a id="1794" href="Relation.Binary.PropositionalEquality.Core.html#980" class="Function">sym</a> <a id="1798" class="Symbol">(</a><a id="1799" href="Relation.Nullary.Reflects.html#1031" class="Function">invert</a> <a id="1806" href="Data.Fin.Permutation.Components.html#1756" class="Bound">[k≡j]</a><a id="1811" class="Symbol">)</a>
<a id="1813" class="Symbol">...</a> <a id="1817" class="Symbol">|</a> <a id="1819" href="Agda.Builtin.Bool.html#154" class="InductiveConstructor">false</a> <a id="1825" href="Relation.Nullary.html#1556" class="InductiveConstructor Operator">because</a> <a id="1833" href="Data.Fin.Permutation.Components.html#1833" class="Bound">[k≢j]</a> <a id="1839" class="Keyword">with</a> <a id="1844" class="Bound">k</a> <a id="1846" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="1848" class="Bound">i</a>
<a id="1850" class="Symbol">...</a>   <a id="1856" class="Symbol">|</a> <a id="1858" href="Agda.Builtin.Bool.html#160" class="InductiveConstructor">true</a>  <a id="1864" href="Relation.Nullary.html#1556" class="InductiveConstructor Operator">because</a> <a id="1872" href="Data.Fin.Permutation.Components.html#1872" class="Bound">[k≡i]</a>
        <a id="1886" class="Keyword">rewrite</a> <a id="1894" href="Relation.Nullary.Decidable.Core.html#3016" class="Function">dec-false</a> <a id="1904" class="Symbol">(</a><a id="1905" class="Bound">j</a> <a id="1907" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="1909" class="Bound">i</a><a id="1910" class="Symbol">)</a> <a id="1912" class="Symbol">(</a><a id="1913" href="Relation.Nullary.Reflects.html#1031" class="Function">invert</a> <a id="1920" class="Bound">[k≢j]</a> <a id="1926" href="Function.Base.html#992" class="Function Operator">∘</a> <a id="1928" href="Relation.Binary.PropositionalEquality.Core.html#1025" class="Function">trans</a> <a id="1934" class="Symbol">(</a><a id="1935" href="Relation.Nullary.Reflects.html#1031" class="Function">invert</a> <a id="1942" href="Data.Fin.Permutation.Components.html#1872" class="Bound">[k≡i]</a><a id="1947" class="Symbol">)</a> <a id="1949" href="Function.Base.html#992" class="Function Operator">∘</a> <a id="1951" href="Relation.Binary.PropositionalEquality.Core.html#980" class="Function">sym</a><a id="1954" class="Symbol">)</a>
                <a id="1972" class="Symbol">|</a> <a id="1974" href="Relation.Nullary.Decidable.Core.html#2874" class="Function">dec-true</a> <a id="1983" class="Symbol">(</a><a id="1984" class="Bound">j</a> <a id="1986" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="1988" class="Bound">j</a><a id="1989" class="Symbol">)</a> <a id="1991" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a>
                <a id="2012" class="Symbol">=</a> <a id="2014" href="Relation.Binary.PropositionalEquality.Core.html#980" class="Function">sym</a> <a id="2018" class="Symbol">(</a><a id="2019" href="Relation.Nullary.Reflects.html#1031" class="Function">invert</a> <a id="2026" href="Data.Fin.Permutation.Components.html#1872" class="Bound">[k≡i]</a><a id="2031" class="Symbol">)</a>
<a id="2033" class="Symbol">...</a>   <a id="2039" class="Symbol">|</a> <a id="2041" href="Agda.Builtin.Bool.html#154" class="InductiveConstructor">false</a> <a id="2047" href="Relation.Nullary.html#1556" class="InductiveConstructor Operator">because</a> <a id="2055" href="Data.Fin.Permutation.Components.html#2055" class="Bound">[k≢i]</a> <a id="2061" class="Keyword">rewrite</a> <a id="2069" href="Relation.Nullary.Decidable.Core.html#3016" class="Function">dec-false</a> <a id="2079" class="Symbol">(</a><a id="2080" class="Bound">k</a> <a id="2082" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="2084" class="Bound">i</a><a id="2085" class="Symbol">)</a> <a id="2087" class="Symbol">(</a><a id="2088" href="Relation.Nullary.Reflects.html#1031" class="Function">invert</a> <a id="2095" href="Data.Fin.Permutation.Components.html#2055" class="Bound">[k≢i]</a><a id="2100" class="Symbol">)</a>
                                  <a id="2136" class="Symbol">|</a> <a id="2138" href="Relation.Nullary.Decidable.Core.html#3016" class="Function">dec-false</a> <a id="2148" class="Symbol">(</a><a id="2149" class="Bound">k</a> <a id="2151" href="Data.Fin.Properties.html#2179" class="Function Operator">≟</a> <a id="2153" class="Bound">j</a><a id="2154" class="Symbol">)</a> <a id="2156" class="Symbol">(</a><a id="2157" href="Relation.Nullary.Reflects.html#1031" class="Function">invert</a> <a id="2164" class="Bound">[k≢j]</a><a id="2169" class="Symbol">)</a> <a id="2171" class="Symbol">=</a> <a id="2173" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a>

<a id="reverse-prop"></a><a id="2179" href="Data.Fin.Permutation.Components.html#2179" class="Function">reverse-prop</a> <a id="2192" class="Symbol">:</a> <a id="2194" class="Symbol">∀</a> <a id="2196" class="Symbol">{</a><a id="2197" href="Data.Fin.Permutation.Components.html#2197" class="Bound">n</a><a id="2198" class="Symbol">}</a> <a id="2200" class="Symbol">→</a> <a id="2202" class="Symbol">(</a><a id="2203" href="Data.Fin.Permutation.Components.html#2203" class="Bound">i</a> <a id="2205" class="Symbol">:</a> <a id="2207" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="2211" href="Data.Fin.Permutation.Components.html#2197" class="Bound">n</a><a id="2212" class="Symbol">)</a> <a id="2214" class="Symbol">→</a> <a id="2216" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2220" class="Symbol">(</a><a id="2221" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2229" href="Data.Fin.Permutation.Components.html#2203" class="Bound">i</a><a id="2230" class="Symbol">)</a> <a id="2232" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="2234" href="Data.Fin.Permutation.Components.html#2197" class="Bound">n</a> <a id="2236" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸</a> <a id="2238" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2242" class="Symbol">(</a><a id="2243" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2247" href="Data.Fin.Permutation.Components.html#2203" class="Bound">i</a><a id="2248" class="Symbol">)</a>
<a id="2250" href="Data.Fin.Permutation.Components.html#2179" class="Function">reverse-prop</a> <a id="2263" class="Symbol">{</a><a id="2264" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2268" href="Data.Fin.Permutation.Components.html#2268" class="Bound">n</a><a id="2269" class="Symbol">}</a> <a id="2271" href="Data.Fin.Permutation.Components.html#2271" class="Bound">i</a> <a id="2273" class="Symbol">=</a> <a id="2275" href="Relation.Binary.PropositionalEquality.Core.html#2517" class="Function Operator">begin</a>
  <a id="2283" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2287" class="Symbol">(</a><a id="2288" href="Data.Fin.Base.html#3055" class="Function">inject≤</a> <a id="2296" class="Symbol">(</a><a id="2297" href="Data.Fin.Permutation.Components.html#2268" class="Bound">n</a> <a id="2299" href="Data.Fin.Base.html#4922" class="Function Operator">ℕ-</a> <a id="2302" href="Data.Fin.Permutation.Components.html#2271" class="Bound">i</a><a id="2303" class="Symbol">)</a> <a id="2305" class="Symbol">_)</a>  <a id="2309" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">≡⟨</a> <a id="2312" href="Data.Fin.Properties.html#13213" class="Function">toℕ-inject≤</a> <a id="2324" class="Symbol">_</a> <a id="2326" class="Symbol">(</a><a id="2327" href="Data.Nat.Properties.html#40995" class="Function">ℕₚ.m∸n≤m</a> <a id="2336" class="Symbol">(</a><a id="2337" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2341" href="Data.Fin.Permutation.Components.html#2268" class="Bound">n</a><a id="2342" class="Symbol">)</a> <a id="2344" class="Symbol">(</a><a id="2345" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2349" href="Data.Fin.Permutation.Components.html#2271" class="Bound">i</a><a id="2350" class="Symbol">))</a> <a id="2353" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">⟩</a>
  <a id="2357" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2361" class="Symbol">(</a><a id="2362" href="Data.Fin.Permutation.Components.html#2268" class="Bound">n</a> <a id="2364" href="Data.Fin.Base.html#4922" class="Function Operator">ℕ-</a> <a id="2367" href="Data.Fin.Permutation.Components.html#2271" class="Bound">i</a><a id="2368" class="Symbol">)</a>              <a id="2383" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">≡⟨</a> <a id="2386" href="Data.Fin.Properties.html#15951" class="Function">toℕ‿ℕ-</a> <a id="2393" href="Data.Fin.Permutation.Components.html#2268" class="Bound">n</a> <a id="2395" href="Data.Fin.Permutation.Components.html#2271" class="Bound">i</a> <a id="2397" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">⟩</a>
  <a id="2401" href="Data.Fin.Permutation.Components.html#2268" class="Bound">n</a> <a id="2403" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸</a> <a id="2405" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2409" href="Data.Fin.Permutation.Components.html#2271" class="Bound">i</a>                 <a id="2427" href="Relation.Binary.PropositionalEquality.Core.html#2816" class="Function Operator">∎</a>

<a id="reverse-involutive"></a><a id="2430" href="Data.Fin.Permutation.Components.html#2430" class="Function">reverse-involutive</a> <a id="2449" class="Symbol">:</a> <a id="2451" class="Symbol">∀</a> <a id="2453" class="Symbol">{</a><a id="2454" href="Data.Fin.Permutation.Components.html#2454" class="Bound">n</a><a id="2455" class="Symbol">}</a> <a id="2457" class="Symbol">→</a> <a id="2459" href="Algebra.Definitions.html#2976" class="Function">Involutive</a> <a id="2470" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">_≡_</a> <a id="2474" class="Symbol">(</a><a id="2475" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2483" class="Symbol">{</a><a id="2484" href="Data.Fin.Permutation.Components.html#2454" class="Bound">n</a><a id="2485" class="Symbol">})</a>
<a id="2488" href="Data.Fin.Permutation.Components.html#2430" class="Function">reverse-involutive</a> <a id="2507" class="Symbol">{</a><a id="2508" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2512" href="Data.Fin.Permutation.Components.html#2512" class="Bound">n</a><a id="2513" class="Symbol">}</a> <a id="2515" href="Data.Fin.Permutation.Components.html#2515" class="Bound">i</a> <a id="2517" class="Symbol">=</a> <a id="2519" href="Data.Fin.Properties.html#3061" class="Function">toℕ-injective</a> <a id="2533" class="Symbol">(</a><a id="2534" href="Relation.Binary.PropositionalEquality.Core.html#2517" class="Function Operator">begin</a>
  <a id="2542" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2546" class="Symbol">(</a><a id="2547" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2555" class="Symbol">(</a><a id="2556" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2564" href="Data.Fin.Permutation.Components.html#2515" class="Bound">i</a><a id="2565" class="Symbol">))</a> <a id="2568" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">≡⟨</a> <a id="2571" href="Data.Fin.Permutation.Components.html#2179" class="Function">reverse-prop</a> <a id="2584" class="Symbol">(</a><a id="2585" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2593" href="Data.Fin.Permutation.Components.html#2515" class="Bound">i</a><a id="2594" class="Symbol">)</a> <a id="2596" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">⟩</a>
  <a id="2600" href="Data.Fin.Permutation.Components.html#2512" class="Bound">n</a> <a id="2602" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸</a> <a id="2604" class="Symbol">(</a><a id="2605" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2609" class="Symbol">(</a><a id="2610" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2618" href="Data.Fin.Permutation.Components.html#2515" class="Bound">i</a><a id="2619" class="Symbol">))</a>     <a id="2626" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">≡⟨</a> <a id="2629" href="Relation.Binary.PropositionalEquality.Core.html#1131" class="Function">cong</a> <a id="2634" class="Symbol">(</a><a id="2635" href="Data.Fin.Permutation.Components.html#2512" class="Bound">n</a> <a id="2637" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸_</a><a id="2639" class="Symbol">)</a> <a id="2641" class="Symbol">(</a><a id="2642" href="Data.Fin.Permutation.Components.html#2179" class="Function">reverse-prop</a> <a id="2655" href="Data.Fin.Permutation.Components.html#2515" class="Bound">i</a><a id="2656" class="Symbol">)</a> <a id="2658" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">⟩</a>
  <a id="2662" href="Data.Fin.Permutation.Components.html#2512" class="Bound">n</a> <a id="2664" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸</a> <a id="2666" class="Symbol">(</a><a id="2667" href="Data.Fin.Permutation.Components.html#2512" class="Bound">n</a> <a id="2669" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸</a> <a id="2671" class="Symbol">(</a><a id="2672" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2676" href="Data.Fin.Permutation.Components.html#2515" class="Bound">i</a><a id="2677" class="Symbol">))</a>         <a id="2688" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">≡⟨</a> <a id="2691" href="Data.Nat.Properties.html#45060" class="Function">ℕₚ.m∸[m∸n]≡n</a> <a id="2704" class="Symbol">(</a><a id="2705" href="Data.Nat.Properties.html#3462" class="Function">ℕₚ.≤-pred</a> <a id="2715" class="Symbol">(</a><a id="2716" href="Data.Fin.Properties.html#3584" class="Function">toℕ&lt;n</a> <a id="2722" href="Data.Fin.Permutation.Components.html#2515" class="Bound">i</a><a id="2723" class="Symbol">))</a> <a id="2726" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">⟩</a>
  <a id="2730" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2734" href="Data.Fin.Permutation.Components.html#2515" class="Bound">i</a>                     <a id="2756" href="Relation.Binary.PropositionalEquality.Core.html#2816" class="Function Operator">∎</a><a id="2757" class="Symbol">)</a>

<a id="reverse-suc"></a><a id="2760" href="Data.Fin.Permutation.Components.html#2760" class="Function">reverse-suc</a> <a id="2772" class="Symbol">:</a> <a id="2774" class="Symbol">∀</a> <a id="2776" class="Symbol">{</a><a id="2777" href="Data.Fin.Permutation.Components.html#2777" class="Bound">n</a><a id="2778" class="Symbol">}</a> <a id="2780" class="Symbol">{</a><a id="2781" href="Data.Fin.Permutation.Components.html#2781" class="Bound">i</a> <a id="2783" class="Symbol">:</a> <a id="2785" href="Data.Fin.Base.html#1061" class="Datatype">Fin</a> <a id="2789" href="Data.Fin.Permutation.Components.html#2777" class="Bound">n</a><a id="2790" class="Symbol">}</a> <a id="2792" class="Symbol">→</a> <a id="2794" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2798" class="Symbol">(</a><a id="2799" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2807" class="Symbol">(</a><a id="2808" href="Data.Fin.Base.html#1114" class="InductiveConstructor">suc</a> <a id="2812" href="Data.Fin.Permutation.Components.html#2781" class="Bound">i</a><a id="2813" class="Symbol">))</a> <a id="2816" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="2818" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2822" class="Symbol">(</a><a id="2823" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2831" href="Data.Fin.Permutation.Components.html#2781" class="Bound">i</a><a id="2832" class="Symbol">)</a>
<a id="2834" href="Data.Fin.Permutation.Components.html#2760" class="Function">reverse-suc</a> <a id="2846" class="Symbol">{</a><a id="2847" href="Data.Fin.Permutation.Components.html#2847" class="Bound">n</a><a id="2848" class="Symbol">}</a> <a id="2850" class="Symbol">{</a><a id="2851" href="Data.Fin.Permutation.Components.html#2851" class="Bound">i</a><a id="2852" class="Symbol">}</a> <a id="2854" class="Symbol">=</a> <a id="2856" href="Relation.Binary.PropositionalEquality.Core.html#2517" class="Function Operator">begin</a>
  <a id="2864" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2868" class="Symbol">(</a><a id="2869" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="2877" class="Symbol">(</a><a id="2878" href="Data.Fin.Base.html#1114" class="InductiveConstructor">suc</a> <a id="2882" href="Data.Fin.Permutation.Components.html#2851" class="Bound">i</a><a id="2883" class="Symbol">))</a>      <a id="2891" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">≡⟨</a> <a id="2894" href="Data.Fin.Permutation.Components.html#2179" class="Function">reverse-prop</a> <a id="2907" class="Symbol">(</a><a id="2908" href="Data.Fin.Base.html#1114" class="InductiveConstructor">suc</a> <a id="2912" href="Data.Fin.Permutation.Components.html#2851" class="Bound">i</a><a id="2913" class="Symbol">)</a> <a id="2915" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">⟩</a>
  <a id="2919" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2923" href="Data.Fin.Permutation.Components.html#2847" class="Bound">n</a> <a id="2925" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸</a> <a id="2927" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2931" class="Symbol">(</a><a id="2932" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2936" class="Symbol">(</a><a id="2937" href="Data.Fin.Base.html#1114" class="InductiveConstructor">suc</a> <a id="2941" href="Data.Fin.Permutation.Components.html#2851" class="Bound">i</a><a id="2942" class="Symbol">))</a>  <a id="2946" href="Relation.Binary.PropositionalEquality.Core.html#2575" class="Function Operator">≡⟨⟩</a>
  <a id="2952" href="Data.Fin.Permutation.Components.html#2847" class="Bound">n</a> <a id="2954" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸</a> <a id="2956" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2960" class="Symbol">(</a><a id="2961" href="Data.Fin.Base.html#1114" class="InductiveConstructor">suc</a> <a id="2965" href="Data.Fin.Permutation.Components.html#2851" class="Bound">i</a><a id="2966" class="Symbol">)</a>            <a id="2979" href="Relation.Binary.PropositionalEquality.Core.html#2575" class="Function Operator">≡⟨⟩</a>
  <a id="2985" href="Data.Fin.Permutation.Components.html#2847" class="Bound">n</a> <a id="2987" href="Agda.Builtin.Nat.html#388" class="Primitive Operator">∸</a> <a id="2989" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2993" class="Symbol">(</a><a id="2994" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="2998" href="Data.Fin.Permutation.Components.html#2851" class="Bound">i</a><a id="2999" class="Symbol">)</a>            <a id="3012" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">≡⟨</a> <a id="3015" href="Relation.Binary.PropositionalEquality.Core.html#980" class="Function">sym</a> <a id="3019" class="Symbol">(</a><a id="3020" href="Data.Fin.Permutation.Components.html#2179" class="Function">reverse-prop</a> <a id="3033" href="Data.Fin.Permutation.Components.html#2851" class="Bound">i</a><a id="3034" class="Symbol">)</a> <a id="3036" href="Relation.Binary.PropositionalEquality.Core.html#2634" class="Function">⟩</a>
  <a id="3040" href="Data.Fin.Base.html#1187" class="Function">toℕ</a> <a id="3044" class="Symbol">(</a><a id="3045" href="Data.Fin.Permutation.Components.html#1321" class="Function">reverse</a> <a id="3053" href="Data.Fin.Permutation.Components.html#2851" class="Bound">i</a><a id="3054" class="Symbol">)</a>            <a id="3067" href="Relation.Binary.PropositionalEquality.Core.html#2816" class="Function Operator">∎</a>
</pre></body></html>