<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.W</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- W-types</a>
<a id="117" class="Comment">------------------------------------------------------------------------</a>

<a id="191" class="Symbol">{-#</a> <a id="195" class="Keyword">OPTIONS</a> <a id="203" class="Pragma">--without-K</a> <a id="215" class="Pragma">--safe</a> <a id="222" class="Symbol">#-}</a>

<a id="227" class="Keyword">module</a> <a id="234" href="Data.W.html" class="Module">Data.W</a> <a id="241" class="Keyword">where</a>

<a id="248" class="Keyword">open</a> <a id="253" class="Keyword">import</a> <a id="260" href="Level.html" class="Module">Level</a>
<a id="266" class="Keyword">open</a> <a id="271" class="Keyword">import</a> <a id="278" href="Function.html" class="Module">Function</a>
<a id="287" class="Keyword">open</a> <a id="292" class="Keyword">import</a> <a id="299" href="Data.Product.html" class="Module">Data.Product</a> <a id="312" class="Keyword">hiding</a> <a id="319" class="Symbol">(</a><a id="320" href="Data.Product.html#2190" class="Function">map</a><a id="323" class="Symbol">)</a>
<a id="325" class="Keyword">open</a> <a id="330" class="Keyword">import</a> <a id="337" href="Data.Container.Core.html" class="Module">Data.Container.Core</a> <a id="357" class="Keyword">hiding</a> <a id="364" class="Symbol">(</a><a id="365" href="Data.Container.Core.html#745" class="Function">map</a><a id="368" class="Symbol">)</a>
<a id="370" class="Keyword">open</a> <a id="375" class="Keyword">import</a> <a id="382" href="Relation.Nullary.html" class="Module">Relation.Nullary</a>
<a id="399" class="Keyword">open</a> <a id="404" class="Keyword">import</a> <a id="411" href="Agda.Builtin.Equality.html" class="Module">Agda.Builtin.Equality</a>

<a id="434" class="Comment">-- The family of W-types.</a>

<a id="461" class="Keyword">data</a> <a id="W"></a><a id="466" href="Data.W.html#466" class="Datatype">W</a> <a id="468" class="Symbol">{</a><a id="469" href="Data.W.html#469" class="Bound">s</a> <a id="471" href="Data.W.html#471" class="Bound">p</a><a id="472" class="Symbol">}</a> <a id="474" class="Symbol">(</a><a id="475" href="Data.W.html#475" class="Bound">C</a> <a id="477" class="Symbol">:</a> <a id="479" href="Data.Container.Core.html#443" class="Record">Container</a> <a id="489" href="Data.W.html#469" class="Bound">s</a> <a id="491" href="Data.W.html#471" class="Bound">p</a><a id="492" class="Symbol">)</a> <a id="494" class="Symbol">:</a> <a id="496" class="PrimitiveType">Set</a> <a id="500" class="Symbol">(</a><a id="501" href="Data.W.html#469" class="Bound">s</a> <a id="503" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="505" href="Data.W.html#471" class="Bound">p</a><a id="506" class="Symbol">)</a> <a id="508" class="Keyword">where</a>
  <a id="W.sup"></a><a id="516" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="520" class="Symbol">:</a> <a id="522" href="Data.Container.Core.html#633" class="Function Operator">⟦</a> <a id="524" href="Data.W.html#475" class="Bound">C</a> <a id="526" href="Data.Container.Core.html#633" class="Function Operator">⟧</a> <a id="528" class="Symbol">(</a><a id="529" href="Data.W.html#466" class="Datatype">W</a> <a id="531" href="Data.W.html#475" class="Bound">C</a><a id="532" class="Symbol">)</a> <a id="534" class="Symbol">→</a> <a id="536" href="Data.W.html#466" class="Datatype">W</a> <a id="538" href="Data.W.html#475" class="Bound">C</a>

<a id="541" class="Keyword">module</a> <a id="548" href="Data.W.html#548" class="Module">_</a> <a id="550" class="Symbol">{</a><a id="551" href="Data.W.html#551" class="Bound">s</a> <a id="553" href="Data.W.html#553" class="Bound">p</a><a id="554" class="Symbol">}</a> <a id="556" class="Symbol">{</a><a id="557" href="Data.W.html#557" class="Bound">C</a> <a id="559" class="Symbol">:</a> <a id="561" href="Data.Container.Core.html#443" class="Record">Container</a> <a id="571" href="Data.W.html#551" class="Bound">s</a> <a id="573" href="Data.W.html#553" class="Bound">p</a><a id="574" class="Symbol">}</a> <a id="576" class="Symbol">(</a><a id="577" class="Keyword">open</a> <a id="582" href="Data.Container.Core.html#443" class="Module">Container</a> <a id="592" href="Data.W.html#557" class="Bound">C</a><a id="593" class="Symbol">)</a>
         <a id="604" class="Symbol">{</a><a id="605" href="Data.W.html#605" class="Bound">s</a> <a id="607" class="Symbol">:</a> <a id="609" href="Data.Container.Core.html#523" class="Field">Shape</a><a id="614" class="Symbol">}</a> <a id="616" class="Symbol">{</a><a id="617" href="Data.W.html#617" class="Bound">f</a> <a id="619" class="Symbol">:</a> <a id="621" href="Data.Container.Core.html#544" class="Field">Position</a> <a id="630" href="Data.W.html#605" class="Bound">s</a> <a id="632" class="Symbol">→</a> <a id="634" href="Data.W.html#466" class="Datatype">W</a> <a id="636" href="Data.W.html#557" class="Bound">C</a><a id="637" class="Symbol">}</a> <a id="639" class="Keyword">where</a>

 <a id="647" href="Data.W.html#647" class="Function">sup-injective₁</a> <a id="662" class="Symbol">:</a> <a id="664" class="Symbol">∀</a> <a id="666" class="Symbol">{</a><a id="667" href="Data.W.html#667" class="Bound">t</a> <a id="669" href="Data.W.html#669" class="Bound">g</a><a id="670" class="Symbol">}</a> <a id="672" class="Symbol">→</a> <a id="674" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="678" class="Symbol">(</a><a id="679" href="Data.W.html#605" class="Bound">s</a> <a id="681" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="683" href="Data.W.html#617" class="Bound">f</a><a id="684" class="Symbol">)</a> <a id="686" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="688" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="692" class="Symbol">(</a><a id="693" href="Data.W.html#667" class="Bound">t</a> <a id="695" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="697" href="Data.W.html#669" class="Bound">g</a><a id="698" class="Symbol">)</a> <a id="700" class="Symbol">→</a> <a id="702" href="Data.W.html#605" class="Bound">s</a> <a id="704" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="706" href="Data.W.html#667" class="Bound">t</a>
 <a id="709" href="Data.W.html#647" class="Function">sup-injective₁</a> <a id="724" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a> <a id="729" class="Symbol">=</a> <a id="731" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a>

 <a id="738" class="Comment">-- See also Data.W.WithK.sup-injective₂.</a>

<a id="780" class="Comment">-- Projections.</a>

<a id="797" class="Keyword">module</a> <a id="804" href="Data.W.html#804" class="Module">_</a> <a id="806" class="Symbol">{</a><a id="807" href="Data.W.html#807" class="Bound">s</a> <a id="809" href="Data.W.html#809" class="Bound">p</a><a id="810" class="Symbol">}</a> <a id="812" class="Symbol">{</a><a id="813" href="Data.W.html#813" class="Bound">C</a> <a id="815" class="Symbol">:</a> <a id="817" href="Data.Container.Core.html#443" class="Record">Container</a> <a id="827" href="Data.W.html#807" class="Bound">s</a> <a id="829" href="Data.W.html#809" class="Bound">p</a><a id="830" class="Symbol">}</a> <a id="832" class="Symbol">(</a><a id="833" class="Keyword">open</a> <a id="838" href="Data.Container.Core.html#443" class="Module">Container</a> <a id="848" href="Data.W.html#813" class="Bound">C</a><a id="849" class="Symbol">)</a> <a id="851" class="Keyword">where</a>

  <a id="860" href="Data.W.html#860" class="Function">head</a> <a id="865" class="Symbol">:</a> <a id="867" href="Data.W.html#466" class="Datatype">W</a> <a id="869" href="Data.W.html#813" class="Bound">C</a> <a id="871" class="Symbol">→</a> <a id="873" href="Data.Container.Core.html#523" class="Field">Shape</a>
  <a id="881" href="Data.W.html#860" class="Function">head</a> <a id="886" class="Symbol">(</a><a id="887" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="891" class="Symbol">(</a><a id="892" href="Data.W.html#892" class="Bound">x</a> <a id="894" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="896" href="Data.W.html#896" class="Bound">f</a><a id="897" class="Symbol">))</a> <a id="900" class="Symbol">=</a> <a id="902" href="Data.W.html#892" class="Bound">x</a>

  <a id="907" href="Data.W.html#907" class="Function">tail</a> <a id="912" class="Symbol">:</a> <a id="914" class="Symbol">(</a><a id="915" href="Data.W.html#915" class="Bound">x</a> <a id="917" class="Symbol">:</a> <a id="919" href="Data.W.html#466" class="Datatype">W</a> <a id="921" href="Data.W.html#813" class="Bound">C</a><a id="922" class="Symbol">)</a> <a id="924" class="Symbol">→</a> <a id="926" href="Data.Container.Core.html#544" class="Field">Position</a> <a id="935" class="Symbol">(</a><a id="936" href="Data.W.html#860" class="Function">head</a> <a id="941" href="Data.W.html#915" class="Bound">x</a><a id="942" class="Symbol">)</a> <a id="944" class="Symbol">→</a> <a id="946" href="Data.W.html#466" class="Datatype">W</a> <a id="948" href="Data.W.html#813" class="Bound">C</a>
  <a id="952" href="Data.W.html#907" class="Function">tail</a> <a id="957" class="Symbol">(</a><a id="958" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="962" class="Symbol">(</a><a id="963" href="Data.W.html#963" class="Bound">x</a> <a id="965" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="967" href="Data.W.html#967" class="Bound">f</a><a id="968" class="Symbol">))</a> <a id="971" class="Symbol">=</a> <a id="973" href="Data.W.html#967" class="Bound">f</a>

<a id="976" class="Comment">-- map</a>

<a id="984" class="Keyword">module</a> <a id="991" href="Data.W.html#991" class="Module">_</a> <a id="993" class="Symbol">{</a><a id="994" href="Data.W.html#994" class="Bound">s₁</a> <a id="997" href="Data.W.html#997" class="Bound">s₂</a> <a id="1000" href="Data.W.html#1000" class="Bound">p₁</a> <a id="1003" href="Data.W.html#1003" class="Bound">p₂</a><a id="1005" class="Symbol">}</a> <a id="1007" class="Symbol">{</a><a id="1008" href="Data.W.html#1008" class="Bound">C₁</a> <a id="1011" class="Symbol">:</a> <a id="1013" href="Data.Container.Core.html#443" class="Record">Container</a> <a id="1023" href="Data.W.html#994" class="Bound">s₁</a> <a id="1026" href="Data.W.html#1000" class="Bound">p₁</a><a id="1028" class="Symbol">}</a> <a id="1030" class="Symbol">{</a><a id="1031" href="Data.W.html#1031" class="Bound">C₂</a> <a id="1034" class="Symbol">:</a> <a id="1036" href="Data.Container.Core.html#443" class="Record">Container</a> <a id="1046" href="Data.W.html#997" class="Bound">s₂</a> <a id="1049" href="Data.W.html#1003" class="Bound">p₂</a><a id="1051" class="Symbol">}</a>
         <a id="1062" class="Symbol">(</a><a id="1063" href="Data.W.html#1063" class="Bound">m</a> <a id="1065" class="Symbol">:</a> <a id="1067" href="Data.W.html#1008" class="Bound">C₁</a> <a id="1070" href="Data.Container.Core.html#919" class="Record Operator">⇒</a> <a id="1072" href="Data.W.html#1031" class="Bound">C₂</a><a id="1074" class="Symbol">)</a> <a id="1076" class="Keyword">where</a>

  <a id="1085" href="Data.W.html#1085" class="Function">map</a> <a id="1089" class="Symbol">:</a> <a id="1091" href="Data.W.html#466" class="Datatype">W</a> <a id="1093" href="Data.W.html#1008" class="Bound">C₁</a> <a id="1096" class="Symbol">→</a> <a id="1098" href="Data.W.html#466" class="Datatype">W</a> <a id="1100" href="Data.W.html#1031" class="Bound">C₂</a>
  <a id="1105" href="Data.W.html#1085" class="Function">map</a> <a id="1109" class="Symbol">(</a><a id="1110" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="1114" class="Symbol">(</a><a id="1115" href="Data.W.html#1115" class="Bound">x</a> <a id="1117" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1119" href="Data.W.html#1119" class="Bound">f</a><a id="1120" class="Symbol">))</a> <a id="1123" class="Symbol">=</a> <a id="1125" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="1129" class="Symbol">(</a><a id="1130" href="Data.Container.Core.html#1192" class="Function Operator">⟪</a> <a id="1132" href="Data.W.html#1063" class="Bound">m</a> <a id="1134" href="Data.Container.Core.html#1192" class="Function Operator">⟫</a> <a id="1136" class="Symbol">(</a><a id="1137" href="Data.W.html#1115" class="Bound">x</a> <a id="1139" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1141" class="Symbol">λ</a> <a id="1143" href="Data.W.html#1143" class="Bound">p</a> <a id="1145" class="Symbol">→</a> <a id="1147" href="Data.W.html#1085" class="Function">map</a> <a id="1151" class="Symbol">(</a><a id="1152" href="Data.W.html#1119" class="Bound">f</a> <a id="1154" href="Data.W.html#1143" class="Bound">p</a><a id="1155" class="Symbol">)))</a>

<a id="1160" class="Comment">-- induction</a>

<a id="1174" class="Keyword">module</a> <a id="1181" href="Data.W.html#1181" class="Module">_</a> <a id="1183" class="Symbol">{</a><a id="1184" href="Data.W.html#1184" class="Bound">s</a> <a id="1186" href="Data.W.html#1186" class="Bound">p</a> <a id="1188" href="Data.W.html#1188" class="Bound">ℓ</a><a id="1189" class="Symbol">}</a> <a id="1191" class="Symbol">{</a><a id="1192" href="Data.W.html#1192" class="Bound">C</a> <a id="1194" class="Symbol">:</a> <a id="1196" href="Data.Container.Core.html#443" class="Record">Container</a> <a id="1206" href="Data.W.html#1184" class="Bound">s</a> <a id="1208" href="Data.W.html#1186" class="Bound">p</a><a id="1209" class="Symbol">}</a> <a id="1211" class="Symbol">(</a><a id="1212" href="Data.W.html#1212" class="Bound">P</a> <a id="1214" class="Symbol">:</a> <a id="1216" href="Data.W.html#466" class="Datatype">W</a> <a id="1218" href="Data.W.html#1192" class="Bound">C</a> <a id="1220" class="Symbol">→</a> <a id="1222" class="PrimitiveType">Set</a> <a id="1226" href="Data.W.html#1188" class="Bound">ℓ</a><a id="1227" class="Symbol">)</a>
         <a id="1238" class="Symbol">(</a><a id="1239" href="Data.W.html#1239" class="Bound">alg</a> <a id="1243" class="Symbol">:</a> <a id="1245" class="Symbol">∀</a> <a id="1247" class="Symbol">{</a><a id="1248" href="Data.W.html#1248" class="Bound">t</a><a id="1249" class="Symbol">}</a> <a id="1251" class="Symbol">→</a> <a id="1253" href="Data.Container.Core.html#1434" class="Function">□</a> <a id="1255" href="Data.W.html#1212" class="Bound">P</a> <a id="1257" href="Data.W.html#1248" class="Bound">t</a> <a id="1259" class="Symbol">→</a> <a id="1261" href="Data.W.html#1212" class="Bound">P</a> <a id="1263" class="Symbol">(</a><a id="1264" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="1268" href="Data.W.html#1248" class="Bound">t</a><a id="1269" class="Symbol">))</a> <a id="1272" class="Keyword">where</a>

 <a id="1280" href="Data.W.html#1280" class="Function">induction</a> <a id="1290" class="Symbol">:</a> <a id="1292" class="Symbol">(</a><a id="1293" href="Data.W.html#1293" class="Bound">w</a> <a id="1295" class="Symbol">:</a> <a id="1297" href="Data.W.html#466" class="Datatype">W</a> <a id="1299" href="Data.W.html#1192" class="Bound">C</a><a id="1300" class="Symbol">)</a> <a id="1302" class="Symbol">→</a> <a id="1304" href="Data.W.html#1212" class="Bound">P</a> <a id="1306" href="Data.W.html#1293" class="Bound">w</a>
 <a id="1309" href="Data.W.html#1280" class="Function">induction</a> <a id="1319" class="Symbol">(</a><a id="1320" href="Data.W.html#516" class="InductiveConstructor">sup</a> <a id="1324" class="Symbol">(</a><a id="1325" href="Data.W.html#1325" class="Bound">s</a> <a id="1327" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1329" href="Data.W.html#1329" class="Bound">f</a><a id="1330" class="Symbol">))</a> <a id="1333" class="Symbol">=</a> <a id="1335" href="Data.W.html#1239" class="Bound">alg</a> <a id="1339" href="Function.html#1927" class="Function Operator">$</a> <a id="1341" class="Symbol">λ</a> <a id="1343" href="Data.W.html#1343" class="Bound">p</a> <a id="1345" class="Symbol">→</a> <a id="1347" href="Data.W.html#1280" class="Function">induction</a> <a id="1357" class="Symbol">(</a><a id="1358" href="Data.W.html#1329" class="Bound">f</a> <a id="1360" href="Data.W.html#1343" class="Bound">p</a><a id="1361" class="Symbol">)</a>

<a id="1364" class="Keyword">module</a> <a id="1371" href="Data.W.html#1371" class="Module">_</a> <a id="1373" class="Symbol">{</a><a id="1374" href="Data.W.html#1374" class="Bound">s</a> <a id="1376" href="Data.W.html#1376" class="Bound">p</a> <a id="1378" href="Data.W.html#1378" class="Bound">ℓ</a><a id="1379" class="Symbol">}</a> <a id="1381" class="Symbol">{</a><a id="1382" href="Data.W.html#1382" class="Bound">C</a> <a id="1384" class="Symbol">:</a> <a id="1386" href="Data.Container.Core.html#443" class="Record">Container</a> <a id="1396" href="Data.W.html#1374" class="Bound">s</a> <a id="1398" href="Data.W.html#1376" class="Bound">p</a><a id="1399" class="Symbol">}</a> <a id="1401" class="Symbol">(</a><a id="1402" class="Keyword">open</a> <a id="1407" href="Data.Container.Core.html#443" class="Module">Container</a> <a id="1417" href="Data.W.html#1382" class="Bound">C</a><a id="1418" class="Symbol">)</a>
         <a id="1429" class="Symbol">{</a><a id="1430" href="Data.W.html#1430" class="Bound">P</a> <a id="1432" class="Symbol">:</a> <a id="1434" class="PrimitiveType">Set</a> <a id="1438" href="Data.W.html#1378" class="Bound">ℓ</a><a id="1439" class="Symbol">}</a> <a id="1441" class="Symbol">(</a><a id="1442" href="Data.W.html#1442" class="Bound">alg</a> <a id="1446" class="Symbol">:</a> <a id="1448" href="Data.Container.Core.html#633" class="Function Operator">⟦</a> <a id="1450" href="Data.W.html#1382" class="Bound">C</a> <a id="1452" href="Data.Container.Core.html#633" class="Function Operator">⟧</a> <a id="1454" href="Data.W.html#1430" class="Bound">P</a> <a id="1456" class="Symbol">→</a> <a id="1458" href="Data.W.html#1430" class="Bound">P</a><a id="1459" class="Symbol">)</a> <a id="1461" class="Keyword">where</a>

 <a id="1469" href="Data.W.html#1469" class="Function">foldr</a> <a id="1475" class="Symbol">:</a> <a id="1477" href="Data.W.html#466" class="Datatype">W</a> <a id="1479" href="Data.W.html#1382" class="Bound">C</a> <a id="1481" class="Symbol">→</a> <a id="1483" href="Data.W.html#1430" class="Bound">P</a>
 <a id="1486" href="Data.W.html#1469" class="Function">foldr</a> <a id="1492" class="Symbol">=</a> <a id="1494" href="Data.W.html#1280" class="Function">induction</a> <a id="1504" class="Symbol">(</a><a id="1505" href="Function.html#1145" class="Function">const</a> <a id="1511" href="Data.W.html#1430" class="Bound">P</a><a id="1512" class="Symbol">)</a> <a id="1514" class="Symbol">(λ</a> <a id="1517" href="Data.W.html#1517" class="Bound">p</a> <a id="1519" class="Symbol">→</a> <a id="1521" href="Data.W.html#1442" class="Bound">alg</a> <a id="1525" class="Symbol">(_</a> <a id="1528" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1530" href="Data.W.html#1517" class="Bound">p</a><a id="1531" class="Symbol">))</a>

<a id="1535" class="Comment">-- If Position is always inhabited, then W_C is empty.</a>

<a id="1591" class="Keyword">module</a> <a id="1598" href="Data.W.html#1598" class="Module">_</a> <a id="1600" class="Symbol">{</a><a id="1601" href="Data.W.html#1601" class="Bound">s</a> <a id="1603" href="Data.W.html#1603" class="Bound">p</a><a id="1604" class="Symbol">}</a> <a id="1606" class="Symbol">{</a><a id="1607" href="Data.W.html#1607" class="Bound">C</a> <a id="1609" class="Symbol">:</a> <a id="1611" href="Data.Container.Core.html#443" class="Record">Container</a> <a id="1621" href="Data.W.html#1601" class="Bound">s</a> <a id="1623" href="Data.W.html#1603" class="Bound">p</a><a id="1624" class="Symbol">}</a> <a id="1626" class="Symbol">(</a><a id="1627" class="Keyword">open</a> <a id="1632" href="Data.Container.Core.html#443" class="Module">Container</a> <a id="1642" href="Data.W.html#1607" class="Bound">C</a><a id="1643" class="Symbol">)</a> <a id="1645" class="Keyword">where</a>

  <a id="1654" href="Data.W.html#1654" class="Function">inhabited⇒empty</a> <a id="1670" class="Symbol">:</a> <a id="1672" class="Symbol">(∀</a> <a id="1675" href="Data.W.html#1675" class="Bound">s</a> <a id="1677" class="Symbol">→</a> <a id="1679" href="Data.Container.Core.html#544" class="Field">Position</a> <a id="1688" href="Data.W.html#1675" class="Bound">s</a><a id="1689" class="Symbol">)</a> <a id="1691" class="Symbol">→</a> <a id="1693" href="Relation.Nullary.html#500" class="Function Operator">¬</a> <a id="1695" href="Data.W.html#466" class="Datatype">W</a> <a id="1697" href="Data.W.html#1607" class="Bound">C</a>
  <a id="1701" href="Data.W.html#1654" class="Function">inhabited⇒empty</a> <a id="1717" href="Data.W.html#1717" class="Bound">b</a> <a id="1719" class="Symbol">=</a> <a id="1721" href="Data.W.html#1469" class="Function">foldr</a> <a id="1727" class="Symbol">((</a><a id="1729" href="Function.html#1927" class="Function Operator">_$</a> <a id="1732" href="Data.W.html#1717" class="Bound">b</a> <a id="1734" class="Symbol">_)</a> <a id="1737" href="Function.html#805" class="Function Operator">∘</a> <a id="1739" href="Agda.Builtin.Sigma.html#237" class="Field">proj₂</a><a id="1744" class="Symbol">)</a>
</pre></body></html>